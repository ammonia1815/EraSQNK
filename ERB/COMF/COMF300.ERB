@COM300
;달콤한말
;一度の조교に3回までしか実行できない. 
;成功率判定を行う. 成功時には반감を買わずに순종・욕망が上昇. 

PRINTL 달콤한말

;実行回数判定　
CFLAG:268 += 1
;これを制限すると스투나での랄스君攻略が무리になってしまうのでそこだけ緩和. 
IF CFLAG:268 >= 4 && NO:TARGET != 94
	PRINTFORMW 그러나 %조사처리(CALLNAME:TARGET,"는")% 더 이상 이야기를 들을 기분이 아닌 것 같다…
	RETURN 0
ENDIF

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = 달콤한말

;-------------------------------------------------
;成功率の判定
;-------------------------------------------------
;基本成功率は30%. 무드依存で上昇する
CFLAG:269 = 30

;무드で成功率変化
SIF PLAYER == MASTER
	CFLAG:269 += MOOD() / 5

;成功率Up보정
SIF TALENT:솔직
	CFLAG:269 += 3
SIF TALENT:교만
	CFLAG:269 += 10
SIF TALENT:호기심
	CFLAG:269 += 3
SIF TALENT:양기
	CFLAG:269 += 3
SIF TALENT:눈에띄고싶어함
	CFLAG:269 += 3
SIF TALENT:해방
	CFLAG:269 += 3
SIF TALENT:부끄럼쟁이
	CFLAG:269 += 3
SIF TALENT:미련함
	CFLAG:269 += 3
SIF TALENT:음란
	CFLAG:269 += 5
SIF TALENT:연모
	CFLAG:269 += 10
SIF TALENT:망신
	CFLAG:269 += 5
SIF TALENT:서약
	CFLAG:269 += 2
SIF TALENT:상사상애
	CFLAG:269 += 10
;마법사は誉められるのが大好き! 
SIF TALENT:마법사
	CFLAG:269 += 10
SIF ABL:순종
	CFLAG:269 += ABL:순종 * 3
SIF TEQUIP:새댁플레이
	CFLAG:269 += 5
;前回조교が애무系
SIF PREVCOM == 0 || PREVCOM == 5 || PREVCOM == 7
	CFLAG:269 += 10
;前回조교が키스
SIF PREVCOM == 6 || TEQUIP:키스
	CFLAG:269 += 15
;見つめ合って삽입中(반발각인0LV)
SIF INSERT("정상위") + INSERT("대면좌위") && MARK:반발각인 == 0
	CFLAG:269 += 15
;무드 업인센스
SELECTCASE GETBITS(TEQUIP:MASTER:인센스, 4, 6)
CASE 1p4
	CFLAG:269 += 3
CASE 1p5
	CFLAG:269 += 8
CASE 1p6
	CFLAG:269 += 15
ENDSELECT

;핌블베트と셀마
SIF NO:TARGET == 49
	CFLAG:269 += 50
;二人とも달콤한말の놀림손
IF ASSI
	SIF TALENT:ASSI:달콤한말 && TALENT:MASTER:달콤한말
		CFLAG:269 += 30
ENDIF
;詩集の持ってる数
CFLAG:269 += MIN(ITEM:로즈닐의시집, 10)
;前回절정
CFLAG:269 += PREVEX("절정수합계")*5

;成功率Down보정
SIF TALENT:겁쟁이
	CFLAG:269 -= 8
SIF TALENT:반항적
	CFLAG:269 -= 10
SIF TALENT:건방짐
	CFLAG:269 -= 10
SIF TALENT:냉정
	CFLAG:269 -= 3
SIF TALENT:무관심
	CFLAG:269 -= 8
SIF TALENT:감정결핍
	CFLAG:269 -= 5
SIF TALENT:보수적
	CFLAG:269 -= 3
SIF TALENT:음기
	CFLAG:269 -= 5
SIF TALENT:일선을넘지않음
	CFLAG:269 -= 8
SIF TALENT:저항
	CFLAG:269 -= 10
SIF TALENT:현명함
	CFLAG:269 -= 3
SIF TALENT:남성혐오 && TALENT:PLAYER:남자
	CFLAG:269 -= 10
SIF TALENT:광기
	CFLAG:269 -= 15
IF MARK:반발각인
	CFLAG:269 -= (MARK:반발각인 + MARK:반발각인의취득이력) * 10
ELSEIF MARK:반발각인의취득이력
	CFLAG:269 -= MARK:반발각인의취득이력 * 5
ENDIF
SIF TEQUIP:바이브
	CFLAG:269 -= 2
SIF TEQUIP:애널바이브
	CFLAG:269 -= 3
SIF TEQUIP:클리캡 || TEQUIP:니플캡 || TEQUIP:착유기 || TEQUIP:오나홀
	CFLAG:269 -= 2
SIF TEQUIP:미약
	CFLAG:269 -= 5
SIF TEQUIP:이뇨제
	CFLAG:269 -= 5
SIF TEQUIP:아이마스크
	CFLAG:269 -= 1
SIF TEQUIP:구속
	CFLAG:269 -= 5
SIF TEQUIP:재갈
	CFLAG:269 -= 3
SIF TEQUIP:관장 || TEQUIP:확장벌룬
	CFLAG:269 -= 10
SIF TEQUIP:비디오카메라
	CFLAG:269 -= 3

;成功率操作(最初の成功判定だけ)
CFLAG:269 -= 30
CFLAG:269 += (FLAG:30 * 2) + (ABL:PLAYER:기교 * 3)
SIF TALENT:PLAYER:수수께끼의매력
	CFLAG:269 += 10
SIF CFLAG:269 < 0
	CFLAG:269 = 0

;合計を表示
;PRINTFORML 成功率 ： {CFLAG:269}％

;話を聞いてくれる
IF CFLAG:269  >= MAX(1, RAND:100)
	TEQUIP:달콤한말 = 2
;話を聞いてくれない
ELSE
	TEQUIP:달콤한말 = 0
	CFLAG:269 = 0
	DOWNBASE:체력 += 30
	DOWNBASE:기력 += 120
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM300
PRINTFORM %CALLNAME:PLAYER%
;인서트中
IF INSERT("합체中") && RANDIF(1, TEQUIP:키스)
	PRINTFORM %조사만처리(CALLNAME:PLAYER,"는")% %TEXTS("합체中", PLAYER)%
ELSEIF TEQUIP:키스
	PRINTFORM %조사만처리(CALLNAME:PLAYER,"는")% %TEXTS("키스継続")%,
	CALL SETFLAG, "추가키스플래그", TARGET
ELSEIF PREVCOM == 91
	PRINTFORM %조사만처리(CALLNAME:PLAYER,"는")% 입으로 페니스에 봉사하고 있는 
ELSEIF PREVCOM == 98
	PRINTFORM %조사만처리(CALLNAME:PLAYER,"는")% 가랑이에 얼굴을 묻고 봉사하고 있는 
ELSEIF ASSI && TALENT:ASSI:달콤한말 && TALENT:MASTER:달콤한말 && ASSIPLAY
	PRINTFORM %조사만처리(CALLNAME:PLAYER,"와")%  %조사처리(CALLNAME:MASTER,"는")% 
ELSEIF ASSI && TALENT:ASSI:달콤한말 && TALENT:MASTER:달콤한말 && ASSIPLAY == 0
	PRINTFORM %조사만처리(CALLNAME:PLAYER,"와")%  %조사처리(CALLNAME:ASSI,"는")% 
ELSE
	PRINTFORM %조사만처리(CALLNAME:PLAYER,"는")% 
ENDIF
IF TALENT:PLAYER:조야 && AGE(TARGET) < AGE(PLAYER) && INSERT("합체中") == 0
	PRINTFORMW %CALLNAME:TARGET%에게 부드러운 말을 속삭였다…
ELSE
	PRINTFORMW %CALLNAME:TARGET%에게 달콤한 말을 속삭였다…
ENDIF


@EQUIP_COM33_2
;実行ターンの成功時のSOURCEはここで処理
IF SELECTCOM == 300
	DOWNBASE:체력 += 30
	DOWNBASE:기력 += 30
	;成功率Downの소질がある場合, 取得sourceに보너스がつく
	;ソースの基準量や보정倍率はまだ調整途上. 셀마の攻略にちょうど良い程度にコントロールしたい. 
	LOCAL = CALCF("달콤한말")

	SOURCE:정애 += LOCAL
	IF TALENT:남자 == 0
		IF TALENT:젖기쉬움
			SOURCE:액체추가 += UP_PALAMLV(LOCAL / 10)
		ELSEIF TALENT:젖기어려움
			SOURCE:액체추가 += DOWN_PALAMLV(LOCAL / 10)
		ELSE
			SOURCE:액체추가 += LOCAL / 10
		ENDIF
	ENDIF
	SOURCE:순종추가 += LOCAL / 2
	SOURCE:욕정추가 += LOCAL / 4
	;반항적な人は순종が上がらな過ぎるので, 보정
	SIF TALENT:반항적
		SOURCE:순종추가 += LOCAL * 3
	;무드加算
	TFLAG:33 += CFLAG:269/10 + 5

	;호감도
	SIF PLAYER == MASTER
		TFLAG:30 += 8 + CFLAG:269/10
ENDIF
;무드 업中
PRINTFORML ＜달콤한말효과 발휘중＞
TEQUIP:달콤한말 += 1
;호감도
SIF PLAYER == MASTER
	TFLAG:30 += 2

RETURN 1

;-------------------------------------------------
;범용구상
;-------------------------------------------------
@KOJO_MESSAGE_COM300
VARSET LOCAL

;黙って키스orお금や아이템を貢ぐ
SELECTCASE NO:TARGET
CASE 3, 10, 12, 13, 16 TO 19, 23, 25, 27, 28, 30
	LOCAL:1 = 1
ENDSELECT

;容姿を褒めたり, 情熱的アタック! 
SELECTCASE NO:TARGET
CASE 3, 8 TO 10, 13, 17 TO 19, 22, 23, 25, 27, 28, 30, 32
	LOCAL:2 = 1
ENDSELECT

;お菓子を올린다から悪戯させて! ｗ
SELECTCASE NO:TARGET
CASE 2, 50
	LOCAL:3 = 1
ENDSELECT

SIF INSERT("합체中")
	VARSET LOCAL

IF LOCAL:3 && RANDIF(3, LOCAL:1, LOCAL:2)
	CALL TALK_3
ELSEIF LOCAL:2 && RANDIF(3, LOCAL:1)
	CALL TALK_2
ELSEIF LOCAL:1 && RAND:3 == 0
	CALL TALK_1
;범용成功
ELSEIF TEQUIP:달콤한말
	IF TALENT:교만 && RAND:2
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 꼭 싫지만도 않은 것같다…
	ELSEIF TALENT:양기
		IF RAND:2
			PRINTFORM %조사처리(CALLNAME:TARGET,"는")%, 히죽 웃는 얼굴로%CALLNAME:PLAYER%
			IF AGE(TARGET) >= 3
				PRINTFORMW %조사만처리(CALLNAME:PLAYER,"를")% 안아주었다! 
			ELSE
				PRINTW 에게 안겨들었다! 
			ENDIF
		ELSE
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:PLAYER%의 말을 곱씹으며 얼굴을 이완시키고 있다! 
		ENDIF
	ELSEIF TALENT:부끄럼쟁이
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 얼굴을 새빨갛게 물들이고 꼼지락꼼지락거리고 있다! 
	ELSE
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 기뻐하고 있다! 
	ENDIF
;범용失敗
ELSE
	IF TALENT:교만 && RAND:2
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")%, %CALLNAME:PLAYER%의 입에 발린 소리를 비웃었다…
	ELSE
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")%, %CALLNAME:PLAYER%의 말을 귓등으로 흘려보냈다…
	ENDIF
ENDIF


;黙って키스orお금や아이템を貢ぐ
@TALK_1
SELECTCASE NO:TARGET
CASE 13
	PRINTFORMW 「너는 %CNAME("一人称")%에게, 뭘 해줄거야? 」
CASE 8, 9, 32
	PRINTFORMW 「그러니까………」
	PRINTFORMW 「%조사처리(CNAME("一人称"),"를")% 어떻게 하고싶어? 」
CASE 30
	PRINTFORMW 「저기………」
	PRINTFORMW 「%조사처리(CNAME("一人称"),"를")% 어떻게 하고 싶은겁니까? 」
CASE 16
	PRINTFORMW 「호오…………」
	PRINTFORMW 「그럼 %CNAME("二人称")%의 성의를 보여보시게」
CASE 28
	PRINTFORMW 「%조사처리(CNAME("一人称"),"가")% 기뻐할 일, 스스로 생각해보세요」
CASE 12
	PRINTFORMW 「그…그런 말따위에 유혹당하지 않으니까!」
	PRINTFORMW 「……지……진심이라면 뭔가 행동으로 보여줘……」
CASE 23
	PRINTFORML 「아…, 혹시, 주군」
	PRINTFORMW 「내 잃어버린 물건을 돌려줄 순 없는 걸까나? 」
	CALL TALK_1_GIFT
CASE 3, 10, 17, 18, 19, 25, 27
	IF CFLAG:269 >= 70
		PRINTFORMW 「어머…%조사처리(CNAME("一人称"),"를")% 어떻게 하고싶은걸까? 」
	ELSE
		PRINTFORML  ｢흐으응……」
		PRINTFORMW 「신용할 수가 없네요」
		PRINTFORMW 「성의를 보여보실까요? ｣
	ENDIF
ENDSELECT

CALL TALK_1_MAIN

@TALK_1_MAIN
PRINTL [0] 입다물고 키스를 한다
PRINTL [1] 돈을 바친다
SIF MONEY >= 50
	PRINTL [2] 아이템을 바친다

IF MONEY >= 50
	CALL INPUT_SELECT, 3
ELSE
	CALL INPUT_SELECT, 2
ENDIF

IF RESULT == 0
	CALL TALK_1_KISS
ELSEIF RESULT == 1
	CALL TALK_1_MONEY
ELSEIF RESULT == 2
	CALL TALK_1_GIFT
ENDIF

@TALK_1_KISS
PRINTFORML %조사처리(CALLNAME:MASTER,"는")% 조용히 %CALLNAME:TARGET%에게 부드럽게 키스했다
;成功
IF (TEQUIP:달콤한말 || CFLAG:269 >= RAND:100)
	SELECTCASE NO:TARGET
	CASE 13
		PRINTFORMW 「싫어……ㅅ♪」
	CASE 8, 9, 30, 32
		PRINTFORMW 「싫어……ㅅ♪」
	CASE 16
		PRINTFORMW 「무…무례한 놈……」
	CASE 28
		PRINTFORMW 「무…, 뭘 잘난듯이 흉내내고 있는거야!  이, 이런 걸로 느끼지는 않으니까!」
	CASE 12
		PRINTFORMW 「에…, 싫엇………♪」
	CASE 3, 10, 17, 18, 19, 25, 27
		PRINTFORMW 「응……, 멋져♪」
	ENDSELECT
	TEQUIP:달콤한말 = 2
ELSE
	IF CFLAG:269 <= 50
		PRINTL 
		PRINTFORMW 그러나 %조사처리(CALLNAME:TARGET,"는")% 싸늘한 눈으로 이쪽을 바라보고있다! 
		TFLAG:30 -= 10
		TFLAG:33 -= 5
		TEQUIP:달콤한말 = 0
		RETURN 0
	ENDIF

	SELECTCASE NO:TARGET
	CASE 8, 9, 30, 32
		PRINTFORMW 「꺄……ㅅ! ? 」
		TEQUIP:달콤한말 = 0
		RETURN 0
	CASE 28
		PRINTFORMW 「런…, 그런……,  건방진!」
		TEQUIP:달콤한말 = 0
		RETURN 0
	CASE 16
		PRINTFORMW 「무례한 녀석」
		PRINTFORMW 「……그걸로 끝인가? 」
	CASE 12
		PRINTFORMW 「……끝? 」
	CASE 13
		PRINTFORMW 「……끝? 」
	CASE 3, 10, 17, 18, 19, 25, 27
		PRINTFORMW 「……끝? 」
	ENDSELECT
	;一度失望
	CFLAG:269 = LIMIT(CFLAG:269 - 10, 0, 999)

	PRINTL [0] 예
	PRINTL [1] 아니오
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		SELECTCASE NO:TARGET
		CASE 13
			PRINTFORMW 「너…바보취급하는 거야? 」
		CASE 16
			IF TALENT:PLAYER:남자
				PRINTFORMW 「시시한 남자군……」
			ELSE
				PRINTFORMW 「재미없어어……」
			ENDIF
		CASE 12
			PRINTFORMW 「재미없네요……」
		CASE 3, 10, 17, 18, 19, 25, 27
			PRINTFORMW 「재미없네요……」
		ENDSELECT
		TFLAG:30 -= 10
		TFLAG:33 -= 5
		TEQUIP:달콤한말 = 0
		RETURN 0
	ELSEIF RESULT == 1
		PRINTL 
		PRINTL 어떻게 합니까? 
		CALL TALK_1_MAIN
	ENDIF
ENDIF

@TALK_1_MONEY
;금額基礎値
LOCAL = 10 + RAND:100 + TALENT:교만*RAND:300
SELECTCASE NO:TARGET
CASE 13
	PRINTFORMW 「…돈따위엔 흥미없어요」
	TEQUIP:달콤한말 = 0
	RETURN 0
CASE 8, 9, 32
	PRINTFORMW 「음, 그럼……%MONEY_C(LOCAL)%…줄래？」
CASE 30
	PRINTFORMW 「…돈따윈 흥미없습니다」
	TEQUIP:달콤한말 = 0
	RETURN 0
CASE 16, 22
	LOCAL *= 3 + RAND:5
	PRINTFORMW 「호오. 그럼………%MONEY_C(LOCAL)% 받아보실까」
CASE 28
	LOCAL *= 1 + RAND:5
	PRINTFORMW 「후-응.  그럼, %MONEY_C(LOCAL)% 줘」
CASE 12
	PRINTFORMW 「…돈따위엔 흥미없어요」
	TEQUIP:달콤한말 = 0
	RETURN 0
CASE 10, 25
	PRINTFORML 「…돈따위엔 흥미없어요.  내가 원하는건…다･앙･신…」
	PRINTFORMW 「후후훗…♪」
	PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:PLAYER%에게 다가가, 요염하게 미소지으면서 뺨을 쓰다듬었다…
	RETURN 1
CASE 17, 19
	LOCAL *= 2 + RAND:5
	PRINTFORMW 「저기, 그럼 %MONEY_C(LOCAL)% 줘｣
CASE 27
	LOCAL *= 5 + RAND:5
	PRINTFORMW 「그렇네, 그러면……%MONEY_C(LOCAL)% 줄 수 있을까？｣
CASE 3, 18
	PRINTFORMW 「…돈따위엔 흥미없어요」
	TEQUIP:달콤한말 = 0
	RETURN 0
ENDSELECT
PRINTL [0] 예
PRINTL [1] 아니오
CALL INPUT_SELECT, 2
IF RESULT == 0 && MONEY >= LOCAL
	MONEY -= LOCAL
	PRINTFORML %CALLNAME:TARGET%에게 %MONEY_C(LOCAL)% 주었다
	SELECTCASE NO:TARGET
	CASE 8, 9, 32
		PRINTFORMW 「고, 고마워……♪」
	CASE 16
		PRINTFORMW 「후후, 고맙구나」
	CASE 22
		PRINTFORMW 「흐응, 솔직하네」
	CASE 28
		PRINTFORMW 「…아, 정말 줬네. 고마워♪」
	CASE 17, 19
		PRINTFORMW 「고마워♪｣
	CASE 27
		PRINTFORMW 「후후훗, 고마워♪｣
	ENDSELECT
	TEQUIP:달콤한말 = 2
ELSE
	SELECTCASE NO:TARGET
	CASE 8, 9, 32
		PRINTFORMW 「놀린거네……」
	CASE 16
		PRINTFORMW 「%MONEY_C(LOCAL)%도 갖고있지 않으면서 이 몸을 꼬신건가. 분수를 알아라!」
	CASE 22
		PRINTFORMW 「결국 그 정도인가……」
	CASE 28
		PRINTFORMW 「아～아.  괜히 기대했다!」
	CASE 17, 19
		PRINTFORMW 「싫다, %MONEY_C(LOCAL)%정도도 없어？｣
		PRINTFORMW 「기대한 내가 바보였네……」
	CASE 27
		PRINTFORMW 「싫다, %MONEY_C(LOCAL)%정도도 없어？｣
		PRINTFORMW 「재미없네요……」
	ENDSELECT
	TFLAG:30 -= 10
	TFLAG:33 -= 10
	TEQUIP:달콤한말 = 0
ENDIF

@TALK_1_GIFT
;10万＄
IF MONEY >= 100000 && RAND:3
	SELECTCASE RAND:10
	CASE 9
		LOCALS = 다이아몬드 부적
		LOCAL = 1780
	CASE 8
		LOCALS = 크리스탈 부적
		LOCAL = 1780
	CASE 7
		LOCALS = 페리도트 부적
		LOCAL = 1780
	CASE 6
		LOCALS = 에메랄드 부적
		LOCAL = 1780
	CASE 5
		LOCALS = 사파이어 부적
		LOCAL = 1780
	CASE 4
		LOCALS = 은 피어스
		LOCAL = 1080
	CASE 3
		LOCALS = 포실 팬던트
		LOCAL = 1620
	CASE 2
		LOCALS = 마노 피어스
		LOCAL = 1780
	CASE 1
		LOCALS = 월장석 브레이슬렛
		LOCAL = 2360
	CASEELSE
		LOCALS = 파이어플라이 브로치
		LOCAL = 2180
	ENDSELECT
;1万＄
ELSEIF MONEY >= 10000 && RAND:2
	SELECTCASE RAND:6
	CASE 5
		LOCALS = 네롤리의 장갑
		LOCAL = 850
	CASE 4
		LOCALS = 컴플렉스 스카프
		LOCAL = 710
	CASE 3
		LOCALS = 실버 네크리스
		LOCAL = 230
	CASE 2
		LOCALS = 청은의 브레이슬렛
		LOCAL = 340
	CASE 1
		LOCALS = 쉘 이어링
		LOCAL = 280
	CASEELSE
		LOCALS = 버터플라이 브로치
		LOCAL = 480
	ENDSELECT
ELSEIF MONEY >= 500 && TALENT:유치 == 0
	SELECTCASE RAND:7
	CASE 6
		LOCALS = 레드 그루펜
		LOCAL = 370
	CASE 5
		LOCALS = 빌리지안 호로스
		LOCAL = 370
	CASE 4
		LOCALS = 폿나파 앰버
		LOCAL = 310
	CASE 3
		LOCALS = 스노우 오브 바사
		LOCAL = 290
	CASE 2
		LOCALS = 블루프리베리
		LOCAL = 250
	CASE 1
		LOCALS = 시프리즈화이트
		LOCAL = 230
	CASEELSE
		LOCALS = 브라운렌젯트
		LOCAL = 210
	ENDSELECT
ELSEIF MONEY >= 50
	SELECTCASE RAND:7
	CASE 6
		LOCALS = 인어의 입맞춤
		LOCAL = 50
	CASE 5
		LOCALS = 디바인 푸딩
		LOCAL = 50
	CASE 4
		LOCALS = 그루펜 골드
		LOCAL = 40
	CASE 3
		LOCALS = 츄로스
		LOCAL = 25
	CASE 2
		LOCALS = 풋라
		LOCAL = 25
	CASE 1
		LOCALS = 슈･아･라･크렘
		LOCAL = 20
	CASEELSE
		LOCALS = 허니 비스킷
		LOCAL = 10
	ENDSELECT
ENDIF
SELECTCASE NO:TARGET
CASE 13
	PRINTFORMW 「그럼…… 그 %LOCALS%(%MONEY_C(LOCAL)%상당)…줄래？」
CASE 8, 9, 32
	PRINTFORMW 「음, 그럼…… 그 %LOCALS%(%MONEY_C(LOCAL)%상당)…줄래？」
CASE 30
	PRINTFORMW 「그러면…… 그 %LOCALS%(%MONEY_C(LOCAL)%상당)…줄 수 있나요？」
CASE 16
	PRINTFORMW 「그렇군, 그럼……」
	PRINTFORMW 「그 %LOCALS%(%MONEY_C(LOCAL)%상당) 받아보실까」
CASE 28
	PRINTFORMW 「자아, 그 %LOCALS%(%MONEY_C(LOCAL)%상당) 줘」
CASE 12
	PRINTFORMW 「혹시… 그 %LOCALS%(%MONEY_C(LOCAL)%상당), 나한테…？」
CASE 23
	IF MONEY < 50
		PRINTFORMW 「아무것도 가지고 있지 않잖아!」
		TEQUIP:달콤한말 = 0
		TFLAG:30 -= 10
		TFLAG:33 -= 5
		RETURN 0
	ELSE
		PRINTFORMW 「고마워!  %LOCALS%(%MONEY_C(LOCAL)%상당)%조사만처리(LOCALS,"를")% 돌려주는거야!」
	ENDIF
CASE 3, 10, 17, 18, 19, 25, 27
	PRINTFORML 「그렇네, 그러면……」
	PRINTFORMW 「그 %LOCALS%(%MONEY_C(LOCAL)%상당) 주실까요？」
ENDSELECT
PRINTL [0] 예
PRINTL [1] 아니오
CALL INPUT_SELECT, 2
IF RESULT == 0
	MONEY -= LOCAL
	SELECTCASE NO:TARGET
	CASE 13
		PRINTFORMW 「기뻐……♪」
	CASE 8, 9, 32
		PRINTFORMW 「고, 고마워……♪」
	CASE 30
		IF LOCAL >= 500
			PRINTFORMW 「……소중히 여기겠습니다♪」
		ELSE
			PRINTFORMW 「감사합니다……♪」
		ENDIF
	CASE 16
		PRINTFORMW 「음, 고맙다♪」
	CASE 28
		PRINTFORMW 「…아, 정말로 주었다. 고마워♪」
	CASE 12
		PRINTFORMW 「바, 받아주겠지만, 착각하지 마!」
	CASE 23
		PRINTFORMW 「고마워!」
	CASE 17, 19
		PRINTFORMW 「고마워♪｣
	CASE 3, 10, 18, 25, 27
		PRINTFORMW 「후후훗, 고마워♪｣
	ENDSELECT
	TFLAG:30 += 10
	TFLAG:33 += 10
	TEQUIP:달콤한말 = 2
ELSE
	SELECTCASE NO:TARGET
	CASE 13
		PRINTFORMW 「그래….  나를 가지고 논거네……」
	CASE 8, 9, 32
		PRINTFORMW 「나를 희롱한거네? 」
		PRINTFORMW 「너무해…」
	CASE 30
		PRINTFORML 「…후후」
		PRINTFORMW 「%CNAME("一人称")%에게 정말로 줄리가 없겠지요……」
	CASE 16
		PRINTFORMW 「이 어리석은 놈이…」
	CASE 28
		PRINTFORMW 「아～아.  괜히 기대했다」
	CASE 12
		PRINTFORMW 「바보!  바보!」
		PRINTFORMW 「바보옷!!」
	CASE 23
		PRINTFORMW 「도둑놈!」
	CASE 17, 19
		PRINTFORMW 「…그런 각오로, 잘도 말을 걸었군? ｣
	CASE 3, 10, 18, 25, 27
		PRINTFORMW 「…바보 취급하는 걸까? ｣
	ENDSELECT
	TFLAG:30 -= 10
	TFLAG:33 -= 10
	TEQUIP:달콤한말 = 0
ENDIF

;容姿を褒めたり, 情熱的アタック! 
@TALK_2
SELECTCASE NO:TARGET
CASE 22
	PRINTFORMW 「나를 설득하려하다니 목숨이 아깝지 않은거군……」
	PRINTFORMW 「나를 원하는가? 」
CASE 23
	PRINTFORMW 「그렇게 나랑 같이있고 싶어? 」
CASE 28
	PRINTFORMW 「흐응, 나의 매력에 헤롱헤롱인거네? 」
CASE 30
	PRINTFORMW 「에….  %CNAME("一人称")%, 그렇게 아름답나요? 」
CASEELSE
	PRINTFORMW 「어머, 굉장한 걸 말하네요」
	PRINTFORMW 「%CNAME("一人称")%, 그렇게 예뻐요? 」
ENDSELECT
PRINTL [0] 예
PRINTL [1] 아니오
CALL INPUT_SELECT, 2
IF RESULT == 0 && (TEQUIP:달콤한말 || CFLAG:269 >= RAND:100 || NO:TARGET == 22 || NO:TARGET == 23)
	SELECTCASE NO:TARGET
	CASE 22
		IF CFLAG:269 >= RAND:100
			PRINTFORMW 「후후…정직한 녀석이군」
		ELSEIF RAND:2
			CALL TALK_1_MONEY
			RETURN 1
		ELSE
			CALL TALK_2_1
			RETURN 1
		ENDIF
	CASE 23
		IF RAND:2
			PRINTFORMW 「그럼, 같이 가자♪」
		ELSE
			PRINTFORMW 「그럼, 이상한 나라로 렛츠·고!」
			PRINTFORMW 「잡아봐라!」
		ENDIF
	CASE 28
		PRINTFORMW 「무리도 아니지만…후후후훗♪」
	CASE 30
		PRINTFORMW 「부, 부끄럽습니다…♪」
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 고개를 숙이고, 양손으로 긴 귀를 말캉말캉 만지작거리기 시작했다…
	CASEELSE
		PRINTFORMW 「후훗, 고마워♪」
	ENDSELECT
	TEQUIP:달콤한말 = 2
	;虜になる？
	SELECTCASE NO:TARGET
	CASE 3, 10, 13, 17, 18, 25
		IF RAND:2
			PRINTL 
			CALL TALK_2_1
		ENDIF
	ENDSELECT
ELSEIF RESULT == 0
	IF NO:TARGET == 28
		PRINTFORMW 「…너, 아부떨고있는거지.  바보같아…」
		TFLAG:30 -= 5
		TFLAG:33 -= 5
		TEQUIP:달콤한말 = 0
	ELSE
		PRINTFORMW 「…왠지 바보취급당한 기분이네요」
		PRINTFORMW 「성의를 보여보실까요? ｣
		CFLAG:269 = LIMIT(CFLAG:269 - 10, 0, 999)
		CALL TALK_1_MAIN
	ENDIF
ELSEIF RESULT == 1
	SELECTCASE NO:TARGET
	CASE 22
		PRINTFORMW 「메마른 녀석이군…」
	CASE 23
		PRINTFORMW 「분하다!」
	CASE 28
		PRINTFORMW 「건방진…」
	CASE 30
		PRINTFORMW 「………하아…」
	CASEELSE
		IF RAND:2
			PRINTFORMW 「…어쩐지 바보취급 당한 기분이네요」
		ELSE
			PRINTFORMW 「……어머, 그래」
		ENDIF
	ENDSELECT
	TFLAG:30 -= 10
	TFLAG:33 -= 5
	TEQUIP:달콤한말 = 0
ENDIF

;主に容姿を褒めたあとの展開…虜になる？
@TALK_2_1
;머메이드
SELECTCASE NO:TARGET
CASE 13
	PRINTFORMW 「%CNAME("一人称")%의 노래, 들어줄래? 」
CASE 22
	PRINTFORMW 「후후…그럼 이쪽으로 와」
CASEELSE
	PRINTFORMW 「%조사처리(CNAME("一人称"),"랑")% 같이 있고싶어? 」
ENDSELECT
PRINTL [0] 예
PRINTL [1] 아니오
CALL INPUT_SELECT, 2
IF RESULT == 0 || NO:TARGET == 22
	TEQUIP:달콤한말 = 2
	SELECTCASE NO:TARGET
	CASE 13
		PRINTFORMW 「후후…♪」
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 하고 싶은대로 노래하기 시작했다…
		TFLAG:30 += 5
	CASE 25
		PRINTFORMW 「굉장해, 당신……잔뜩 느끼게 해줄게♪」
	CASE 22
		IF RESULT == 0
			PRINTFORMW 「자아…원하는대로 해주겠어!」
		ELSE
			PRINTFORMW 「이미 늦었다!」
			TEQUIP:달콤한말 = 0
		ENDIF
		PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% %CALLNAME:TARGET%에게 밀려넘어져 버렸다…! 
		TEQUIP:넘어뜨리기 = 1
	CASEELSE
		PRINTFORMW 「원하는 대로 해줄게♪」
		SELECTCASE NO:TARGET
			CASE 3, 10, 17, 18
				LASTWORD = %CALLNAME:TARGET%
				IF NO:TARGET == 3
					LASTWORD = %LASTWORD%의 덩굴
				ELSEIF NO:TARGET == 10
					LASTWORD = %LASTWORD%의 꼬리
				ELSEIF NO:TARGET == 17
					LASTWORD = %LASTWORD%의 채찍
				ELSEIF NO:TARGET == 18
					LASTWORD = %LASTWORD%의 실
				ENDIF
				PRINTFORML %LASTWORD가()% %CALLNAME:PLAYER%의 몸을 구속한다! 
		ENDSELECT
		PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 밀려넘어져 버렸다…
		TEQUIP:넘어뜨리기 = 1
	ENDSELECT
ELSEIF RESULT == 1
	IF CFLAG:269 >= RAND:100
		PRINTFORMW 「어머…나를 어떻게 하고싶은걸까? 」
		CFLAG:269 = LIMIT(CFLAG:269 - 10, 0, 999)
		CALL TALK_1_MAIN
	ELSE
		PRINTFORMW 「……어머, 그래」
		TFLAG:30 -= 10
		TFLAG:33 -= 5
		TEQUIP:달콤한말 = 0
	ENDIF
ENDIF

;お菓子を올린다から悪戯させて! ｗ
@TALK_3
IF TEQUIP:달콤한말 == 0 || CFLAG:269 <= RAND:100 || MONEY < 50
	PRINTFORMW 「응～～?  %CNAME("一人称")%, 잘 모르겠어♪」
	TEQUIP:달콤한말 = 0
	RETURN 0
ENDIF

PRINTFORMW 「응～～～～」
PRINTFORMW 「저기, 그런거보단 뭔가 달콤한 것 좀 줘♪」
SELECTCASE RAND:7
CASE 6
	LOCALS = 인어의 입맞춤
	LOCAL = 50
CASE 5
	LOCALS = 디바인 푸딩
	LOCAL = 50
CASE 4
	LOCALS = 그루펜 골드
	LOCAL = 40
CASE 3
	LOCALS = 츄로스
	LOCAL = 25
CASE 2
	LOCALS = 풋라
	LOCAL = 25
CASE 1
	LOCALS = 슈･아･라･크렘
	LOCAL = 20
CASEELSE
	LOCALS = 허니 비스킷
	LOCAL = 10
ENDSELECT
PRINTFORMW 「%CNAME("一人称")%, 그 %LOCALS%(%MONEY_C(LOCAL)%상당)%조사만처리(LOCALS,"가")% 먹고싶구나아♪」
PRINTL [0] 예
PRINTL [1] 아니오
CALL INPUT_SELECT, 2
IF RESULT == 0
	PRINTFORMW 「와아, 고마워～～ㅅ♪」
	TEQUIP:달콤한말 = 2
ELSE
	PRINTFORMW 「에～, 구두쇠～～!」
	TFLAG:30 -= 10
	TFLAG:33 -= 5
	TEQUIP:달콤한말 = 0
ENDIF

