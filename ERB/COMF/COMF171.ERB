@COM171
;야외플레이
;TEUIP:54のビットが 0人里, 1森林, 2花園, 3水辺, 4海
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

IF TEQUIP:야외플레이 == 0
	PREVCOM = 171
	RETURN 0
ELSEIF GETBIT(TFLAG:16, 0)
	STR:0 = 야외플레이(마을)
ELSEIF GETBIT(TFLAG:16, 1)
	STR:0 = 야외플레이(삼림)
ELSEIF GETBIT(TFLAG:16, 2)
	STR:0 = 야외플레이(화원)
ELSEIF GETBIT(TFLAG:16, 3)
	STR:0 = 야외플레이(강가)
ELSEIF GETBIT(TFLAG:16, 4)
	STR:0 = 야외플레이(海)
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM171
#DIM MEMO_KISEKAE
#DIM MEMO_LINECOUNT

IF TEQUIP:야외플레이 == 0
	PRINTFORML %조사처리(CALLNAME:TARGET,"를")% 어디로 데리고 갑니까? 
	PRINTFORML 
	PRINTL  [ 0] 마을에 데리고 간다
	PRINTL  [ 1] 삼림에 데리고 간다
	PRINTL  [ 2] 화원에 데리고 간다
	PRINTL  [ 3] 강가에 데리고 간다
	PRINTL  [ 4] 바다에 데리고 간다
	PRINTL [100] 그만둔다
	CALL INPUT_SELECT, 5, 100
ELSEIF TEQUIP:야외플레이
	;야외플레이行き先플래그
	TFLAG:16 = TEQUIP:야외플레이
	PRINTFORMW 방으로 돌아왔다…
	TEQUIP:야외플레이 = 0
	SIF ASSI
		TEQUIP:ASSI:야외플레이 = 0
	CALL KOJO_MESSAGE_COM
	RETURN 1
ENDIF

IF RESULT == 100
	TFLAG:14 = 1
	RETURN 0
ELSEIF RESULT == 0
	LOCALS = 마을
ELSEIF RESULT == 1
	LOCALS = 삼림
ELSEIF RESULT == 2
	LOCALS = 화원
ELSEIF RESULT == 3
	LOCALS = 강가
ELSEIF RESULT == 4
	LOCALS = 海
ENDIF

SETBIT TEQUIP:야외플레이, RESULT
SIF ASSI
	TEQUIP:ASSI:야외플레이 = TEQUIP:야외플레이

;야외플레이行き先플래그
TFLAG:16 = TEQUIP:야외플레이

;갈아입히기させた状態を체크
MEMO_KISEKAE = 0

;선택지の存在判定に用いる
VARSET LOCAL
;선택지の二重出現を防ぐ
MEMO_LINECOUNT = LINECOUNT

;外出時の服装は登録されている？
SELECTCASE LOCALS
CASE "강가", "바다"
	IF GETBIT(IS_FAVORITE_DRESS(TARGET), 7)
		PRINTFORML %조사처리(CALLNAME:TARGET,"를")% 수영복으로 갈아입게 합니까？
		SETCOLOR DEF_COLOR("황색")
		PRINTFORM 수영복：
		CALL PRINT_FAVORITE_DRESS, 7
		PRINTFORML 
		RESETCOLOR
		PRINTFORML 
		PRINTFORML  [ 0] 예
		PRINTFORML  [ 1] 아니오
		IF EQUIP:신발 == 0 && CHECK_FAVORITE("신발", 7)
			PRINTFORML  [ 2] 신발만 신게한다
			LOCAL:2 = 2
		ENDIF
		IF EQUIP:팬티
			PRINTFORML  [ 3] 팬티만 벗게한다
			LOCAL:3 = 3
		ENDIF

		CALL INPUT_SELECT, 2, LOCAL:2, LOCAL:3

		SELECTCASE RESULT
		CASE 0
			CALL SET_FAVORITE_DRESS, TARGET, 7
			MEMO_KISEKAE = 1
		CASE 2
			EQUIP:신발 = CHECK_FAVORITE("신발", 7)
			CSTR:47 = %CHECK_FAVORITES("신발", 7)%
			MEMO_KISEKAE = 2
		CASE 3
			CALL SET_PANTIES, TARGET
			MEMO_KISEKAE = 3
		ENDSELECT
		PRINTL 
	ENDIF
CASEELSE
	IF GETBIT(IS_FAVORITE_DRESS(TARGET), 8)
		PRINTFORML %조사처리(CALLNAME:TARGET,"를")% 나들이 복장으로 갈아입게합니까？
		SETCOLOR DEF_COLOR("황색")
		PRINTFORM 외출복：
		CALL PRINT_FAVORITE_DRESS, 8
		PRINTFORML 
		RESETCOLOR
		PRINTFORML 
		PRINTFORML  [ 0] 예
		PRINTFORML  [ 1] 아니오
		IF EQUIP:신발 == 0 && CHECK_FAVORITE("신발", 8)
			PRINTFORML  [ 2] 신발만 신게한다
			LOCAL:2 = 2
		ENDIF
		IF EQUIP:팬티
			PRINTFORML  [ 3] 팬티만 벗게한다
			LOCAL:3 = 3
		ENDIF

		CALL INPUT_SELECT, 2, LOCAL:2, LOCAL:3

		SELECTCASE RESULT
		CASE 0
			CALL SET_FAVORITE_DRESS, TARGET, 8
			MEMO_KISEKAE = 1
		CASE 2
			EQUIP:신발 = CHECK_FAVORITE("신발", 8)
			CSTR:47 = %CHECK_FAVORITES("신발", 8)%
			MEMO_KISEKAE = 2
		CASE 3
			CALL SET_PANTIES, TARGET
			MEMO_KISEKAE = 3
		ENDSELECT
		PRINTL 
	ENDIF
ENDSELECT
;その他の場合
IF MEMO_LINECOUNT == LINECOUNT && EQUIP:팬티
	PRINTFORML …외출에 앞서서 %CALLNAME:TARGET%의 팬티를 벗게합니까？
	SETCOLOR DEF_COLOR("황색")
	PRINTFORM 현재의 복장：
	CALL PRINT_DRESS, TARGET
	PRINTFORML 
	RESETCOLOR
	PRINTFORML 
	PRINTFORML  [ 0] 예
	PRINTFORML  [ 1] 아니오

	CALL INPUT_SELECT, 2

	SELECTCASE RESULT
	CASE 0
		CALL SET_PANTIES, TARGET
		MEMO_KISEKAE = 3
	ENDSELECT
	PRINTL 
ENDIF

IF MEMO_KISEKAE == 1
	PRINT 옷을 갈아입힌 
ELSEIF CHECK_CLO("알몸")
	PRINT 알몸의 
ENDIF
PRINTFORM %CALLNAME:TARGET%
IF MEMO_KISEKAE == 2
	PRINTFORM 에게 %SHOES(TARGET, 1)%
	IF CHECK_CLO("알몸")
		PRINT 만 신게하고 
	ELSE
		PRINTFORMW %조사만처리(SHOES(TARGET, 1),"를")% 신게하고 
	ENDIF
ELSEIF MEMO_KISEKAE == 3 && CHECK_CLO("알몸") == 0
	PRINT 에게서 팬티를 빼앗고
ELSEIF (MARK:굴복각인 == 3 || TALENT:연모) && (ABL:마조끼 < 3 || RAND:5 == 0 || ITEM:개목걸이 == 0)
	PRINT 의 손을 잡고 
ELSE
	PRINT 에게 개목걸이를 채우고 
ENDIF
PRINTFORMW %LOCALS%에 데리고 나갔다…
PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
IF (ABL:노출벽 >= 3 || TALENT:부끄럼쟁이) && GETBIT(TEQUIP:야외플레이, 0)
	PRINTFORMW 누군가의 시선을 느끼고, 얼굴에 홍조를 띄우면서 떨고 있다…
ELSEIF ABL:노출벽 >= 3
	PRINTFORMW 자신에게 향하는 시선을 상상하고, 얼굴에 홍조를 띄우면서 떨고 있다…
ELSEIF TALENT:부끄럼쟁이
	PRINTFORMW 작은 소리에도 민감하게 반응해 신체를 떨고 있다…
ELSEIF TALENT:소악마 || TALENT:심술쟁이
	PRINTFORM 걸으면서 %CALLNAME:MASTER%에게 달라붙어, 얼굴을 바라보면서 
	IF TALENT:작은몸집
		PRINT 미성숙한 
	ELSEIF TALENT:거유
		PRINT 풍만한 
	ELSE
		PRINT 부드러운 
	ENDIF
	PRINTW 몸을 문지르고 있다…
ELSEIF TALENT:무관심
	PRINTFORMW 주변에 흥미를 보이지않고, %CALLNAME:MASTER%의 뒤를 따라서 걸었다…
ELSEIF TALENT:호기심
	PRINTFORMW 주변의 경치가 흥미진진한 것처럼 보인다…
ELSE
	PRINTFORMW 지금부터 일어날 일을 상상하고있는 것 같다…
ENDIF

RETURN 1

@FLAG_COM171, ARG

@SOURCE_COM171, ARG
VARSET LOCAL

IF GETBIT(TEQUIP:야외플레이, 0)
	LOCAL:20 += 20
	LOCAL:21 += 80
	LOCAL:12 = 2000
	LOCAL:14 = 400
ELSEIF GETBIT(TEQUIP:야외플레이, 1)
	LOCAL:20 += 0
	LOCAL:21 += 30
	LOCAL:12 = 400
ELSEIF GETBIT(TEQUIP:야외플레이, 2)
	LOCAL:20 += 20
	LOCAL:21 += 30
	LOCAL:12 = 400
ELSEIF GETBIT(TEQUIP:야외플레이, 3) || GETBIT(TEQUIP:야외플레이, 4)
	LOCAL:20 += 30
	LOCAL:21 += 20
	LOCAL:12 = 600
ENDIF
LOCAL:14 += 100

;옷の有無で羞恥, 일탈度が上昇. 마을だと物凄い事に. 
IF CHECK_CLO("알몸")
	;종족衣装が알몸でない
	IF CLO_ORI(TARGET, "옷", "슈츠", "겉옷", "下着")
		IF GETBIT(TEQUIP:야외플레이, 0)
			LOCAL:12 += 6000
			LOCAL:14 += 1000
		ELSE
			LOCAL:12 += 2000
			LOCAL:14 += 500
		ENDIF
	ENDIF
ELSEIF CHECK_CLO("下着姿")
	;종족衣装が옷or보디슈츠or겉옷を含む
	IF CLO_ORI(TARGET, "옷", "슈츠", "겉옷")
		IF GETBIT(TEQUIP:야외플레이, 0)
			LOCAL:12 += 4000
			LOCAL:14 += 500
		ELSE
			LOCAL:12 += 1000
			LOCAL:14 += 200
		ENDIF
	ENDIF
ELSEIF CHECK_CLO("노팬티")
	;종족衣装に下着あり
	IF CLO_ORI(TARGET, "下着")
		IF GETBIT(TEQUIP:야외플레이, 0)
			LOCAL:12 += 2000
			LOCAL:14 += 300
		ELSE
			LOCAL:12 += 600
			LOCAL:14 += 100
		ENDIF
	ENDIF
ENDIF

;낮だと恥ずかしい
IF TIME == 0
	TIMES LOCAL:12 , 2.00
	TIMES LOCAL:14 , 2.00
ENDIF

;PALAM:욕정をみる
SELECTCASE PALAM:욕정
CASE IS < PALAMLV:1
	TIMES LOCAL:12 , 0.80
CASE IS < PALAMLV:2
	TIMES LOCAL:12 , 0.90
CASE IS < PALAMLV:3
	TIMES LOCAL:12 , 1.00
CASE IS < PALAMLV:4
	TIMES LOCAL:12 , 1.10
CASE IS >= PALAMLV:4
	TIMES LOCAL:12 , 1.20
ENDSELECT
;ABL:노출벽をみる
SELECTCASE ABL:노출벽
CASE 0
	TIMES LOCAL:14 , 1.20
CASE 1
	LOCAL:7 += 50
	LOCAL:11 += 50
CASE 2
	LOCAL:7 += 150
	LOCAL:11 += 150
	TIMES LOCAL:14 , 0.80
CASE 3
	LOCAL:7 += 400
	LOCAL:11 += 400
	TIMES LOCAL:14 , 0.60
CASE 4
	LOCAL:7 += 750
	LOCAL:11 += 750
	TIMES LOCAL:14 , 0.40
CASEELSE
	LOCAL:7 += 1300
	LOCAL:11 += 1300
	TIMES LOCAL:14 , 0.20
ENDSELECT
;ABL:마조끼をみる
SELECTCASE ABL:마조끼
CASE 0
	TIMES LOCAL:12 , 0.80
CASE 1
	TIMES LOCAL:12 , 1.00
CASE 2
	TIMES LOCAL:12 , 1.30
CASE 3
	TIMES LOCAL:12 , 1.60
CASE 4
	TIMES LOCAL:12 , 2.00
CASEELSE
	TIMES LOCAL:12 , 3.00
ENDSELECT

;눈에띄고싶어함
SIF TALENT:눈에띄고싶어함
	TIMES LOCAL:12 , 1.50
;해방
SIF TALENT:해방
	TIMES LOCAL:12 , 1.50
;겁쟁이
SIF TALENT:겁쟁이
	TIMES LOCAL:14 , 2.00
;부끄럼쟁이
SIF TALENT:부끄럼쟁이
	TIMES LOCAL:14 , 2.00

LOCAL:16 += LOCAL:12 / 4
LOCAL:9 += LOCAL:14 / 2

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@EQUIP_COM54_1
CALL SOURCE_COM171, 100

@EQUIP_COM54_2
;노출경험の値を管理
LOCAL = 1
IF GETBIT(TEQUIP:야외플레이, 0)
	PRINTL ＜야외플레이중(마을)＞
	IF TIME == 0
		LOCAL = 3
	ELSE
		LOCAL = 2
	ENDIF
ELSEIF GETBIT(TEQUIP:야외플레이, 1)
	PRINTL ＜야외플레이중(삼림)＞

	;동물耳なら보너스
	SIF PLAYER == MASTER && TALENT:동물귀
		TFLAG:30 += 1
	;一部종족に보너스
	SIF BASE:종족 == 3
		TFLAG:30 += 1
ELSEIF GETBIT(TEQUIP:야외플레이, 2)
	PRINTL ＜야외플레이중(화원)＞

	;화원보정. 호감도に보너스. 
	SIF PLAYER == MASTER
		TFLAG:30 += 1
ELSEIF GETBIT(TEQUIP:야외플레이, 3) || GETBIT(TEQUIP:야외플레이, 4)
	PRINTL ＜야외플레이중(강가)＞

	;여름なら보너스
	SIF PLAYER == MASTER && FLAG:873 == 1
		TFLAG:30 += 2
	;一部종족に보너스
	SIF TALENT:머메이드
		TFLAG:30 += 1

	;강가보정. 汚れを洗い流せる. 
	;汚れを初期化
	CALL RESET_STAIN_EX, "全部", MASTER
	CALL RESET_STAIN_EX, "全部", TARGET
	;조수がいる場合は, 조수もきれいに
	SIF ASSI
		CALL RESET_STAIN_EX, "全部", ASSI
ENDIF

EXP:노출경험 += LOCAL
PRINTFORML %EXPNAME:34%＋{LOCAL}

SIF PLAYER == MASTER && ABL:노출벽 >= 3
	TFLAG:30 += 1
