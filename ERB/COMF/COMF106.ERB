@COM106
;역바이브
;PLAYER側が바이브で楽しむ커맨드. 通常の바이브커맨드とは違い, 入れっぱなしにはしない. 
PRINT 역바이브
SIF TALENT:PLAYER:요정
	PRINT (요정용)
PRINTL 

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 106

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = 역바이브
;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@TRAIN_MESSAGE_COM106
;바이브선택
CALL SELECT_VIB, PLAYER

SIF REFUSE_CHECK()
	RETURN 0

IF PREVCOM == SELECTCOM && COND("前Ｔ조교자") == PLAYER
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")%, %CALLNAME:PLAYER%의 그곳을 
	IF SIZE("바이브", PLAYER) >= 3
		PRINT 극태 
	ELSEIF SIZE("바이브", PLAYER) == 2
		PRINT 커다란 
	ENDIF
	PRINTW 바이브로 계속 휘젓고있다…
	RETURN 1
ENDIF

PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
IF TEQUIP:바이브 && SIZE("바이브", PLAYER) > SIZE("바이브", TARGET)
	IF RAND:2
		PRINT 자신이 물고 있는 것보다 
	ELSE
		PRINT 자신의 질을 휘젓고 있는 것보다 
	ENDIF
	SIF SIZE("바이브", PLAYER) > SIZE("바이브", TARGET) + 1
		PRINT 훨씬 더 
	SIF SIZE("바이브", PLAYER) >= 3
		PRINT 흉악한 
ELSEIF SIZE("바이브", PLAYER) >= 2 && RAND:2
	IF RAND:2
		PRINT 커다란 귀두가 달린 
	ELSE
		PRINT 묵직한 
	ENDIF
ELSEIF RAND:2
	IF RAND:2
		PRINT 돌기로 뒤덮힌 
	ELSE
		PRINT 로션으로 미끈거리는 
	ENDIF
ELSE
	IF PLAYER == MASTER && ASSI && RAND:2
		PRINTFORM %CALLNAME:ASSI%에게서 건네받은 
	ELSE
		PRINT 준비된 
	ENDIF
ENDIF
IF SIZE("바이브", PLAYER) >= 3
	PRINT 극태 
ELSEIF SIZE("바이브", PLAYER) == 2
	PRINT 커다란 
ENDIF
PRINT 바이브를, 
IF (TALENT:연모 || TALENT:PLAYER:호색) && RAND:2
	PRINT 다리를 벌리고 유혹하고 있는 
ELSE
	PRINT 무방비하게 눈앞에 노출된 
ENDIF
PRINTFORM %CALLNAME:PLAYER%의 
IF COND("음모", PLAYER) && COND("음모", PLAYER) <= 3 && RAND:3 == 0
	PRINT 맨질맨질한 
ELSEIF TALENT:PLAYER:작은몸집 && RAND:3 == 0
	PRINT 어린 
ELSEIF RAND:2
	PRINT 축축한 
ENDIF
PRINT 그곳에, 
IF TALENT:새드 && RAND:3 == 0
	PRINT 비틀면서 
ELSEIF (ABL:PLAYER:Ｖ확장 >= SIZE("바이브", PLAYER) || TALENT:새드) && RAND:2
	IF ABL:PLAYER:Ｖ확장 > SIZE("바이브", PLAYER) && RAND:2
		PRINT 아무런 저항없이 
	ELSEIF RAND:2 == 0
		PRINT 단번에 
	ELSE
		PRINT 깊숙히 
	ENDIF
	SIF ABL:PLAYER:Ｖ확장 > SIZE("바이브", PLAYER) && RAND:2
		PRINT 뿌리끝까지 
ELSEIF RAND:2
	PRINT 고기주름을 문질러 올리면서 
ELSE
	PRINT 천천히 
ENDIF
IF (ABL:PLAYER:Ｖ확장 >= SIZE("바이브", PLAYER) || TALENT:새드) && RAND:3 == 0
	IF RAND:2
		PRINTW 밀어넣었다. 
	ELSE
		PRINTW 찔러넣었다. 
	ENDIF
ELSEIF RAND:2
	PRINTW 파묻었다. 
ELSE
	PRINTW 삽입했다. 
ENDIF

LOCAL = RAND:2

IF LOCAL == 0
ELSEIF ABL:새드끼 >= 3 && RAND:3 == 0
	PRINT 벅벅 굉장한 기세로 
ELSEIF ABL:기교 >= 3 && RAND:3 == 0
	PRINT 긴 스트로크로 
ELSEIF RAND:2
	PRINT 꾸불거리면서 
ELSE
	PRINT 천천히 
ENDIF
IF (ABL:기교 >= 4 || ABL:새드끼 >= 3) && RAND:3 == 0
	IF LOCAL == 0 && RAND:2 == 0
		PRINT 가차없이 
	ELSEIF SIZE("바이브", PLAYER) >= 2
		PRINT 질 안에 
	ENDIF
	PRINT 처넣어진 
ELSEIF RAND:2
	PRINT 넣었다 빼지는 
ELSE
	PRINT 질벽을 휘젓는 
ENDIF
PRINT 바이브
IF LOCAL
	PRINT 에 
ELSEIF SIZE("바이브", PLAYER) >= 2 && RAND:3 == 0
	IF RAND:2 && ABL:PLAYER:Ｖ확장 <= SIZE("바이브", PLAYER)
		PRINT 의 질을 부술 것 같은 두께에 
	ELSE
		PRINT 가 질을 벌리는 듯한 감각에 
	ENDIF
ELSEIF (ABL:PLAYER:Ｖ확장 < SIZE("바이브", PLAYER) && TALENT:새드) || (SIZE("바이브", PLAYER) >= 3 && RAND:3 == 0)
	PRINT 의 흉폭함에 
ELSEIF RAND:3 == 0
	PRINT 의 움직임에 
ELSEIF RAND:2
	PRINT 의 위력에 
ELSE
	PRINT 의 기분좋음에 
ENDIF
PRINTFORM %조사처리(CALLNAME:PLAYER,"가")% 
IF ABL:PLAYER:Ｖ확장 >= SIZE("바이브", PLAYER) && EXP:PLAYER:Ｖ경험 >= EXP_LV(SIZE("바이브", PLAYER)) && RAND:3 == 0
	IF (TALENT:연모 || TALENT:PLAYER:호색) && RAND:2
		PRINT 가랑이를 크게 벌려 만족하는 
	ELSEIF RAND:2
		PRINT 애액을 흘리는 
	ELSE
		PRINT 황홀해 하고 있는 
	ENDIF
ELSEIF ABL:PLAYER:Ｖ확장 < SIZE("바이브", PLAYER) && TALENT:새드
	PRINT 울 것 같이 된 
	LOCAL = 2
ELSEIF RAND:3 == 0
	PRINT 허리를 쑥 내미는 
ELSEIF RAND:2
	PRINT 몸을 튕기는 
ELSE
	PRINT 달콤한 한숨을 흘리는 
ENDIF
PRINTFORM 모습을 
IF RAND:2
	PRINT 곁눈질로 보면서, 
ELSE
	PRINT 바라보면서, 
ENDIF
PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
IF LOCAL == 2
	PRINTW 가학의 즐거움에 빠져있다…
ELSEIF TALENT:현명함 && RANDIF(3, TALENT:소악마 + TALENT:양기, TALENT:심술쟁이)
	PRINTW 교묘하게 말로 괴롭히고있다…
ELSEIF TALENT:심술쟁이 && RANDIF(3, TALENT:소악마 + TALENT:양기)
	IF ABL:PLAYER:Ｖ확장 >= 3
		PRINT 질의 느슨함을 
	ELSE
		PRINT 음란한 모습을 
	ENDIF
	PRINTW 심술궂게 따지고있다…
;양기
ELSEIF TALENT:양기 + TALENT:소악마 && RANDIF(3, TALENT:소악마)
	SIF TALENT:소악마
		PRINT 히죽히죽 
	PRINTW 즐거운듯이 미소짓고있다…
ELSEIF ABL:새드끼 >= 3 && RAND:3 == 0
	IF SIZE("바이브", PLAYER) >= 2 && RAND:2
		PRINTW 대담한 그것으로 계속 괴롭히고있다…
	ELSE
		PRINTW 걱정하는 척하며 말로 괴롭히고있다…
	ENDIF
ELSE
	;IF RAND:2
	;	PRINT 애무も交えながら
	;ELSE
	;	PRINT 動きを変えたりしながら
	;ENDIF
	IF ABL:욕망 >= 3 && SIZE("바이브", PLAYER) >= 2 && RANDIF(2, ABL:새드끼 >= 3, ABL:기교 >= 3, ABL:봉사정신 >= 3)
		PRINTW 배를 문지르며 태내에서 날뛰는 바이브의 감촉을 즐기고있다…
	ELSEIF ABL:봉사정신 >= 3 && RANDIF(2, ABL:새드끼 >= 3, ABL:기교 >= 3)
		PRINTW 더욱 봉사에 몰두했다…
	ELSEIF ABL:기교 >= 3 && RANDIF(2, ABL:새드끼 >= 3)
		PRINTW 더욱 교묘하게 괴롭혔다…
	ELSEIF ABL:새드끼 >= 3 && RAND:2
		PRINTW 더욱 심하게 괴롭혔다…
	ELSE
		PRINTW 봉사를 계속했다…
	ENDIF
ENDIF

@FLAG_COM106, ARG
IF TALENT:PLAYER:처녀
	CALL SETFLAG, "처녀상실플래그", PLAYER
	CALL SETFLAG, "처녀탈취플래그", TARGET
ENDIF

@SOURCE_COM106, ARG
VARSET LOCAL

LOCAL:20 += 100
LOCAL:21 += 100

LOCAL:3 = 1000
LOCAL:11 = CALCF("실행치による욕정추가")
LOCAL:14 = 500
;바이브の大きさなど
IF SIZE("바이브", PLAYER) >= 3
	TIMES LOCAL:11 , 3.00
ELSEIF SIZE("바이브", PLAYER) >= 2
	TIMES LOCAL:11 , 1.50
ENDIF
IF SIZE("바이브", PLAYER) > ABL:PLAYER:Ｖ확장
	TIMES LOCAL:11 , 1.50
ELSEIF SIZE("바이브", PLAYER) < ABL:PLAYER:Ｖ확장
	TIMES LOCAL:11 , 0.50
ENDIF

;ABL:봉사정신をみる
SELECTCASE ABL:봉사정신
CASE 0
	LOCAL:4 = 420
	LOCAL:5 = 150
CASE 1
	LOCAL:4 = 500
	LOCAL:5 = 300
CASE 2
	LOCAL:4 = 580
	LOCAL:5 = 600
CASE 3
	LOCAL:4 = 660
	LOCAL:5 = 900
CASE 4
	LOCAL:4 = 740
	LOCAL:5 = 1500
CASEELSE
	LOCAL:4 = 820
	LOCAL:5 = 2200
ENDSELECT

;ABL:기교をみる
SELECTCASE ABL:기교
CASE 0
	TIMES LOCAL:4 , 0.50
	TIMES LOCAL:5 , 0.50
CASE 1
	TIMES LOCAL:4 , 0.80
	TIMES LOCAL:5 , 0.80
CASE 2
CASE 3
	TIMES LOCAL:4 , 1.20
	TIMES LOCAL:5 , 1.20
CASE 4
	TIMES LOCAL:4 , 1.50
	TIMES LOCAL:5 , 1.50
CASEELSE
	TIMES LOCAL:4 , 2.00
	TIMES LOCAL:5 , 2.00
ENDSELECT

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@COM_ORDER_COM106, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF
;ABL:새드끼
IF ABL:새드끼
	LOCAL += ABL:새드끼 * 5
	LOCALS = %LOCALS% + %ABLNAME:15%LV{ABL:새드끼}({ABL:새드끼 * 5})
ENDIF
;헌신的
IF TALENT:헌신
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF
;새드
IF TALENT:새드
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:83%(5)
ENDIF
;남자
IF TALENT:남자
	LOCAL += 20
	LOCALS = %LOCALS% + %TALENTNAME:122%(20)
ENDIF

;合計を表示(30이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 30

RETURN RESULT
