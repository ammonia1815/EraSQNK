@COM170
;비디오카메라
;부끄럼쟁이や청초, 정조관념持ちだと凄い反発. 

PRINTL 비디오카메라

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

IF TEQUIP:비디오카메라
	STR:0 = 비디오카메라(촬영개시)
ELSE
	STR:0 = 비디오카메라(촬영종료)
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM170
IF TEQUIP:비디오카메라
	;ビデオ촬영がされていれば終了させる
	TEQUIP:비디오카메라 = 0
	PRINTW ★★★비디오 촬영을 종료합니다★★★
	;尺が短いor조수が居ない
	IF GETBITS(CFLAG:69, 2) || ASSI <= 0 || ASSIPLAY

	ELSEIF TEQUIP:비디오카메라 >= 8 && ABL:ASSI:자위중독 >= RAND:6 && RAND:2
		IF TALENT:ASSI:냉정 + TALENT:ASSI:무관심 + TALENT:ASSI:감정결핍 && RAND:2
			PRINTFORMW 촬영을 끝낸 %조사처리(CALLNAME:ASSI,"는")% 냉정를 가장하고 있지만, 열을 가진 스스로의 가랑이를 손대고 싶은 모습으로 내퇴[內腿]에 손를 기게 하고 있다…
		ELSE
			PRINTFORMW 충분히 보게 된 %조사처리(CALLNAME:ASSI,"는")% 인내 할 수 없었던 것일까, 한손을 가랑이에 넣고 있다…
		ENDIF
	;減点요정素無し, かつ尺が超短編ではない
	ELSEIF GETBITS(CFLAG:69, 1, 2) == 0
		IF TALENT:ASSI:젖기쉬움 && ABL:ASSI:욕망 >= RAND:6 && RAND:2
			PRINTFORMW %조사처리(CALLNAME:ASSI,"는")%%TEXTS("조수の명칭")%로서 확실히 일을 해냈지만, 그 다리에는 음액이 주르륵 내려오고 있다…
		ELSE
			PRINTFORMW %조사처리(CALLNAME:ASSI,"는")% 좋은 영상이 찍혔다고 기쁜듯이 하고 있다…
		ENDIF
	ENDIF
ELSE
	;ビデオ촬영してなければ初期化して開始
	;TODO 起動条件を作成する
	;テープは開始時に数を減らす
	;カウントは10回とします
	ITEM:비디오테이프 -= 1
	TEQUIP:비디오카메라 = 1
	PRINTFORML ★★★비디오 촬영을 개시합니다★★★

	IF TALENT:무관심
		PRINT 멍하니 이쪽의 일거수일투족을 보고있는 
	ELSEIF EXP:촬영경험 == 0
		IF TALENT:호기심
			PRINT 처음 본 것에 흥미진진의
		ELSE
			PRINT 이상한 것을 보는 것 같이 촬영기재를 바라보는
		ENDIF
	ELSEIF ABL:노출벽 >= 3 || TALENT:부끄럼없음
		IF TALENT:상사상애 && TALENT:음란 && RAND:2
			PRINT 자신들 사랑을 타인에게 과시해 주려고 분위기를 살리고 있는
		ELSEIF COND("인기：ＡＶ") && RAND:2
			IF TALENT:강기 || TALENT:교만
				PRINT 자신의 인기의 상태를 받아 그렇지만도 않은 모습으로 준비를 하고 있는
			ELSE
				PRINT 작품 만들기에도 익숙해져 렌즈 너머의 손님의 접수를 염려하고 있는
			ENDIF
		ELSE
			PRINT 비디오카메라가 시야에 들어간 순간에 눈을 빛내는
		ENDIF
	ELSEIF TEQUIP:야외플레이
		PRINT 주위를 불안한 듯이 두리번거리고 있는
	ELSEIF TALENT:부끄럼쟁이
		PRINT 얼굴을 새빨갛게 붉히고 이쪽의 상태를 엿보는 
	ELSE
		PRINT 어떤 플레이를 하려는지 의심하는  
	ENDIF
	PRINTFORM %조사처리(CALLNAME:TARGET,"를")% 거들떠도 안보고, 
	IF ASSI <= 0
		PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 비디오카메라를 손높이의 받침대에 둬 조교를 재개했다. 
		PRINTFORMW （%조사처리(TEXTS("조수の명칭"),"가")% 없기 때문에 조금 질이 떨어지지만 어쩔 수 없다…）
	ELSEIF ASSIPLAY
		PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 촬영을 %CALLNAME:MASTER%에 맡겨 조교를 재개했다. 
	ELSE
		LOCALS = 
		IF TEQUIP:야외플레이 || TEQUIP:관내노출플레이
			IF CHARA_NUM("관내노출관객数")
				LOCALS = 갤러리들과 함께 킥킥거리며, 
			ELSEIF ABL:ASSI:새드끼 >= 3
				LOCALS = 갤러리가 없는 것을 아쉬워하면서, 
			ELSE
				LOCALS = 주위에 사람이 없는것을 확인하면서, 
			ENDIF
		ELSEIF (COND("모친") == ASSI || COND("부친") == ASSI || COND("모친", ASSI) == TARGET || COND("부친", ASSI) == TARGET) && RAND:3 == 0
			IF COND("모친") == ASSI || COND("부친") == ASSI
				LOCALS = 자신의 아기 성적 성장기록을 확실히 남긴다고 머릿속에서 생각하면서, 
			ELSE
				LOCALS = 자신의 부모님을 찍는다는 것에 흥분하면서, 
			ENDIF
		ELSEIF (TALENT:ASSI:음란 || TALENT:ASSI:상사상애) && RAND:4 == 0
			IF TALENT:ASSI:음란 && RANDIF(1, TALENT:ASSI:상사상애)
				IF TALENT:ASSI:교만
					LOCALS = 과격한 씬을 보여주라고 지시하며, 
				ELSE
					LOCALS = 앞으로 보여줄 정사에 기대를 하면서, 
				ENDIF
			ELSE
				IF TALENT:ASSI:음란
					LOCALS = 「지금은 %CALLNAME:TARGET%의 차례」 라고 자신에게 말하면서, 
				ELSE
					LOCALS = 외로운 표정을 짓고 있었지만, 마지못해
				ENDIF
			ENDIF
		ELSEIF ABL:ASSI:자위중독 >= 3 && RAND:3 == 0
			LOCALS = 한 손을\@ TALENT:ASSI:남자 ? 가랑이 # 비소 \@에 뻗을것만 같은 걸 참으면서, 

		ELSEIF TALENT:ASSI:새드 && RANDIF(1, TALENT:ASSI:유치, TALENT:ASSI:솔직, TALENT:ASSI:호기심, TALENT:ASSI:고무, TALENT:ASSI:조야, TALENT:ASSI:질투)
			LOCALS = 직접 조교하지 않는 것을 질투하면서도, 제대로 일을 해내려고
		ELSEIF TALENT:ASSI:질투 && RANDIF(1, TALENT:ASSI:유치, TALENT:ASSI:솔직, TALENT:ASSI:호기심, TALENT:ASSI:고무, TALENT:ASSI:조야)
			LOCALS = 행위에 가담하지 않는 것에 질투하며, %마스터를()% 위해서라고 자신에게 말하게 하면서
		ELSEIF TALENT:ASSI:조야 && RANDIF(1, TALENT:ASSI:유치, TALENT:ASSI:솔직, TALENT:ASSI:호기심, TALENT:ASSI:고무)
			LOCALS = 자신에게 맡기라고 가슴을 내밀며, 
		ELSEIF TALENT:ASSI:고무 && RANDIF(1, TALENT:ASSI:유치, TALENT:ASSI:솔직, TALENT:ASSI:호기심)
			LOCALS = %조사처리(CALLNAME:TARGET,"를")% 열심히 응원하면서, 
		ELSEIF TALENT:ASSI:호기심 && RANDIF(1, TALENT:ASSI:유치, TALENT:ASSI:솔직)
			IF RELATION:ASSI:TARGET >= 150
				LOCALS = %CALLNAME:TARGET%의 치태를 빠짐없이 기록하려고 기를 쓰고
			ELSE
				LOCALS = %CALLNAME:MASTER%들의 치태를 빠짐없이 기록하려고 기를 쓰고
			ENDIF
		ELSEIF TALENT:ASSI:솔직 && RANDIF(1, TALENT:ASSI:유치)
			LOCALS = %조사처리(CALLNAME:MASTER,"가")% 말한 대로
		ELSEIF TALENT:ASSI:유치
			LOCALS = %CALLNAME:MASTER%의 말에 씩씩하게 대답하면서, 즐거운듯이 
		ELSEIF ABL:ASSI:자위중독 >= 3 && RAND:3 == 0
			LOCALS = 한 손을\@ TALENT:ASSI:남자 ? 가랑이 # 비소 \@에 뻗을것만 같은 걸 참으면서 
		ENDIF

		IF LOCALS == ""
			PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 비디오카메라를 %CALLNAME:ASSI%에 건네줘 촬영을 시작하게 했다…
		ELSE
			PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 비디오카메라를 %CALLNAME:ASSI%에 건네주었다. 
			PRINTFORMW %조사처리(CALLNAME:ASSI,"는")% %LOCALS%촬영을 개시했다…
		ENDIF
		TRYCALLFORM GUEST_KOJO_K{NO:ASSI}_{CFLAG:ASSI:209}, "조수：촬영개시", ASSI
	ENDIF
ENDIF

@EQUIP_COM53_1
;初回は커맨드自体で呼ばれる
SIF SELECTCOM == 170
	RETURN 0

CALL SOURCE_COM170, 10

RETURN 1

@FLAG_COM170, ARG

@SOURCE_COM170, ARG
SIF TEQUIP:비디오카메라 == 0
	RETURN 0

VARSET LOCAL

LOCAL:20 += 10
SELECTCASE EXP:촬영경험
CASE IS < 50
	LOCAL:21 += 50
CASE IS < 150
	LOCAL:21 += 40
CASE IS < 300
	LOCAL:21 += 30
CASE IS < 600
	LOCAL:21 += 20
CASEELSE
	LOCAL:21 += 10
ENDSELECT

;욕정など
LOCAL:11 = 100
;노출など
LOCAL:12 = 3000
;일탈など
LOCAL:14 = 5000

;알몸
IF CHECK_CLO("알몸")
	;종족衣装が알몸でない
	IF CLO_ORI(TARGET, "옷", "슈츠", "겉옷", "下着")
		LOCAL:12 += 3000
		LOCAL:14 += 1000
	ENDIF
;팬티のみ
ELSEIF CHECK_CLO("下着姿")
	;종족衣装が옷or보디슈츠or겉옷を含む
	IF CLO_ORI(TARGET, "옷", "슈츠", "겉옷")
		LOCAL:12 += 2000
		LOCAL:14 += 500
	ENDIF
;노팬티
ELSEIF CHECK_CLO("노팬티")
	;종족衣装に下着あり
	IF CLO_ORI(TARGET, "下着")
		LOCAL:12 += 1000
		LOCAL:14 += 300
	ENDIF
ENDIF

;구속の状態により노출を追加
IF GETBIT(TEQUIP:구속, 5)
	LOCAL:12 += 1000
ELSEIF GETBIT(TEQUIP:구속, 4)
	LOCAL:12 += 500
ELSEIF GETBIT(TEQUIP:구속, 3)
	LOCAL:12 += 200
ENDIF
;PALAM:욕정をみる
SELECTCASE PALAM:욕정
CASE IS < PALAMLV:1
	TIMES LOCAL:11, 0.80
CASE IS < PALAMLV:2
	TIMES LOCAL:11, 0.90
CASE IS < PALAMLV:3
	TIMES LOCAL:11, 1.00
CASE IS < PALAMLV:4
	TIMES LOCAL:11, 1.10
CASE IS >= PALAMLV:4
	TIMES LOCAL:11, 1.20
ENDSELECT

;ABL:노출벽をみる
SELECTCASE ABL:노출벽
CASE 0
	TIMES LOCAL:11, 0.80
	TIMES LOCAL:14, 1.00
CASE 1
	TIMES LOCAL:11, 1.00
	TIMES LOCAL:14, 0.90
CASE 2
	TIMES LOCAL:11, 1.30
	TIMES LOCAL:14, 0.70
CASE 3
	TIMES LOCAL:11, 1.60
	TIMES LOCAL:14, 0.50
CASE 4
	TIMES LOCAL:11, 2.00
	TIMES LOCAL:14, 0.30
CASEELSE
	TIMES LOCAL:11, 3.00
	TIMES LOCAL:14, 0.10
ENDSELECT

;노출はTRACHECKで보정がかかるので, 일탈に부끄럼쟁이などで보정
IF TALENT:부끄럼쟁이
	TIMES LOCAL:11, 0.20
	TIMES LOCAL:14, 2.00
ENDIF
;청초
IF TALENT:청초
	TIMES LOCAL:11, 0.50
	TIMES LOCAL:14, 1.50
ENDIF

;도착적
SIF TALENT:도착적
	TIMES LOCAL:11, 2.00
;눈에띄고싶어함
IF TALENT:눈에띄고싶어함
	TIMES LOCAL:11, 1.50
	TIMES LOCAL:14, 0.50
ENDIF
;겁쟁이
IF TALENT:겁쟁이
	TIMES LOCAL:14, 1.70
	SIF LOCAL:14 >= 1200
		LOCAL:21 += 70
ENDIF
;습득早い
SIF TALENT:현명함
	TIMES LOCAL:12, 1.50
;습득遅い
SIF TALENT:미련함
	TIMES LOCAL:12, 0.70
;음란
IF TALENT:눈에띄고싶어함
	TIMES LOCAL:11, 1.50
	TIMES LOCAL:14, 0.50
ENDIF

LOCAL:11 += LOCAL:11
LOCAL:9 += LOCAL:14 / 10

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@EQUIP_COM53_1_2
SIF SELECTCOM == 170
	RETURN 0

PRINTFORML ＜비디오촬영중{TEQUIP:비디오카메라}/10＞
EXP:노출경험 += 1
EXP:촬영경험 += 1
PRINTFORML %EXPNAME:34%＋１
PRINTFORML %EXPNAME:59%＋１

;ターン終了時の色々な체크
@EQUIP_COM53_2
;ここでは主にジャンルの체크をする
;1 疲れてるのに촬영継続, 2 短すぎる（１～３コマ）, 3 短い（４～６コマ）
;10 처녀상실, 11 모유, 12 방뇨, 13 배설, 14 多重절정, 15 청소펠라, 16 Ａ청소펠라, 17 구속, 18 동정喪失, 19 동정狩り
;20 자위, 21, 내사정見せつけ, 22 本番, 23 Ａ本番, 24 Ｖ피스트, 25 Ａ피스트, 26 Ｗ피스트, 27 Ｖ용간, 28 Ａ용간, 29 이라마치오
;30 ＢＬ, 31 レズ, 32 후타나리, 33 野外, 34 野外全裸, 35 野外노출, 36 Ｖ확장, 37 Ａ확장, 38 聖水, 39 飲尿, 40 근친
;41 , 42 本番
;50～ コスプレ

;촬영開始時
IF TEQUIP:비디오카메라 == 1
	CFLAG:69 = 0

	IF CHECK_SHIRT("여교사")
		SETBIT CFLAG:69, 50
	ELSEIF CHECK_SHIRT("메이드복")
		SETBIT CFLAG:69, 51
	ELSEIF CHECK_SHIRT("체조복")
		SETBIT CFLAG:69, 52
	ELSEIF CHECK_SHIRT("간호사")
		SETBIT CFLAG:69, 53
	ELSEIF CHECK_SHIRT("세라복")
		SETBIT CFLAG:69, 54
	ELSEIF CHECK_SHIRT("무녀")
		SETBIT CFLAG:69, 55
	ELSEIF CHECK_CLO("수영복차림")
		SETBIT CFLAG:69, 56
	ELSEIF CHECK_BODYS("본디지")
		SETBIT CFLAG:69, 57
	ELSEIF CHECK_BODYS("바니슈트")
		SETBIT CFLAG:69, 58
	ELSEIF CHECK_SHIRT("고스로리드레스")
		SETBIT CFLAG:69, 59
	ELSEIF CHECK_SHIRT("치어리더")
		SETBIT CFLAG:69, 60
	ENDIF
	SIF TALENT:남자 && TALENT:PLAYER:남자
		SETBIT CFLAG:69, 30
	SIF TALENT:여자 && TALENT:PLAYER:여자
		SETBIT CFLAG:69, 31
	SIF TALENT:후타나리 && TALENT:PLAYER:후타나리
		SETBIT CFLAG:69, 32
	SIF TEQUIP:야외플레이
		SETBIT CFLAG:69, 33
	SIF TEQUIP:아이마스크 + TEQUIP:구속 + TEQUIP:재갈
		SETBIT CFLAG:69, 17
ENDIF
SIF SELECTCOM == 170
	RETURN 0

;このターンの플레이を記録
SIF SELECTCOM >= 0
	CSTR:81 = %CSTR:81%%TRAINNAME:SELECTCOM%\/

;기력０なのに촬영したら減点
SIF BASE:기력 <= 0 && GETBIT(CFLAG:69, 0)
	SETBIT CFLAG:69, 1

;처녀상실촬영플래그
SIF NOWEX:처녀상실
	SETBIT CFLAG:69, 10
;분유촬영플래그
SIF NOWEX:분유 || COND("모유마시기플래그", PLAYER)
	SETBIT CFLAG:69, 11
;방뇨촬영플래그
SIF NOWEX:절정실금
	SETBIT CFLAG:69, 12
;배설플래그
SIF NOWEX:배설
	SETBIT CFLAG:69, 13
;四重절정
SIF CHECK_ECST(TARGET) >= 4
	SETBIT CFLAG:69, 14
SIF COND("청소펠라플래그")
	SETBIT CFLAG:69, 15
SIF COND("Ａ청소펠라플래그")
	SETBIT CFLAG:69, 16
SIF TEQUIP:아이마스크 + TEQUIP:구속 + TEQUIP:재갈
	SETBIT CFLAG:69, 17
;동정喪失촬영플래그
SIF COND("동정喪失플래그")
	SETBIT CFLAG:69, 18
;동정狩り촬영플래그
SIF COND("동정喪失플래그", PLAYER)
	SETBIT CFLAG:69, 19

;커맨드依存
SELECTCASE SELECTCOM
;使用前と使用後の比較
CASE 3
	SETBIT CFLAG:69, 20
CASE 13
	SIF PREVEX("Ｖ내사정")
		SETBIT CFLAG:69, 21
CASE 15, 151
	SETBIT CFLAG:69, 41
CASE 31
	SIF SIZE("바이브") >= 3
		SETBIT CFLAG:69, 36
CASE 33
	SIF SIZE("Ａ바이브") >= 3
		SETBIT CFLAG:69, 37
CASE 60 TO 69, 177
	SETBIT CFLAG:69, 22
CASE 70 TO 79
	SETBIT CFLAG:69, 22
	SETBIT CFLAG:69, 23
CASE 83
	SETBIT CFLAG:69, 42
CASE 140
	SETBIT CFLAG:69, 29
CASE 141
	IF CHECK("피스트") && SIZE("피스트") >= 3
		SETBIT CFLAG:69, 24
		SETBIT CFLAG:69, 36
	ENDIF
CASE 142
	IF CHECK("피스트") && SIZE("피스트") >= 3
		SETBIT CFLAG:69, 25
		SETBIT CFLAG:69, 37
	ENDIF
CASE 143
	IF CHECK("피스트") && SIZE("피스트") >= 3
		SETBIT CFLAG:69, 26
		SETBIT CFLAG:69, 36
		SETBIT CFLAG:69, 37
	ENDIF
CASE 304
	SIF TEQUIP:넘어뜨리기 == 0
		SETBIT CFLAG:69, 27
CASE 305
	SIF TEQUIP:넘어뜨리기 == 0
		SETBIT CFLAG:69, 28
ENDSELECT

IF TALENT:남자 && TALENT:PLAYER:남자
ELSEIF TALENT:여자 && TALENT:PLAYER:여자
ELSEIF TALENT:후타나리 && TALENT:PLAYER:후타나리
	CFLAG:69 = CLEARBITS(CFLAG:69, 30, 32)
ENDIF
IF TEQUIP:야외플레이 == 0
	CLEARBIT CFLAG:69, 33
ELSEIF CHECK_CLO("알몸")
	SETBIT CFLAG:69, 34
ELSEIF CHECK_CLO("Ｖ노출") || CHECK_CLO("Ａ노출")
	SETBIT CFLAG:69, 35
ENDIF

;성수플레이
SIF COND("飲尿플래그", PLAYER)
	SETBIT CFLAG:69, 38
;飲尿もの
SIF COND("飲尿플래그", TARGET)
	SETBIT CFLAG:69, 39
;근친もの
SIF COND("근친")
	SETBIT CFLAG:69, 40

;脱いだらコスプレではない! 
IF GETBIT(CFLAG:69, 50) && CHECK_SHIRT("여교사") == 0
	CLEARBIT CFLAG:69, 50
ELSEIF GETBIT(CFLAG:69, 51) && CHECK_SHIRT("메이드복") == 0
	CLEARBIT CFLAG:69, 51
ELSEIF GETBIT(CFLAG:69, 52) && CHECK_SHIRT("체조복") == 0
	CLEARBIT CFLAG:69, 52
ELSEIF GETBIT(CFLAG:69, 53) && CHECK_SHIRT("간호사") == 0
	CLEARBIT CFLAG:69, 53
ELSEIF GETBIT(CFLAG:69, 54) && CHECK_SHIRT("세라복") == 0
	CLEARBIT CFLAG:69, 54
ELSEIF GETBIT(CFLAG:69, 55) && CHECK_SHIRT("무녀") == 0
	CLEARBIT CFLAG:69, 55
ELSEIF GETBIT(CFLAG:69, 56) && CHECK_CLO("수영복차림") == 0
	CLEARBIT CFLAG:69, 56
ELSEIF GETBIT(CFLAG:69, 57) && CHECK_BODYS("본디지") == 0
	CLEARBIT CFLAG:69, 57
ELSEIF GETBIT(CFLAG:69, 58) && CHECK_BODYS("바니슈트") == 0
	CLEARBIT CFLAG:69, 58
ELSEIF GETBIT(CFLAG:69, 59) && CHECK_SHIRT("고스로리드레스") == 0
	CLEARBIT CFLAG:69, 59
ELSEIF GETBIT(CFLAG:69, 60) && CHECK_SHIRT("치어리더") == 0
	CLEARBIT CFLAG:69, 60
ENDIF

;作品の長さ플래그
SELECTCASE TEQUIP:비디오카메라
;超短編
CASE IS <= 4
	SETBIT CFLAG:69, 2
;短編
CASE IS <= 7
	CLEARBIT CFLAG:69, 2
	SETBIT CFLAG:69, 3
;通常
CASEELSE
	CLEARBIT CFLAG:69, 3
ENDSELECT

;テープ残量がなくなると強制的にEQUIP플래그を外す
IF TEQUIP:비디오카메라 >= 10
	TEQUIP:비디오카메라 = 0
	PRINTL ＜테이프의 잔량이 없어졌습니다. 비디오 촬영을 종료합니다＞
	RETURN 1
ELSEIF BASE:기력 <= 0 && GETBIT(CFLAG:69, 0) == 0
	PRINTFORML 
	PRINTFORML %CALLNAME:TARGET%에게서 지루한 분위기가 풍겨나오고 있다. 
	PRINTFORML 이 이상의 촬영은 그만두는 편이 좋을 것같다…
	SETBIT CFLAG:69, 0
ENDIF

TEQUIP:비디오카메라 += 1

RETURN 1
