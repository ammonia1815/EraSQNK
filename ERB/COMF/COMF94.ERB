@COM94
;거품춤
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 94

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;-------------------------------------------------
;사정체크
;-------------------------------------------------
;거품목욕
IF TEQUIP:MASTER:입욕제 == 1
	CALL SAMEN_CHECK, 150
ELSE
	CALL SAMEN_CHECK
ENDIF

RETURN 1

@FLAG_COM94, ARG
;汚れを初期化
CALL RESET_STAIN_EX, "全部", PLAYER
CALL RESET_STAIN_EX, "全部", TARGET

@SOURCE_COM94, ARG
VARSET LOCAL
;거품목욕
IF TEQUIP:MASTER:입욕제 == 1
	LOCAL:20 += 20
	LOCAL:21 += 100
ELSE
	LOCAL:20 += 50
	LOCAL:21 += 180
ENDIF

LOCAL:11 = CALCF("실행치による욕정추가")
LOCAL:13 = 2000
LOCAL:14 = 1500

;ABL:봉사정신をみる
SELECTCASE ABL:봉사정신
CASE 0
	LOCAL:4 = 500
	LOCAL:5 = 300
	LOCAL:8 = 200
CASE 1
	LOCAL:4 = 700
	LOCAL:5 = 500
	LOCAL:8 = 1500
CASE 2
	LOCAL:4 = 900
	LOCAL:5 = 800
	LOCAL:8 = 100
CASE 3
	LOCAL:4 = 1100
	LOCAL:5 = 1200
	LOCAL:8 = 50
CASE 4
	LOCAL:4 = 1300
	LOCAL:5 = 1800
	LOCAL:8 = 20
CASEELSE
	LOCAL:4 = 1500
	LOCAL:5 = 2500
ENDSELECT

;ABL:Ｃ감각をみる
LOCAL:0 = CALCF("Ｃ刺激", 2, 50)
;ABL:Ｂ감각をみる
LOCAL:17 = CALCF("Ｂ刺激", 2, 50)

SIF TALENT:거유
	TIMES LOCAL:17 , 1.20

;ABL:기교をみる
SELECTCASE ABL:기교
CASE 0
	TIMES LOCAL:4 , 0.50
	TIMES LOCAL:5 , 0.50
CASE 1
	TIMES LOCAL:4 , 0.80
	TIMES LOCAL:5 , 0.80
CASE 2
CASE 3
	TIMES LOCAL:4 , 1.20
	TIMES LOCAL:5 , 1.20
CASE 4
	TIMES LOCAL:4 , 1.50
	TIMES LOCAL:5 , 1.50
CASEELSE
	TIMES LOCAL:4 , 2.00
	TIMES LOCAL:5 , 2.00
ENDSELECT

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM94
IF CHECK_COM("Ｗ봉사")
	;二人とも前回と同じ行動
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM == PREVCOM
		PRINTFORM 두사람은 
		IF TALENT:(TARGET:1):음란 + TALENT:(TARGET:1):연모 && TALENT:음란 + TALENT:연모 && RAND:3 == 0
			PRINT 황홀해하면서 
		ELSEIF RAND:2 == 0
			PRINT 정신없이 
		ENDIF
		PRINTFORMW %조사처리(CALLNAME:MASTER,"를")% 거품투성이 가슴으로 계속 씻고있다…
	ELSE
		PRINTFORM %타겟과()%%조사처리(CALLNAME:(TARGET:1),"는")%
		SIF TALENT:(TARGET:1):음란 + TALENT:(TARGET:1):연모 + TALENT:음란 + TALENT:연모 && RAND:2 == 0
			PRINTFORM 상기된 눈동자로 %조사처리(CALLNAME:MASTER,"를")% 바라보면서, 
		IF RAND:2
			PRINTFORM 경쟁하듯이 거품투성이 
		ELSE
			PRINTFORM 협력해서 거품투성이 
		ENDIF
		;それぞれの身体の差も描写すべきかなー
		IF RAND:3 == 0
			IF RAND:3 == 0
				PRINT 젖가슴을 
			ELSEIF RAND:2
				PRINT 유방을 
			ELSE
				PRINT 가슴을 
			ENDIF
		ELSEIF RAND:2
			PRINT 신체를 
		ELSE
			PRINT 가랑이를 
		ENDIF
		PRINTW 갖다대고 문질렀다…
	ENDIF
;同時넘어뜨리기
ELSEIF TEQUIP:Ｗ넘어뜨리기
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	;조수が前回と同じ行動(違う行動の時は, ここでなく, EQUIP_COM32で記述)
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM , %TEXTS("Ｗ넘어뜨리기 중")%
	ENDIF
	IF SELECTCOM == PREVCOM
		PRINTFORMW %CALLNAME:MASTER%에게 거품이 묻은 몸을 밀착해, 열심히 계속 문지르고있다…
	ELSE
		PRINTFORMW %조사처리(CALLNAME:MASTER,"를")% 스폰지같은 가슴에 끼워서 문질러 씻었다…
	ENDIF
;前回と同じ
ELSEIF PREVCOM == SELECTCOM && COND("前Ｔ조교자") == PLAYER
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	IF TEQUIP:키스
		PRINTFORM 일 있을 때 마다%TEXTS("키스継続")%,
		CALL SETFLAG, "추가키스플래그", TARGET
	ENDIF
	IF TALENT:빈유 == 0 && RAND:3 == 0
		PRINTFORMW 가슴을 스폰지로 써서, %CALLNAME:PLAYER%의 팔이나 얼굴을 문질러 씻기고있다…
	ELSEIF RAND:2
		PRINTFORMW 거품투성이인 가랑이에 팔을 끼우거나 하면서, %CALLNAME:PLAYER%의 욕정을 자극하면서 몸을 씻겼다…
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%에게 거품이 뭍은 몸을 문질러 붙여, \@ ABL:봉사정신 >= 3 ? 열심히 # \@ 문지르고있다…
	ENDIF
ELSE
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	IF RAND:2 == 0
		IF RAND:2 == 0
			PRINTFORM %CALLNAME:PLAYER%의 팔에 걸터앉아, 거품이 이는 
		ELSE
			PRINTFORM 매트에 엎드려 누운 %CALLNAME:PLAYER%에게 걸터앉아, 거품이 이는 
		ENDIF
		SELECTCASE COND("음모")
		CASE IS >= 6
			PRINT 음모
		CASE 1 TO 3
			PRINT 털없는 가랑이
		CASEELSE
			PRINT 가랑이
		ENDSELECT
		PRINTW 를 문질러 붙이며 닦았다…
	ELSE
		PRINTFORM 비누에 거품을 이르켜서 자신에게 펴발라, 
		SIF TALENT:연모
			PRINT 황홀한 표정을 지으며 

		IF TALENT:임신 && RAND:2
			PRINTFORM %CALLNAME:PLAYER%에게 임신하여 커져버린 배를 
		ELSEIF TALENT:거유 && RAND:2
			IF TALENT:작은몸집
				PRINTFORM 풍만한 가슴을 
			ELSE
				PRINTFORM 풍만한 신체를 
			ENDIF
		ELSEIF TALENT:작은몸집 && RAND:2
			PRINTFORM 작은 신체를 
		ELSEIF RAND:2
			PRINTFORM 전신을 
		ELSE
			PRINTFORM 몸을 
		ENDIF
		SIF ABL:봉사정신 >= 3
			PRINTFORM 힘껏 
		PRINTW 문질렀다…
	ENDIF
ENDIF

@COM_ORDER_COM94, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF
;ABL:정액중독
IF ABL:정액중독
	LOCAL += ABL:정액중독 * 2
	LOCALS = %LOCALS% + %ABLNAME:11%LV{ABL:정액중독}({ABL:정액중독 * 2})
ENDIF
;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 3
	LOCALS = %LOCALS% - %TALENTNAME:35%(3)
ENDIF
;헌신的
IF TALENT:헌신
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF

;불결있음
IF COM_ORDER_STAIN("Ｐ", "Ｖ")
	LOCAL -= COM_ORDER_STAIN("Ｐ", "Ｖ")
	LOCALS = %LOCALS% - 불결있음
	;악취둔감
	SIF TALENT:악취둔감 || TALENT:악취민감
		LOCALS = %LOCALS%, \@TALENT:악취둔감 ? %TALENTNAME:61% # %TALENTNAME:62%\@
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｐ", "Ｖ")})
ENDIF

;合計を表示(30이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 30

RETURN RESULT
