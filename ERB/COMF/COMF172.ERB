@COM172
;목욕탕플레이
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;風呂場への出入り
;終了時は修正なし
IF TEQUIP:목욕탕플레이
	TEQUIP:샤워 = 0
	TEQUIP:목욕탕플레이 = 0
	IF ASSI
		TEQUIP:ASSI:샤워 = 0
		TEQUIP:ASSI:목욕탕플레이 = 0
	ENDIF
	;입욕제リセット
	TEQUIP:MASTER:입욕제 = 0
	RETURN 1
ELSE
	TEQUIP:목욕탕플레이 = 1
	TEQUIP:목욕후 = 1
	IF ASSI
		TEQUIP:ASSI:목욕탕플레이 = 1
		TEQUIP:ASSI:목욕후 = 1
	ENDIF
	TEQUIP:MASTER:목욕후 = 1
ENDIF

RETURN 1

@FLAG_COM172, ARG

@SOURCE_COM172, ARG
VARSET LOCAL

LOCAL:20 += 0
LOCAL:21 += 30

LOCAL:12 = 200

;PALAM:욕정をみる
SELECTCASE PALAM:욕정
CASE IS < PALAMLV:1
	TIMES LOCAL:12 , 0.80
CASE IS < PALAMLV:2
	TIMES LOCAL:12 , 0.90
CASE IS < PALAMLV:3
	TIMES LOCAL:12 , 1.00
CASE IS < PALAMLV:4
	TIMES LOCAL:12 , 1.10
CASE IS >= PALAMLV:4
	TIMES LOCAL:12 , 1.20
ENDSELECT
;ABL:노출벽をみる
SELECTCASE ABL:노출벽
CASE 0
CASE 1
	LOCAL:7 += 50
CASE 2
	LOCAL:7 += 80
CASE 3
	LOCAL:7 += 100
CASE 4
	LOCAL:7 += 200
CASEELSE
	LOCAL:7 += 300
ENDSELECT

LOCAL:16 += LOCAL:12 / 4

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM172
#DIM MEMO_KISEKAE

IF TEQUIP:목욕탕플레이
	PRINTFORMW 방으로 돌아왔다…
ELSE
	;갈아입히기させた状態を체크
	MEMO_KISEKAE = 0

	IF GETBIT(IS_FAVORITE_DRESS(TARGET), 9)
		PRINTFORML %조사처리(CALLNAME:TARGET,"를")% 목욕탕플레이용 코스튬으로 갈아입게합니까？
		SETCOLOR DEF_COLOR("황색")
		PRINTFORM 목욕탕플레이용：
		CALL PRINT_FAVORITE_DRESS, 9
		PRINTFORML 
		RESETCOLOR
		PRINTFORML 
		PRINTFORML  [ 0] 예
		PRINTFORML  [ 1] 아니오

		CALL INPUT_SELECT, 2

		SELECTCASE RESULT
		CASE 0
			CALL SET_FAVORITE_DRESS, TARGET, 9
			MEMO_KISEKAE = 1
		ENDSELECT
		PRINTL 
	ENDIF

	PRINTFORM %조사처리(CALLNAME:TARGET,"를")% 
	IF MEMO_KISEKAE
		PRINTFORM 전용 복장으로 갈아입히고 
	ELSEIF CHECK_CLO("수영복차림")
		PRINTFORM 수영복차림으로 
		CALL SET_SHIRT, TARGET
		CALL SET_OUTER, TARGET
		CALL SET_SOCKS, TARGET
		CALL SET_GLOVE, TARGET
		CALL SET_SHIRT, TARGET
	ELSEIF CHECK_CLO("全裸") == 0
		PRINTFORM 탈의실에서 옷을 벗겨서 
		CALL SETFLAG, "衣装全脱ぎ", TARGET
	ENDIF
	PRINTFORMW 목욕탕에 데리고 갔다
	IF TEQUIP:아이마스크
		PRINTFORM 눈이 가려진 %조사처리(CALLNAME:TARGET,"는")%, %CALLNAME:PLAYER%
		IF TALENT:겁쟁이 || TALENT:솔직 || TALENT:내성적
			PRINTFORM 손을 잡고 조심조심
		ELSE
			PRINTFORM 팔을 잡고 미끄러지지 않도록 주의하면서
		ENDIF
		PRINTFORMW 목욕탕에 들어왔다…
	ELSEIF TALENT:부끄럼쟁이 && ABL:노출벽 < 3 && CHECK_CLO("알몸")
		PRINTFORMW 목욕탕에 들어온 %조사처리(CALLNAME:TARGET,"는")% 는 얼굴을 붉히며 몸을 손으로 가리고 있다…
	ELSEIF TALENT:헌신 && (TALENT:호색 || TALENT:음란) && ITEM:플레이매트
		PRINTFORMW 목욕탕에 들어온 %조사처리(CALLNAME:TARGET,"는")% 호색한 미소를 지으며, 비누를 손에 취한…
	ELSEIF TALENT:헌신
		PRINTFORMW 목욕탕에 들어온 %조사처리(CALLNAME:TARGET,"는")%, %CALLNAME:MASTER%의 등을 밀려고 물통을 잡았다…
	ELSEIF (TALENT:호색 || TALENT:음란 || TALENT:소악마 || TALENT:심술쟁이) && TALENT:부끄럼쟁이 == 0 && CHECK_CLO("알몸")
		PRINTFORMW 목욕탕에 들어온 %조사처리(CALLNAME:TARGET,"는")% 드러난 알몸을 감추지 않고, %CALLNAME:MASTER%에%TEXTR("심술궂은", "당돌한")%미소를 띄우고 있다…
	ELSEIF TALENT:유치 || TALENT:유아／유아퇴행 || TALENT:하피
		PRINTFORMW 목욕탕에 들어온 %조사처리(CALLNAME:TARGET,"는")%, 바로 욕조에서 목욕을 즐겼다…
	ELSEIF TALENT:부끄럼없음 && CHECK_CLO("알몸")
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 는 드러난 알몸을 숨기려고도 하지 않고 , 상쾌한 얼굴을 하고있다…
	ENDIF
ENDIF


@EQUIP_COM58
#DIM LCOUNT
SIF SELECTCOM == 172
	RETURN 0
;목욕탕플레이
CALL SOURCE_COM172, 30
;입욕제
IF TEQUIP:MASTER:입욕제 >= 1 && TEQUIP:MASTER:입욕제 <= 19
	TFLAG:33 += 1

	;バラの花びら+드라이어드
	IF TEQUIP:MASTER:입욕제 == 4 && BASE:종족사고 == 3
		TFLAG:33 += 2
	;死湖の입욕제+아이
	ELSEIF TEQUIP:MASTER:입욕제 == 5
		;傷に染みる
		SIF TALENT:머메이드 == 0 && PALAM:고통 >= 500
			SOURCE:아픔 += MIN(PALAM:고통/10, 300)
		SIF TALENT:작은몸집 && TALENT:유치 && TALENT:호기심
			TFLAG:33 += 2
	;노송나무목욕+쿠즈노하or쿠노이치
	ELSEIF TEQUIP:MASTER:입욕제 == 6
		TFLAG:33 += 1
		SIF BASE:종족사고 == 16 || BASE:종족사고 == 22
			TFLAG:33 += 2
	;ミステリアス風呂+魔女
	ELSEIF TEQUIP:MASTER:입욕제 == 7
		SIF BASE:종족사고 == 19 || BASE:종족사고 == 47
			TFLAG:33 += 2
	ENDIF
ELSEIF TEQUIP:MASTER:입욕제 == 30
	CALL SETFLAG, "윤활추가", 1000, TARGET
	CALL SETFLAG, "윤활추가", 1000, PLAYER
	SIF TEQUIP:Ｗ넘어뜨리기
		CALL SETFLAG, "潤滑추가", 1000, TARGET:1
ELSEIF TEQUIP:MASTER:입욕제 == 31
	IF TALENT:약독내성 == 0
		SOURCE:욕정추가 += 1000
		SOURCE:일탈 += 500
	ENDIF
ELSEIF TEQUIP:MASTER:입욕제 == 79
	;PLAYERとTARGETは정액まみれ
	FOR LOCAL, 1, CHARANUM
		SIF LOCAL != PLAYER && LOCAL != TARGET
			CONTINUE
		FOR LCOUNT, 0, 6
			SIF LCOUNT == 2 && PENIS(LOCAL) == 0
				CONTINUE
			SIF LCOUNT == 3 && TALENT:LOCAL:남자
				CONTINUE
			;애널の中には流れ込まない
			SIF LCOUNT == 4
				CONTINUE
			SIF LCOUNT == 5 && TALENT:LOCAL:남자
				CONTINUE
			SELECTCASE LCOUNT
			CASE 0
				LOCALS = Ｍ
			CASE 1
				LOCALS = 손
			CASE 2
				LOCALS = Ｐ
			CASE 3
				LOCALS = Ｖ
			CASE 4
				LOCALS = Ａ
			CASE 5
				LOCALS = Ｂ
			ENDSELECT

			CALL ADD_STAIN, LOCAL, LOCALS, "정액"
			STAIN:LOCAL:LOCALS = SET_STAIN(STAIN:LOCAL:LOCALS, "정액")
		NEXT
	NEXT
	LOCAL = TFLAG:16
	TFLAG:16 = 8
	CALL SOURCE_COM147, 50
	TFLAG:16 = LOCAL
ENDIF

RETURN 1

@EQUIP_COM58_2
SIF SELECTCOM == 172
	RETURN 0
VARSET LOCAL
SIF TEQUIP:MASTER:입욕제 >= 1 && TEQUIP:MASTER:입욕제 <= 19
	LOCAL += 1
;목욕탕플레이
SELECTCASE TEQUIP:MASTER:입욕제
CASE 1
	PRINTL ＜거품목욕＞
CASE 2
	PRINTL ＜비탕목욕＞
CASE 3
	PRINTL ＜탄산목욕＞
CASE 4
	PRINTL ＜장미목욕＞
CASE 5
	PRINTL ＜염수목욕＞
CASE 6
	PRINTL ＜노송나무목욕＞
CASE 7
	PRINTL ＜신비목욕＞
CASE 30
	PRINTL ＜로션목욕＞
CASE 31
	PRINTL ＜미약목욕＞
	IF TEQUIP:미약 == 0 && RAND:5 == 0
		CALL ADD_STATE, TARGET, "미약"
		PRINTFORML %조사처리(CALLNAME:TARGET,"는")% 미약이 피부에서 흡수된건지, 요염한 한숨을 토해냈다…
	ENDIF
	IF TEQUIP:Ｗ넘어뜨리기 && TEQUIP:(TARGET:1):미약 == 0 && RAND:5 == 0
		CALL ADD_STATE, TARGET:1, "미약"
		PRINTFORML %조사처리(CALLNAME:(TARGET:1),"는")% 완전히 미약이 돌아 버린 것 같고, 녹은 눈동자로 %조사처리(CALLNAME:MASTER,"를")% 보고 있다…
	ENDIF
	IF STATE("센시브", PLAYER) == 0 && RAND:5 == 0
		PRINTFORML %조사처리(CALLNAME:PLAYER,"는")% 미약이 녹아있는 목욕물때문에, 민감하게 되어버렸다…
		CALL ADD_STATE, PLAYER, "센시브"
	ENDIF
CASE 79
	PRINTL ＜정액목욕＞
	PRINTFORML %EXPNAME:20%＋3
	EXP:정액경험 += 3
	IF TEQUIP:Ｗ넘어뜨리기
		PRINTFORML %EXPNAME:20%＋3（%CALLNAME:(TARGET:1)%）
		EXP:(TARGET:1):정액경험 += 3
	ENDIF
	EXP:PLAYER:정액경험 += 3
	LOCAL += ABL:정액중독 - 1
CASEELSE
	PRINTL ＜목욕탕플레이중＞
ENDSELECT

SIF ABL:순종 >= 1
	LOCAL += 1
SIF PLAYER == MASTER
	TFLAG:30 += LOCAL

RETURN 1
