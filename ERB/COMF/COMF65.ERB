@COM65
;기승위
;넘어뜨리기の時に一定条件を満たすと, 와일드스윙になり, 効果上昇. 
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 65

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM65, ARG
IF INSERT("와일드스윙")
	CALL FLAG_INSERTION, "Ｖ인서트", ARG, 120
ELSE
	CALL FLAG_INSERTION, "Ｖ인서트", ARG, 70
ENDIF


@SOURCE_COM65, ARG
VARSET LOCAL

LOCAL:20 += 100
LOCAL:21 += 50
IF INSERT("와일드스윙")
	LOCAL:20 += 50
	LOCAL:21 += 20
	CALL SOURCE_INSERTION, "Ｖ인서트", ARG, 120
ELSE
	CALL SOURCE_INSERTION, "Ｖ인서트", ARG, 70
ENDIF

IF TALENT:장사
	TIMES LOCAL:20, 0.50
	TIMES LOCAL:21, 0.50
ENDIF

LOCAL:11 = CALCF("실행치による욕정추가")
LOCAL:12 = 900

;根元まで受け入れられる場合にはクリを擦られてしまう
IF ABL:Ｖ확장 > SIZE("페니스", PLAYER) && PENIS(TARGET) == 0
	;ABL:Ｃ감각をみる
	LOCAL:0 = CALCF("Ｃ刺激", 2, 30)
	LOCAL:0 = SOURCE_EXABL(LOCAL:0, 33, PLAYER)
ENDIF

;ABL:봉사정신をみる
SELECTCASE ABL:봉사정신
CASE 0
	LOCAL:4 = 200
	LOCAL:5 = 50
	LOCAL:14 = 300
CASE 1
	LOCAL:4 = 300
	LOCAL:5 = 200
	LOCAL:14 = 100
CASE 2
	LOCAL:4 = 500
	LOCAL:5 = 550
	LOCAL:14 = 30
CASE 3
	LOCAL:4 = 800
	LOCAL:5 = 900
	LOCAL:14 = 0
CASE 4
	LOCAL:4 = 1000
	LOCAL:5 = 1500
	LOCAL:14 = 0
CASEELSE
	LOCAL:4 = 1200
	LOCAL:5 = 2200
	LOCAL:14 = 0
ENDSELECT

;ABL:Ｖ감각をみる
SELECTCASE ABL:Ｖ감각
CASE 0
	LOCAL:3 = 25
	TIMES LOCAL:4 , 0.50
CASE 1
	LOCAL:3 = 75
	TIMES LOCAL:4 , 0.80
CASE 2
	LOCAL:3 = 125
CASE 3
	LOCAL:3 = 200
	TIMES LOCAL:4 , 1.20
CASE 4
	LOCAL:3 = 300
	TIMES LOCAL:4 , 1.50
CASEELSE
	LOCAL:3 = 500
	TIMES LOCAL:4 , 2.00
ENDSELECT

;PALAM:욕정をみる
SELECTCASE PALAM:욕정
CASE IS < PALAMLV:1
	TIMES LOCAL:3 , 0.30
CASE IS < PALAMLV:2
	TIMES LOCAL:3 , 0.60
CASE IS < PALAMLV:3
	TIMES LOCAL:3 , 1.00
CASE IS < PALAMLV:4
	TIMES LOCAL:3 , 1.50
CASE IS >= PALAMLV:4
	TIMES LOCAL:3 , 1.80
ENDSELECT

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM65
;同時넘어뜨리기
IF TEQUIP:Ｗ넘어뜨리기
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	;조수が前回と同じ行動(違う行動の時は, ここでなく, EQUIP_COM32で記述)
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM %TEXTS("Ｗ넘어뜨리기 중")%
	ELSEIF PREVCOM:1 == 65 || PREVCOM:1 == 66 || PREVCOM:1 == 67
		PRINTFORM %조사처리(CALLNAME:(TARGET:1),"가")% 질로부터 해방된
	ELSEIF PREVCOM:1 == 75
		PRINTFORM %CALLNAME:(TARGET:1)%의 애널로부터 해방된지 얼마 안된
	ELSEIF PREVCOM:1 == 83
		PRINTFORM %CALLNAME:(TARGET:1)%의 오줌의 구멍으로부터해방된지 얼마 안된
	ENDIF
	IF INSERT("Ｖ인서트")
		PRINTFORMW %CALLNAME:MASTER%의 페니스를, 요염하게 허리를 흔들면서 계속 맛보고있다…
	ELSEIF INSERT("Ａ인서트")
		PRINTFORMW %CALLNAME:MASTER%의 페니스를 애널에서 뽑아, 이번엔 질에 뿌리까지 파묻었다…
	ELSEIF INSERT("요도인서트")
		PRINTFORMW %CALLNAME:MASTER%의 페니스로부터 뽑아, 이번은 질로 근원까지 삼켜 왔다…
	ELSE
		PRINTFORMW %CALLNAME:MASTER%의 페니스에 걸터앉아서, 천천히 허리를 내렸다…
	ENDIF
	;보지를노출させる
	SIF CHECK_CLO("Ｖ노출") == 0
		CALL SETFLAG, "바텀을노출：텍스트없음", TARGET
;러브트랩
ELSEIF TEQUIP:러브트랩
	;前回と今回の조교자が同じで, 抜かない플래그がある場合
	IF INSERT("인서트") && COND("前Ｔ조교자") == PLAYER
		;체위が기승위
		IF INSERT("기승위")
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 삽입된 채인 페니스를 단단히 조이고, 다시 허리를 움직이기 시작했다…
		ELSE
			PRINTFORM %조사처리(CALLNAME:TARGET,"는")% %조사처리(CALLNAME:PLAYER,"를")% 밀어넘어뜨리고, 신체를 위아래로 
			IF (TALENT:임신 && RAND:4 > 0) || (TALENT:임신 == 0 && RAND:4 == 0)
				PRINT 완만하게 
			ELSE
				PRINT 격하게 
			ENDIF
			PRINTFORMW 흔들고있다…
		ENDIF
	ELSE
		PRINTFORM %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:PLAYER%의 위에 걸터앉아, 
		IF TALENT:임신
			PRINT 커다란 배를 흔들흔들 
		ELSE
			PRINT 신체를 위아래로 격하게 
		ENDIF
		PRINTFORMW 흔들며 쾌감을 탐닉하고있다…
	ENDIF
ELSEIF CHECK_COM("３Ｐ봉사")
	CALL TRAIN_MESSAGE_3P
ELSE
	;前回と今回の조교자が同じで, 抜かない플래그がある場合
	IF INSERT("인서트") && COND("前Ｔ조교자") == PLAYER
		;押し倒されているとき
		IF TEQUIP:넘어뜨리기
			;와일드스윙
			IF INSERT("와일드스윙")
				CALL TEXT, "와일드스윙"
			ELSEIF INSERT("Ｖ인서트")
				IF TEQUIP:키스
					PRINTFORMW %타겟은()% %조사처리(CALLNAME:PLAYER,"를")% 해방하지 않고,%TEXTS("키스継続")%다시 허리를 움직이기 시작했다…
					CALL SETFLAG, "추가키스플래그", TARGET
				ELSE
					PRINTFORMW %타겟은()% %조사처리(CALLNAME:PLAYER,"를")% 해방하지 않고, 질을 힘들게 잡은 채로 다시 허리를 움직이기 시작했다…
					CALL TEXT, "추가地の文：기승위"
				ENDIF
			ELSEIF INSERT("Ａ인서트")
				PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 허리를 띄워 애널에서 %조사처리(NAMES("페니스", PLAYER),"를")% \@ PENIS(PLAYER) ? 풀어주고 # 뽑고 \@, 장액투성이가 된  그것을 이번에는 질로 
				IF TALENT:임신 == 0
					IF RAND:2 && ABL:Ｖ확장 > SIZE("페니스", PLAYER)
						PRINT 뿌리끝까지 
					ELSEIF ABL:Ｖ확장 >= 2
						PRINT 깊숙히 
					ENDIF
				ENDIF
				PRINTFORMW 삼켜버렸다…
			ELSEIF INSERT("요도인서트")
				PRINTFORM %CALLNAME:TARGET%는 허리를 띄우고 나서%NAMES("페니스", PLAYER)%를\@ PENIS(PLAYER) ? 풀어주고 # 뽑고 \@, 젖은 그것을 이번은 질로
				IF TALENT:임신 == 0
					IF RAND:2 && ABL:Ｖ확장 > SIZE("페니스", PLAYER)
						PRINT 뿌리끝까지 
					ELSEIF ABL:Ｖ확장 >= 2
						PRINT 깊숙히 
					ENDIF
				ENDIF
				PRINTFORMW 삼켜버렸다…
			ENDIF
		ELSE
			;前回は애널
			IF INSERT("Ａ인서트")
				PRINTFORM %조사처리(CALLNAME:TARGET,"를")% 안아올리고, 애널에서 뽑아낸 %조사처리(NAMES("페니스", PLAYER),"로")% 
				PRINTFORMW %조사처리(TEXTS("Ｖ삽입前"),"를")% 찔러 올렸다…
			;前回は
			ELSEIF INSERT("요도인서트")
				PRINTFORM %타겟을()% 안아 올려, 오줌의 구멍으로부터 뽑아 낸%NAMES("페니스", PLAYER)%로
				PRINTFORMW %조사처리(TEXTS("Ｖ삽입前"),"를")% 찔러 올렸다…
			;체위が정상위
			ELSEIF INSERT("정상위") || INSERT("굴곡위")
				PRINTFORMW 질내에서 뽑지 않고 %조사처리(CALLNAME:TARGET,"를")% 안아올려, 아래에서 허리를 찔러 올렸다…
			;체위が후배위
			ELSEIF INSERT("후배위")
				PRINTFORMW 질내에서 뽑지 않고 %조사처리(CALLNAME:TARGET,"를")% 안아올려, 아래에서 허리를 찔러 올렸다…
			;체위が대면좌위
			ELSEIF INSERT("대면좌위")
				PRINTFORMW 질내에서 뽑지 않고 %조사처리(CALLNAME:TARGET,"를")% 안아올려, 아래에서 허리를 찔러 올렸다…
			;체위が배면좌위
			ELSEIF INSERT("배면좌위")
				PRINTFORMW 질내에서 뽑지 않고 %조사처리(CALLNAME:TARGET,"를")% 안아올려, 아래에서 허리를 찔러 올렸다…
			;체위が기승위
			ELSEIF INSERT("기승위")
				IF PENIS(PLAYER)
					PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
					IF ABL:노출벽 >= 3 && RAND:2
						PRINT 허리를 들고 육봉이 애액으로 젖어 빛나는 모습을 과시한 후에 다시 질에 넣고, 
					ELSE
						PRINT 삽입된 채인 페니스를 꽉 조여, 
					ENDIF
				ELSE
					PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 삽입된 상태인 남근을 꽉 조이며, 
				ENDIF

				IF TEQUIP:키스
					PRINTFORMW %CALLNAME:PLAYER%%TEXTS("키스継続助詞付き")%허리를 움직이기 시작했다…
					CALL SETFLAG, "추가키스플래그", TARGET
				ELSEIF ABL:Ｖ감각 >= 3 && RAND:3 == 0
					PRINTW 황홀한 얼굴로 하복부를 어루만지며 허리를 움직이기 시작했다…
				ELSEIF RAND:2
					PRINTW 격렬하게 허리를 움직이기 시작했다…
				ELSE
					PRINTW 치골을 문지르면서 허리를 움직이기 시작했다…
				ENDIF
			ENDIF
		ENDIF
	ELSE
		PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
		IF COND("前Ｔから대상のみ변경") && PREVCOM:1 >= 60 && PREVCOM:1 <= 79
			CALL TEXT, "추가地の文：穴から穴へ"
		;穴開きでない팬티をはいていて, ずらされていないなら, 必ずずらす
		ELSEIF CHECK_CLO("Ｖ노출") == 0 && CHECK_CLO("Ｖ노출可")
			CALL SETFLAG, "바텀을노출：텍스트없음", TARGET
		ENDIF
		IF PREVCOM == 1 || PREVCOM == 9 || (PREVCOM >= 90 && PREVCOM <= 119)
			PRINTFORM %CALLNAME:PLAYER%의 위에 걸터앉아, 
		ELSEIF TALENT:호색 && RAND:2 == 0
			PRINTFORM %CALLNAME:PLAYER%의 페니스에 %TEXTR("조준을 하고", "질구를 가져다대고")%, 
		ELSE
			PRINTFORM %CALLNAME:PLAYER%의 페니스의 위치를 확인하고, 
		ENDIF

		;스커트などをはいているなら
		IF EQUIP:스커트 + TEQUIP:새댁플레이 && (TALENT:호색 + TALENT:음란 || ABL:노출벽 >= 3) && RAND:2
			;에이프런
			IF TEQUIP:새댁플레이
				PRINTFORM 에이프런을 
			ELSE
				PRINTFORM %SKIRT(TARGET, 1)%를
			ENDIF
			IF COND("입의 사용") && RANDIF(2, COND("손가락 사용") )
				PRINT 입에 물고 
			ELSEIF COND("손가락 사용") && RAND:2
				PRINT 손가락으로 집고 
			ELSE
				PRINT 넘기고 
			ENDIF
			IF RAND:2
				PRINTFORM %TEXTS("보지：視覚")%
			ELSEIF RAND:2 && PALAM:Ｖ윤 >= 10000 && PALAM:욕정 >= 10000
				PRINTFORM 군침을 흘리는 구멍
			ELSE
				PRINTFORM %TEXTR("부끄러운 곳", "고간")%
			ENDIF
			PRINTFORM 을 %TEXTR("과시하", "보여주")%면서 
		ELSE
			IF TALENT:호색 + TALENT:음란 + TALENT:연모 && COND("視力") && RAND:3 == 0
				PRINT 얼굴을 바라보면서 
			ELSEIF TEQUIP:넘어뜨리기 && ABL:새드끼 >= 2 && RAND:2
				IF TALENT:교만 + TALENT:강기 && RANDIF(2, TALENT:건방짐 + TALENT:소악마)
					PRINT 대담한 미소를 지으면서 
				ELSEIF TALENT:건방짐 + TALENT:소악마 && RAND:2
					PRINT 히죽히죽거리면서 
				ELSE
					PRINT 가학적인 미소를 지으면서 
				ENDIF
			ELSEIF ABL:Ｖ감각 >= 3 && RAND:2
				IF RAND:2
					PRINT 달콤한 한숨을 흘리면서 
				ELSE
					PRINT 기분 좋은듯한 얼굴을 하면서 
				ENDIF
			ELSE
				PRINT 뜨거운 숨을 내쉬면서 
			ENDIF
			IF ABL:Ｖ확장 >= SIZE("페니스", PLAYER) && ABL:새드끼 && RAND:2
				PRINT 단숨에 
			ELSE
				PRINT 느긋하게 
			ENDIF
		ENDIF
		IF RAND:2
			PRINTFORM %조사처리(TEXTS("엉덩이：視覚"),"를")% 내
		ELSE
			PRINTFORM 허리를 내
		ENDIF
		IF ABL:Ｖ확장 >= SIZE("페니스", PLAYER) && PALAM:Ｖ윤 >= 3000 && RAND:3
			IF ABL:Ｖ확장 > SIZE("페니스", PLAYER) && RAND:2
				PRINTW 려, 뿌리까지 페니스를 물었다…
			ELSE
				PRINTW 려, 벌어진 질로 페니스를 물었다…
			ENDIF
		ELSE
			PRINTW 렸다…
		ENDIF
	ENDIF
ENDIF


@COM_ORDER_COM65, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:Ｖ감각
IF ABL:Ｖ감각
	LOCAL += ABL:Ｖ감각 * 2
	LOCALS = %LOCALS% + %ABLNAME:4%LV{ABL:Ｖ감각}({ABL:Ｖ감각 * 2})
ENDIF
;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF
;ABL:정액중독
IF ABL:정액중독
	LOCAL += ABL:정액중독 * 3
	LOCALS = %LOCALS% + %ABLNAME:11%LV{ABL:정액중독}({ABL:정액중독 * 3})
ENDIF
;PALAM:Ｖ윤（の부족）
IF PALAM:Ｖ윤 < PALAMLV:3
	LOCAL -= 5
	LOCALS = %LOCALS% - Ｖ윤부족(5)
ENDIF

;강기
IF TALENT:강기
	LOCAL += 10
	LOCALS = %LOCALS% + %TALENTNAME:18%(10)
ENDIF
;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 10
	LOCALS = %LOCALS% - %TALENTNAME:35%(10)
ENDIF
;Ｖ손상
IF TALENT:Ｖ손상
	LOCAL -= 30 * TALENT:Ｖ손상
	LOCALS = %LOCALS%- %TALENTNAME:44%({30 * TALENT:Ｖ손상})
ENDIF
;헌신的
IF TALENT:헌신
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF

;처녀
IF TALENT:처녀
	LOCAL -= 20
	LOCALS = %LOCALS% - %TALENTNAME:0%(20)
ELSEIF EXP:Ｖ경험 < EXPLV:2
	LOCAL -= 5
	LOCALS = %LOCALS% - %EXPNAME:0%부족(5)
ENDIF

;미약
IF TEQUIP:미약
	LOCAL += 6
	LOCALS = %LOCALS% + %ITEMNAME:31%(6)
ENDIF

;불결있음
IF COM_ORDER_STAIN("Ｐ", "Ｖ")
	LOCAL -= COM_ORDER_STAIN("Ｐ", "Ｖ")
	LOCALS = %LOCALS% - 불결있음
	;악취둔감
	SIF TALENT:악취둔감 || TALENT:악취민감
		LOCALS = %LOCALS%, \@TALENT:악취둔감 ? %TALENTNAME:61% # %TALENTNAME:62%\@
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｐ", "Ｖ")})
ENDIF

;合計を表示(24이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 24

RETURN RESULT
