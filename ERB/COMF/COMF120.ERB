@COM120
;정상위봉사
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 120

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%
;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM120, ARG
CALL FLAG_INSERTION, "Ｖ역인서트", ARG, 100

@SOURCE_COM120, ARG
VARSET LOCAL

LOCAL:20 += 50
LOCAL:21 += 100

LOCAL:3 = 1800
LOCAL:4 = 1400
LOCAL:5 = 800
LOCAL:11 = CALCF("실행치による욕정추가") + 300

;남자か후타나리
IF PENIS(TARGET)
	;ABL:Ｃ감각をみる
	LOCAL:0 = CALCF("Ｃ刺激", 3)
	LOCAL:0 = SOURCE_EXABL(LOCAL:0, 33, PLAYER)
ENDIF

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM120
CALL SETFLAG, "下半身を脱がす", PLAYER
CALL SETFLAG, "바텀을노출：텍스트없음", TARGET
;同時넘어뜨리기
IF TEQUIP:Ｗ넘어뜨리기
	;양구멍삽입
	IF SELECTCOM:1 == 121
		;二人とも前回と同じ行動
		IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM == PREVCOM
			PRINTFORM 두사람은 
			IF TALENT:(TARGET:1):음란 + TALENT:(TARGET:1):연모 && TALENT:음란 + TALENT:연모 && RAND:3 == 0
				PRINT 황홀해하면서 
			ELSEIF RAND:2 == 0
				PRINT 정신없이 
			ELSE
				PRINT 즐거운듯이 
			ENDIF
			PRINTFORMW %CALLNAME:MASTER%의 양 구멍을 계속 범하고있다…! 
		;조수が前回と同じ行動
		ELSEIF SELECTCOM:1 == PREVCOM:1
			PRINTFORM %타겟은()% %CALLNAME:(TARGET:1)%에 애널을 범해지고 있는 %CALLNAME:MASTER%의 다리를 펴넓히고,
			IF TALENT:MASTER:처녀
				PRINT 남자를 모르는 
			ELSE
				PRINT 흠뻑 젖은 
			ENDIF
			IF TALENT:PLAYER:작은몸집
				PRINT 어린 균열을 
			ELSE
				PRINT 질을 
			ENDIF
			;ペニバン
			IF PENIS(TARGET) == 0
				IF ABL:새드끼 >= 5
					PRINTFORM 흉악한 
				ELSEIF ABL:새드끼 >= 3
					IF TALENT:MASTER:후타나리 || TALENT:MASTER:남자
						PRINTFORM %CALLNAME:MASTER%의 물건과 꼭 닮은 
					ELSE
						PRINTFORM 커다란 
					ENDIF
				ENDIF
			ELSE
				IF RAND:3 == 0
					PRINT 우뚝 솟은 
				ELSEIF RAND:2 == 0 && TEQUIP:아이마스크 == 0
					PRINT 딱딱하게 발기한 
				ELSE
					PRINT 맥박치는 
				ENDIF
			ENDIF
			PRINTFORM %조사처리(NAMES("페니스"),"로")%  
			IF TALENT:새드 && RAND:3 == 0
				PRINT 즐거운 듯이 
			ELSEIF ABL:새드끼 && RAND:2 == 0
				PRINT 거칠게 
			ELSEIF ABL:욕망 >= 3 && RAND:2 == 0
				PRINT 깊숙히 
			ENDIF
			PRINTFORMW 범하기시작했다…! 
		;役割交換
		ELSEIF PREVCOM:1 == 120 && SELECTCOM:1 == 121
			PRINTFORMW %타겟과()% %조사처리(CALLNAME:(TARGET:1),"는")% 범하는 구멍을 교환하거나 해, %마스터로()% 즐기고 있다…！
		ELSE
			PRINTFORM %타겟과()% %조사처리(CALLNAME:(TARGET:1),"는")%
			IF RAND:2 == 0
				PRINTFORM 경쟁하듯이 
			ELSE
				PRINTFORM 협력해서 
			ENDIF
			PRINTFORMW %CALLNAME:MASTER%의 양구멍을 범하기시작했다…! 
		ENDIF
	ELSE
		PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
		;조수が前回と同じ行動(違う行動の時は, ここでなく, EQUIP_COM32で記述)
		SIF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
			PRINTFORM , %TEXTS("Ｗ넘어뜨리기 중")%

		IF SELECTCOM == PREVCOM
			PRINTFORMW %조사처리(CALLNAME:MASTER,"를")% 마음대로 범하고있다…
		ELSE
			PRINTFORMW %조사처리(CALLNAME:MASTER,"를")% 범하기시작했다…
		ENDIF
	ENDIF
ELSEIF TEQUIP:넘어뜨리기
	IF PREVCOM == SELECTCOM && COND("前Ｔ조교자") == PLAYER
		PRINTFORM %타겟은()% %CALLNAME:PLAYER%
		IF TEQUIP:키스
			PRINTFORM %TEXTS("키스継続助詞付き")%, 콧김 난폭하게
			CALL SETFLAG, "추가키스플래그", TARGET
		ELSEIF ABL:기교 >= 4 && STATE("미니멈", TARGET) && RAND:2 == 0
			PRINTFORM 의 질 안쪽을 후벼파면서, 즐거운 듯이
		ELSEIF ABL:기교 >= 3 && RAND:2 == 0
			PRINTFORM 의 Ｇ스팟을 중점적으로 비비면서, 격렬하고
		ELSE
			PRINTFORM 의 질을 마음껏
		ENDIF
		PRINTFORMW 계속 범하고있다…
	ELSE
		PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
		SIF TALENT:새드
			PRINTFORM 가학적인 미소를 지으면서, 
		;ペニバン
		IF PENIS(TARGET) == 0
			IF PREVCOM == 121
				PRINTFORM 애널에서 뽑혀 더러워진 
			ELSEIF ABL:새드끼 >= 5
				PRINTFORM 흉악한 
			ELSEIF ABL:새드끼 >= 3
				IF TALENT:MASTER:후타나리 || TALENT:MASTER:남자
					PRINTFORM %CALLNAME:MASTER%의 물건과 꼭 닮은 
				ELSE
					PRINTFORM 커다란 
				ENDIF
			ENDIF
		ELSE
			IF PREVCOM == 121
				PRINTFORM 애널에서 뽑혀 더러워진 
			ELSEIF RAND:3 == 0
				PRINT 우뚝 솟은 
			ELSEIF RAND:2 == 0 && TEQUIP:아이마스크 == 0
				PRINT 딱딱하게 발기한 
			ELSE
				PRINT 맥박치는 
			ENDIF
		ENDIF
		PRINTFORM %조사처리(NAMES("페니스"),"로")%  %CALLNAME:PLAYER%의 
		IF TALENT:PLAYER:처녀
			PRINT 남자를 모르는 
		ELSE
			PRINT 흠뻑 젖은 
		ENDIF
		IF TALENT:PLAYER:작은몸집
			PRINT 어린 균열을 
		ELSE
			PRINT 질을 
		ENDIF
		IF TALENT:새드 && RAND:3 == 0
			PRINT 즐거운듯이 
		ELSEIF ABL:새드끼 && RAND:2 == 0
			PRINT 거칠게 
		ELSEIF ABL:욕망 >= 3 && RAND:2 == 0
			PRINT 깊숙히 
		ENDIF
		IF ABL:새드끼 >= 5 && RAND:2 == 0
			PRINTFORMW 능욕했다…
		ELSE
			PRINTFORMW 범하기 시작했다…
		ENDIF
	ENDIF
;前回と今回の조교자が同じで, 前回の조교が정상위, 역레이프であり, 抜かない플래그がある場合
ELSEIF INSERT("Ｖ정상위", PLAYER) && COND("前Ｔ조교자") == PLAYER
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	;연모で追加
	IF (TALENT:연모 && RAND:3 == 0) || TEQUIP:키스
		PRINTFORM %TEXTS("키스継続")%
		CALL SETFLAG, "추가키스플래그", TARGET
	ENDIF
	;「TARGETが진성の새드か」「새드끼持ちか」で分岐
	IF ABL:기교 >= 4 && EXP:허리사용경험 + ABL:허리놀림*10 >= 50 && RAND:4 == 0
		PRINTFORM 자궁구를 밀어내듯이 
	ELSEIF ABL:기교 >= 3 && EXP:허리사용경험 + ABL:허리놀림*10 >= 30 && RAND:3 == 0
		PRINTFORM Ｇ스팟을 긁어올리듯이 
	ELSEIF RAND:2 == 0
		PRINTFORM 감질나게 하듯이 천천히 
	ELSE
		PRINTFORM 두들기듯이 격렬하게 
	ENDIF
	PRINTFORMW 허리를 움직여서, %조사처리(CALLNAME:PLAYER,"를")% 기분좋게 하고있다…
;同様に前回の조교が후배위か기승위の場合
ELSEIF INSERT("Ｖ인서트", PLAYER) && COND("前Ｔ조교자") == PLAYER
	PRINTFORMW %조사처리(CALLNAME:TARGET,"는")%, 질내에서 뽑지 않고 %조사처리(CALLNAME:PLAYER,"를")% 위로 향하게 해, 다시 허리를 움직이기 시작했다…
ELSEIF INSERT("Ａ인서트", PLAYER) && COND("前Ｔ조교자") == PLAYER
	PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 애널에서 뽑은 물건을 %CALLNAME:PLAYER%의 질에 직접 삽입해, 다시 허리를 움직이기 시작했다…
ELSE
	PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% 
	;腰つきだし
	IF ABL:PLAYER:욕망 + ABL:PLAYER:Ｖ감각 + TALENT:PLAYER:음란 >= 8 && RAND:3 == 0
		SIF TEQUIP:넘어뜨리기
			PRINT 몸을 돌려 
		IF TALENT:PLAYER:음란 && RAND:3 == 0
			PRINTFORM 양다리로 %CALLNAME:TARGET%의 머리를 끌어당기고 
		ELSEIF RAND:2 == 0
			PRINTFORM %조사처리(NAMES("両腕", PLAYER),"로")% 허벅지를 붙잡아 가랑이를 크게열고, 
		ELSE
			PRINTFORM %CALLNAME:TARGET%에게 훤히 보이도록 허리를 내밀어서, 
		ENDIF
	;大開다리
	ELSEIF ABL:PLAYER:욕망 + ABL:PLAYER:Ｖ감각 + TALENT:PLAYER:음란 >= 5 && RAND:2 == 0
		IF RAND:2 == 0
			PRINTFORM %조사처리(CALLNAME:TARGET,"를")% 향해 다리를 벌리고, 
		ELSEIF TEQUIP:목욕탕플레이
			PRINTFORM 욕조 가장자리에 앉아 양다리를 크게 벌리고, 
		ELSEIF TEQUIP:관내노출플레이 || TEQUIP:야외플레이
			PRINTFORM 앉아서 양다리를 크게 벌리고, 
		ELSE
			PRINTFORM 침대 위에서 양다리를 크게 벌리고, 
		ENDIF
	;開다리
	ELSE
		SIF TEQUIP:넘어뜨리기
			PRINT 덮쳐진 자세로 
		IF TALENT:PLAYER:부끄럼없음 && RAND:2 == 0
			PRINTFORM 주저없이 다리를 벌리고, 
		ELSEIF (TALENT:PLAYER:겁쟁이 || TALENT:PLAYER:솔직 || TALENT:PLAYER:부끄럼쟁이) && RAND:2 == 0
			PRINTFORM 흠칫흠칫 다리를 좌우로 벌리고, 
		ELSE
			PRINTFORM 다리를 살짝 벌리고, 
		ENDIF
	ENDIF
	IF TEQUIP:아이마스크 == 0
		;「부끄럼쟁이」で「겁쟁이」でない
		IF TALENT:PLAYER:부끄럼쟁이 && TALENT:PLAYER:겁쟁이 == 0 && RAND:3 == 0
			PRINT 얼굴을 붉히면서 
		;「소악마」で랜덤1/2
		ELSEIF TALENT:PLAYER:소악마 && RAND:2 == 0
			PRINT 히죽히죽 거리면서 
		ELSEIF RAND:2 == 0
			PRINT 미소지으면서 
		;반항적かつ음란
		ELSEIF TALENT:PLAYER:반항적 && TALENT:PLAYER:음란 && RAND:2 == 0
			PRINT 고개를 돌리고 얼굴을 붉히면서 
		;음란or욕정3000이상
		ELSEIF (TALENT:PLAYER:음란 || PALAM:욕정 >= 3000) && RAND:4 > 0
			PRINT 요염하게 미소지으면서 
		ELSEIF TALENT:연모
			PRINTFORM %조사처리(CALLNAME:TARGET,"를")% 눈을 치켜뜨고 보면서
		ENDIF
	ENDIF
	IF RAND:2 == 0
		PRINTFORMW %조사처리(TEXTS("보지：視覚", PLAYER),"를")% 벌리고 졸라댔다! 
	ELSE
		PRINTFORMW %조사처리(CALLNAME:TARGET,"를")% 유혹했다. 
	ENDIF
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 유혹당하는대로, 
	IF TALENT:PLAYER:처녀 == 1
		PRINTFORMW 남자를 받아들인 적 없는 %CALLNAME:PLAYER%의 바기나를 %조사처리(NAMES("페니스"),"로")% 꿰뚫었다…
	ELSE
		PRINTFORM %CALLNAME:PLAYER%의 
		SIF TALENT:연모
			PRINT 얼굴을 바라보면서 
		PRINTFORMW 바기나에 %조사처리(NAMES("페니스"),"를")% 밀어넣고 허리를 움직이기 시작했다…
	ENDIF
ENDIF


@COM_ORDER_COM120, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

IF PENIS(TARGET) == 0
	;ABL:레즈끼
	IF ABL:레즈끼
		LOCAL += ABL:레즈끼 * 5
		LOCALS = %LOCALS% + %ABLNAME:9%LV{ABL:레즈끼}({ABL:레즈끼 * 5})
	ENDIF
ELSE
	LOCAL += 25
	LOCALS = %LOCALS% + 페니스있음(25)
ENDIF
;정조관념
IF TALENT:정조관념
	LOCAL -= 10
	LOCALS = %LOCALS% - 정조관념(10)
ENDIF
;정조관결여
IF TALENT:정조관결여
	LOCAL += 5
	LOCALS = %LOCALS% + 정조관결여(5)
ENDIF
;쾌감에솔직
IF TALENT:쾌감에솔직
	LOCAL += 2
	LOCALS = %LOCALS% + %TALENTNAME:70%(2)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF
;조교자が처녀
IF EXP:PLAYER:Ｖ경험 == 0
	LOCAL -= 15
	LOCALS = %LOCALS% - %조사처리(CALLNAME:PLAYER,"가")% 처녀(15)
ENDIF

;불결있음
IF COM_ORDER_STAIN("Ｖ", "Ｐ")/2
	LOCAL -= COM_ORDER_STAIN("Ｖ", "Ｐ")/2
	LOCALS = %LOCALS% - 불결있음
	;악취둔감
	SIF TALENT:악취둔감 || TALENT:악취민감
		LOCALS = %LOCALS%, \@TALENT:악취둔감 ? %TALENTNAME:61% # %TALENTNAME:62%\@
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｖ", "Ｐ")/2})
ENDIF

;合計を表示(45이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 45

RETURN RESULT
