@COM95
;풋잡
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 95

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = 풋잡
;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM95, ARG

@SOURCE_COM95, ARG
VARSET LOCAL

LOCAL:20 += 40
LOCAL:21 += 130

IF PENIS(PLAYER)
	LOCAL:8 = COM_ORDER_STAIN("Ｐ", "")*10 + 60
ELSE
	LOCAL:8 = COM_ORDER_STAIN("Ｖ", "")*10 + 60
ENDIF
LOCAL:11 = CALCF("실행치による욕정추가")
LOCAL:13 = 400
LOCAL:14 = 400


;ABL:봉사정신をみる
SELECTCASE ABL:봉사정신
CASE 0
	LOCAL:4 = 200
	LOCAL:5 = 100
CASE 1
	LOCAL:4 = 250
	LOCAL:5 = 180
CASE 2
	LOCAL:4 = 300
	LOCAL:5 = 250
CASE 3
	LOCAL:4 = 350
	LOCAL:5 = 350
CASE 4
	LOCAL:4 = 400
	LOCAL:5 = 500
CASEELSE
	LOCAL:4 = 450
	LOCAL:5 = 800
ENDSELECT

;ABL:기교をみる
SELECTCASE ABL:기교
CASE 0
	TIMES LOCAL:4 , 0.50
	TIMES LOCAL:5 , 0.50
CASE 1
	TIMES LOCAL:4 , 0.80
	TIMES LOCAL:5 , 0.80
CASE 2
CASE 3
	TIMES LOCAL:4 , 1.20
	TIMES LOCAL:5 , 1.20
CASE 4
	TIMES LOCAL:4 , 1.50
	TIMES LOCAL:5 , 1.50
CASEELSE
	TIMES LOCAL:4 , 2.00
	TIMES LOCAL:5 , 2.00
ENDSELECT

;새드끼をみる
IF ABL:새드끼
	IF TALENT:새드
		TIMES LOCAL:13 , 0.50
		TIMES LOCAL:14 , 0.20
	ENDIF
	SELECTCASE ABL:새드끼
	CASE 0
	CASE 1
		TIMES LOCAL:14 , 0.80
	CASE 2
		TIMES LOCAL:14 , 0.60
	CASE 3
		TIMES LOCAL:14 , 0.40
	CASE 4
		TIMES LOCAL:14 , 0.20
	CASEELSE
		TIMES LOCAL:14 , 0.05
	ENDSELECT
ENDIF

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM95
;TEQUIP:풋잡のbitで行動管理
;0bit 踏みつけ,1bit 가랑이見せ, 2bit 両발
IF CHECK_COM("Ｗ봉사")
	;二人とも前回と同じ行動
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM == PREVCOM
		PRINTFORM 두사람은 
		IF TALENT:(TARGET:1):음란 + TALENT:(TARGET:1):연모 && TALENT:음란 + TALENT:연모 && RAND:3 == 0
			PRINT 황홀해하면서 
		ELSEIF RAND:2 == 0
			PRINT 정신없이 
		ENDIF
		PRINTFORM %CALLNAME:MASTER%의 
		IF PENIS(PLAYER)
			PRINT 페니스
		ELSE
			PRINTFORM \@ PREVPLAY("Ｖ인서트", MASTER) ? 질척질척하게 풀어진 # \@ 보지
		ENDIF
		PRINTFORMW 를 발로 이리저리 돌리며 갖고 놀고있다…
	ELSE
		PRINTFORM %CALLNAME:TARGET%(와)과%CALLNAME:(TARGET:1)%하
		IF TALENT:(TARGET:1):음란 + TALENT:(TARGET:1):연모 && TALENT:음란 + TALENT:연모
			IF RAND:3 == 0
				PRINTFORM 상기된 눈동자로 %조사처리(CALLNAME:MASTER,"를")% 바라보면서, 
			ELSEIF RAND:2 == 0
				PRINTFORM 가랑이가 훤히 보이는 체위를 하고, 
				SETBIT TEQUIP:풋잡, 1
			ENDIF
		ENDIF
		SIF RAND:2 == 0
			PRINT 협력해서 
		IF PENIS(PLAYER)
			PRINTFORMW 페니스를 발로 훑기 시작했다…
		ELSE
			PRINTFORMW 보지를 발로 주무르기 시작했다…
		ENDIF
	ENDIF
;同時넘어뜨리기
ELSEIF TEQUIP:Ｗ넘어뜨리기
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	;조수が前回と同じ行動(違う行動の時は, ここでなく, EQUIP_COM32で記述)
	SIF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM %TEXTS("Ｗ넘어뜨리기 중")%
	PRINTFORM %CALLNAME:MASTER%의 
	IF PENIS(PLAYER)
		PRINT 페니스를 
	ELSE
		PRINT 보지를 
	ENDIF
	IF ABL:새드끼 >= 3 && ABL:기교 + ABL:발놀림 >= 3 && RAND:2 == 0
		PRINT 교묘하게 
	ELSEIF ABL:새드끼 >= 3
		PRINT 거칠게 
	ELSEIF ABL:기교 + ABL:발놀림 >= 3 && PENIS(PLAYER)
		PRINT 쓱쓱 
	ELSEIF ABL:새드끼 < 3 || ABL:기교 + ABL:발놀림 < 3
		PRINT 서투르게 
	ENDIF
	IF PREVCOM == SELECTCOM && COND("前Ｔ조교자") == PLAYER
		IF PENIS(PLAYER)
			PRINTW 계속 훑고있다…
		ELSE
			PRINTW 계속 주무르고있다…
		ENDIF
	ELSE
		IF PENIS(PLAYER)
			PRINTW 훑었다…
		ELSE
			PRINTW 주물렀다…
		ENDIF
	ENDIF
ELSE
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	;쓰러뜨려짐중
	IF TEQUIP:넘어뜨리기
		SIF PENIS(PLAYER) == 0
			SETBIT TEQUIP:풋잡, 0
		IF TEQUIP:안면기승 && ABL:새드끼 >= 3
			PRINTFORM %CALLNAME:PLAYER%의 얼굴을 가랑이로 눌러 뭉개는 자세로, 
			TEQUIP:풋잡 = 0
		ELSEIF SELECTCOM == PREVCOM
		;새드끼3이상
		ELSEIF ABL:새드끼 >= 3 && RAND:2 && GETBIT(TEQUIP:풋잡, 0) == 0
			PRINTFORM %CALLNAME:PLAYER%의 
			IF TALENT:작은몸집
				PRINT 가슴
			ELSE
				PRINT 얼굴
			ENDIF
			PRINT 에 허리를 내리고 다리를 뻗어, 
		ELSEIF GETBIT(TEQUIP:풋잡, 0)
			IF ABL:새드끼 >= 3 && COND("다리 사용", PLAYER) && RAND:2
				PRINTFORM %CALLNAME:PLAYER%의 발목을 잡고 전기안마 자세를 잡고, 
			ELSE
				PRINTFORM %조사처리(CALLNAME:PLAYER,"를")% 
				SIF ABL:새드끼 >= 3
					PRINT 유쾌하게 
				PRINT 내려다보면서, 
			ENDIF
			;안면기승解除
			TEQUIP:안면기승 = 0
		ENDIF
	;바지
	ELSEIF EQUIP:겉옷 && CHECK_OUTER("바지")
	;노출벽が低め
	ELSEIF ABL:노출벽 - TALENT:부끄럼쟁이 + TALENT:부끄럼없음 + TALENT:호색 + TALENT:음란 + TALENT:소악마 < RAND:4
		PRINT 자연스럽게 가랑이를 
		;에이프런
		IF TEQUIP:새댁플레이
			PRINT 에이프런으로 
		;스커트のある옷
		ELSEIF EQUIP:스커트
			PRINTFORM %조사처리(SKIRT(TARGET, 1),"로")% 
		ELSE
			PRINTFORM %조사처리(NAMES("손"),"로")% 
		ENDIF
		PRINT 숨기면서, 
	;노출벽3이상or새드or음란で바지(これは上のほうで弾き済み)でない
	ELSEIF ABL:봉사정신 + TALENT:헌신*2 >= 4 || ABL:노출벽 + ABL:새드끼 + TALENT:새드*3 >= 3 || TALENT:호색 + TALENT:음란 + TALENT:연모 + TALENT:소악마 || PALAM:욕정 >= 10000
		;스커트などをはいているなら
		IF EQUIP:스커트 + TEQUIP:새댁플레이 && RAND:3
			;에이프런
			IF TEQUIP:59
				PRINT 에이프런을 
			ELSE
				PRINTFORM %조사처리(SKIRT(TARGET, 1),"를")% 
			ENDIF
			IF COND("입의 사용") && RANDIF(2, COND("손가락 사용") )
				PRINT 입에 물고, 
			ELSEIF COND("손가락 사용") && RAND:2
				PRINT 들어올리고, 
			ELSE
				PRINT 넘기고
			ENDIF
		;팬티をはいているなら, ずらすことも
		ELSEIF !CHECK_CLO("Ｖ노출") && CHECK_CLO("Ｖ노출可") && RAND:5 == 0
			CALL SETFLAG, "바텀을１枚ずらす", TARGET
		ENDIF
		IF TEQUIP:바이브 || TEQUIP:애널바이브 || COND("애널플러그")
		    LASTWORD = ""
			SIF TEQUIP:바이브 || TEQUIP:애널바이브
				LASTWORD = 바이브
			SIF TEQUIP:바이브 && COND("애널플러그")
				LASTWORD = %LASTWORD와()% 
			SIF COND("애널플러그")
				LASTWORD = %LASTWORD%플러그
			;음란
			IF TALENT:음란
				LASTWORD = %LASTWORD%를 물고있는 
			ELSE
				LASTWORD = %LASTWORD%가 박힌 
			ENDIF
			LASTWORD = %LASTWORD%가랑이
		ELSEIF TALENT:남자
			LASTWORD = 발기한 페니스
		;팬티をはいていて, ずらしていない
		ELSE
			LASTWORD = %TEXTS("보지：視覚")%
		ENDIF
		PRINTFORM %LASTWORD를()% 과시하면서, 
		SETBIT TEQUIP:풋잡, 1
	ENDIF
	IF PENIS(PLAYER)
		SIF TEQUIP:넘어뜨리기 == 0
			PRINTFORM %CALLNAME:PLAYER%의 
		PRINT 페니스를 
	ELSE
		PRINTFORM \@ PREVPLAY("Ｖ인서트", MASTER) ? 범해진지 얼마 안된 # 무방비한 \@ 보지를 
	ENDIF
	;머메이드なら
	IF TALENT:머메이드
		PRINT 지느러미로 
	ELSEIF TEQUIP:넘어뜨리기 == 0 && ABL:새드끼 >= 3 && RAND:3 == 0
		PRINT 양다리로 
		SETBIT TEQUIP:풋잡, 2
	;신발を履いている
	ELSEIF EQUIP:신발
		PRINTFORM %조사처리(SHOES(TARGET, 1),"를")% 신은 발로 
	;삭스をはいている
	ELSEIF EQUIP:양말
		PRINTFORM %조사처리(SOCKS(TARGET, 1),"를")% 신은 발로
	;동물다리
	ELSEIF TALENT:동물다리 && CHOSECOM(SELECTCOM) == 0
		PRINT 사람과는 다른 발로 
	ELSE
		PRINT 발로 
	ENDIF
	IF GETBIT(TEQUIP:풋잡, 0)
		PRINT 빙글빙글 
	ELSEIF ABL:새드끼 >= 3 && ABL:기교 + ABL:발놀림 >= 3 && RAND:2 == 0
		PRINT 교묘하게 
		SIF ABL:발놀림 >= 3 && RAND:2
			PRINT 움직임에 변화를 주면서 
	ELSEIF ABL:새드끼 >= 3
		PRINT 거칠게 
	ELSEIF ABL:기교 + ABL:발놀림 >= 3 && PENIS(PLAYER)
		PRINT 쓱쓱 
	ELSEIF ABL:새드끼 < 3 || ABL:기교 + ABL:발놀림 < 3
		PRINT 서투르게 
	ENDIF
	IF PREVCOM == SELECTCOM && GETBIT(TEQUIP:풋잡, 0) && COND("前Ｔ조교자") == PLAYER
		PRINT 계속 짓밟고 있다
	ELSEIF PREVCOM == SELECTCOM && COND("前Ｔ조교자") == PLAYER
		IF PENIS(PLAYER)
			PRINT 계속 잡아당기고 있다
		ELSE
			PRINT 계속 만지작거리고 있다
		ENDIF
	ELSEIF GETBIT(TEQUIP:풋잡, 0)
		PRINT 짓밟아 왔다
	ELSE
		IF PENIS(PLAYER)
			PRINT 다뤄 왔다
		ELSE
			PRINT 만지작거려 왔다
		ENDIF
	ENDIF
	;다리は二本ある…あとはわかるな？
	IF CHECK_COM("풋잡：양발풋잡") == 0 && COND("다리에약함") && (ABL:봉사정신 >= 3 || ABL:새드끼 >= 3)
		PRINTW .
		PRINTFORM 
	ENDIF
	PRINTW …
ENDIF


@COM_ORDER_COM95, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:새드끼
IF ABL:새드끼
	LOCAL += ABL:새드끼 * 5
	LOCALS = %LOCALS% + %ABLNAME:15%LV{ABL:새드끼}({ABL:새드끼 * 5})
ENDIF
;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF
;ABL:정액중독
IF ABL:정액중독
	LOCAL += ABL:정액중독 * 3
	LOCALS = %LOCALS% + %ABLNAME:11%LV{ABL:정액중독}({ABL:정액중독 * 3})
ENDIF

;강기
IF TALENT:강기
	LOCAL += 10
	LOCALS = %LOCALS% + %TALENTNAME:18%(10)
ENDIF
;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 5
	LOCALS = %LOCALS% - %TALENTNAME:35%(5)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF
;새드
IF TALENT:새드
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:83%(5)
ENDIF

IF PENIS(PLAYER)
	LOCAL:1 = COM_ORDER_STAIN("Ｐ", "") / 5
ELSE
	LOCAL:1 = COM_ORDER_STAIN("Ｖ", "") / 5
ENDIF
;불결있음
IF LOCAL:1
	LOCAL -= LOCAL:1
	LOCALS = %LOCALS% - 불결있음
	;악취둔감
	SIF TALENT:악취둔감 || TALENT:악취민감
		LOCALS = %LOCALS%, \@TALENT:악취둔감 ? %TALENTNAME:61% # %TALENTNAME:62%\@
	LOCALS = %LOCALS%({LOCAL:1})
ENDIF

;合計を表示(30이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 30

RETURN RESULT
