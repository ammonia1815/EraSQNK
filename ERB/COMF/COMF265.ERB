@COM265
;인센스
;TEQUIP:인센스の値で何を用いているかを管理する
; 0～ 3bit ターン数の管理に用いる
; 4～ 6bit 무드 업(小)～(大)
; 7～ 9bit 체력회복(小)～(大)
;10～12bit 정력회복(小)～(大)

PRINTL 인센스

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

SIF TEQUIP:MASTER:인센스 == 0
	RETURN 0

STR:0 = %TEXTS("인센스", TEQUIP:MASTER:인센스)%
ITEM:(STR:0) -= 1

CALL KOJO_MESSAGE_COM
PRINTL 

PREVCOM = 265

RETURN 0

@TRAIN_MESSAGE_COM265
SIF TEQUIP:MASTER:인센스 == 0
	CALL INCENSE_SELECT

SIF TEQUIP:MASTER:인센스 == 0
	RETURN 0

PRINTFORML %조사처리(CALLNAME:PLAYER,"는")% %TEXTS("인센스", TEQUIP:MASTER:인센스)%의 향을 사용했다! 
PRINTFORMW 주위에 달콤한 향기가 퍼진다! 

@INCENSE_SELECT
PRINTL 어느 인센스를 사용합니까? 
SIF ITEM:재스민
	PRINTL  [ 0] 재스민 무드 업(소)
SIF ITEM:라벤더
	PRINTL  [ 1] 라벤더 무드 업(중)
SIF ITEM:머스크
	PRINTL  [ 2] 머스크 무드 업(대)
SIF ITEM:샌들우드
	PRINTL  [ 3] 샌들우드 무드 업(중)＆체력회복(중)
SIF ITEM:로즈마리
	PRINTL  [ 4] 로즈마리 무드 업(중)＆정력회복(중)
SIF ITEM:프랭킨센스
	PRINTL  [ 9] 프랭킨센스 무드 업(대)＆체력회복(대)＆정력회복(대)
PRINTL [100] 돌아간다

$INPUT_LOOP
INPUT

IF RESULT == 0 && ITEM:재스민
	TEQUIP:MASTER:인센스 = 1p4
ELSEIF RESULT == 1 && ITEM:라벤더
	TEQUIP:MASTER:인센스 = 1p5
ELSEIF RESULT == 2 && ITEM:머스크
	TEQUIP:MASTER:인센스 = 1p6
ELSEIF RESULT == 3 && ITEM:샌들우드
	TEQUIP:MASTER:인센스 = 1p5 + 1p8
ELSEIF RESULT == 4 && ITEM:로즈마리
	TEQUIP:MASTER:인센스 = 1p5 + 1p11
ELSEIF RESULT == 9 && ITEM:프랭킨센스
	TEQUIP:MASTER:인센스 = 1p6 + 1p9 + 1p12
ELSEIF RESULT == 100
	TEQUIP:MASTER:인센스 = 0
ELSE
	GOTO INPUT_LOOP
ENDIF

@INCENSE
SIF TEQUIP:MASTER:인센스 && (SELECTCOM == 171 || SELECTCOM == 172 || SELECTCOM == 265) && ASSI <= 0
	RETURN 0

;인센스による무드 업効果. 他の効果は@INCENSE_2で. 
SELECTCASE GETBITS(TEQUIP:MASTER:인센스, 4, 6)
CASE 1p4
	TFLAG:33 += 2
CASE 1p5
	TFLAG:33 += 5
CASE 1p6
	TFLAG:33 += 10
ENDSELECT


@INCENSE_2
;인센스
IF TEQUIP:MASTER:인센스 && ASSI && (SELECTCOM == 171 || SELECTCOM == 172 || SELECTCOM == 265)
	PRINTL 
	PRINTFORML %CALLNAME:ASSI%에게 향을 가져오도록 부탁했습니다. 
ELSEIF TEQUIP:MASTER:인센스 && (SELECTCOM == 171 || SELECTCOM == 172 || SELECTCOM == 265)
	PRINTL 
	PRINTFORML 장소를 옮기므로 향을 두고 갑니다. 아아, 이런 때 %조사처리(TEXTS("조수の명칭"),"가")% 있으면…
	TEQUIP:MASTER:인센스 = 0
ENDIF

SIF TEQUIP:MASTER:인센스 == 0
	RETURN 0

PRINTL ＜인센스＞
SETCOLOR DEF_COLOR("하트핑크")
;PRINTL 인센스効果! 
PRINTFORM %TEXTS("인센스", TEQUIP:MASTER:인센스)%의 향이 달콤한 무드를 고조시킨다! 

VARSET LOCAL
;체력회복
SELECTCASE GETBITS(TEQUIP:MASTER:인센스, 7, 9)
CASE 1p7
	LOCAL:0 += 50
CASE 1p8
	LOCAL:0 += 100
CASE 1p9
	LOCAL:0 += 200
ENDSELECT
;정력회복
SELECTCASE GETBITS(TEQUIP:MASTER:인센스, 10, 12)
CASE 1p10
	LOCAL:1 += 1
CASE 1p11
	LOCAL:1 += 2
CASE 1p12
	LOCAL:1 += 4
ENDSELECT
;인센스による무드 업効果. ここでは표기だけ
SELECTCASE GETBITS(TEQUIP:MASTER:인센스, 4, 6)
CASE 1p4
	LOCAL:2 += 2
CASE 1p5
	LOCAL:2 += 5
CASE 1p6
	LOCAL:2 += 10
ENDSELECT

SETCOLOR DEF_COLOR("핑크")
LOCALS = 
SIF LOCAL:2
	LOCALS = 무드+{LOCAL:2}
SIF LOCAL:0
	LOCALS = %LOCALS%\@ LOCAL:2 ? ＆ # \@체력+{LOCAL:0}
SIF LOCAL:1
	LOCALS = %LOCALS%\@ LOCAL:0 + LOCAL:2 ? ＆ # \@정력+{LOCAL:1}
SIF LOCALS != ""
	PRINTFORM （%LOCALS%）
PRINTL 

RESETCOLOR

CALL CALC, "체력회복", LOCAL
CALL CALC, "정력회복", LOCAL:1
IF ASSI
	CALL CALC, "체력회복", LOCAL, ASSI
	CALL CALC, "정력회복", LOCAL:1, ASSI
ENDIF
CALL CALC, "체력회복", LOCAL, MASTER
CALL CALC, "정력회복", LOCAL:1, MASTER

;인센스の終了はSOURCE.ERBの最後の方に配置
TEQUIP:MASTER:인센스 += 1
