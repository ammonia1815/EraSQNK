@COM92
;파이즈리
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 92

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;-------------------------------------------------
;사정체크
;-------------------------------------------------
IF CHECK_COM("파이즈리펠라")
	CALL SAMEN_CHECK, 70
	SELECTCOM = 91
	CALL SAMEN_CHECK, 50
	SELECTCOM = 92
ELSE
	CALL SAMEN_CHECK
ENDIF

RETURN 1

@FLAG_COM92, ARG
SIF CHECK_COM("파이즈리펠라") == 92
	CALL FLAG_COM91, 50
;대상のＢ⇔조교자のＰの汚れが移動
CALL EQ_STAIN, TARGET, PLAYER, 5, 2
SIF ASSIPLAY == 0 && EXP:펠라경험 >= EXPLV:3
	TFLAG:30 += 1

@SOURCE_COM92, ARG
VARSET LOCAL

;파이즈리펠라
SIF CHECK_COM("파이즈리펠라") == 92
	CALL SOURCE_COM91, 50

LOCAL:20 += 65
LOCAL:21 += 160

LOCAL:8 = COM_ORDER_STAIN("Ｐ", "Ｂ")*40
LOCAL:11 = CALCF("실행치による욕정추가")
LOCAL:13 = 1800
LOCAL:14 = 300

;거유
SIF TALENT:거유
	LOCAL:14 -= 200
;빈유
SIF TALENT:빈유
	LOCAL:14 += 400

;ABL:봉사정신をみる
SELECTCASE ABL:봉사정신
CASE 0
	LOCAL:4 = 420
	LOCAL:5 = 150
CASE 1
	LOCAL:4 = 500
	LOCAL:5 = 300
CASE 2
	LOCAL:4 = 580
	LOCAL:5 = 600
CASE 3
	LOCAL:4 = 660
	LOCAL:5 = 900
CASE 4
	LOCAL:4 = 740
	LOCAL:5 = 1500
CASEELSE
	LOCAL:4 = 820
	LOCAL:5 = 2200
ENDSELECT

;ABL:Ｂ감각をみる
LOCAL:17 = CALCF("Ｂ刺激", 2)

;ABL:기교をみる
SELECTCASE ABL:기교
CASE 0
	TIMES LOCAL:4 , 0.50
	TIMES LOCAL:5 , 0.50
CASE 1
	TIMES LOCAL:4 , 0.80
	TIMES LOCAL:5 , 0.80
CASE 2
CASE 3
	TIMES LOCAL:4 , 1.20
	TIMES LOCAL:5 , 1.20
CASE 4
	TIMES LOCAL:4 , 1.50
	TIMES LOCAL:5 , 1.50
CASEELSE
	TIMES LOCAL:4 , 2.00
	TIMES LOCAL:5 , 2.00
ENDSELECT

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM92
IF TALENT:거유 && CONFIG("３사이즈") && RAND:3 == 0
	LOCALS = %TOFULL(CONDS("바스트컵") )%컵의 %TALENT_NAME(110, TALENT:거유)%
ELSEIF RAND:2
	LOCALS = 유방
ELSE
	LOCALS = 가슴
ENDIF
IF CHECK_COM("Ｗ봉사")
	;二人とも前回と同じ行動
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM == PREVCOM
		PRINTFORM 두사람은 
		IF TALENT:(TARGET:1):음란 + TALENT:(TARGET:1):연모 && TALENT:음란 + TALENT:연모 && RAND:3 == 0
			PRINT 황홀해하면서 
		ELSEIF RAND:2 == 0
			PRINT 정신없이 
		ENDIF
		PRINTFORMW %CALLNAME:MASTER%의 페니스를 가슴으로 계속 훑었다…
	ELSE
		PRINTFORM %타겟과()%%조사처리(CALLNAME:(TARGET:1),"는")%
		SIF TALENT:(TARGET:1):음란 + TALENT:(TARGET:1):연모 + TALENT:음란 + TALENT:연모 && RAND:2 == 0
			PRINTFORM 상기된 눈동자로 %조사처리(CALLNAME:MASTER,"를")% 바라보면서 , 
		IF RAND:3 == 0
			PRINTFORMW 경쟁하듯이 페니스에 가슴을 문질렀다…
		ELSEIF RAND:2 == 0
			PRINTFORMW 가슴골짜기에 페니스를 끼워서 자극했다…
		ELSE
			PRINTFORMW 협력해서 페니스를 가슴으로 봉사했다…
		ENDIF
	ENDIF
ELSEIF CHECK_COM("３Ｐ봉사")
	CALL TRAIN_MESSAGE_3P
;同時넘어뜨리기
ELSEIF TEQUIP:Ｗ넘어뜨리기
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	;조수が前回と同じ行動(違う行動の時は, ここでなく, EQUIP_COM32で記述)
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM , %TEXTS("Ｗ넘어뜨리기 중")%
	ENDIF
	IF SELECTCOM == PREVCOM
		PRINTFORMW %CALLNAME:MASTER%의 페니스를 가슴에 끼우고 열심히 계속 문지르고있다…
	ELSE
		PRINTFORMW %CALLNAME:MASTER%의 페니스를 가슴에 끼우고 문질렀다…
	ENDIF
;파이즈리펠라
ELSEIF CHECK_COM("파이즈리펠라")
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	IF TALENT:연모 && RAND:4 == 0
		PRINTFORM , 황홀해하면서 %CALLNAME:PLAYER%의 
	ELSEIF ABL:봉사정신 >= 3 && RAND:3 == 0
		PRINTFORM %CALLNAME:PLAYER%의 얼굴을 바라보면서 
	ELSEIF ABL:기교 >= 3 && RAND:2 == 0
		PRINTFORM 완급을 조절하면서 %CALLNAME:PLAYER%의 
	ELSE
		PRINTFORM %CALLNAME:PLAYER%의 
	ENDIF
	IF RAND:4 == 0
		PRINTFORM 귀두를 혀로 문질러올리면서, 
	ELSEIF RAND:3 == 0
		PRINTFORM 요도를 혀끝으로 자극하면서,  
	ELSEIF RAND:2
		PRINTFORM 페니스에 혀를 문대면서, 
	ELSE
		PRINTFORM 페니스를 빨면서, 
	ENDIF
	;임신
	IF TALENT:임신 && TALENT:빈유 == 0 && RAND:3 == 0
		PRINT 탱탱하게 발기된 
	;거유
	ELSEIF TALENT:거유
		;폭유
		IF TALENT:거유 >= 2 && RAND:3 == 0
			PRINT 묵직한 중량감이 있는 
		ELSEIF RAND:2
			PRINT 탄력 넘치는 
		ELSE
			PRINT 풍만한 
		ENDIF
	;빈유
	ELSEIF TALENT:빈유
		PRINT 작은
		LOCALS = ふくらみ
	ELSE
		IF RAND:3 == 0
			PRINT 상당한 탄력의 
		ELSEIF RAND:2
			PRINT 찰진 
		ELSE
			PRINT 모양좋은 
		ENDIF
	ENDIF

	PRINTFORM %조사처리(LOCALS,"로")% 페니스를 
	IF TALENT:거유 >= 2 && RAND:2
		PRINTFORM 감싸
	ELSEIF TALENT:빈유 == 0
		PRINTFORM 끼우
	ENDIF
	PRINTFORMW 고 문지르기시작했다…
;前回と同じ
ELSEIF PREVCOM == SELECTCOM && COND("前Ｔ조교자") == PLAYER
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")%, 
	IF TALENT:연모 && RAND:4 == 0
		PRINTFORM 황홀해하면서 %CALLNAME:PLAYER%의 페니스
	ELSEIF ABL:봉사정신 >= 3 && RAND:3 == 0
		PRINTFORM %CALLNAME:PLAYER%의 얼굴을 바라보면서 페니스
	ELSEIF ABL:기교 >= 3 && RAND:2
		PRINTFORM 완급을 조절하면서 %CALLNAME:PLAYER%의 페니스
	ELSEIF ABL:정액중독 >= 3 && RAND:2
		PRINTFORM 열심히 %CALLNAME:PLAYER%의 페니스
	ELSE
		PRINTFORM %CALLNAME:PLAYER%의 페니스
	ENDIF
	PRINTFORMW 를 %조사처리(LOCALS,"로")%  훑었다…
;前回は가슴계곡을범한다
ELSEIF PREVCOM == 81 && COND("前Ｔ조교자") == PLAYER
	PRINTFORM %조사처리(CALLNAME:PLAYER,"가")% 허리를 움직이는 것을 멈추고 가슴으로 봉사해줄 것을 요구하자, %조사처리(CALLNAME:TARGET,"는")% 
	;연모かつ봉사정신5or헌신的かつ봉사정신3이상
	IF ( (TALENT:연모 && ABL:봉사정신 == 5) || (TALENT:헌신 && ABL:봉사정신 >= 3) ) && RAND:3 == 0
		IF RAND:2 == 0
			PRINT 기쁜듯이 
		ELSE
			PRINT 눈을 치켜뜨고 바라보면서 
		ENDIF
	;연모or헌신的or봉사정신3이상
	ELSEIF TALENT:연모 || TALENT:헌신 || ABL:봉사정신 >= 3
		PRINT 즐거운 듯한 표정을 지으면서 
	;강기
	ELSEIF TALENT:강기 && RAND:3
		PRINTFORM 강한 눈빛으로 %조사처리(CALLNAME:PLAYER,"를")% 바라보면서, 
	ENDIF
	IF TALENT:빈유
		PRINTFORM 빈약한 가슴으로 열심히 페니스를 
	ELSEIF TALENT:거유
		PRINTFORM 풍만한 가슴으로 페니스를 감싸서 
	ELSE
		PRINTFORM %LOCALS%에 페니스를 끼워서
	ENDIF
	PRINTW 문지르기 시작했다…

;냄새민감分岐
ELSEIF PLAYER == MASTER && TALENT:냄새민감 && TALENT:연모 && (CHOSECOM(SELECTCOM) == 0 || RAND:3 == 0)
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	IF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｐ치구") && RANDIF(2, CHECK_STAIN(STAIN:PLAYER:Ｐ, "정액") )
		IF RAND:2
			PRINTFORMW %CALLNAME:PLAYER%의 페니스를 가슴으로 끼우고, 치구의 %조사처리(TEXTS("匂い"),"로")% 흥분하면서 가슴으로 다뤘다…
		ELSE
			PRINTFORMW %조사처리(CALLNAME:PLAYER,"가")% 열기를 띈 사타구니의 %조사처리(TEXTS("匂い"),"를")% 맡으면서,페니스를 유방으로 사이에 두고 비비기 시작했다…
		ENDIF
	ELSEIF CHECK_STAIN(STAIN:PLAYER:Ｐ, "정액") && RAND:2
		IF RAND:2
			PRINTFORMW 정액으로 더러워진 %CALLNAME:PLAYER%의 페니스를 가슴사이에 두고, %조사처리(TEXTS("匂い"),"를")% 맡으면서 흥분한 모습으로 페니스를 가슴으로 다뤘다…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%의 정액의 %조사처리(TEXTS("匂い"),"이")% 나는 페니스를 가슴사이에 두고, 기쁜 듯한 표정으로 위아래로 가슴을 흔들었다…
		ENDIF
	ELSEIF RAND:2
		PRINTFORMW %조사처리(TEXTS("匂い"),"를")% 발하는 페니스를 가슴에 끼워 비비면서, 열기를 띈 %CALLNAME:PLAYER%의 사타구니의 %조사처리(TEXTS("匂い"),"를")% 맡아 흥분하고 있다…
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%의 페니스를 유방으로 잡아당기면서, 때때로 끝에 코를 접근하고 %조사처리(TEXTS("匂い"),"를")% 맡고 있다…
	ENDIF
;押し倒されているとき
ELSEIF TEQUIP:넘어뜨리기
	IF TEQUIP:안면기승
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:PLAYER%의 얼굴에 걸터앉으면서, 앞가슴을 가랑이에 갖다 대었다…
	ELSE
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:PLAYER%의 하반신을 억누르고, 앞가슴을 가랑이에 갖다 대었다…
	ENDIF
	;음란or새드or새드끼Lv.5이상で「부끄럼쟁이」を持たない
	SIF (TALENT:음란 || TALENT:소악마 || ABL:새드끼 >= 5) && TALENT:부끄럼쟁이 == 0
		PRINT 침을 흘려 로션대신으로 하면서, 
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	;가슴元が노출しているなら, そこではさむ
	IF CHECK_CLO("가슴골노출") && RAND:3
		IF TEQUIP:식스나인
			PRINTFORM %TEXTS("６９")%
		ELSEIF TALENT:빈유
		ELSEIF TALENT:거유 && RAND:2 == 0
			PRINT 가슴계곡이 엿보이는 
		ELSE
			PRINT 대담하게 노출된 
		ENDIF
	;はだけられるのに, 가슴元をはだけていないなら, ずらす
	ELSEIF CHECK_CLO("はだけ可")
		IF CHECK_SHIRT("はだけ可")
			PRINTFORM %조사처리(SHIRT(TARGET, 1),"를")% 걷고, 
		ELSE
			PRINT 앞가슴을 풀어헤치고, 
		ENDIF
		CALL SETFLAG, "가슴을はだける", TARGET
	ENDIF
	IF TALENT:빈유
		PRINT 빈약한 가슴에 페니스를 
	ELSEIF GETBITS(CFLAG:468, 0, 1) && RAND:2 == 0
		PRINT 이전과는 다른 큰 가슴에 페니스를 끼워 
	ELSEIF TALENT:거유 && RAND:2
		;슬라임분신中
		IF NO:TARGET == 29
			PRINT 네개의 풍만한 가슴에 페니스를 파묻어 
		ELSE
			PRINT 풍만한 가슴으로 페니스를 감싸 
		ENDIF
	ELSE
		PRINTFORM %LOCALS%에 페니스를 끼워 
	ENDIF
	PRINTW 문질렀다…
;お風呂(가슴は当然剥き出しと想定)
ELSEIF TEQUIP:목욕탕플레이
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	IF TALENT:빈유
		PRINTW 거품을 묻힌 얇은 가슴으로, 열심히 페니스를 문질러서 씻었다…
	ELSEIF TALENT:거유
		PRINTW 계곡에 거품을 듬뿍 채운 풍만한 가슴으로, 페니스를 뿌리까지 감싸서 문질러 씻었다…
	ELSE
		PRINTW 거품이 묻은 가슴을 스폰지처럼 써서, 페니스를 끼워서 닦았다…
	ENDIF
;それ以外
ELSE
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	;가슴元が노출しているなら, そこではさむ
	IF CHECK_CLO("가슴골노출")
		IF TEQUIP:식스나인
			PRINTFORM %TEXTS("６９")%
		ELSEIF TALENT:빈유
		ELSEIF TALENT:거유 && RAND:2 == 0
			PRINT 가슴계곡이 엿보이는 
		ELSE
			PRINT 대담하게 노출된 
		ENDIF
	;はだけられるのに, 가슴元をはだけていないなら, ずらす
	ELSEIF CHECK_CLO("はだけ可")
		IF CHECK_SHIRT("はだけ可")
			PRINTFORM %조사처리(SHIRT(TARGET, 1),"를")% 걷고, 
		ELSE
			PRINT 앞가슴을 풀어헤치고, 
		ENDIF
		CALL SETFLAG, "가슴을はだける", TARGET
	ENDIF
	IF TALENT:빈유
		PRINTFORM 얇은 가슴으로 열심히 페니스를 
	ELSEIF TALENT:거유
		PRINTFORM 풍만한 가슴으로 페니스를 감싸 
	ELSE
		PRINTFORM %LOCALS%에 페니스를 끼워 
	ENDIF
	PRINTW 문질렀다…
ENDIF
;새드끼LV3이상で요도刺激, 볼개그사용중は不可
SIF ABL:새드끼 >= 3 && TEQUIP:재갈 == 0 && RAND:2 == 0
	PRINTFORMW 동시에, 혀끝을 날카롭게 세우고 요도를 자극했다…


@COM_ORDER_COM92, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF
;ABL:정액중독
IF ABL:정액중독
	LOCAL += ABL:정액중독 * 3
	LOCALS = %LOCALS% + %ABLNAME:11%LV{ABL:정액중독}({ABL:정액중독 * 3})
ENDIF
;ABL:Ｂ감각
IF ABL:Ｂ감각
	LOCAL += ABL:Ｂ감각 * 3
	LOCALS = %LOCALS% + %ABLNAME:14%LV{ABL:Ｂ감각}({ABL:Ｂ감각 * 3})
ENDIF
;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 3
	LOCALS = %LOCALS% - %TALENTNAME:35%(3)
ENDIF
;헌신的
IF TALENT:헌신
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF
;거유
IF TALENT:거유
	LOCAL += TALENT:거유 * 5
	IF TALENT:거유 >= 2
		LOCALS = %LOCALS% + 폭유(10)
	ELSE
		LOCALS = %LOCALS% + %TALENTNAME:110%(5)
	ENDIF
ENDIF

;불결있음
IF COM_ORDER_STAIN("Ｐ", "Ｂ")
	LOCAL -= COM_ORDER_STAIN("Ｐ", "Ｂ")
	LOCALS = %LOCALS% - 불결있음
	;악취둔감
	SIF TALENT:악취둔감 || TALENT:악취민감
		LOCALS = %LOCALS%, \@TALENT:악취둔감 ? %TALENTNAME:61% # %TALENTNAME:62%\@
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｐ", "Ｂ")})
ENDIF

;合計を表示(30이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 30

RETURN RESULT
