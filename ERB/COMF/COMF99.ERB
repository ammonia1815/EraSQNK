@COM99
;식스나인
CALL PRINT_TRAIN_NAME(SELECTCOM)

IF TEQUIP:식스나인
	PRINTL 
	PRINTFORMW \@ TEQUIP:넘어뜨리기 ? %CALLNAME:TARGET% # %CALLNAME:PLAYER% \@は식스나인の体勢をやめた…
	STR:0 = %STR:2%
	TEQUIP:식스나인 = 0
	TEQUIP:PLAYER:식스나인 = 0
	PREVCOM = 99
	RETURN 0
ENDIF
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 99

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
;서로청소플레이
IF CHECK_COM("청소펠라", 91) + CHECK_COM("청소컨닐링구스", 98) && CHECK_COM("청소펠라", 4) + CHECK_COM("청소컨닐링구스", 1)
	IF CHECK_COM("청소펠라", 91)
		TEQUIP:식스나인 = 91
	ELSE
		TEQUIP:식스나인 = 98
	ENDIF
	IF CHECK_COM("청소펠라", 4)
		TEQUIP:PLAYER:식스나인 = 4
	ELSE
		TEQUIP:PLAYER:식스나인 = 1
	ENDIF
ELSE
	;페니스あるならそっち優先
	IF PENIS(PLAYER)
		TEQUIP:식스나인 = 91
	ELSE
		TEQUIP:식스나인 = 98
	ENDIF
	IF PENIS(TARGET)
		TEQUIP:PLAYER:식스나인 = 4
	ELSE
		TEQUIP:PLAYER:식스나인 = 1
	ENDIF
ENDIF

CALL TRAIN_MESSAGE_B

IF REFUSE_CHECK()
	TEQUIP:식스나인 = 0
	TEQUIP:PLAYER:식스나인 = 0
	RETURN 0
ENDIF

STR:0 = %STR:2%

;안면기승ＯＮ
SIF TEQUIP:안면기승 + TEQUIP:넘어뜨리기 && TEQUIP:PLAYER:식스나인 == 1
	TEQUIP:안면기승 = 1
;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM99, ARG
TRYCALLFORM FLAG_COM{TEQUIP:PLAYER:식스나인}, 70
TRYCALLFORM FLAG_COM{TEQUIP:식스나인}, 70

@SOURCE_COM99, ARG
VARSET LOCAL

LOCAL:20 += 10
LOCAL:21 += 50

TRYCALLFORM SOURCE_COM{TEQUIP:PLAYER:식스나인}, 70
TRYCALLFORM SOURCE_COM{TEQUIP:식스나인}, 70

LOCAL:11 = CALCF("실행치による욕정추가") + 500
LOCAL:12 = 1400
LOCAL:13 = 1300
LOCAL:14 = 800

;ABL:봉사정신をみる
SELECTCASE ABL:봉사정신
CASE 0
	LOCAL:4 = 20
	LOCAL:5 = 150
CASE 1
	LOCAL:4 = 100
	LOCAL:5 = 300
CASE 2
	LOCAL:4 = 250
	LOCAL:5 = 600
CASE 3
	LOCAL:4 = 500
	LOCAL:5 = 900
CASE 4
	LOCAL:4 = 800
	LOCAL:5 = 1500
CASEELSE
	LOCAL:4 = 1260
	LOCAL:5 = 2200
ENDSELECT

;ABL:기교をみる
SELECTCASE ABL:기교
CASE 0
	TIMES LOCAL:4 , 0.50
	TIMES LOCAL:5 , 0.50
CASE 1
	TIMES LOCAL:4 , 0.80
	TIMES LOCAL:5 , 0.80
CASE 2
CASE 3
	TIMES LOCAL:4 , 1.20
	TIMES LOCAL:5 , 1.20
CASE 4
	TIMES LOCAL:4 , 1.50
	TIMES LOCAL:5 , 1.50
CASEELSE
	TIMES LOCAL:4 , 2.00
	TIMES LOCAL:5 , 2.00
ENDSELECT

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM99
;서로청소플레이
IF CHECK_COM("청소펠라", 91) + CHECK_COM("청소컨닐링구스", 98) && CHECK_COM("청소펠라", 4) + CHECK_COM("청소컨닐링구스", 1)
	PRINTFORMW %조사처리(CALLNAME:PLAYER,"과")% %조사처리(CALLNAME:TARGET,"는")% 서로의 성기에 묻은 더러운 것들을 입으로 깨끗하게 해주었다…
;넘어뜨리기
ELSEIF TEQUIP:넘어뜨리기
	IF PREVCOM == SELECTCOM && COND("前Ｔ조교자") == PLAYER
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 자기가 주도하는 식스나인을 만끽하고있다…
	ELSEIF PREVCOM == 91 && COND("前Ｔ조교자") == PLAYER
		PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 페니스를 문 채로 %CALLNAME:PLAYER%의 얼굴에 걸터앉아, 
		IF TEQUIP:PLAYER:식스나인 == 4
			PRINTFORMW 눈 앞의 물건을 빨라고 재촉했다…
		ELSE
			PRINTFORMW 얼굴에 보지를 밀어붙였다…
		ENDIF
	ELSE
		IF TEQUIP:안면기승 && COND("前Ｔ조교자") == PLAYER
			PRINTFORM %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:PLAYER%의 얼굴에 올라탄채로, 앞으로 몸을 기울이고 
		ELSE
			PRINTFORM %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:PLAYER%\@ COND("다리 사용") ? 에 올라타,  # 의 \@
			IF TEQUIP:PLAYER:식스나인 == 4
				PRINTFORM 입을 범하면서
				SIF TEQUIP:식스나인 == 91
					PRINTFORM \@ COND("다리 사용") ?  # ,  \@ 자신도 
			ELSE
				PRINTFORM 얼굴에 가랑이를 문질러붙이면서 
				SIF TEQUIP:식스나인 == 98
					PRINTFORM \@ COND("다리 사용") ?  # ,  \@자신도 
			ENDIF
		ENDIF
		IF TEQUIP:식스나인 == 91
			PRINTFORMW 페니스를 빨기 시작했다…
		ELSE
			PRINTFORMW 보지에 혀를 가져다댔다…
		ENDIF
	ENDIF
ELSE
	IF PREVCOM == SELECTCOM && COND("前Ｔ조교자") == PLAYER
		PRINTFORMW %조사처리(CALLNAME:TARGET,"과")% %조사처리(CALLNAME:PLAYER,"는")% 서로의 %TEXTR("성기를 계속 할고있다", "가랑이에 얼굴을 묻고있다, 계속 서로의 성기를 핥아주고있다")%…
	ELSE
		IF COND("다리 사용") == 0
			PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% %조사처리(CALLNAME:TARGET,"과")% 식스나인의 자세를 취하고, 
		ELSE
			PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% %CALLNAME:TARGET%에게 얼굴을 깔려서, 
		ENDIF
		IF PENIS(PLAYER) == PENIS(TARGET)
			PRINTFORM 서로의 \@ PENIS(PLAYER) ? 페니스를 빨 # 보지를 핥 \@았다…
		ELSE
			IF TEQUIP:식스나인 == 91
				PRINTFORM 우뚝 솟은 페니스를 빨게 하면서, 
			ELSE
				PRINTFORM 보지를 핥게 하면서, 
			ENDIF
			IF TEQUIP:PLAYER:식스나인 == 4
				PRINTFORMW 눈앞에 흔들리고 있는 육봉을 맛보았다…
			ELSE
				PRINTFORMW 엣치한 %(TEXTS("匂い"),"를")% 내뿜고 있는 저기에 혀를 기게 했다…
			ENDIF
		ENDIF
	ENDIF
ENDIF

@SET_PLAY69_TURNEND
;도구커맨드等は, 連続するのが自然な他の커맨드にする
SELECTCASE TEQUIP:PLAYER:식스나인
CASE 31
	TEQUIP:PLAYER:식스나인 = 8
CASE 33
	TEQUIP:PLAYER:식스나인 = 2
CASE 34
	TEQUIP:PLAYER:식스나인 = 7
CASE 39
	TEQUIP:PLAYER:식스나인 = 1
CASE 141
	TEQUIP:PLAYER:식스나인 = 8
CASE 142
	TEQUIP:PLAYER:식스나인 = 2
CASE 143
	IF RAND:2
		TEQUIP:PLAYER:식스나인 = 2
	ELSE
		TEQUIP:PLAYER:식스나인 = 8
	ENDIF
ENDSELECT

@SET_PLAY69
;커맨드連続선택による派生
CALL COMABLE_69
;前回と今回の조교자が違うorＷ넘어뜨리기 중or식스나인不可
IF RESULT == 0 || COND("前Ｔ조교자") != PLAYER || TEQUIP:Ｗ넘어뜨리기
	TEQUIP:식스나인 = 0
	TEQUIP:PLAYER:식스나인 = 0
	RETURN 0
ELSE
	;褒めた時など, 体勢が変わらない커맨드
	SELECTCASE SELECTCOM
	;넘어뜨리기状態の미약は기승위に移行するので解除
	CASE 261
		IF TEQUIP:넘어뜨리기
			TEQUIP:식스나인 = 0
			TEQUIP:PLAYER:식스나인 = 0
		ENDIF
	CASE 13, 14, 24, 146, 176, 230 TO 301
		RETURN 0
	ENDSELECT
ENDIF

SELECTCASE CHECK_EQUIP_69(SELECTCOM)
CASE 1
	;前回の조교が펠라치오, 컨닐링구스強制か식스나인だと식스나인に
	IF PREVCOM == 91 || PREVCOM == 98 || PREVCOM == 150 || TEQUIP:식스나인
		SIF TEQUIP:식스나인 == 0
			TEQUIP:식스나인 = PREVCOM
		TEQUIP:PLAYER:식스나인 = SELECTCOM
	ENDIF
CASE 2
	;前回の조교が컨닐링구스, 펠라한다か식스나인だと식스나인に
	IF PREVCOM == 1 || PREVCOM == 4 || TEQUIP:안면기승 || TEQUIP:식스나인
		;69に移行した時（TEQUIP:PLAYER:식스나인は0も意味を持つので, TEQUIP:식스나인で判定. ）
		IF TEQUIP:식스나인 == 0
			IF TEQUIP:안면기승
				TEQUIP:PLAYER:식스나인 = TEQUIP:안면기승
			ELSE
				TEQUIP:PLAYER:식스나인 = PREVCOM
			ENDIF
		ENDIF
		TEQUIP:식스나인 = SELECTCOM
	ENDIF
CASE 3
	;식스나인中だと継続
	SIF TEQUIP:식스나인
		TEQUIP:PLAYER:식스나인 = SELECTCOM
CASE 4
	;식스나인中だと継続
	SIF TEQUIP:식스나인
		TEQUIP:식스나인 = SELECTCOM
CASEELSE
	IF SELECTCOM != 99
		TEQUIP:식스나인 = 0
		TEQUIP:PLAYER:식스나인 = 0
	ENDIF
ENDSELECT

@CHECK_EQUIP_69(ARG)
#FUNCTION
SELECTCASE ARG
;PLAYERの行動　식스나인に移行する가랑이弄り커맨드
CASE 1, 4
	RETURNF 1
;TARGETの行動　식스나인に移行する봉사커맨드
CASE 91, 98, 150
	RETURNF 2
;PLAYERの行動　식스나인体勢で가능な애무系の가랑이弄り커맨드
CASE 0 TO 2, 4, 7 TO 10, 15
	RETURNF 3
;PLAYERの行動　도구커맨드や스팽킹, 피스트퍽
CASE 30 TO 34, 39, 130, 141 TO 143
	RETURNF 3
;TARGETの行動　식스나인体勢で가능な봉사커맨드
CASE 90 TO 92, 97, 98, 105
	RETURNF 4
;식스나인も가능な커맨드(TEQUIP:식스나인は変更させない)
CASE 13, 14, 24, 146, 176, 230 TO 301
	RETURNF 6
CASE 99
	RETURNF 7
ENDSELECT
RETURNF 0


@COMABLE_69
;식스나인実行判定
SIF TEQUIP:식스나인
	RETURN 1
;구속（발or体勢変更不可）だとダメ
SIF GETBITS(TEQUIP:구속, 21, 22)
	RETURN 0
;요정だとダメ
SIF TALENT:요정
	RETURN 0
;목욕탕플레이중は플레이매트が無いとダメ
IF TEQUIP:목욕탕플레이
SIF ITEM:플레이매트 == 0 && NOITEM == 0
	RETURN 0
ENDIF
;実行条件判定
CALL COM_ORDER_COM99, 1
RETURN RESULT


@EQUIP_COM69
;アクティブでない方の動きによるSOURCE等の変化を記述
SIF SELECTCOM == 99
	RETURN 0

SELECTCASE SELECTCOM
;PLAYERの行動　컨닐링구스or펠라한다
CASE 0 TO 2, 4, 7 TO 10, 15, 30 TO 34, 39, 130, 141 TO 143, 150
	CALLFORM SOURCE_COM{TEQUIP:식스나인}, 50
	CALLFORM FLAG_COM{TEQUIP:식스나인}, 50
;TARGETの行動　펠라치오or파이즈리or컨닐링구스시킨다
CASE 13, 14, 90 TO 92, 97, 98, 105, 150
	CALLFORM SOURCE_COM{TEQUIP:PLAYER:식스나인}, 50
	CALLFORM FLAG_COM{TEQUIP:PLAYER:식스나인}, 50
ENDSELECT

@EQUIP_COM69_2
;アクティブでない方の動きを描写する
SIF SELECTCOM == 99
	RETURN 0
PRINTL ＜식스나인안＞

LOCAL:100 = SELECTCOM

SELECTCASE SELECTCOM
;PLAYERの行動　컨닐링구스or펠라한다
CASE 0 TO 2, 4, 7 TO 10, 15, 30 TO 34, 39, 130, 141 TO 143, 150
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 

	SELECTCASE SELECTCOM
	CASE 0
		LOCALS = 비부
	CASE 2, 9, 33
		LOCALS = 애널
	CASE 4, 10
		LOCALS = 페니스
	CASE 8
		LOCALS = 민감한 장소
	CASE 15
		LOCALS = オシッコの穴
	CASE 30, 32, 34
		LOCALS = 클리토리스
	CASE 31, 39
		LOCALS = 질
	CASEELSE
		LOCALS = 그곳
	ENDSELECT

	LOCAL = 50
	IF TEQUIP:넘어뜨리기
		IF SELECTCOM == 1
			IF CHECK_CLO("Ｖ노출")
				PRINT 얼굴에 그곳을 문지르면서, 
			ELSE
				PRINT 얼굴에 소중한 부분을 억누르면서, 
			ENDIF
		ELSE
			PRINT 쾌락을 탐하면서, 
		ENDIF
	ELSE
		IF SELECTCOM == 130
			LASTWORD = 계속되는 손바닥때리기
		ELSEIF SELECTCOM >= 141 && SELECTCOM <= 143
			LASTWORD = 파고드는 손바닥
		ELSEIF SELECTCOM == 31 || SELECTCOM == 33
			LASTWORD = %조사처리(LOCALS,"를")% 휘젓는 바이브
		ELSE
			LASTWORD = %조사처리(LOCALS,"를")% 엄습하는 쾌감
		ENDIF
		;快感の量で反撃力変動
		IF NOWEX:절정수합계 && RAND:(1 + NOWEX:절정수합계)
			PRINTFORML %LASTWORD를()% 견디면서, 마음대로 반격도 하지 못하고있다…
			LOCAL = 10
		ELSEIF SOURCE:아픔 > MAX(PALAM_LV(ABL:마조끼), 500) && RAND:3
			PRINTFORML %LASTWORD가()% 불러오는 
			SIF SOURCE:아픔 >= 3000
				PRINT 심한 
			PRINTL 고통에 마음대로 반격도 하지 못하고있다…
			LOCAL = 10
		ELSEIF MAX(CUP:쾌Ｃ, CUP:쾌Ｖ, CUP:쾌Ａ, CUP:쾌Ｂ, CUP:쾌Ｍ) >= 4000 && RAND:3
			PRINTFORM %LASTWORD%에 몸부림치면서, 
			LOCAL = 30
		ELSEIF (MAX(CUP:쾌Ｃ, CUP:쾌Ｖ, CUP:쾌Ａ, CUP:쾌Ｂ, CUP:쾌Ｍ) >= 1000 && RAND:3) || SOURCE:아픔 >= 500
			PRINTFORM %LASTWORD를()% 견디면서, 
			LOCAL = 50
		ELSE
			PRINTFORM %LASTWORD%에도 아랑곳하지 않고, 
			LOCAL = 70
		ENDIF
	ENDIF

	IF LOCAL == 10

	ELSE
		SELECTCASE TEQUIP:식스나인
		CASE 90
			PRINTFORM %CALLNAME:PLAYER%의 페니스를
			IF ABL:기교 >= 3 && RAND:3 == 0
				PRINTFORML 완급을 붙이면서 계속 잡아당기고 있다…
			ELSEIF ABL:새드끼 && RAND:3 == 0
				PRINTFORML 슥싹슥싹 격렬하게 다루고 있다…
			ELSEIF ABL:욕망 + ABL:봉사정신 >= 3 && RAND:2 == 0
				PRINTFORML 열중해 계속 잡아당기고 있다…
			ELSE
				PRINTFORML 계속 잡아당기고 있다…
			ENDIF
		CASE 91, 150
			PRINTFORM %CALLNAME:PLAYER%의 페니스를
			IF TEQUIP:딥스로트
				;기교４かつ봉사정신３
				IF ABL:기교 >= 4 && ABL:봉사정신 >= 3 && RAND:3 == 0
					PRINTFORML 촉촉히 목구멍을 조이면서 훑어 내고 있다…
				;봉사정신3이상か욕정4이상
				ELSEIF (ABL:봉사정신 >= 3 || PALAM:욕정 >= PALAMLV:4) && RAND:2 == 0
					PRINTFORML 열중해 맛보고 있다…
				ELSEIF  RAND:2 == 0
					PRINTFORML 계속 격렬하게 빨고 있다…
				ELSE
					PRINTFORML 들이마시듯 목구멍으로 빨아먹고 있다…
				ENDIF
			ELSEIF TALENT:요정 || ABL:순종 <= 1 || ABL:봉사정신 <= 1
				;작은몸집で追加
				SIF TALENT:작은몸집
					PRINT 작은
				PRINTFORML 혀끝에서 핥아대고 있다…
			ELSEIF (ABL:순종 >= 4 || ABL:봉사정신 >= 3 || ABL:마조끼 >= 3) && ABL:기교 >= 3 && EXP:펠라경험 >= 100 && RAND:3 == 0
				;기교４かつ봉사정신３
				IF ABL:기교 >= 4 && ABL:봉사정신 >= 3 && RAND:3 == 0
					PRINTFORML 촉촉히 목구멍을 조이면서 훑어 내고 있다…
				;봉사정신3이상か욕정4이상
				ELSEIF (ABL:봉사정신 >= 3 || PALAM:욕정 >= PALAMLV:4) && RAND:2 == 0
					PRINTFORML 열중해 맛보고 있다…
				ELSEIF  RAND:2 == 0
					PRINTFORML 계속 격렬하게 빨고 있다…
				ELSE
					PRINTFORML 들이마시듯 목구멍으로 빨아먹고 있다…
				ENDIF
			ELSE
				;봉사정신3이상か욕정4이상
				IF  (ABL:봉사정신 >= 3  || PALAM:욕정 >= PALAMLV:4) && RAND:3 >= 1
					PRINTFORML 계속 격렬하게 빨고 있다…
				ELSEIF  RAND:2 == 0
					PRINTFORML 들이마시듯 빨고 있다…
				ELSE
					PRINTFORML 핥아대고 있다…
				ENDIF
			ENDIF
		CASE 92
			PRINTFORM %CALLNAME:PLAYER%의 페니스를
			IF ABL:기교 >= 3 && RAND:3 == 0
				PRINTFORM 완급을 붙이면서
			ELSEIF ABL:새드끼 && RAND:3 == 0
				PRINTFORM 물컹물컹 격렬하게
			ELSEIF ABL:욕망 + ABL:봉사정신 >= 3 && RAND:2 == 0
				PRINTFORM 열중해
			ENDIF
			IF TALENT:빈유
				PRINT 얇은
			ELSEIF TALENT:거유
				PRINT 풍만한
			ENDIF
			PRINTL 가슴으로 쓸어올리고 있다…
		CASE 97
			PRINTFORM %CALLNAME:PLAYER%의 페니스를
			IF CHECK_CLO("濡れ") && RAND:2 == 0
				PRINT 습기찬
			ELSEIF CHECK_CLO("蒸れ")
				PRINT 열띈
			ENDIF
			PRINTFORM %CLO("가랑이")%
			IF PREVCOM == SELECTCOM && COND("前Ｔ조교자") == PLAYER
				PRINTL 로 쓱쓱 다루고 있다…
			ELSEIF RAND:2 == 0 && (TALENT:부끄럼쟁이 == 0 || TALENT:음란 || TALENT:연모)
				PRINTL 로 보지로 접하고 있던 부분에서 비비고 있다…
			ELSE
				PRINTL 로 싸 다루고 있다…
			ENDIF
		CASE 98
			PRINTFORML %CALLNAME:PLAYER%의 보지를 정신없이 맛보고 있다…
		CASE 105
			PRINTFORM %CALLNAME:PLAYER%의 페니스를 오나홀로
			IF ABL:새드끼 >= 3 && RAND:3 == 0
				PRINTFORM 아플 정도에 힘을 집중해
			ELSEIF TALENT:호기심 && RAND:3 == 0
				PRINTFORM 흥미로운 것 같게
			ELSEIF MARK:굴복각인 == 0 && RAND:3 == 0
				PRINTFORM 복잡한 얼굴를 하면서
			ELSEIF RAND:3 == 0
				PRINTFORM 격렬하게
			ELSEIF RAND:2 == 0
				PRINTFORM 질척질척
			ELSE
				PRINTFORM 천천히
			ENDIF
			PRINTFORML 다루고 있다…
		ENDSELECT
	ENDIF

	SELECTCOM = TEQUIP:식스나인
	CALL GET_EXP_COM

	;사정게이지の増加
	CALL SAMEN_CHECK, LOCAL
;TARGETの行動　펠라치오or파이즈리or컨닐링구스시킨다
CASE 13, 14, 90 TO 92, 97, 98, 105, 150
	SELECTCOM = TEQUIP:PLAYER:식스나인
	CALL GET_EXP_COM
ENDSELECT
SELECTCOM = LOCAL:100


@COM_ORDER_COM99, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF
;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 10
	LOCALS = %LOCALS% - %TALENTNAME:35%(10)
ENDIF
;헌신的
IF TALENT:헌신
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;쾌감에솔직
IF TALENT:쾌감에솔직
	LOCAL += 3
	LOCALS = %LOCALS% + %TALENTNAME:70%(3)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF

;미약
IF TEQUIP:미약
	LOCAL += 6
	LOCALS = %LOCALS% + %ITEMNAME:31%(6)
ENDIF

;서로청소플레이
IF CHECK_COM("청소펠라", 91) + CHECK_COM("청소컨닐링구스", 98) && CHECK_COM("청소펠라", 4) + CHECK_COM("청소컨닐링구스", 1)
	IF CHECK_COM("청소펠라", 91)
		LOCAL:1 = COM_ORDER_STAIN("Ｐ", "Ｍ")
	ELSE
		LOCAL:1 = COM_ORDER_STAIN("Ｖ", "Ｍ")
	ENDIF
ELSEIF PENIS(PLAYER)
	LOCAL:1 = COM_ORDER_STAIN("Ｐ", "Ｍ")
ELSE
	LOCAL:1 = COM_ORDER_STAIN("Ｖ", "Ｍ")
ENDIF

;불결있음
IF LOCAL:1
	LOCAL -= LOCAL:1
	LOCALS = %LOCALS% - 불결있음
	;악취둔감
	SIF TALENT:악취둔감 || TALENT:악취민감
		LOCALS = %LOCALS%, \@TALENT:악취둔감 ? %TALENTNAME:61% # %TALENTNAME:62%\@
	LOCALS = %LOCALS%({LOCAL:1})
ENDIF

;合計を表示(33이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 33

RETURN RESULT
