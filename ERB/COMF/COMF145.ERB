@COM145
;제모
;노출벽や마조度が高い場合, かなり興奮する

PRINTL 제모

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 145

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
SIF TEQUIP:구속
	TCVAR:28 = MAX(TCVAR:28 - 30, 0)

CALL TRAIN_MESSAGE_B

;どのように剃ったか
SELECTCASE TFLAG:16
CASE 1
	BASE:음모 = 300
CASE 2
	BASE:음모 = 200
CASE 3
	BASE:음모 = 500
ENDSELECT

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = 제모

RETURN 1


@FLAG_COM145, ARG
CALL SETFLAG, "제모플래그", TARGET
;대상のＶ⇔조교자の손가락の汚れが移動
CALL EQ_STAIN, TARGET, PLAYER, 3, 1
;보지를노출させる
CALL SETFLAG, "바텀을노출：텍스트없음", TARGET
;날짜を5にする. 上手だと날짜が0になります. ツルツルに剃った場合も. 
SIF ABL:PLAYER:기교 < 4 && COND("음모") > 3
	BASE:음모 += 5

@SOURCE_COM145, ARG
VARSET LOCAL

LOCAL:20 += 50
LOCAL:21 += 100

;욕정
LOCAL:11 = 2000
;노출
LOCAL:12 = 6000
;굴복
LOCAL:13 = 1000
;일탈
LOCAL:14 = 1000

;縛って무리やり剃ると…
SIF TEQUIP:구속 && TCVAR:28 <= 0
	LOCAL:14 += 3000

SELECTCASE COND("음모")
CASE 3
	LOCAL:20 += 50
	LOCAL:21 += 100
	LOCAL:12 += 4000
	LOCAL:13 += 1000
	LOCAL:14 += 1000
CASE 2
	LOCAL:20 += 150
	LOCAL:21 += 200
	LOCAL:12 += 6000
	LOCAL:13 += 1500
	LOCAL:14 += 2000
ENDSELECT

;ABL:노출벽と마조끼をみる
SELECTCASE ABL:노출벽 + ABL:마조끼
CASE IS >= 9
	LOCAL:11 += 5000
	TIMES LOCAL:11 , 1.50
	TIMES LOCAL:13 , 0.30
	TIMES LOCAL:14 , 0.10
CASE IS >= 7
	LOCAL:11 += 3000
	TIMES LOCAL:11 , 1.20
	TIMES LOCAL:13 , 0.60
	TIMES LOCAL:14 , 0.40
CASE IS >= 5
	LOCAL:11 += 1000
	TIMES LOCAL:13 , 0.70
	TIMES LOCAL:14 , 0.70
CASE IS >= 3
	TIMES LOCAL:11 , 0.50
CASE 1
	TIMES LOCAL:11 , 0.10
	TIMES LOCAL:13 , 1.20
	TIMES LOCAL:14 , 1.50
CASEELSE
	TIMES LOCAL:11 , 0.00
	TIMES LOCAL:13 , 1.50
	TIMES LOCAL:14 , 2.00
ENDSELECT

;PALAM:욕정をみる
SELECTCASE PALAM:욕정
CASE IS < 1000
	TIMES LOCAL:14 , 1.20
CASE IS < 3000
	TIMES LOCAL:14 , 1.00
CASE IS < 10000
	TIMES LOCAL:14 , 0.90
CASEELSE
	TIMES LOCAL:14 , 0.80
ENDSELECT

;ビデオ촬영, 관내노출, 야외플레이は経験等にプラス
IF TEQUIP:비디오카메라
	TIMES LOCAL:12 , 1.50
	TIMES LOCAL:13 , 1.50
	TIMES LOCAL:14 , 1.50
ENDIF
IF TEQUIP:관내노출플레이 || TEQUIP:야외플레이
	TIMES LOCAL:12 , 1.50
	TIMES LOCAL:13 , 1.50
	TIMES LOCAL:14 , 1.50
ENDIF

CALL SOURCE_SMPLAY, "제모", ARG

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM145
SELECTCASE CONDS("음모")
CASE "剃り跡"
	LOCALS = 까칠까칠한 음모
CASE "生えかけ"
	LOCALS = 옆은 덤불
CASEELSE
	IF PENIS(TARGET)
		LOCALS = 음모
	ELSE
		LOCALS = 그곳의 털
	ENDIF
ENDSELECT
IF PREVCOM == 13
	PRINTFORM %조사처리(CALLNAME:PLAYER,"를")% 향해 소중한 장소를 \@ CHECK("음모관리") ? 열고 기다리고 # 열어 보이고 \@ 있다
;긴박중
ELSEIF GETBIT(TEQUIP:구속, 21)
	PRINTFORM 크게 가랑이를 연 자세로 
	IF BIND("縄", TEQUIP:구속)
		PRINT 묶여있는 
	ELSE
		PRINT 구속되어 있는 
	ENDIF
ELSEIF CHECK("음모관리") && MARK:반발각인 < 3 && COND("다리が無い") == 0
	PRINTFORM 가랑이를 벌리고 준비하고 있는 
ENDIF
PRINTFORMW %CALLNAME:TARGET%의 가랑이의 털을 깎습니다. 
PRINTFORML 그런데,현재상태는[%CONDS("음모")%]그렇지만……이번은 어떻게 하지？

PRINTL  [ 0] 반들반들하게 깎는다
PRINTL  [ 1] 탈모처리를 한다
SIF COND("음모") >= 6
	PRINTL  [ 2] 음모 형태를 다듬는다
PRINTL [100] 돌아간다

$INPUT_LOOP
INPUT

TFLAG:16 = RESULT + 1

IF RESULT == 100
	TFLAG:14 = 1
	TFLAG:16 = 0
	RETURN 0
ELSEIF RESULT == 0 || RESULT == 1
	IF PENIS(TARGET)
		PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% %CALLNAME:TARGET%의 %조사처리(LOCALS,"를")% 
		IF TFLAG:16 == 2
			PRINTFORMW \@ABL:PLAYER:기교 >= 4 ? 정성들여 # \@ 탈모했다…
		ELSE
			PRINTFORMW \@ABL:PLAYER:기교 >= 4 ? 깔끔하게 # \@ 밀었다…
		ENDIF
	ELSE
		PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% %CALLNAME:TARGET%의 %조사처리(LOCALS,"를")% 
		IF TFLAG:16 == 2
			PRINTFORM \@ABL:PLAYER:기교 >= 4 ?  정성들여 #  \@ 탈모해서, 
		ELSE
			PRINTFORM \@ABL:PLAYER:기교 >= 4 ? 깔끔하게 # 정성스럽게 \@ 밀어서, 
		ENDIF
		IF TALENT:클리피어스 + TALENT:라비아피어스 && COND("음모") >= 6 && RAND:3 == 0
			IF TALENT:부끄럼쟁이 && RAND:3 == 0
				PRINT 부끄러운 
			ELSEIF RAND:3 == 0
				IF TALENT:클리피어스 && RANDIF(1, TALENT:라비아피어스)
					IF PENIS(TARGET)
						PRINT 남근을 장식하고 있는 
					ELSE
						PRINT 클리토리스를 꿰뚫고 있는 
					ENDIF
				ELSE
					PRINT 음순을 
					IF AGE(TARGET) >= 4
						PRINT 늘어뜨리고 있는 
					ELSE
						PRINT 장식하고 잇는 
					ENDIF
				ENDIF
			ELSEIF RAND:2
				PRINT 보통이라면 있을 수 없는 장소에 달린 
			ELSE
				PRINT 음란한 
			ENDIF
			PRINTFORMW 피어스가 잘 보이도록 했다…
		ELSEIF TALENT:부끄럼쟁이 && COND("음모") >= 6 && COND("다리が無い") == 0 && RAND:3 == 0
			PRINTFORMW 다리를 조금만 열어도 부끄러운 부분이 훤히 보이도록 만들었다…
		ELSEIF RAND:2 || COND("음모") < 6
			IF RAND:2
				IF AGE(TARGET) <= 2
					PRINT 벗겨낸 삶은 달걀같이 
				ELSE
					PRINT 어린애같이 
				ENDIF
				PRINTFORMW 맨질맨질하게 되었다…
			ELSE
				IF TALENT:작은몸집
					PRINT 작은 몸에 잘 어울리는 
				ELSEIF AGE(TARGET) >= 4
					PRINT 성숙한 성기와의 언밸런스가 흥분되는 
				ELSE
					PRINT 아름다운 곡선을 감상하게 좋게 
				ENDIF
				PRINTFORMW 맨질맨질한 상태가 되었다…
			ENDIF
		ELSE
			IF AGE(TARGET) >= 4
				IF EXP:Ｖ경험 >= 1000
					PRINT 음순이 나온 
				ELSEIF EXP:Ｖ경험 >= 300
					PRINT 살짝 음순이 나온 
				ELSE
					PRINT 부드러운 
				ENDIF
			ELSEIF TALENT:작은몸집 && RAND:3
				PRINT 말랑말랑한 
			ELSE
				PRINT 귀여운 
			ENDIF
			PRINTFORMW 균열이 잘 보이도록 했다…
		ENDIF
		IF TALENT:장사 && AGE(TARGET) >= 2 && COND("다리が無い") == 0
			PRINTFORM 그와 동시에 시야에 들어오는 
			IF RAND:2
				PRINTFORM 팽팽한 
			ELSE
				PRINTFORM 부드러운 
			ENDIF
			IF RAND:2
				PRINTFORM 하복부
			ELSE
				PRINTFORM 허벅지
			ENDIF
			PRINTFORM 와의 상승효과가, 
			IF RAND:2
				PRINTFORMW 너무나 아름다운 광경이다
			ELSE
				PRINTFORMW 손대지 않고는 견딜 수 없는 매력으로 넘쳐 흘렀다
			ENDIF
		ENDIF
	ENDIF
ELSEIF RESULT == 2 && COND("음모") >= 6
	;음모の状態リセット
	CALL DEL_CEVENT_GROUP, "음모の形"
	CALL DEL_CEVENT_GROUP, "음모の명칭"

	IF PENIS(TARGET)
		PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% %CALLNAME:TARGET%의 음모의 형태를\@ABL:PLAYER:기교 >= 4 ? 깔끔하게 #  \@ 정돈했다…
	ELSE
		PRINTFORML 어떤 모양으로 합니까? 
		PRINTFORML  [ 0] ▼ 역삼각형
		PRINTFORML  [ 1] ● 타원형 
		PRINTFORML  [ 2] ■ 사각형
		PRINTFORM  [ 3] 
		CALL HEARTMARK
		PRINTFORML  하트모양
		PRINTFORML  [90] 직접입력한다
		PRINTFORML [100] 딱히 신경쓰지 않는다（설명하지않습니다）

		CALL INPUT_SELECT, 4, 90, 100

		SELECTCASE RESULT
		CASE 100

		CASE 0
			CALL SET_CEVENT, "음모の形：▼"
			CALL SET_CEVENT, "음모の명칭：逆三角形"
		CASE 1
			CALL SET_CEVENT, "음모の形：●"
			CALL SET_CEVENT, "음모の명칭：卵型"
		CASE 2
			CALL SET_CEVENT, "음모の形：■"
			CALL SET_CEVENT, "음모の명칭：四角形"
		CASE 3
			CALL SET_CEVENT, "음모の形：HEARTMARK"
			CALL SET_CEVENT, "음모の명칭：ハート型"
		CASE 90
			PRINTL 전각으로８문자이하（반각이라면16문자까지）의 캐릭터 라인을 입력해 주세요
			PRINTL ("～으로 다듬어진" 등과 같이 사용해도 위화감이 없는 것을 권장)
			PRINTL 또한, 아무것도 입력하지 않으면 딱히 신경쓰지 않는 것으로 합니다
			CALL INPUTS_SELECT, 16

			LOCALS = 음모の形：%RESULTS%
			CALL SET_CEVENT, LOCALS
		ENDSELECT

	PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% %CALLNAME:TARGET%의 대음순을 가리는 털을 확실히 깎아 치부를 장식하는 음모의 형태를\@ STRLENS(CEVENTS("음모の명칭") ) ? %CEVENTS("음모の명칭")% # 예쁜 모양 \@으로 깎았다 …
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF


@COM_ORDER_COM145, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:노출벽
IF ABL:노출벽
	LOCAL += ABL:노출벽 * 5
	LOCALS = %LOCALS% + %ABLNAME:7%LV{ABL:노출벽}({ABL:노출벽 * 5})
ENDIF
;ABL:마조끼
IF ABL:마조끼
	LOCAL += ABL:마조끼 * 5
	LOCALS = %LOCALS% + %ABLNAME:8%LV{ABL:마조끼}({ABL:마조끼 * 5})
ENDIF
;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 15
	LOCALS = %LOCALS% - %TALENTNAME:35%(15)
ENDIF
;부끄럼없음
IF TALENT:부끄럼없음
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:36%(5)
ENDIF
;도착적
IF TALENT:도착적
	LOCAL += 10
	LOCALS = %LOCALS% + %TALENTNAME:80%(10)
ENDIF
;縄
IF TEQUIP:구속
	LOCAL += 30
	LOCALS = %LOCALS% + 구속중(30)
ENDIF
;ビデオ촬영
IF TEQUIP:비디오카메라
	LOCAL -= 20
	LOCALS = %LOCALS% - 촬영중(20)
ENDIF

;合計を表示(60이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 60

RETURN RESULT
