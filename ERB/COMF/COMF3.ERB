@COM3
;자위
;바이브, 애널바이브, ビデオ촬영の時に内容変化
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 3

;実行できない
IF TCVAR:28 == 0
	PRINTFORML %CALLNAME:TARGET%에게 자위하도록 명령했지만 거부당했다. 좀 더 조교가 필요한 것 같다. 
	RETURN 0
ENDIF
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;-------------------------------------------------
;사정체크
;-------------------------------------------------
IF CHECK_COM("펠라자위")
	SELECTCOM = 91
	CALL SAMEN_CHECK, 10
	SELECTCOM = 3
ENDIF

RETURN 1

@FLAG_COM3, ARG
SIF CHECK_COM("펠라자위")
	CALL FLAG_COM91, 10
CALL SETFLAG, "자위플래그", TARGET
IF GETBIT(CHECK_ONANIE(SELECTCOM), 0)
	CALL EQ_STAIN, TARGET, TARGET, 1, 3
	SIF PENIS(TARGET)
		CALL EQ_STAIN, TARGET, TARGET, 1, 2
ENDIF
SIF GETBIT(CHECK_ONANIE(SELECTCOM), 3)
	CALL EQ_STAIN, TARGET, TARGET, 1, 5
IF GETBIT(CHECK_ONANIE(SELECTCOM), 1)
	SIF TEQUIP:바이브
		CALL FLAG_COM31, 30
	CALL EQ_STAIN, TARGET, TARGET, 1, 3
ENDIF
IF GETBIT(CHECK_ONANIE(SELECTCOM), 2)
	SIF TEQUIP:애널바이브
		CALL SOURCE_COM33, 30
	CALL EQ_STAIN, TARGET, TARGET, 1, 4
ENDIF
;샤워オナニーの場合汚れをリセット, 各種윤활半分. 
IF TEQUIP:샤워 == 1
	CALL RESET_STAIN_EX, "全部", TARGET
	PALAM:Ｖ윤 /= 2
	PALAM:Ａ윤 /= 2
	PALAM:Ｐ윤 /= 2
ENDIF

@SOURCE_COM3, ARG
SIF CHECK_COM("펠라자위")
	CALL SOURCE_COM91, 10

VARSET LOCAL

LOCAL:20 += 30
LOCAL:21 += 80

;ABL:기교をみる
LOCAL:4 = 100 + ABL:기교*100

;ABL:노출벽をみる
SELECTCASE ABL:노출벽
CASE 0
	LOCAL:12 = 2000
	LOCAL:13 = 500
	LOCAL:14 = 800
CASE 1
	LOCAL:12 = 2300
	LOCAL:13 = 800
	LOCAL:14 = 600
CASE 2
	LOCAL:12 = 2600
	LOCAL:13 = 1200
	LOCAL:14 = 400
CASE 3
	LOCAL:12 = 2900
	LOCAL:13 = 1900
	LOCAL:14 = 200
CASE 4
	LOCAL:12 = 3200
	LOCAL:13 = 2500
	LOCAL:14 = 50
CASEELSE
	LOCAL:12 = 3500
	LOCAL:13 = 3000
	LOCAL:14 = 0
ENDSELECT
;公開のときは노출のSOURCE増加
SIF TEQUIP:비디오카메라
	TIMES LOCAL:12, 1.50

IF GETBIT(CHECK_ONANIE(SELECTCOM), 0)
	LOCAL:0 = CALCF("Ｃ刺激", 1)
	;샤워사용중
	SIF TEQUIP:샤워
		LOCAL:0 += 200
	;オナホ자위
	SIF GETBIT(CHECK_ONANIE(SELECTCOM), 14)
		CALL SOURCE_COM37, 30
ENDIF

SIF GETBIT(CHECK_ONANIE(SELECTCOM), 3)
	LOCAL:17 = CALCF("Ｂ刺激", 1)

IF GETBIT(CHECK_ONANIE(SELECTCOM), 1)
	LOCAL:1 = CALCF("Ｖ刺激", 1)
	SIF TEQUIP:바이브
		CALL SOURCE_COM31, 30
ENDIF

IF GETBIT(CHECK_ONANIE(SELECTCOM), 2)
	LOCAL:2 = CALCF("Ａ刺激", 1)
	SIF TEQUIP:애널바이브
		CALL SOURCE_COM33, 30
	;애널플레이なら장液分泌
	LOCAL:19 = 100 * ABL:자위중독
ENDIF

;ここから下は쾌ＣＶＡＢに対する보정が大量に来るので, それをLOCAL:30に代入して短く表す
LOCAL:30 = ABL:기교*10 - 100

;ABL:자위중독をみる
SELECTCASE ABL:자위중독
CASE 1
	LOCAL:7 = 100
	LOCAL:30 += 10
CASE 2
	LOCAL:7 = 300
	LOCAL:30 += 20
CASE 3
	LOCAL:7 = 600
	LOCAL:30 += 30
CASE 4
	LOCAL:7 = 800
	LOCAL:30 += 50
CASEELSE
	LOCAL:7 = 1000
	LOCAL:30 += 70
ENDSELECT

;ABL:노출벽をみる
SELECTCASE ABL:노출벽
CASE 0
	LOCAL:30 -= 50
CASE 1
	LOCAL:7 += 100
	LOCAL:30 -= 30
CASE 2
	LOCAL:7 += 300
	LOCAL:30 -= 10
CASE 3
	LOCAL:7 += 500
	LOCAL:30 += 10
CASE 4
	LOCAL:7 += 700
	LOCAL:30 += 30
CASEELSE
	LOCAL:7 += 1000
	LOCAL:30 += 50
ENDSELECT

;TARGETの손가락놀림をみる
IF ABL:손가락놀림
	SIF LOCAL:0
		LOCAL:0 = SOURCE_EXABL(LOCAL:0, 31, TARGET)
	SIF LOCAL:1
		LOCAL:1 = SOURCE_EXABL(LOCAL:1, 31, TARGET)
	SIF LOCAL:2
		LOCAL:2 = SOURCE_EXABL(LOCAL:2, 31, TARGET)
	SIF LOCAL:17
		LOCAL:17 = SOURCE_EXABL(LOCAL:17, 31, TARGET)

	LOCAL:30 += ABL:손가락놀림 * 10
ENDIF

;자위경험
SELECTCASE EXP:자위경험
CASE IS < 5
	LOCAL:30 -= 20
CASE IS < 20
	LOCAL:30 -= 10
CASE IS < 50
CASEELSE
	LOCAL:30 += EXP:자위경험/5
ENDSELECT

;最低でも10％
LOCAL:30 = MAX(LOCAL:30, -90)

LOCAL:0 = LOCAL:0*(100 + LOCAL:30)/100
LOCAL:1 = LOCAL:1*(100 + LOCAL:30)/100
LOCAL:2 = LOCAL:2*(100 + LOCAL:30)/100
LOCAL:17 = LOCAL:17*(100 + LOCAL:30)/100

;자위중독が無いなら, 자위で得た중독충족のSOURCEは０にする
SIF ABL:자위중독 == 0
	LOCAL:7 = 0

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM3

VARSET LOCAL
;자위箇所の判定. 0bit Ｃ자위, 1bit Ｖ자위, 2bit Ａ자위, 3bit Ｂ자위
SIF GETBIT(CHECK_ONANIE(SELECTCOM), 0)
	LOCAL:0 = ABL:Ｃ감각 + TALENT:음핵 + 1
SIF GETBIT(CHECK_ONANIE(SELECTCOM), 1)
	LOCAL:1 = ABL:Ｖ감각 + TALENT:음호 + 1
SIF GETBIT(CHECK_ONANIE(SELECTCOM), 2)
	LOCAL:2 = ABL:Ａ감각 + TALENT:애널광 + 1
SIF GETBIT(CHECK_ONANIE(SELECTCOM), 3)
	LOCAL:3 = ABL:Ｂ감각 + TALENT:음유 + 1

;eratohoから引用
IF PREVCOM != SELECTCOM
	;손대중は終了
	IF TEQUIP:손대중
		PRINTW （손대중을 멈춥니다）
		PRINTL 
		TEQUIP:손대중 = 0
	ENDIF
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	IF INSERT("인서트")
		IF INSERT("Ｖ인서트")
			LOCALS = 膣
		ELSEIF INSERT("Ａ인서트")
			LOCALS = 애널
		ELSEIF INSERT("요도인서트")
			LOCALS = 
		ENDIF
		IF INSERT("기승위")
			PRINTFORM %CALLNAME:PLAYER%에 걸터앉아 %LOCALS%에 %조사처리(NAMES("페니스", PLAYER),"를")% 문 채로, 
		ELSEIF INSERT("배면좌위")
			PRINTFORM %조사처리(LOCALS,"로")% %조사처리(NAMES("페니스", PLAYER),"를")% 조인채로 %CALLNAME:PLAYER%에게 기대어, 
		ELSEIF INSERT("굴곡위")
			PRINTFORM %LOCALS%에 %조사처리(NAMES("페니스", PLAYER),"를")% 비틀어넣은 빠듯한 자세를 한 채로, 
		ELSE
			PRINTFORM %LOCALS%에게 삽입한 자세로 허리의 움직임을 멈춘 %CALLNAME:PLAYER%의 명령에 따라, 
		ENDIF
	ELSE
		SELECTCASE TCVAR:28
		;やる気満々
		CASE IS >= 40
			IF CHECK_COM("펠라자위")
				PRINTFORM %CALLNAME:PLAYER%의 페니스
				IF RAND:2
					PRINTFORM 에 키스를 하면서, 
				ELSE
					PRINTFORM 를 할짝할짝 핥으면서, 
				ENDIF
			ELSEIF TEQUIP:비디오카메라 && RANDIF(3, ABL:노출벽 >= 3, TEQUIP:미약)
				PRINTFORM 렌즈에 시선을 보내면서, 
			ELSEIF TEQUIP:미약 && RANDIF(2, ABL:노출벽 >= 3)
				PRINTFORM 미약으로 신체를 흥분시키면서, 
				SIF ABL:자위중독 >= 3
					PRINTFORM 탐하듯이 
			ELSEIF ABL:노출벽 >= 3 && RAND:2
				PRINTFORM %조사처리(CALLNAME:PLAYER,"를")% 바라보면서, 
			ELSEIF RAND:2
				PRINTFORM 싫어하는 기색도 보이지않고, 
			ELSE
				PRINTFORM 뺨을 살짝 붉게 물들이고, 
			ENDIF
		;저항なし
		CASE IS >= 20
			PRINTFORM %CALLNAME:PLAYER%의 명령에 따라, 
		;仕方なく
		CASEELSE
			IF TALENT:교만 && RANDIF(1, TALENT:반항적)
				PRINTFORM 거만한 태도로 노려보았지만, 재차 말하자 마지못해 
			ELSEIF TALENT:반항적
				PRINTFORM 눈에 반항적인 색을 담고 노려보았지만, 재차 말하자 마지못해 
			ENDIF
		ENDSELECT
	ENDIF

	;オナホ자위
	IF GETBIT(CHECK_ONANIE(SELECTCOM), 14)
		PRINTFORM 오나홀로 페니스를 다뤄
	;샤워자위
	ELSEIF GETBIT(CHECK_ONANIE(SELECTCOM), 10)
		PRINTFORM 강으로 맞춰놓은 샤워기로 가랑이를 자극하면서 
	ELSEIF GETBIT(CHECK_ONANIE(SELECTCOM), 13)
		IF COND("애널비즈")
			PRINTFORM 물고있는 %조사처리(NAMES("바이브", TARGET),"과")% 애널비즈를 스스로 넣고빼
		ELSEIF SIZE("바이브", TARGET) >= 2 && SIZE("Ａ바이브", TARGET) >= 2
			PRINTFORM 물고있는 두개의 커다란 바이브를 스스로 밀
		ELSE
			PRINTFORM 물고있는 두개의 바이브를 스스로 꽂아서 넣고빼
		ENDIF
		IF NOWEX:절정수합계 && RAND:2
			PRINTFORM 해 굉장한 %조사처리(TEXTS("匂い"),"를")% 감돌게 하면서
		ELSE
			PRINTFORM 면서
		ENDIF
	ELSEIF GETBIT(CHECK_ONANIE(SELECTCOM), 11)
		PRINTFORM 보지에넣은 %조사처리(NAMES("바이브", TARGET),"를")% 스스로 넣고빼면서 
	ELSEIF GETBIT(CHECK_ONANIE(SELECTCOM), 12)
		IF COND("애널비즈")
			PRINTFORM 애널에서 늘어져나온 비즈를 스스로 넣고빼면서 
		ELSE
			PRINTFORM 엉덩이에 박혀있는 %조사처리(NAMES("Ａ바이브", TARGET),"를")% 스스로 넣고빼면서 
		ENDIF
	ELSE
		IF LOCAL:0 && (RANDIF(1, LOCAL:1, LOCAL:2, LOCAL:3) || LOCAL:0 > MAX(LOCAL:1, LOCAL:2, LOCAL:3) )
			IF PENIS(TARGET)
				SIF PALAM:욕정 >= 3000
					PRINTFORM %TEXTS("페니스：視覚", TARGET)%
				PRINTFORM 페니스를 훑으면서
			ELSE
				PRINTFORM 클리토리스를 자신의 %조사처리(NAMES("손가락"),"로")% 
				IF RAND:3 == 0
					PRINTFORM 문지르면서 
				ELSEIF RAND:2 == 0
					PRINTFORM 희롱하면서 
				ELSE
					PRINTFORM 휘저으면서 
				ENDIF
			ENDIF
		ELSEIF LOCAL:1 && (RANDIF(1, LOCAL:2, LOCAL:3) || LOCAL:1 > MAX(LOCAL:0, LOCAL:2, LOCAL:3) )
			IF RAND:2 == 0
				PRINTFORM 보지
			ELSE
				PRINTFORM 비소
			ENDIF
			IF RAND:3 == 0
				PRINTFORM 를 스스로 손가락으로 만지작거리면서 
			ELSEIF RAND:2 == 0
				PRINTFORM 를 자신의 손가락으로 휘저으면서 
			ELSE
				PRINTFORM 에 스스로 손가락을 넣고빼면서 
			ENDIF
		ELSEIF LOCAL:2 && (RANDIF(1, LOCAL:3) || LOCAL:2 > MAX(LOCAL:0, LOCAL:1, LOCAL:3) )
			IF RAND:2 == 0
				PRINTFORM 애널
			ELSE
				PRINTFORM 엉덩이구멍
			ENDIF
			IF RAND:3 == 0 || !CHECK_CLO("Ａ노출")
				PRINTFORM 을 스스로 만지작거리면서 
			ELSEIF RAND:2 == 0
				PRINTFORM 을 자기 손가락으로 휘저으면서 
			ELSE
				PRINTFORM 에 스스로 손가락을 넣고빼면서 
			ENDIF
		ELSEIF LOCAL:3
			IF RAND:2 && CALCF("Ｂ발정")
				PRINTFORM 유두를 
			ELSE
				PRINTFORM 젖을 
			ENDIF
			PRINTFORM 자신의 %조사처리(NAMES("손가락"),"로")% 
			IF RAND:3 == 0
				PRINTFORM 문지르면서 
			ELSEIF RAND:2 == 0
				PRINTFORM 만지작거리면서 
			ELSE
				PRINTFORM 주물러 돌리면서 
			ENDIF
		ENDIF
	ENDIF
	PRINTW 쾌감을 맛보고있다…
ELSE
	IF TEQUIP:바이브 && TEQUIP:애널바이브 && ABL:Ｖ감각 >= 5 && ABL:Ａ감각 >= 5 && ABL:Ｃ감각 >= 5 && ABL:Ｂ감각 >= 5 && PALAM:욕정 >= PALAMLV:4
		PRINTFORML %조사처리(CALLNAME:TARGET,"는")% 입에서 흐르는 군침을 닦으며, 부들부들 신체를 경련시키면서 비명을 지르고 있다
		PRINTFORMW 아무래도 절정에서 내려올 수 없는 듯, 계속 가버린 상태인 것 같다…
	ELSEIF TEQUIP:바이브 && ABL:Ｖ감각 >= 5 && ABL:Ｃ감각 >= 5 && ABL:Ｂ감각 >= 5 && PALAM:욕정 >= PALAMLV:4
		PRINTFORML %조사처리(CALLNAME:TARGET,"는")% 애액이 흩날리는 것도 상관하지않고, 바이브를 격렬하게 넣고빼고있다
		PRINTFORMW 아무래도 더이상 다른 일을 신경쓸 여유도 없는 것 같다…
	ELSEIF TEQUIP:애널바이브 && COND("애널비즈") == 0 && ABL:Ａ감각 >= 5 && ABL:Ｃ감각 >= 5 && ABL:Ｂ감각 >= 5 && PALAM:욕정 >= PALAMLV:4
		PRINTFORML %조사처리(CALLNAME:TARGET,"는")% 부들부들 몸을 떨면서, 애널에 바이브를 쑤셔넣고 있다
		PRINTFORMW 아무래도 주름을 후벼질 때마다 수번씩 절정해버리는 것 같다…
	ELSEIF ABL:자위중독 >= 3
		IF TALENT:남자 || (TALENT:후타나리 && RAND:2 == 0)
			PRINTFORML %조사처리(CALLNAME:TARGET,"는")% 난폭한 손놀림으로 단단하게 발기한 페니스를 계속 훑고 있다
		ELSE
			PRINTFORML %조사처리(CALLNAME:TARGET,"는")% 교성을 지르면서 일심분란하게 자신의 비부를 만지작거리고 있다
		ENDIF
		PRINTFORMW 아무래도 멈추고 싶어도 손이 멋대로 움직여 멈출 수 없는 것 같다…
	ELSE
		PRINTFORML %타겟을()% 소리를 눌러 참으면서, 기분이 좋은 장소를 계속 만지작거리고 있다
		PRINTFORMW ……사람에게 보여지면, 무심코 몰두할 수는 없는 것 같다.
	ENDIF
ENDIF

;-------------------------------------------------
;どの種類のオナニーをしているかを決定する
;ARGにはどのSELECTCOMの時かを代入する. 省略した場合にはSELECTCOMが代入されるが非推奨
;内容が複雑かつ#FUNCTIONを多数用いるため独立
;-------------------------------------------------
@CHECK_ONANIE(ARG)
#FUNCTION
;자위に使える손の本数
#DIM USE_HAND

;커맨드선택時の表示ではARG == 3となっている
;0bit Ｃ자위, 1bit Ｖ자위, 2bit Ａ자위, 3bit Ｂ자위
;10bit 샤워자위, 11bit Ｖ바이브자위, 12bit Ａ바이브자위, 13bit 양구멍바이브자위, 14bit オナホ자위
USE_HAND = CALCF("自由な손", ARG)
SIF USE_HAND == 0
	RETURNF 0

LOCAL = CALCF("好みの자위", ARG)

SIF ARG == 0
	ARG = SELECTCOM

IF INSERT("인서트") && CHECK_COM("인서트자위")
ELSEIF USE_HAND >= 2
	IF CHECK_COM("오나홀자위")
		SETBIT LOCAL, 0
	ELSEIF TEQUIP:샤워
		SETBIT LOCAL, 0
	ELSEIF TEQUIP:바이브 && TEQUIP:애널바이브 && COND("애널비즈") == 0
		;どっちかの바이브を弄ってるなら両穴바이브オナニー
		IF GETBIT(LOCAL, 1) || GETBIT(LOCAL, 2)
			SETBIT LOCAL, 1
			SETBIT LOCAL, 2
		ELSEIF TALENT:남자 == 0 && (ABL:Ｖ감각 >= ABL:Ａ감각 || ABL:Ａ감각 < 3)
			SETBIT LOCAL, 1
		ELSE
			SETBIT LOCAL, 2
		ENDIF
	ELSEIF ARG == 3
		IF TEQUIP:바이브 && !GETBIT(LOCAL, 1) && ABL:Ｖ감각 >= 3
			SETBIT LOCAL, 1
		ELSEIF TEQUIP:애널바이브 && COND("애널비즈") == 0 && !GETBIT(LOCAL, 2) && ABL:Ａ감각 >= 3
			SETBIT LOCAL, 2
		ELSEIF GETBIT(LOCAL, 3)
			IF ABL:Ｃ감각 >= MAX(ABL:Ｖ감각, ABL:Ａ감각)
				SETBIT LOCAL, 0
			ELSEIF TALENT:남자 == 0 && ABL:Ｖ감각 >= ABL:Ａ감각
				SETBIT LOCAL, 1
			ELSE
				SETBIT LOCAL, 2
			ENDIF
		ELSE
			SETBIT LOCAL, 3
		ENDIF
	ENDIF
ENDIF
IF CHECK_COM("오나홀자위") && GETBIT(LOCAL, 0)
	SETBIT LOCAL, 14
ELSEIF TEQUIP:샤워 && GETBIT(LOCAL, 0)
	SETBIT LOCAL, 10
ELSEIF TEQUIP:바이브 && TEQUIP:애널바이브 && COND("애널비즈") == 0 && GETBIT(LOCAL, 1) && GETBIT(LOCAL, 2)
	SETBIT LOCAL, 13
	SETBIT LOCAL, 11
	SETBIT LOCAL, 12
ELSEIF TEQUIP:바이브 && GETBIT(LOCAL, 1)
	SETBIT LOCAL, 11
ELSEIF TEQUIP:애널바이브 && COND("애널비즈") == 0 && GETBIT(LOCAL, 2)
	SETBIT LOCAL, 12
ENDIF
RETURNF LOCAL

@PLAY_ONANIE
SIF COND("조교中") == 0
	RETURN 0
;자위禁止中
SIF COND("身嗜み：자위금지")
	RETURN 0
;자위중독0LV
SIF ABL:자위중독 == 0
	RETURN 0
;고통あり、성욕만족、Ｗ넘어뜨리기中、손が余ってない
SIF TEQUIP:Ｗ넘어뜨리기 || CUP:고통 > PALAM_LV(ABL:마조끼) || BASE:성욕 >= MAXBASE:성욕 || CALCF("自由な손") <= 0
	RETURN 0
;TARGETが삽입봉사している側
SIF INSERT("삽입中")
	RETURN 0
;욕망, 순종, 봉사정신も判定に使用
;前のターンに자위しているorこのターンに아무것도하지않는다なら高확률で자위しちゃう
IF PREVCOM == 3 && ABL:자위중독 >= RAND:4
ELSEIF SELECTCOM == 139 && ABL:자위중독 >= RAND:5
ELSEIF ABL:자위중독 <= RAND:8 || ABL:자위중독*3 + ABL:욕망 + RAND:5 < ABL:순종 + ABL:봉사정신*2
	RETURN 0
ENDIF
;SOURCEの増加
CALL SOURCE_COM3,30
CALL FLAG_COM3, 30

CALL SETFLAG, "ながら자위플래그", TARGET


@PLAY_ONANIE_2
SIF COND("ながら자위플래그") == 0
	RETURN 0

SELECTCASE SELECTCOM
CASE 60 TO 79
	PRINTL ＜자위중독＞
	IF SELECTCOM == 65 || SELECTCOM == 75 || TEQUIP:넘어뜨리기
		PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 허리를 흔들면서 %NAMES("손가락")%끝을 
	ELSE
		PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 범해지면서 %NAMES("손가락")%끝을 
	ENDIF
	LOCALS = 뻗어
;아무것도하지않는다
CASE 139
	PRINTL ＜자위중독＞
	PRINTFORM %CALLNAME:TARGET%의 %NAMES("손가락")% 끝을 \@ ABL:자위중독 >= 3 ? 무의식적으로 # \@ 
	LOCALS = 뻗어
CASEELSE
	PRINTFORM %NAMES("손가락")%끝을 
	LOCALS = 뻗어
ENDSELECT

SELECTCASE CALCF("好みの자위", SELECTCOM)
CASE 1
	IF PENIS(TARGET)
		IF CHECK_COM("오나홀자위")
			PRINTFORMW 가랑이에%LOCALS%, %TEXTR("찔꺽찔꺽 격렬하게 오나홀을 위아래로 하고있다", "오나홀로 마음껏 페니스를 다루고 있다")%…
		ELSE
			PRINTFORMW 페니스에%LOCALS%, %TEXTR("민감한 귀두를 쓰다듬고 있다", "찔꺽찔꺽 격렬하게 다루고 있다")%…
		ENDIF
		CALL EQ_STAIN, TARGET, TARGET, 1, 2
	ELSE
		PRINTFORM 가랑이에 %LOCALS%, 
		IF RAND:2 == 0 || (SELECTCOM >= 60 && SELECTCOM <= 79)
			IF CALCF("Ｃ발정") >= 3
				PRINTFORM 비대해진 
			ELSE
				SIF CALCF("Ｃ발정") <= 1
					PRINTFORM 포피너머로 
				PRINTFORM 민감한 
			ENDIF
			PRINTFORM 돌기를 
			IF TALENT:클리피어스 && RAND:2 == 0
				PRINTW 꿰뚫은 피어스를 만지작거리고있다…
			ELSE
				PRINTW 주무르면서 돌리고있다…
			ENDIF
		ELSE
			IF TALENT:클리피어스 && RAND:2 == 0
				IF RAND:2
					PRINTFORMW 피어스를 비틀어 클리토리스를 괴롭히고 있다…
				ELSE
					PRINTFORMW 음핵에 붙여진 피어스를 희롱하고 있다…
				ENDIF
			ELSEIF CALCF("Ｃ발정") < 3 && RAND:2
				PRINTW 서서히 날카로워져 가는 클리토리스를 주물러대고 있다…
			ELSE
				PRINTW 질척질척 음미로운 소리를 시키고 있다…
			ENDIF
		ENDIF
		CALL EQ_STAIN, TARGET, TARGET, 1, 3
	ENDIF
CASE 2
	PRINTFORM 가랑이에 %LOCALS%, 
	IF TEQUIP:바이브
		IF SIZE("바이브", TARGET) >= 2
			PRINTFORM 질을 확대하고 있는 
		ELSE
			PRINTFORM 물고 있는 
		ENDIF
		SIF SIZE("바이브", TARGET) == 2
			PRINTFORM 거대한 
		SIF SIZE("바이브", TARGET) >= 3
			PRINTFORM 두터운 
		PRINTW 바이브를 넣고빼면서 즐기고있다…
	ELSEIF INSERT("정상위") + INSERT("굴곡위") && ABL:노출벽 >= 3 && RAND:2
		PRINTFORMW 때때로 비부를 벌리거나 하면서 자위를 즐기고있다…
	ELSE
		PRINTW 츄릅츄릅 음란한 소리를 내고있다…
		CALL EQ_STAIN, TARGET, TARGET, 1, 3
	ENDIF
	EXP:Ｖ경험 += 1
	PRINTFORML Ｖ경험＋1
CASE 4
	PRINTFORM 가랑이에 %LOCALS%, 
	IF TEQUIP:애널바이브
		PRINTW 애널에서 얼굴을 내밀고 있는 장난감을 만지작거리면서 즐기고있다…
	ELSE
		PRINTW 부드러운 엉덩이구멍을 만지고 있는 것같다…
		CALL EQ_STAIN, TARGET, TARGET, 1, 4
	ENDIF
	EXP:Ａ경험 += 1
	PRINTFORML Ａ경험＋1
CASEELSE
	IF TALENT:거유
		IF RAND:2
			PRINTFORM 풍만한 
		ELSE
			PRINTFORM 부드러운 
		ENDIF
	ELSEIF TALENT:빈유
		PRINTFORM 작은 
	ENDIF
	PRINTFORM 가슴을 에 %LOCALS%, 
	IF RAND:2 == 0 && TALENT:거유
		SIF ABL:Ｂ감각 >= 4 && TALENT:하피 == 0 && RAND:2
			PRINTFORM 손가락을 잠기게 하면서 
		PRINTW 유방을 비비어 뭉개고있다…
	ELSE
		LOCAL = 0
		IF TALENT:니플피어스 && RAND:2 == 0
			PRINTFORM %조사처리(TEXTS("니플피어스"),"로")% 장식된 
			LOCAL = 1
		ELSEIF CALCF("Ｂ발정") >= 3
			PRINTFORM 쾌감에 부푼 
		ELSEIF CALCF("Ｂ발정") >= 2
			PRINTFORM 뾰족하게 솟은 
		ELSEIF CALCF("Ｂ발정")
			PRINTFORM 발기상태인 
		ELSE
			PRINTFORM 분홍색의 
		ENDIF
		PRINTFORM 돌기
		IF TALENT:니플피어스 && RAND:3 == 0 && LOCAL == 0
			PRINTW 를 꿰뚫은 피어스를 만지작거리고있다…
		ELSE
			IF RAND:2
				PRINTW 를 훑어내고있다…
			ELSE
				PRINTW 를 주무르면서 돌리고있다…
			ENDIF
		ENDIF
	ENDIF
	CALL EQ_STAIN, TARGET, TARGET, 1, 5
ENDSELECT

EXP:자위경험 += 1
PRINTFORML 자위경험＋1


@COM_ORDER_COM3, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:노출벽
IF ABL:노출벽
	LOCAL += ABL:노출벽 * 4
	LOCALS = %LOCALS% + %ABLNAME:7%LV{ABL:노출벽}({ABL:노출벽 * 4})
ENDIF
;ABL:자위중독
IF ABL:자위중독
	LOCAL += ABL:자위중독 * 3
	LOCALS = %LOCALS% + %ABLNAME:10%LV{ABL:자위중독}({ABL:자위중독 * 3})
ENDIF

;강기
IF TALENT:강기
	LOCAL -= 5
	LOCALS = %LOCALS% - %TALENTNAME:18%(5)
ENDIF
;自制心
IF TALENT:냉정
	LOCAL -= 5
	LOCALS = %LOCALS% - %TALENTNAME:20%(5)
ENDIF
;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 15
	LOCALS = %LOCALS% - %TALENTNAME:35%(15)
ENDIF
;부끄럼없음
IF TALENT:부끄럼없음
	LOCAL += 2
	LOCALS = %LOCALS% + %TALENTNAME:36%(2)
ENDIF
;자위하기쉬움
IF TALENT:자위하기쉬움
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:60%(5)
ENDIF
;쾌감에솔직
IF TALENT:쾌감에솔직
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:70%(5)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 5
	LOCALS = %LOCALS% - %TALENTNAME:71%(5)
ENDIF
;미약
IF TEQUIP:미약
	LOCAL += 6
	LOCALS = %LOCALS% + %ITEMNAME:31%(6)
ENDIF
;샤워
IF TEQUIP:샤워
	LOCAL -= 3
	LOCALS = %LOCALS% - 샤워(3)
ENDIF
;바이브
IF TEQUIP:바이브
	LOCAL -= 5
	LOCALS = %LOCALS% - %ITEMNAME:1%(5)
ENDIF
;Ａ바이브
IF TEQUIP:애널바이브 && COND("애널비즈") == 0
	LOCAL -= 10
	LOCALS = %LOCALS% - %ITEMNAME:3%(10)
ENDIF
;ビデオ촬영
IF TEQUIP:비디오카메라
	LOCAL -= 20
	LOCALS = %LOCALS% - 촬영중(20)
ENDIF
IF TEQUIP:야외플레이
	LOCAL -= 10
	LOCALS = %LOCALS% - 야외플레이(10)
ENDIF

;合計を表示(33이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 33

RETURN RESULT
