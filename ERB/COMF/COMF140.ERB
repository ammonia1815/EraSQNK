@COM140
;이라마치오

PRINTL 이라마치오

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 140

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
SIF TEQUIP:구속
	TCVAR:28 -= 30
SIF TEQUIP:재갈 == 2
	TCVAR:28 -= 30
TCVAR:28 = MAX(TCVAR:28, 0)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = 이라마치오

;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1


@FLAG_COM140, ARG
;대상の입⇔조교자のＰの汚れが移動
CALL EQ_STAIN, TARGET, PLAYER, 0, 2

@SOURCE_COM140, ARG
VARSET LOCAL

LOCAL:20 += 150
LOCAL:21 += 100

IF TEQUIP:촉수 && TEQUIP:촉수구욕
	LOCAL:8 = 1000
ELSE
	LOCAL:8 = COM_ORDER_STAIN("Ｐ", "Ｍ")*200 + 100
ENDIF

LOCAL:13 = 3000
LOCAL:14 = 3000

;快Mのソース・M감각を見る
LOCAL:18 = CALCF("Ｍ刺激", 2, 120)

;인후성감がないならば激減
IF TALENT:인후성감 == 0
	TIMES LOCAL:18, 0.05
	SIF LOCAL:18 < 100
		LOCAL:18 = 0
ENDIF

IF TEQUIP:촉수 && TEQUIP:촉수구욕
	LOCAL:30 = ABL:목확장 - TEQUIP:촉수구욕
ELSE
	LOCAL:30 = ABL:목확장 - SIZE("페니스", PLAYER)
ENDIF

;喉ＬＶを見る
SELECTCASE LOCAL:30
CASE IS <= -2
	LOCAL:6 = 3000
	LOCAL:9 = 8000
	TIMES LOCAL:18, 0.00
CASE -1
	LOCAL:6 = 1000
	LOCAL:9 = 3000
	TIMES LOCAL:18, 0.00
CASE 0
	LOCAL:6 = 500
	LOCAL:9 = 1000
CASE 1
	LOCAL:6 = 100
	LOCAL:9 = 300
ENDSELECT

;堅さを見る
IF TEQUIP:촉수구욕 == 0
	SELECTCASE HARD("페니스", PLAYER)
	CASE 3
		TIMES LOCAL:6, 1.20
		TIMES LOCAL:9, 1.20
	CASE IS >= 4
		TIMES LOCAL:6, 1.50
		TIMES LOCAL:9, 1.50
	ENDSELECT
ENDIF

;ABL:봉사정신をみる
SELECTCASE ABL:봉사정신
CASE 0
	LOCAL:4 = 420
	LOCAL:5 = 50
	LOCAL:14 += 2000
CASE 1
	LOCAL:4 = 500
	LOCAL:5 = 100
	LOCAL:14 += 1200
CASE 2
	LOCAL:4 = 580
	LOCAL:5 = 200
	LOCAL:14 += 800
CASE 3
	LOCAL:4 = 660
	LOCAL:5 = 300
	LOCAL:14 += 500
CASE 4
	LOCAL:4 = 740
	LOCAL:5 = 500
	LOCAL:14 += 200
CASEELSE
	LOCAL:4 = 820
	LOCAL:5 = 700
ENDSELECT

;ABL:기교をみる
SELECTCASE ABL:기교
CASE 0
	TIMES LOCAL:4, 0.50
	TIMES LOCAL:5, 0.50
	TIMES LOCAL:6, 2.00
	TIMES LOCAL:9, 2.00
	TIMES LOCAL:14, 2.00
CASE 1
	TIMES LOCAL:4, 0.80
	TIMES LOCAL:5, 0.80
	TIMES LOCAL:6, 1.50
	TIMES LOCAL:9, 1.50
	TIMES LOCAL:14, 1.50
CASE 2
CASE 3
	TIMES LOCAL:4, 1.20
	TIMES LOCAL:5, 1.20
	TIMES LOCAL:6, 0.70
	TIMES LOCAL:9, 0.70
CASE 4
	TIMES LOCAL:4, 1.50
	TIMES LOCAL:5, 1.50
	TIMES LOCAL:6, 0.20
	TIMES LOCAL:9, 0.40
CASEELSE
	TIMES LOCAL:4, 2.00
	TIMES LOCAL:5, 2.00
	TIMES LOCAL:6, 0.05
	TIMES LOCAL:9, 0.20
ENDSELECT

;ABL:마조끼をみる
SELECTCASE ABL:마조끼
CASE 1
	TIMES LOCAL:13, 1.20
	TIMES LOCAL:9, 0.80
CASE 2
	TIMES LOCAL:13, 1.50
	TIMES LOCAL:9, 0.50
CASE 3
	TIMES LOCAL:13, 2.00
	TIMES LOCAL:9, 0.20
CASE 4
	TIMES LOCAL:13, 2.50
	TIMES LOCAL:9, 0.10
CASE IS >= 5
	TIMES LOCAL:13, 3.00
	TIMES LOCAL:9, 0.05
ENDSELECT

CALL SOURCE_SMPLAY, "이라마치오", ARG

;딥스로트中？
IF TEQUIP:딥스로트
	IF TALENT:구토반사내성 == 0
		LOCAL:20 += 50
		LOCAL:21 += 50
	ENDIF
	TIMES LOCAL:4, 1.50
	TIMES LOCAL:5, 1.50
	TIMES LOCAL:6, 1.50
	TIMES LOCAL:8, 1.50
	TIMES LOCAL:9, 1.50
	TIMES LOCAL:13, 1.50
	TIMES LOCAL:14, 1.50
	TIMES LOCAL:18, 1.50
ENDIF

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM140
IF CHECK_COM("３Ｐ봉사")
	CALL TRAIN_MESSAGE_3P
;前回も이라마치오
ELSEIF PREVCOM == SELECTCOM && COND("前Ｔ조교자") == PLAYER
	PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% %CALLNAME:TARGET%의 
	;角がありますか？
	PRINTFORM \@ COND("뿔ハンドル") ? 뿔을 잡고 # 머리를 누르고 \@, 
	IF SIZE("페니스", PLAYER) >= 2 && RANDIF(2, ABL:PLAYER:새드끼 >= 3)
		PRINTFORM 긴 페니스로
	ELSEIF ABL:PLAYER:새드끼 >= 3 && RAND:2
		PRINTFORM 휴식도 시키지 않을 기세로
	ELSEIF ABL:마조끼 >= 3 && CHECK("喉姦")
		PRINTFORM 바람직하는 대로
	ENDIF
	IF (ABL:기교 >= 4 || TALENT:PLAYER:새드 || TEQUIP:딥스로트) && SIZE("페니스", PLAYER) && RAND:2
		IF SIZE("페니스", PLAYER) >= 3 && TEQUIP:딥스로트 && RAND:2
			LASTWORD = 식도
		ELSE
			LASTWORD = 목
		ENDIF
	ELSE
		LASTWORD = 입
	ENDIF
	PRINTFORMW %LASTWORD를()% 가차없이 계속 범하고있다…
ELSE
	;特に[연모＋봉사정신Lv5], [봉사정신Lv3], [PLAYERが새드], [直前の조교], [角があるか], [아이마스크]等で텍스트が変わります. 
	PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% 
	;前回は펠라치오
	IF PREVCOM == 91 && COND("前Ｔ조교자") == PLAYER
		;汚れてますか？악취둔감で無いならば, 실행치の10分の一の値とRAND:5を比べて判定
		IF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｖ", "Ａ") && TALENT:악취둔감 == 0 && RAND:6 > TCVAR:28/10
			PRINTFORM %TEXTS("匂い")%에 미간을 찌뿌리며
		;연모or헌신的or봉사정신3이상or악취둔감
		ELSEIF (TALENT:연모 || TALENT:헌신 || ABL:봉사정신 >= 3 || TALENT:악취둔감) && RAND:2 == 0
			PRINT 맛있는 듯이 
		;부끄럼쟁이
		ELSEIF TALENT:부끄럼쟁이 && RAND:3 == 0
			PRINT 부끄러운 듯이 
		;욕정4이상
		ELSEIF PALAM:욕정 >= PALAMLV:4 && RAND:3 == 0
			SIF TALENT:도착적 && RAND:3 == 0
				PRINTFORM %TEXTS("匂い")%에
			PRINT 흥분하면서 
		;연모かつ봉사정신5or헌신的かつ봉사정신3이상
		ELSEIF ( (TALENT:연모 && ABL:봉사정신 >= 5) || (TALENT:헌신 && ABL:봉사정신 >= 3) ) && RAND:3 == 0
			PRINT 기쁜듯이 
		;연모or헌신的or봉사정신3이상
		ELSEIF TALENT:연모 || TALENT:헌신 || ABL:봉사정신 >= 3
			PRINT 맛있는듯이 
		ELSEIF RAND:2 == 0 && ABL:기교 < 2
			PRINT 구역질을 하면서도  
		ENDIF
		;前回청소펠라
		IF PREVPLAY("청소펠라")
			PRINTFORM 페니스를 청소하고 있는 %CALLNAME:TARGET%의 
		ELSE
			PRINTFORM 봉사하고 있는 %CALLNAME:TARGET%의 
		ENDIF
	;딥스로트中
	ELSEIF TEQUIP:딥스로트
		PRINTFORM 뿌리까지 페니스를 물었는 %CALLNAME:TARGET%의 
	ELSE
		IF BIND("ベッド", TEQUIP:구속) && RAND:2
			PRINTFORM 위를 향하게 되어 목과 일직선이 되어있는 %CALLNAME:TARGET%의 입안에 
		;링개그
		ELSEIF TEQUIP:재갈 == 2
			PRINTFORM 기구로 닫혀지지 않게 된 %CALLNAME:TARGET%의 입안에 
		;아이마스크+縄
		ELSEIF TEQUIP:아이마스크 && TEQUIP:구속
			PRINTFORM 완전히 구속되어 있는 %CALLNAME:TARGET%의 입안에 
		;아이마스크あり？
		ELSEIF TEQUIP:아이마스크
			PRINTFORM 눈가리개를 하고 있는 %CALLNAME:TARGET%의 입안에 
		;縛ってますか？
		ELSEIF TEQUIP:구속
			PRINTFORM 꼼짝도 할 수 없는 %CALLNAME:TARGET%의 입안에 
		;연모＋봉사정신Lv5
		ELSEIF ABL:봉사정신 >= 5 && TALENT:연모 && RAND:2 == 0
			PRINTFORM %NAMES("페니스", PLAYER)%에 뺨을 부벼오는 %CALLNAME:TARGET%의 입안에 
		ELSE
			PRINTFORM 무릎꿇은 %CALLNAME:TARGET%의 입안에 
		ENDIF
		;直前の조교と汚れで分岐
		IF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｖ", "Ａ", "尿")
			CALL TEXT, "추가地の文：穴から穴へ"
		ELSEIF PENIS(PLAYER) == 0
		;섹스系を直前にしていないなら乱数分岐
		ELSE
			PRINTFORM %TEXTS("페니스：視覚", PLAYER)%
		ENDIF
		IF PENIS(PLAYER)
			PRINTFORM 페니스를 
		ELSE
			PRINTFORM 페니스 밴드를 
		ENDIF
		;봉사정신と「PLAYERが새드か」で分岐
		IF (TALENT:PLAYER:새드 || ABL:봉사정신 >= 5) && RAND:3 == 0
			PRINT 단번에 
		ELSEIF TALENT:PLAYER:새드 || (ABL:봉사정신 >= 3 && RAND:2 == 0)
			PRINT 안까지 
		ELSE
			PRINT 천천히 
		ENDIF
		;봉사정신と「PLAYERが새드か」で分岐
		IF (TALENT:PLAYER:새드 || ABL:봉사정신 >= 5) && RAND:3 == 0
			PRINT 찔러넣고, 
		ELSEIF ABL:봉사정신 >= 3 && RAND:2 == 0
			PRINT 삽입하고, 
		ELSE
			PRINT 넣고, 
		ENDIF
		;봉사정신5かつ연모で追加
		SIF ABL:봉사정신 >= 5 && TALENT:연모 && RAND:3 >= 1
			PRINT 혀를 엮으면서, 
	ENDIF
	;角がありますか？
	IF COND("뿔ハンドル")
		PRINT 뿔을 잡고
	ELSE
		PRINT 머리를 잡고
	ENDIF
	SIF PREVCOM == 91 && COND("前Ｔ조교자") == PLAYER
		PRINT , 
	IF PREVCOM >= 60 && PREVCOM <= 79 && RAND:3 == 0
		PRINT 방금전까지 범하고 있던 구멍과 비교하면서 
	;봉사정신3이상か「PLAYERが새드か」で追加
	ELSEIF (TALENT:PLAYER:새드 || ABL:봉사정신 >= 3) && RAND:3 == 0
		PRINT 섹스할 때처럼 
	ENDIF
	;봉사정신と「PLAYERが새드か」で分岐
	IF (TALENT:PLAYER:새드 || ABL:봉사정신 >= 5) && RAND:3 == 0
		PRINTFORMW 거칠게 입안을 능욕했다…
	ELSEIF TALENT:PLAYER:새드 || (ABL:봉사정신 >= 3 && RAND:2 == 0)
		PRINTFORMW 격렬하게 넣다뺐다…
	ELSE
		PRINTFORMW 허리를 앞뒤로 움직였다…
	ENDIF
ENDIF

;いけるようなら喉奥まで
IF CHECK("喉姦")
	TEQUIP:딥스로트 = PLAYER
	;플래그とTEQUIPが分かれているのは, 사정後などにTEQUIP:딥스로트が０となっている時でも参照するため
	CALL SETFLAG, "딥스로트플래그", TARGET
ENDIF

@COM_ORDER_COM140, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF
;ABL:마조끼
IF ABL:마조끼
	LOCAL += ABL:마조끼 * 3
	LOCALS = %LOCALS% + %ABLNAME:8%LV{ABL:마조끼}({ABL:마조끼 * 3})
ENDIF
;ABL:정액중독
IF ABL:정액중독
	LOCAL += ABL:정액중독 * 3
	LOCALS = %LOCALS% + %ABLNAME:11%LV{ABL:정액중독}({ABL:정액중독 * 3})
ENDIF

;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 5
	LOCALS = %LOCALS% - %TALENTNAME:35%(5)
ENDIF
;헌신的
IF TALENT:헌신
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF

;縄
IF TEQUIP:구속
	LOCAL += 30
	LOCALS = %LOCALS% + 긴박중(30)
ENDIF

;링개그
IF TEQUIP:재갈 == 2
	LOCAL += 30
	LOCALS = %LOCALS% + 링개그(30)
ENDIF

LOCAL:1 = COM_ORDER_STAIN("Ｐ", "Ｍ")
;새드なら, 汚れていても, かまわずに突っ込みます
SIF ABL:PLAYER:새드끼 >= 3
	LOCAL:1 /= 3

;불결있음
IF LOCAL:1
	LOCAL -= LOCAL:1
	LOCALS = %LOCALS% - 불결있음
	;악취둔감
	SIF TALENT:악취둔감 || TALENT:악취민감
		LOCALS = %LOCALS%, \@TALENT:악취둔감 ? %TALENTNAME:61% # %TALENTNAME:62%\@
	LOCALS = %LOCALS%({LOCAL:1})
ENDIF

;合計を表示(50이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 50

RETURN RESULT
