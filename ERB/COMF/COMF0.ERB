@COM0
;애무
CALL PRINT_TRAIN_NAME(SELECTCOM)
;PLAYERの기교に応じて플라트発動（初回は손대중効果もあり）
SIF TRAIN_NAME(SELECTCOM) == "플라트"
	TEQUIP:플라트 = 1

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

RETURN 1

@FLAG_COM0, ARG
SIF CHECK_COM("로션마사지", SELECTCOM) == 1
	CALL FLAG_COM260, 100

;성기애무
CALL FLAG_COM7, 30

;안면기승or식스나인状態でないなら가슴も애무
IF TEQUIP:식스나인 + TEQUIP:안면기승 == 0 && CHECK_COM("로션마사지", SELECTCOM) == 0
	CALL FLAG_COM5, 30

	IF TEQUIP:촉수
		;대상に촉수の汚れがつく
		CALL ADD_STAIN, TARGET, "Ｂ", "촉수"
	ELSE
		;대상のＢ⇔조교자の指の汚れが移動
		CALL EQ_STAIN, TARGET, PLAYER, 3, 1
	ENDIF
ENDIF

@SOURCE_COM0, ARG
SIF CHECK_COM("로션마사지", SELECTCOM) == 1
	CALL SOURCE_COM260, 100

VARSET LOCAL
LOCAL:20 += 5
LOCAL:21 += 10

IF TEQUIP:플라트 == 1
	LOCAL:20 += 45
	LOCAL:21 += 30
ENDIF

;성기애무
CALL SOURCE_COM7, 30

;안면기승or식스나인状態でないなら가슴も애무
SIF TEQUIP:식스나인 + TEQUIP:안면기승 == 0 && CHECK_COM("로션마사지", SELECTCOM) == 0
	CALL SOURCE_COM5, 30

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@EQUIP_COM34_2
SIF TEQUIP:플라트 == 0
	RETURN 0

PRINTFORML ＜플라트 효과발휘중＞

RETURN 1

@TRAIN_MESSAGE_COM0
IF CHECK_COM("로션마사지", SELECTCOM)
	CALL TRAIN_MESSAGE_COM260
ELSEIF TALENT:남자
	PRINTFORMW %CALLNAME:TARGET%의 온몸을 감질나게 애무했다…
;식스나인
ELSEIF TEQUIP:식스나인
	PRINTFORM %CALLNAME:PLAYER%는 %CALLNAME:TARGET%
	IF TEQUIP:넘어뜨리기
		PRINT 에게 가랑이 사이를 탐하여지면서, 
	ELSE
		PRINT 에게 봉사시키면서, 
	ENDIF
	PRINT 눈 앞
	IF RAND:2
		PRINTW 의 부드러운 엉덩이살을 반죽하듯 주물렀다…
	ELSE
		PRINTW 에 펼쳐진 경관을 만끽했다…
	ENDIF
;넘어뜨리기
ELSEIF TEQUIP:넘어뜨리기
	IF INSERT("인서트")
		IF INSERT("요도인서트")
			IF (EXP:요도경험 >= 150 || TALENT:요도광) && RAND:3
				IF RAND:4 == 0
					PRINTFORM 군침을 흘리며 허리를 움직이는 
				ELSEIF RAND:3 == 0
					PRINTFORM 허리를 흔들어 금단의 쾌감을 탐내고 있는
				ELSEIF RAND:2 == 0
					PRINTFORM 요도의 포로가 되어 있는
				ELSE
					PRINTFORM 촉촉한 눈동자로 %조사처리(CALLNAME:PLAYER,"를")% 바라보는 
				ENDIF
			ELSEIF EXP:요도경험 >= 50 || TALENT:요도성감 || TALENT:도착적
				IF PENIS(PLAYER) && ABL:봉사정신 >= 3 && RANDIF(3, TALENT:요도성감)
					PRINTFORM 요도에서의 봉사에 기를 쓰고 있는
				ELSEIF PENIS(PLAYER) && RANDIF(2, TALENT:요도성감)
					PRINTFORM 뿌리까지 페니스를 삼켜 오슬오슬 하고 있는
				ELSEIF TALENT:요도성감 && RAND:2
					PRINTFORM 요도성감의 포로가 되어 있는
				ELSEIF RAND:2
					PRINTFORM 민감한 점막을 %TEXTR("문질러지는", "확장되는")% 감각을 %TEXTR("맛보고", "만끽하고")%있는
				ELSE
					PRINTFORM 홍조를 띈 얼굴로 %조사처리(CALLNAME:PLAYER,"를")% 바라보는 
				ENDIF
			ELSE
				IF RAND:2 == 0
					PRINTFORM 미지의 감각에 희롱해지고 있다
				ELSE
					PRINTFORM 익숙해지지 않는 이물감에 숨을 어지럽히고 있다
				ENDIF
			ENDIF
		ELSEIF INSERT("Ｖ인서트")
			IF (EXP:Ｖ경험 >= 300 || ABL:Ｖ감각 >= 4) && RAND:3
				IF PREVCOM == 67 && PENIS(PLAYER) && RAND:3 == 0
					PRINTFORM %CALLNAME:PLAYER%의 페니스로 자궁구를 문지르고 휘젓는데 정신이 팔려있는 
				ELSEIF PREVCOM == 66 && PENIS(PLAYER) && RAND:3 == 0
					PRINTFORM 페니스에 Ｇ스팟을 얕게 문지르고 있는 
				ELSEIF RAND:4 == 0
					PRINTFORM 조교당하는 질이 주는 쾌락에 빠져버린 얼굴을 하고있는 
				ELSEIF RAND:3 == 0
					PRINTFORM 얌전치 못하게 군침을 늘어뜨리며 허리를 흔드는 
				ELSEIF RAND:2 == 0
					PRINTFORM 깊은 숨을 쉬면서 배안의 감촉을 맛보고 있는 
				ELSE
					PRINTFORM 촉촉한 눈동자로 %조사처리(CALLNAME:PLAYER,"를")% 바라보는 
				ENDIF
			ELSEIF EXP:Ｖ경험 >= 150 || ABL:Ｖ감각 >= 3 || TALENT:호색
				IF PREVCOM == 67 && PENIS(PLAYER) && RAND:2 == 0
					PRINTFORM 질 깊은 안쪽까지 페니스를 물고 두근두근거리고 있는 
				ELSEIF PREVCOM == 66 && RAND:2 == 0
					PRINTFORM Ｇ스팟이 주는 쾌감에 포로가 된
				ELSEIF RAND:2 == 0
					PRINTFORM 질을 넓혀지는 감각을 만끽하고 있는 
				ELSE
					PRINTFORM 홍조를 띈 얼굴로 %조사처리(CALLNAME:PLAYER,"를")% 바라보는 
				ENDIF
			ELSE
				IF PREVCOM == 67 && PENIS(PLAYER) && RAND:2 == 0
					PRINTFORM 민감한 자궁구를 허리를 빼서 페니스로 자극하고 있는 
				ELSEIF PREVCOM == 66
					PRINTFORM 서투르게 Ｇ스팟을 조심조심 문지르고 있는 
				ELSEIF RAND:2 == 0
					PRINTFORM 허리를 떨며 쾌감을 탐하고 있는 
				ELSE
					PRINTFORM 찔러올려지며 희롱당하고 있는 
				ENDIF
			ENDIF
		ELSE
			IF (EXP:Ａ경험 >= 300 || ABL:Ａ감각 >= 4) && RAND:3
				IF PENIS(PLAYER) && RAND:6 == 0
					PRINTFORM 황홀한 얼굴로 애널로 펠라치오를 하고있는 
				ELSEIF PENIS(PLAYER) && RAND:5 == 0
					PRINTFORM 뿌리까지 집어삼킨 페니스의 크기를 애널로 맛보고 있는 
				ELSEIF RAND:4 == 0
					PRINTFORM 군침을 흘리며 허리를 움직이는 
				ELSEIF RAND:3 == 0
					PRINTFORM 허리를 흔들어 애널로 쾌감을 탐닉하고있는 
				ELSEIF RAND:2 == 0
					PRINTFORM 개발된 항문의 노예가 되어버린 
				ELSE
					PRINTFORM 촉촉한 눈동자로 %조사처리(CALLNAME:PLAYER,"를")% 바라보는 
				ENDIF
			ELSEIF EXP:Ａ경험 >= 150 || ABL:Ａ감각 >= 3
				IF PENIS(PLAYER) && ABL:봉사정신 >= 3 && RAND:4 == 0
					PRINTFORM 애널로의 봉사에 기를 쓰고 있는 
				ELSEIF RAND:3 == 0
					PRINTFORM 애널에 깊이 뭍고서 몸을 움찔거리는 
				ELSEIF RAND:2 == 0
					PRINTFORM 민감한 점막을 문질러지는 감촉을 맛보고 있는 
				ELSE
					PRINTFORM 홍조를 띈 얼굴로 %조사처리(CALLNAME:PLAYER,"를")% 바라보는 
				ENDIF
			ELSE
				IF PENIS(PLAYER) && RAND:2 == 0
					PRINTFORM 애널로 페니스를 훑고있는 
				ELSE
					PRINTFORM 뒷구멍에 파묻힌 이물의 감각에 숨을 흐트러뜨리고 있는 
				ENDIF
			ENDIF
		ENDIF
		PRINTFORM %CALLNAME:TARGET%의, 
		IF RAND:3 == 0
			IF TALENT:거유
				PRINT 엄청난 박력으로 튀는 
			ELSEIF TALENT:빈유 && TALENT:작은몸집
				PRINT 평평한 
			ELSEIF TALENT:빈유
				PRINT 귀엽게 흔들리는 
			ELSE
				PRINT 흔들리는 
			ENDIF
			PRINTFORMW 가슴을 비볐다…
		ELSEIF RAND:2 == 0
			IF TALENT:임신 && RAND:2 == 0
				PRINT 커다란 배
			ELSEIF TALENT:작은몸집
				PRINT 미발된 신체
			ELSEIF TALENT:거유
				PRINT 풍만한 신체
			ELSE
				PRINT 부드러운 신체
			ENDIF
			PRINTFORMW 를 애무했다…
		ELSE
			IF INSERT("Ｖ인서트") == 0 && TALENT:남자 == 0 && RAND:2
				PRINTFORMW 외로워보이는 보지를 괴롭혔다…
			ELSEIF PENIS(TARGET)
				PRINTFORMW 발기한 페니스를 희롱했다…
			ELSEIF CALCF("Ｃ발정") >= 2
				PRINTFORMW 충혈된 클리토리스를 희롱했다…
			ELSE
				PRINTFORMW 작은 클리토리스를 손가락의 배로 문질렀다…
			ENDIF
		ENDIF
	ELSEIF RAND:(ABL:기교 + 1) >= 2
		PRINTFORM %CALLNAME:TARGET%는
		IF RAND:2 == 0
			IF TALENT:거유
				PRINT 풍만한 
			ELSEIF TALENT:빈유 && TALENT:작은몸집
				PRINT 편평한 
			ELSEIF TALENT:빈유
				PRINT 작은 
			ENDIF
			PRINTFORMW 가슴을 강조하는 포즈로 %조사처리(CALLNAME:PLAYER,"를")% 유혹했다. 
			PRINTFORM %CALLNAME:PLAYER%는 유혹당하는 대로, %CALLNAME:TARGET%의 가슴을 
			IF TALENT:빈유 || RAND:3 == 0
				PRINTFORMW 어루만졌다…
			ELSE
				PRINTFORMW 주불렀다…
			ENDIF
		ELSE
			PRINTFORM %CALLNAME:PLAYER%에게 
			IF TALENT:작은몸집
				PRINT 미성숙한 몸
			ELSEIF TALENT:거유
				PRINT 풍만한 몸
			ELSE
				PRINT 몸
			ENDIF
			PRINTFORMW 을 기대며, 애무를 졸랐다…
		ENDIF
	ELSE
		PRINTFORM %CALLNAME:TARGET%의 
		IF TALENT:작은몸집
			PRINT 미성숙한 몸
		ELSEIF TALENT:거유
			PRINT 풍만한 몸
		ELSE
			PRINT 몸
		ENDIF
		PRINTW 을 안아올리고, 열심히 애무했다…
	ENDIF
ELSEIF TEQUIP:촉수
	PRINTFORM %CALLNAME:TARGET%의 
		SIF TALENT:작은몸집
			PRINT 미성숙한 
	PRINTW 몸 위에 갖가지 크기의 다양한 촉수가 기어다닌다…
ELSEIF TEQUIP:플라트 == 1
	IF TEQUIP:키스
		PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 키스를 하면서%CALLNAME:TARGET%의 전신을 초조하게 하듯이 애무했다…
		CALL SETFLAG, "추가키스플래그", TARGET
	ELSE
		PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% %CALLNAME:TARGET%의 전신을 초조하게 하듯이 애무했다…
	ENDIF
ELSE
	PRINTFORM %CALLNAME:PLAYER%는 
	;인서트中
	IF INSERT("합체中")
		PRINTFORM %TEXTS("합체中", PLAYER)%%CALLNAME:TARGET%의 
		IF COND("다리에약함", PLAYER) && INSERT("굴곡위") && RAND:3
			IF CHECK_CLO("발노출") && RAND:3
				PRINTFORMW %TEXTR("발가락을 핥았다", "다리에 혀를 내밀었다")%…
				CALL SETFLAG, "발핥기플래그", TARGET
			ELSE
				PRINTFORM %TEXTR("다리에 혀를 내미", "다리에 뺨을 비비", "민감한 무릎 뒤에 키스의 비를 내리")%면서
				IF TALENT:임신 && RAND:2 == 0
					PRINT 큰 배
				ELSEIF TALENT:작은몸집
					PRINT 미발달 몸
				ELSEIF TALENT:거유
					PRINT 풍만한 몸
				ELSE
					PRINT 신체
				ENDIF
				IF TALENT:임신 || RAND:3 == 0
					PRINT 를 상냥하고
				ELSEIF RAND:2
					PRINT 를 정성스럽게
				ELSE
					PRINT 를 열심히
				ENDIF
				PRINTFORMW %TEXTR("쓰다듬어 돌리고", "애무하고")%…
			ENDIF
			RETURN 1
		ELSEIF CHECK_CLO("겨드랑이노출") && COND("성벽：겨드랑이", PLAYER) && INSERT("정상위") + INSERT("굴곡위") && RAND:3
			PRINTFORM %TEXTS("겨드랑이：視覚")%
			IF RAND:3 == 0
				PRINTFORM 를 혀끝으로 간질이면서 
			ELSEIF RAND:2
				PRINTFORM 를 혀로 맛보면서 
			ELSEIF PALAM:욕정 >= 30000 && RAND:2
				PRINTFORM 에 혀를 문대 땀을 맛보면서 
			ELSE
				PRINTFORM 를 혀로 핥으면서 
			ENDIF
			IF TALENT:임신 && RAND:2 == 0
				PRINT 커다란 배
			ELSEIF TALENT:작은몸집
				PRINT 미성숙한 신체
			ELSEIF TALENT:거유
				PRINT 풍만한 신체
			ELSE
				PRINT 신체
			ENDIF
			IF TALENT:임신 || RAND:3 == 0
				PRINT 를 부드럽게 
			ELSEIF RAND:2
				PRINT 를 정성스레 
			ELSE
				PRINT 를 정신없이 
			ENDIF
			PRINTFORMW %TEXTR("주물렀다", "애무했다")%…
			CALL SETFLAG, "겨드랑이핥기플래그", TARGET
			RETURN 1
		ENDIF
	;においフェチ分岐
	ELSEIF PLAYER == MASTER && COND("성벽：냄새페티시", PLAYER) && TALENT:마음에듬 + TALENT:상사상애 && (CHOSECOM(SELECTCOM) == 0 || RAND:3 == 0)
		;腋
		IF CHECK_CLO("겨드랑이노출") && COND("성벽：겨드랑이", PLAYER) && RAND:3 == 0
			SIF TALENT:부끄럼쟁이
				PRINT 부끄러워한다
			IF RAND:2
				PRINTFORMW %CALLNAME:TARGET%의 %TEXTS("겨드랑이")%에 코를 접근해, 땀의%조사처리(TEXTS("匂い"),"를")% 즐기고 나서 혀로 빨아냈다…
				CALL SETFLAG, "겨드랑이핥기플래그", TARGET
				RETURN 1
			ELSE
				PRINTFORM %CALLNAME:TARGET%의 %TEXTS("겨드랑이")%의 %조사처리(TEXTS("匂い"),"를")% 냄새 맡으면서,
			ENDIF
		;汗ばんだ…とりあえず욕정かな
		ELSEIF PALAM:욕정 >= 3000 && RAND:2
			SIF TALENT:부끄럼쟁이
				PRINT 부끄러워한다
			IF RAND:3 == 0
				PRINTFORM %CALLNAME:TARGET%의 체취를 냄새 맡으면서, 땀흘린 신체를%TEXTR("쓰다듬어 돌리고", "애무하고")%있다…
			ELSEIF RAND:2
				PRINTFORMW %CALLNAME:TARGET%의 전신을 애무해, 땀흘린 신체의%조사처리(TEXTS("匂い"),"를")% 즐겼다…
			ELSE
				PRINTFORM %CALLNAME:TARGET%의
				IF CHECK_CLO("겨드랑이노출") && COND("성벽：겨드랑이", PLAYER) && RAND:3 == 0
					PRINTFORM %TEXTS("겨드랑이")%의%TEXTS("匂い")%
				ELSEIF COND("다리 사용") && COND("신발の着用") == 0 && RAND:2
					PRINTFORM 발의 %TEXTS("匂い")%
				ELSEIF CHECK_OUTER("바지") == 0
					PRINTFORM 사타구니의%TEXTS("匂い")%
				ELSE
					PRINTFORM 머리카락의 냄새
				ENDIF
				PRINTFORMW 를 냄새 맡으면서 상냥하게 %TEXTR("쓰다듬어 돌리고", "애무하고")%.
				PRINTFORMW %조사처리(CALLNAME:PLAYER,"가")% 전신을 자극할 정도로, 숨이 막히는 것 같은 열띈 %조사처리(TEXTS("匂い"),"가")% 솟아올라 온다…
			ENDIF
			RETURN 1
		;머리카락は多分みんなある！
		ELSE
			IF RAND:2
				PRINTFORM %타겟을()% 껴안아, 머리카락의 냄새를 맡으면서
			ELSE
				PRINTFORM %CALLNAME:TARGET%의 머리카락의 냄새를 맡으면서,
			ENDIF
		ENDIF
	ELSEIF CHECK_CLO("겨드랑이노출") && COND("성벽：겨드랑이", PLAYER) && RAND:3
		PRINTFORM %CALLNAME:TARGET%의 %TEXTS("겨드랑이：視覚")%
		IF RAND:3 == 0
			PRINTFORM 를 혀끝으로 간질이면서, 
		ELSEIF RAND:2
			PRINTFORM 를 혀로 맛보면서, 
		ELSEIF PALAM:욕정 >= 30000 && RAND:2
			PRINTFORM 에 혀를 문대 땀을 맛보면서, 
		ELSE
			PRINTFORM 를 혀로 핥으면서, 
		ENDIF
		CALL SETFLAG, "겨드랑이핥기플래그", TARGET
	ELSE
		IF CHECK_COM("키스") || TEQUIP:키스
			PRINTFORM %TEXTS("키스継続")%
			CALL SETFLAG, "추가키스플래그", TARGET
		ENDIF
		IF RAND:3 == 0
			IF CHECK_SHIRT("일본전통옷")
				PRINT 기모노의 겹쳐진 부분에 손을 집어넣어, 
			ELSEIF CHECK_SHIRT("차이나드레스")
				PRINT 차이나드레스의 슬릿에 손을 기어넣어, 
			ELSEIF CHECK_SHIRT("세라복")
				PRINT 배의 틈새에 손을 집어넣어, 
			ELSEIF CHECK_SHIRT("베이비돌")
				PRINT 얇은 베이비돌 너머로, 
			ELSEIF CHECK_SHIRT("Ｙ셔츠")
				PRINT Ｙ셔츠의 틈새에 손을 집어넣어, 
			ELSEIF CHECK_BODYS("학교수영복")
				PRINT 물을 빼는 구멍에 손을 찔러넣어, 
			ELSEIF CHECK_BODYS("エナメル")
				PRINT 미끈미끈해진 슈츠에 손을 기게해, 
			ELSEIF CHECK_SHIRT("갑옷")
				PRINT 갑옷의 틈새에 손을 기게해, 
			ELSEIF EQUIP:겉옷 && !CHECK_OUTER("바지")
				PRINT 희미하게 땀에 젖은 허벅다리에 손을 기게해, 
			ENDIF
		ENDIF
		PRINTFORM %CALLNAME:TARGET%의 
	ENDIF

	;임신中 ？
	IF TALENT:임신 && RAND:2 == 0
		PRINT 커다란 배
	ELSEIF TALENT:작은몸집
		PRINT 미성숙한 신체
	ELSEIF TALENT:거유
		PRINT 풍만한 신체
	ELSE
		PRINT 신체
	ENDIF
	;엉덩이好きな당신へ
	IF COND("성벽：엉덩이", PLAYER) && INSERT("정상위") + INSERT("굴곡위") + INSERT("배면좌위") == 0 && RAND:3
		PRINTFORM 와 %TEXTS("엉덩이：감촉")%
	;モノは試しの성감대突き. 専用커맨드に分化するのも良いかも. 
	;角 
	ELSEIF TALENT:뿔 && RANDIF(3, TALENT:구지배자의자취, TALENT:동물귀, TALENT:머메이드, TALENT:꼬리, TALENT:날개)
		SELECTCASE TALENT:뿔
		CASE 5
			PRINT 와 기다란 뿔
		CASE 9
			PRINT 와 작은 뿔
		CASEELSE
			PRINT 와 매끈매끈한 뿔
		ENDSELECT
	;날개or날개
	ELSEIF TALENT:날개 && RANDIF(3, TALENT:구지배자의자취, TALENT:동물귀, TALENT:머메이드, TALENT:꼬리)
		IF RAND:2 == 0
			PRINTFORM 와 민감해보이는 %TALENT_NAME(161, TALENT:날개)%의 뿌리
		ELSE
			SELECTCASE TALENT:날개
			CASE 6
				PRINTFORM 와 박쥐같은 날개날개
			CASE 10
				PRINTFORM 와 나비같은 날개
			CASE 12
				PRINTFORM 와 천사의 품격을 갖춘 날개
			;허니비
			CASE 13
				PRINTFORM 와 벌레와 같이 얇다날개
			CASEELSE
				PRINTFORM 와 맹금같은 날개
			ENDSELECT
		ENDIF
	;꼬리
	ELSEIF TALENT:꼬리 && RANDIF(3, TALENT:구지배자의자취, TALENT:동물귀, TALENT:머메이드)
		SELECTCASE TALENT:꼬리
		;악마
		CASE 6
			IF RAND:2 == 0
				PRINT 와 나긋나긋하게 움직이는 꼬리
			ELSE
				PRINT 와 하트모양의 꼬리 끝
			ENDIF
		;도마뱀or자바워크
		CASE 7, 8
			;멜로나
			IF BASE:종족 == 35 && RAND:2
				PRINT 와 끝이 타오르는 꼬리
			ELSEIF RAND:2 == 0
				PRINT 와 하늘하늘 움직이는 꼬리
			ELSE
				PRINT 와 비늘에 덮인 꼬리
			ENDIF
		;허니비
		CASE 13
			PRINTFORM 줄무늬 모양의 벌의 배
		CASEELSE
			;토끼
			IF TALENT:꼬리 == 4 || RAND:2
				PRINT 와 복실복실한 꼬리
			ELSE
				IF TALENT:꼬리 == 2 || TALENT:꼬리 == 3 || TALENT:꼬리 == 5
					PRINT 와 파닥파닥 움직이는 꼬리
				ELSE
					PRINT 와 나긋나긋 움직이는 꼬리
				ENDIF
			ENDIF
		ENDSELECT
	;ヒレ？鱗？
	ELSEIF TALENT:머메이드 && RANDIF(3, TALENT:구지배자의자취, TALENT:동물귀)
		PRINT 와 민감해보이는 지느러미
	;동물耳
	ELSEIF TALENT:동물귀 && RANDIF(3, TALENT:구지배자의자취)
		IF RAND:2 == 0
			PRINT 과
		ELSEIF RAND:2 == 0
			PRINT 와 곱신곱신 움직이는 
		ELSE
			PRINT 와 손대지않고는 버틸 수 없는 
		ENDIF

		SELECTCASE TALENT:동물귀
		CASE 1
			PRINT 고양이귀
		CASE 2
			PRINT 강아지귀
		CASE 3
			PRINT 여우귀
		CASE 4
			PRINT 토끼귀
		CASE 5
			PRINT 망아지귀
		CASEELSE
			PRINT 동물귀
		ENDSELECT
	;촉수
	ELSEIF TALENT:구지배자의자취 && RAND:3 == 0
		PRINT 와 미끈미끈한 촉수
	ENDIF

	IF TALENT:임신 || RAND:3 == 0
		PRINT 를 상냥하게 
	ELSEIF RAND:2
		PRINT 를 정성스럽게 
	ELSE
		PRINT 를 정신없이
	ENDIF
	IF PREVCOM == SELECTCOM
		PRINTFORMW %TEXTR("계속 주물렀다", "계속 애무했다")%…
	ELSE
		PRINTFORMW %TEXTR("주물렀다", "애무했다")%…
	ENDIF
ENDIF
