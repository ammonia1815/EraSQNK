@COM6
;키스
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 6

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM6, ARG
;대상の입⇔조교자の입の汚れが移動
CALL EQ_STAIN, TARGET, PLAYER, 0, 0


@SOURCE_COM6, ARG
VARSET LOCAL

LOCAL:20 += 10
LOCAL:21 += 75

;汚れデータを元にする
LOCAL:8 = COM_ORDER_STAIN("Ｍ", "Ｍ")*100 + 100

LOCAL:13 = 100

;정애のソース
LOCAL:3 = 300

;ABL:Ｍ감각をみる
LOCAL:18 = CALCF("Ｍ刺激", 1)
;조교자の혀놀림をみる
LOCAL:18 = SOURCE_EXABL(LOCAL:18, 30, PLAYER)

;텐더키스
IF NUM("턴수") == 0
	TIMES LOCAL:18, 0.10
	LOCAL:3 += 700
ENDIF

;ABL:봉사정신をみる
SELECTCASE ABL:봉사정신
CASE 0
	LOCAL:4 = 50
	LOCAL:5 = 0
CASE 1
	LOCAL:4 = 150
	LOCAL:5 = 50
CASE 2
	LOCAL:4 = 200
	LOCAL:5 = 100
CASE 3
	LOCAL:4 = 250
	LOCAL:5 = 180
CASE 4
	LOCAL:4 = 300
	LOCAL:5 = 300
CASEELSE
	LOCAL:4 = 350
	LOCAL:5 = 500
ENDSELECT

;키스をどのぐらい続けたかでボーナス
LOCAL:20 += 10 * TEQUIP:키스
LOCAL:21 += 25 * TEQUIP:키스

LOCAL:3 += 200 * TEQUIP:키스
LOCAL:4 += 200 * TEQUIP:키스
LOCAL:5 += 100 * TEQUIP:키스
LOCAL:13 += 100 * TEQUIP:키스

;ABL:기교をみる
SELECTCASE ABL:기교
CASE 0
	TIMES LOCAL:4, 0.50
	TIMES LOCAL:5, 0.50
CASE 1
	TIMES LOCAL:4, 0.80
	TIMES LOCAL:5, 0.80
CASE 2
	TIMES LOCAL:4, 1.00
	TIMES LOCAL:5, 1.00
CASE 3
	TIMES LOCAL:4, 1.20
	TIMES LOCAL:5, 1.20
CASE 4
	TIMES LOCAL:4, 1.50
	TIMES LOCAL:5, 1.50
CASEELSE
	TIMES LOCAL:4, 2.00
	TIMES LOCAL:5, 2.00
ENDSELECT

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM6
;텐더키스
IF NUM("턴수") == 0
	IF TEQUIP:넘어뜨리기
		PRINTFORM %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:PLAYER%의 뺨에 손을 대고, 부드럽게 
	ELSE
		SIF PLAYER != MASTER
			PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% 
		;상사상애
		IF TALENT:상사상애 &&  PLAYER == MASTER && RAND:3 == 0
			PRINTFORM %CALLNAME:TARGET%에게 사랑을 속삭이면서 상냥하게 
		ELSEIF TALENT:작은몸집 && RAND:2 == 0
			PRINTFORM %CALLNAME:TARGET%의 머리를 쓰다듬으면서, 살짝 
		ELSE
			PRINTFORM %조사처리(CALLNAME:TARGET,"를")% 끌어안고, 부드럽게 
		ENDIF
	ENDIF
	IF RAND:2 == 0
		PRINTFORMW 키스했다…
	ELSE
		PRINTFORMW 입술을 겹쳤다…
	ENDIF
;넘어뜨리기 중
ELSEIF TEQUIP:넘어뜨리기
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	;조수が前回と同じ行動(違う行動の時は, ここでなく, EQUIP_COM32で記述)
	SIF TEQUIP:Ｗ넘어뜨리기 && SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM %TEXTS("Ｗ넘어뜨리기 중")%
	IF TEQUIP:키스
		IF TEQUIP:Ｗ넘어뜨리기 == 0
			PRINTFORMW 공기를 찾아 허덕이는 %CALLNAME:PLAYER%의 입술을 놓치지 않고, 다시 키스로 입을 막아버렸다…
		ELSE
			PRINTFORMW %조사처리(CALLNAME:PLAYER,"과")% 정신없이 키스를 하고있다…
		ENDIF
	ELSEIF INSERT("합체中") && TEQUIP:Ｗ넘어뜨리기 == 0
		PRINTFORM %TEXTS("합체中", TARGET)%
		IF (ABL:욕망 >= 4 || TALENT:강기) && RAND:2
			PRINTFORMW %CALLNAME:PLAYER%의 입술을 빼앗았다…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%에게 키스를 했다…
		ENDIF
	ELSE
		PRINTFORM %CALLNAME:PLAYER%의 입술을 빼앗아, 
		IF PREVPLAY("飲尿") && CHECK_STAIN(STAIN:Ｍ, "尿") && ABL:새드끼 >= 3
			PRINTFORMW 조금 전의 답례라고 할듯이 뇨 섞인 타액을 흘려 넣어 왔다…
		ELSEIF (PREVCOM == 91 || PREVCOM == 99 || PREVCOM == 140) && CHECK_STAIN(STAIN:Ｍ, "정액") && ABL:새드끼 >= 3
			PRINTFORMW 혀를 섞으면서 입에 모은 정액을 입을 이용해서 먹여주었다…
		ELSE
			IF RAND:2 == 0
				PRINTFORMW 혀를 엮으며 입술을 탐했다…
			ELSE
				PRINTFORMW 타액을 흘려넣었다…
			ENDIF
		ENDIF
	ENDIF
;키스継続中
ELSEIF TEQUIP:키스 && COND("前Ｔ조교자") == PLAYER
	PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% %CALLNAME:TARGET%
	IF INSERT("정상위")
		PRINTFORM 에게 허리를 밀어붙이면서, 입술로 쪼아대듯이 
	ELSEIF INSERT("대면좌위")
		PRINTFORM 에게 안긴채로, 허리를 흔들면서 
	ELSE
		IF TALENT:상사상애 && PLAYER == MASTER && RAND:2
			PRINTFORMW %조사만처리(CALLNAME:TARGET,"과")% 서로 사랑을 확인하듯이 
		ELSE
			PRINTFORM %조사만처리(CALLNAME:TARGET,"를")% 껴안고, 
			IF RAND:3 == 0
				PRINT 서로의입안을 혀끝에서 핥아대면서
			ELSEIF RAND:2
				PRINT 혀를 얽어 타액을 교환하듯이 
			ELSE
				PRINT 입의 주위를 끈적끈적하면서
			ENDIF
		ENDIF
	ENDIF
	PRINTFORMW 키스를 계속했다…
;인서트中
ELSEIF INSERT("합체中")
	PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% %TEXTS("합체中", PLAYER)% %CALLNAME:TARGET%
	IF INSERT("삽입中") && CONDS("체위", PLAYER) != "기승위"
		PRINTFORMW 에게 키스를 졸랐다…
	ELSE
		PRINTFORMW 의 입술을 빼앗았다…
	ENDIF
ELSEIF TEQUIP:러브트랩
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 러브트랩으로 움직일 수 없게된 %CALLNAME:PLAYER%
	;새드끼Lv3
	IF ABL:새드끼 >= 3 && RAND:3 == 0
		PRINTFORMW %조사만처리(CALLNAME:PLAYER,"과")%의 키스를 즐기고있다…
	ELSEIF RAND:2 == 0
		PRINTFORMW 의 혀를 소리가 나도록 빨고있다…
	ELSE
		PRINTFORMW 의 입술을 마음껏 만끽했다…
	ENDIF
ELSE
	IF PREVPLAY("飲尿")
		PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% 음뇨 플레이 직후의 오줌냄새나는 숨을 내쉬면서 호흡을 정돈했다
	ELSEIF COND("前回펠라")
		PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% 페니스를 빠는 것을 중단시키고, 
	ELSEIF COND("前Ｔ조교자") == PLAYER && (PREVCOM == 98 || PREVCOM == 99)
		PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% 보지에 봉사하는 것을 그만두게하고, 
	ELSEIF COND("前Ｔ조교자") == PLAYER && PREVCOM == 96
		PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% 엉덩이구멍에 봉사를 하던 
	ELSEIF PLAYER != MASTER
		PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% 
	ENDIF
	PRINTFORM %CALLNAME:TARGET%
	IF RAND:4 == 0
		IF COND("前回펠라") || (COND("前Ｔ조교자") == PLAYER && (PREVCOM == 96 || PREVCOM == 98 || PREVCOM == 99)) && RAND:4 == 0
			PRINT 의 혀놀림을 칭찬하면서, 부드럽게
		ELSEIF RAND:3 == 0
			PRINT 의 머리를 쓰다듬으면서 
		ELSEIF RAND:2
			PRINT 의 머리카락을 쓰다듬으면서 
		ELSE
			PRINT 의 얼굴에 손을 얹고 
		ENDIF
		PRINTW 키스를 했다…
	ELSEIF RAND:3
		PRINTW 의 입술을 덮고 타액을 흘려넣었다…
	ELSEIF RAND:2
		PRINTW 의 입안을 구석구석까지 핥아주었다…
	ELSE
		PRINTFORMW %조사만처리(CALLNAME:TARGET,"를")% 껴안고 키스했다…
	ENDIF
ENDIF

IF INSERT("인서트") && ABL:순종 >= 3 && ABL:욕망 >= 3 && ABL:Ｖ감각 >= 3 && PREVCOM >= 66 && PREVCOM <= 68
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 민감하 장소
	IF RAND:2 == 0
		PRINTFORM 를 습격하는 쾌감에 콧김을 거칠게 하면서, 
	ELSE
		PRINTFORM 를 공격받을 때마다 달콤한 신음을 흘리면서, 
	ENDIF
	IF TEQUIP:넘어뜨리기 || RAND:3 == 0
		PRINTFORMW 정열적으로 %조사처리(CALLNAME:PLAYER,"를")% 갈구하고있다…
	ELSEIF RAND:2
		PRINTFORMW 혀를 얽어온다…
	ELSE
		PRINTFORMW 키스에 응하고있다…
	ENDIF
ELSEIF TEQUIP:넘어뜨리기 == 0 && ABL:순종 >= 2 && (ABL:순종 >= 3 || ABL:Ｍ감각 + ABL:PLAYER:혀놀림 >= 2)
	;テンダー키스
	IF NUM("턴수") == 0
		PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
		IF RAND:2 == 0
			PRINTFORM 눈을 감고 
		ELSE
			PRINTFORM 작게 한숨을 흘리면서 
		ENDIF
		IF RAND:3 == 0
			PRINTFORMW , %CALLNAME:PLAYER%에게 몸을 맡기고있다…
		ELSEIF RAND:2 == 0
			PRINTFORMW , 뺨을 새빨갛게 물들이면서 %CALLNAME:PLAYER%에게 응해왔다…
		ELSE
			PRINTFORMW 황홀해하고있다…
		ENDIF
	ELSEIF TALENT:작은몸집 && TALENT:PLAYER:장신 && INSERT("인서트") + INSERT("삽입中") == 0 && RAND:2
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 신장차를 부들부들 아슬아슬한 까치발로 극복하면서, %CALLNAME:PLAYER%에게 맞춰 적극적으로 혀를 엮어왔다…
	ELSE
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:PLAYER%에게 답하듯이, 적극적으로 혀를 섞고있다…
	ENDIF
ENDIF


@ADD_KISS
SIF TEQUIP:키스 == 0
	RETURN 0

CALL SOURCE_COM6, 30
CALL FLAG_COM6, 30

RETURN 1

@ADD_KISS_2
SIF TEQUIP:키스 == 0
	RETURN 0

PRINTFORML ＜틈만 나면 키스中＞

IF TEQUIP:넘어뜨리기
	LOCAL = MIN(LIMIT(TEQUIP:키스 - 1, 0, ABL:혀놀림) + TALENT:음탕한입술, 4) / 2
ELSE
	LOCAL = MIN(LIMIT(TEQUIP:키스 - 1, 0, ABL:PLAYER:혀놀림) + TALENT:음탕한입술, 4) / 2
ENDIF

EXP:키스경험 += 1 + LOCAL
EXP:PLAYER:키스경험 += 1 + LOCAL
PRINTFORML 키스경험＋{1 + LOCAL}
PRINTFORML 키스경험＋{1 + LOCAL}（%CALLNAME:PLAYER%）

@COM_ORDER_COM6, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF
;정조관결여
IF TALENT:정조관결여
	LOCAL += 20
	LOCALS = %LOCALS% + %TALENTNAME:31%(20)
ENDIF
;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 3
	LOCALS = %LOCALS% - %TALENTNAME:35%(3)
ENDIF
;헌신的
IF TALENT:헌신
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF
;연모
IF TALENT:연모 && ASSIPLAY == 0
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:85%(5)
ENDIF
;Ｍ민감
IF TALENT:Ｍ민감
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:97%(5)
ENDIF

;불결있음
IF COM_ORDER_STAIN("Ｍ", "Ｍ")
	LOCAL -= COM_ORDER_STAIN("Ｍ", "Ｍ")
	LOCALS = %LOCALS% - 불결있음
	;악취둔감
	SIF TALENT:악취둔감 || TALENT:악취민감
		LOCALS = %LOCALS%, \@TALENT:악취둔감 ? %TALENTNAME:61% # %TALENTNAME:62%\@
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｍ", "Ｍ")})
ENDIF

;合計を表示(15이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 15

RETURN RESULT
