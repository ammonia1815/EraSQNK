@COM151
;요도확장기
;何度も実行したくないので, 消耗は激しいが効果は高い커맨드にしたい
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 151

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

RETURN 1

@FLAG_COM151, ARG
;尿の汚れ発生
IF PENIS(TARGET)
	STAIN:Ｐ = SET_STAIN(STAIN:Ｐ, "尿")
ELSE
	STAIN:Ｖ = SET_STAIN(STAIN:Ｖ, "尿")
ENDIF
;팬티をずらす
SIF !CHECK_CLO("Ｖ노출") && INSERT("인서트") == 0
	CALL SETFLAG, "바텀을노출：텍스트없음", TARGET


@SOURCE_COM151, ARG
VARSET LOCAL

;くぱぁ
CALL SOURCE_COM13, 50

;消耗はかなり激しい
LOCAL:20 += 100 * (10 + MAX(TEQUIP:요도확장기 - MIN_STR(LOCALS), 0)*2 ) / 10
LOCAL:21 += 150 * (10 + MAX(TEQUIP:요도확장기 - MIN_STR(LOCALS), 0)*2 ) / 10

LOCAL:8 = 1000
LOCAL:11 = 500 + TEQUIP:요도확장기*100
LOCAL:12 = 2000
LOCAL:13 = 5000
LOCAL:14 = 5000 + TEQUIP:요도확장기*100

;どっちの요도か
LOCALS = %CONDS("요도확장度")%
IF PENIS(TARGET)
	LOCAL:6 = 2500
	LOCAL:9 = 2500
ELSE
	LOCAL:6 = 500
	LOCAL:9 = 500
ENDIF

;ABL:Ｃ감각をみる
LOCAL:0 = CALCF("요도刺激", 0)
;これは凹凸もないし快楽用の도구ではない
TIMES LOCAL:0, 0.20
CALL SETFLAG, "요도성감플래그", TARGET

;初めて経験する太さは痛い. ２回目以降は軽減
IF TEQUIP:요도확장기 > MAX_STR(LOCALS)
	LOCAL:6 += 500
	LOCAL:9 += 500
ENDIF

;아픔(LOCAL:6)と공포(LOCAL:9)は, 主に페니스の有無, TEQUIP:요도확장기とMIN_STR(LOCALS)依存
;요도성감についてはここの最後で大きな보정あり
LOCAL:6 += MAX(TEQUIP:요도확장기 - MIN_STR(LOCALS), 0) * 500
LOCAL:9 += MAX(TEQUIP:요도확장기 - MIN_STR(LOCALS), 0) * 1000

SELECTCASE TEQUIP:요도확장기 - MIN_STR(LOCALS)
CASE 1
	TIMES LOCAL:0, 0.80
	TIMES LOCAL:6, 1.20
	TIMES LOCAL:9, 1.50
CASE 2
	TIMES LOCAL:0, 0.50
	TIMES LOCAL:6, 1.50
	TIMES LOCAL:9, 2.00
CASE 3
	TIMES LOCAL:0, 0.30
	TIMES LOCAL:6, 2.00
	TIMES LOCAL:9, 3.00
CASE 4
	TIMES LOCAL:0, 0.15
	TIMES LOCAL:6, 2.50
	TIMES LOCAL:9, 4.00
CASE IS >= 5
	TIMES LOCAL:0, 0.05
	TIMES LOCAL:6, 3.00
	TIMES LOCAL:9, 5.00
ENDSELECT

;요도확장경험が5, 20, 50, 150…と区切りを越えて大きいなら, 공포を軽減する
SELECTCASE GET_EXP_LV(EXP:요도확장경험) - (TEQUIP:요도확장기 - MIN_STR(LOCALS) )
CASE 1
	TIMES LOCAL:9, 0.50
CASE 2
	TIMES LOCAL:9, 0.30
CASE IS >= 3
	TIMES LOCAL:9, 0.15
ENDSELECT

SELECTCASE EXP:요도경험 + MIN(EXP:PLAYER:요도고문경험, 50)
CASE IS < 5
CASE IS < 20
	TIMES LOCAL:6, 0.80
	TIMES LOCAL:9, 0.80
CASE IS < 50
	TIMES LOCAL:0, 1.20
	TIMES LOCAL:6, 0.60
	TIMES LOCAL:9, 0.60
CASE IS < 150
	TIMES LOCAL:0, 1.35
	TIMES LOCAL:6, 0.40
	TIMES LOCAL:9, 0.40
CASEELSE
	TIMES LOCAL:0, 1.50
	TIMES LOCAL:6, 0.20
	TIMES LOCAL:9, 0.20
ENDSELECT

;PALAM:욕정をみる
SELECTCASE PALAM:욕정
CASE IS < PALAMLV:1
	TIMES LOCAL:14, 1.50
CASE IS < PALAMLV:2
	TIMES LOCAL:14, 1.00
CASE IS < PALAMLV:3
	TIMES LOCAL:14, 0.90
CASE IS < PALAMLV:4
	TIMES LOCAL:14, 0.70
CASE IS >= PALAMLV:4
	TIMES LOCAL:14, 0.50
ENDSELECT

;ABL:마조끼をみる
SELECTCASE ABL:마조끼
CASE 0
	TIMES LOCAL:11, 0.05
CASE 1
	TIMES LOCAL:9, 0.90
	TIMES LOCAL:11, 0.50
CASE 2
	TIMES LOCAL:9, 0.80
CASE 3
	TIMES LOCAL:9, 0.70
	TIMES LOCAL:11, 1.50
CASE 4
	TIMES LOCAL:9, 0.60
	TIMES LOCAL:11, 2.00
CASEELSE
	TIMES LOCAL:9, 0.50
	TIMES LOCAL:11, 3.00
ENDSELECT

;쾌Ｃについては既に参照済み
IF TALENT:요도광
	TIMES LOCAL:6, 0.10
	TIMES LOCAL:9, 0.10
	LOCAL:7 += 10000 + SIZE("페니스の直径", PLAYER)*200
	LOCAL:11 += LOCAL:14
	TIMES LOCAL:14, 0.05
ELSEIF TALENT:요도성감
	TIMES LOCAL:6, 0.20
	TIMES LOCAL:9, 0.20
	LOCAL:7 += 5000 + SIZE("페니스の直径", PLAYER)*100
	LOCAL:11 += LOCAL:14
	TIMES LOCAL:14, 0.20
ENDIF

SELECTCASE EXP:방뇨경험 + EXP:실금경험
CASE IS < EXPLV:1
	TIMES LOCAL:14, 3.00
CASE IS < EXPLV:2
	TIMES LOCAL:14, 2.50
CASE IS < EXPLV:3
	TIMES LOCAL:14, 2.00
CASE IS < EXPLV:4
	TIMES LOCAL:14, 1.00
CASE IS < EXPLV:5
	TIMES LOCAL:14, 0.80
CASEELSE
	TIMES LOCAL:14, 0.60
ENDSELECT

;근육이완제
SIF GETBIT(TEQUIP:근육이완제, 1)
	TIMES LOCAL:6, 0.20

;連続実行だと少々お得
IF PREVCOM == SELECTCOM
	TIMES LOCAL:0, 1.50
	TIMES LOCAL:6, 1.50
	TIMES LOCAL:8, 1.50
	TIMES LOCAL:9, 1.50
	TIMES LOCAL:11, 1.50
	TIMES LOCAL:12, 1.50
	TIMES LOCAL:13, 1.50
	TIMES LOCAL:14, 1.50
ENDIF

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


;요도확장기の太さを선택する
@SELECT_BOUGIE
#DIM LCOUNT
#DIM SELECT_BOUGIE
#DIM BOUGIE, 10
#DIM CHOICE

;どっちの요도か
LOCALS = %CONDS("요도확장度")%

;今までに経験した太さが、아픔のある太さかどうか。最小*1.05…{MIN_STR(LOCALS)*21/20}mmまでなら아픔は少ない
;例えば, LOCALS = 10/10/10/9/9/9/9/8/8/8 ならMAX_STR(LOCALS) = 10, CALCF("요도径") = 9, MIN_STR(LOCALS) = 8 となる
;ちなみにMAX_STR(LOCALS) = MIN_STR(LOCALS) + 1
;初回
IF FIRSTCOM(SELECTCOM) == 0
	PRINTFORML %타겟은()%, 한번도 브지를 받아들였던 적이 없습니다
	PRINTFORML 이번은{MIN_STR(LOCALS)}mm, 굵어도{MIN_STR(LOCALS) + 1}mm를 시험해 보는 것이 무난하다고 생각됩니다
	PRINTFORML ……어느 굵기의 브지를 사용합니까？
;ブジーよりも太いものを入れられたことがありそう
ELSEIF MAX_STR(LOCALS) > CFLAG:452
	PRINTFORML %CALLNAME:TARGET%에 사용한 브지로 제일 굵은 것은{CFLAG:452}mm이지만……그것보다 느슨해진듯 합니다
	PRINTFORML ……어느 굵기의 브지를 사용합니까？
ELSE
	SELECTCASE MAX_STR(LOCALS) - MIN_STR(LOCALS)*21/20
	CASE IS >= 1
		PRINTFORML %타겟은()% 직경{MAX(MAX_STR(LOCALS), CFLAG:452)}mm까지 경험이 끝난 상태입니다만, 아픔이 적은 것은{MIN_STR(LOCALS)*21/20}mm까지와 같습니다
		PRINTFORML {MAX(MIN_STR(LOCALS)*21/20, MIN_STR(LOCALS) + 1)}mm이하의 굵기가 좋다고 생각됩니다만……\@ PREVCOM == SELECTCOM ? 이어서 # 이번은 \@, 어느 굵기의 브지를 사용합니까？
	CASE 0
		PRINTFORML %타겟은()% 직경{MAX(MAX_STR(LOCALS), CFLAG:452)}mm까지 경험이 끝난 상태로, 여기까지라면아픔하지만 적은 것 같습니다
		PRINTFORML {MAX(MAX_STR(LOCALS), CFLAG:452)}mm이하의 굵기가 좋다고 생각됩니다만……\@ PREVCOM == SELECTCOM ? 이어서 # 이번은 \@, 어느 굵기의 브지를 사용합니까？
	CASEELSE
		PRINTFORML %타겟은()% 직경{MAX(MAX_STR(LOCALS), CFLAG:452)}mm까지 경험이 끝난 상태로, 이 굵기까지는 괜찮은 것 같습니다
		PRINTFORML {MAX_STR(LOCALS) + 1}mm에 도전해도 문제 없다고 생각되지만……\@ PREVCOM == SELECTCOM ? 이어서 # 이번에는 \@, 어떤 굵기의 부지를 사용합니까?
	ENDSELECT
ENDIF

;連続実行だと２倍行う
IF PREVCOM == SELECTCOM
	SETCOLOR DEF_COLOR("황색")
	PRINTFORML ※동일명령연속실행 때문에, %CALLNAME:TARGET%의 요도를 꼼꼼히 괴롭힙니다※
	RESETCOLOR
;肉体的な限界がある
ELSEIF CALCF("요도径") >= CALCF("요도확장の壁")
	SETCOLOR DEF_COLOR("황색")
	IF CALCF("요도径") >= CALCF("요도확장の限界")
		PRINTFORML ※%CALLNAME:TARGET%의 요도는 한계입니다……더 이상의 확장은 거의 무리이겠죠※
	ELSE
		PRINTFORML ※%CALLNAME:TARGET%의 요도는 한계에 가까워지고 있습니다……더 이상 확장은 힘들 것 같습니다※
	ENDIF

	SETCOLOR DEF_COLOR("하트핑크")
	;ちょっとお得な情報
	CALL COM_ABLE146
	IF RESULT && COND("身嗜み：요도확장") == 0 && ABL:요도확장 >= 0 && EXP:요도경험 >= 5 && (EXP:요도경험 >= 20 || EXP:요도확장경험 >= 5 || ABL:요도확장 >= 1)
		PRINTFORML 몸관리를 지시하는 일로, 요도의 확장을%CALLNAME:TARGET%에도 실시하게 할 수가 있습니다
		PRINTFORML %CALLNAME:PLAYER%의 손에는 미치지 않다고 생각됩니다만, 어떻습니까？
	ENDIF
	RESETCOLOR
ENDIF
PRINTL 

;MIN_STR(LOCALS)mmの太さは확장という点では弱いが, 요도경험は増えるので一応意味はある
SELECT_BOUGIE = MAX(MAX_STR(LOCALS) - MIN_STR(LOCALS), 3) + MIN_STR(LOCALS)/20
SIF ABL:PLAYER:새드끼 >= 5 || ABL:마조끼 >= 5
	SELECT_BOUGIE += 1
SIF ABL:PLAYER:새드끼 >= 3
	SELECT_BOUGIE += 1
;조교지식によるボーナス
SIF COND("성기확장지식", PLAYER)
	SELECT_BOUGIE += 1
;太さの1.2倍までとする…최대치があまりに低い場合は３保障
SELECT_BOUGIE = MAX(LIMIT(SELECT_BOUGIE, 3, MIN_STR(LOCALS)/5 + 1), 3, MIN_STR(LOCALS)/10 + 1)

VARSET LOCAL
VARSET BOUGIE

FOR LCOUNT, 0, SELECT_BOUGIE
	;선택지が多すぎる場合には削る
	SELECTCASE MIN_STR(LOCALS) + LCOUNT
	CASE IS >= 40
		SIF SELECT_BOUGIE >= 10 && (MIN_STR(LOCALS) + LCOUNT)%5
			CONTINUE
	ENDSELECT

	BOUGIE:LOCAL = MIN_STR(LOCALS) + LCOUNT
	PRINTFORML  [{LOCAL, 2}] {BOUGIE:LOCAL, 2}mm

	LOCAL += 1
NEXT

;클리캡と니플캡を付けるか
IF TEQUIP:클리캡 == 0 && FIRSTCOM(34)
	CALL COM_ABLE34
	IF RESULT
		LOCAL:90 = 90
		PRINTFORML  [90] 클리캡를 붙인다
	ENDIF
ENDIF
IF TEQUIP:니플캡 == 0 && FIRSTCOM(35)
	CALL COM_ABLE35
	IF RESULT
		LOCAL:91 = 91
		PRINTFORML  [91] 니플캡를 붙인다
	ENDIF
ENDIF
PRINTFORML  [99] 보발를 본다
PRINTFORML [100] 그만둔다

CALL INPUT_SELECT, LOCAL, 99, 100, LOCAL:90, LOCAL:91

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	TFLAG:14 = 1
	RETURN 0
CASE 99
	PRINTFORML □ 보충 □---------------------------------------------------------------------
	PRINTFORM 요도를 확장할 때에는 , 
	CALL PRINT_COLORTEXT, "[조교지식(성기확장)]", DEF_COLOR("황색")
	PRINTFORM 과
	CALL PRINT_COLORTEXT, "[확장적정]", DEF_COLOR("황색")
	PRINTFORML 이 있으면 수월합니다
	PRINTFORM 확장의 하기 쉬움에는 영향이 없는 소질이지만,
	CALL PRINT_COLORTEXT, "[도착적]", DEF_COLOR("황색")
	PRINTFORML 이 있으면 반감을 적게 받지요
	PRINTFORML 

	PRINTFORML 요도에서의 쾌감은 Ｃ감각에 의존하고 있습니다……하지만, 초기는 아픔이 강하고 대부분 쾌락을 얻을 수 없습니다
	PRINTFORML 경험을 쌓게해 아픔를 줄여, 요도를 만지작거려지는 일에 익숙하게 하는 것이 필요합니다
	PRINTFORM 요도를 자극하면서 절정을 경험시키것으로 
	CALL PRINT_COLORTEXT, "[요도성감]", DEF_COLOR("황색")
	PRINTFORML 을 각성되는 일이 됩니다
	CALL PRINT_COLORTEXT, "[요도성감]", DEF_COLOR("황색")
	PRINTFORMW 없다면 기본적으로는 기분의 좋지 않은 조교라서, 빠른 취득이 필요하겠지요
	PRINTFORML -------------------------------------------------------------------------------
CASE 90
	CALL TRAIN_MESSAGE_COM34
CASE 91
	CALL TRAIN_MESSAGE_COM35
CASEELSE
	IF BOUGIE:CHOICE
		TEQUIP:요도확장기 = BOUGIE:CHOICE
		;今までに使われた최대の大きさを記録
		SIF TEQUIP:요도확장기 > CFLAG:452
			CFLAG:452 = TEQUIP:요도확장기
		RETURN 1
	ENDIF
ENDSELECT

PRINTL 
RESTART


@TRAIN_MESSAGE_COM151
;ブジーを見た, 몽마の反応です
CALL SELF_KOJO, "요도확장기추가구상"

;ブジーの太さ선택
CALL SELECT_BOUGIE

SIF REFUSE_CHECK()
	RETURN 0

;-------------------------------------------------
;ソースの計算(予測値)
;-------------------------------------------------
CALL CALC, "조교소스の試算"

;どっちの요도か
LOCALS = %CONDS("요도확장度")%

PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% %CALLNAME:TARGET%의 
IF GETBIT(TEQUIP:근육이완제, 1) 
	PRINTFORM 이완 했다요도에, 
ELSE
	PRINTFORM \@ PENIS(TARGET) ? 페니스 # 요도 \@에
ENDIF
SELECTCASE TEQUIP:요도확장기
CASE IS < 15
	PRINTFORMW 젤리를 바른 {TEQUIP:요도확장기}mm의 부지를 삽입했다. 
;손가락並み
CASE IS < 26
	PRINTFORMW 손가락 정도 굵기의 {TEQUIP:요도확장기}mm의 부지를 삽입했다. 
;왜소페니스並み
CASE IS < 33
	PRINTFORMW 가느다란 딜도라 해도 괜찮은 지름{TEQUIP:요도확장기}mm의 부지를 삽입했다. 
;ふつうのちんこ並み
CASE IS < 40
	PRINTFORMW 보통이라면 도저히 들어갈 것으로는 생각되지 않는 {TEQUIP:요도확장기}mm의 부지를 삽입했다. 
;거근並み～
CASEELSE
	PRINTFORMW 지름{TEQUIP:요도확장기}mm의 매끈한 금속제의 딜도를 삽입했다. 
ENDSELECT

;EXP:요도경험, TALENT:요도성감, TALENT:도착적, TEQUIP:요도확장기 > CALCF("요도径") あたりで分岐か
;확장する段階ごとに中間報告（太さを飛ばしてると出ないが些細な事だろう）
IF TEQUIP:요도확장기 > MAX_STR(LOCALS) && CALCF("요도확장の限界") >= TEQUIP:요도확장기 + 2
	SELECTCASE MIN_STR(LOCALS)
	CASE 13 TO 17
		IF GETBIT(CFLAG:453, 0) == 0
			PRINTFORML ……%CALLNAME:TARGET%의 거기는, 상당히 확장이 진행되어 온 것 같다.
			PRINTFORMW 가까운 시일내에, 손가락 정도라면 받아들일 수 있게 될지도 모른다…
			SETBIT CFLAG:453, 0
			RETURN 1
		ENDIF
	CASE 24 TO 28
		IF GETBIT(CFLAG:453, 1) == 0
			PRINTFORML ……%CALLNAME:TARGET%의 거기는, 눈에 띄게 펼쳐져 오고 있다.
			PRINTFORMW 이대로 쭉 해 가면, 혹시 작은 페니스 정도 삽입할 수 있게 될지도 모른다…
			SETBIT CFLAG:453, 1
			RETURN 1
		ENDIF
	CASE 31 TO 35
		IF GETBIT(CFLAG:453, 2) == 0
			PRINTFORML ……%CALLNAME:TARGET%의 거기는, 상당히 넓어져 버린 것 같다.
			PRINTFORMW 혹시, 앞으로 조금으로 일반적인 크기의 남성기도 받아들일 수 있게 되는 것은 아닐까…
			SETBIT CFLAG:453, 2
			RETURN 1
		ENDIF
	CASE 38 TO 42
		IF GETBIT(CFLAG:453, 3) == 0
			PRINTFORML ……%CALLNAME:TARGET%의 거기는, 완전히 큰 구멍으로 되었다
			PRINTFORMW 아마, 성행위를 문제 없게 실시할 수 있을 것이다…
			SETBIT CFLAG:453, 3
			RETURN 1
		ENDIF
	ENDSELECT
ENDIF

;初めてのブジー受け入れ
IF FIRSTCOM(SELECTCOM) == 0 && EXP:요도확장경험 == 0
	;初回から확장
	IF TEQUIP:요도확장기 > MIN_STR(LOCALS)
		PRINTFORMW 구멍에 비해 막대기가 \@ TEQUIP:요도확장기 - MIN_STR(LOCALS) >= 2 ? 거대한 # 큰 \@것 같지만, 매끄러운 금속 막대는 요도 괄약근의 저항을 개의치 않고,배뇨를 위한 구멍을 뚫고 굵기에 길들여 간다…
	ELSEIF PENIS(TARGET)
		PRINTFORMW 처음이라 삽입에 오래 걸리긴 했지만, %CALLNAME:TARGET%의 요도는 침입해온 금속막대의 페니스의 뿌리까지 받아들였다…
	ELSE
		PRINTFORMW 처음이라 삽입에 오래 걸리긴 했지만, %CALLNAME:TARGET%의 요도는 침입해온 금속막대를 제대로 방광까지 받아들였다…
	ENDIF
;各조교ごとの二回目以降のセリフ（簡潔）
ELSEIF CHOSECOM(SELECTCOM)
	IF CHECK("요도플레이")
		IF TALENT:요도성감
			PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
			IF RAND:2
				PRINTFORM 연결되는 막대기를\@ PENIS(TARGET) ? 뿌리 # 방광 \@까지 유유히 받아들였다, 
			ELSE
				PRINTFORM 본래는 액체밖에 통과하는 일이 없는 구멍에서\@ TALENT:요도성감 ? 즐겁게 # \@이물을 삼켜,
			ENDIF
			IF TALENT:요도성감 && RAND:3 == 0
				PRINTFORMW 상기된 얼굴에서 소변을 뚝뚝 떨어지며 플레이를 즐기고 있다…
			ELSEIF RAND:2
				PRINTFORMW 출입에 맞추어 괄약근을 조였다 펴고 있다…
			ELSE
				PRINTFORMW 점막을 문지르는 움직임에, 기분 좋은 자극을 받는듯한 반응을 보이고있다…
			ENDIF
		ELSEIF PENIS(TARGET)
			PRINTFORMW 특히 걸리는 것 없이 깊숙이 침투 한 금속막대는, 출입할때마다 음경 해면체를 제쳐 가면서 %CALLNAME:TARGET%의 요도를 이물질에 익숙해져간다…
		ELSE
			PRINTFORMW 미끄러지듯 방광까지 도달한 금속막대는, 출입 때마다 흘러나온 액체로 요도를 넓히면서%CALLNAME:TARGET%의 요도를 이물질에 익숙해져간다…
		ENDIF
	ELSE
		PRINTFORMW 저항이 강한 구멍을, 금속제의 확장막대가 천천히 그러나 확실히 성기로 만들어 간다…
	ENDIF
;확장
ELSEIF TEQUIP:요도확장기 > MIN_STR(LOCALS)
	;아픔や저항が少ない
	IF CHECK("요도플레이")
		IF TALENT:요도성감 && RAND:2
			PRINTFORMW 완전히 성기로서 눈을 떠버린 %CALLNAME:TARGET%의 요도는, 침입하는 이물질을 태연한 모습으로\@ PENIS(TARGET) ? 20cm남짓 # 방광까지 \@ 받아들이기는 커녕, 뺄 때는 아쉬운 듯이 달라붙는 모습마저 보이고 있다…
		ELSE
			PRINTFORMW 특별한 저항을 보여주지 않고 금속막대기를 받아들인 작은구멍은, 빈틈없이 넓히고 그것의 움직임에 따라 포함하거나 끌려가기도한다 …
		ENDIF
	ELSEIF EXP:요도경험 >= 20 || EXP:요도확장경험 >= 20
		PRINTFORMW 물건을 넣는 것 자체에는 익숙한 %CALLNAME:TARGET%의 요도이지만, 역시 이 굵기는 괴로운건지 강한 거부감을 드러내고 있다.…
	ELSE
		PRINTFORMW 아직 경험이 적은 배설 기관일 뿐인 요도는 삽입된 이물질에 의한 고통으로 괄약근에 힘을 뺄 수 없다…
	ENDIF
;확장はしないで慣らすのみ
ELSE
	IF CHECK("요도플레이")
		IF TALENT:요도성감
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")%는 부지가 요도를 출입 할 때마다 쾌감에 허리를 떨며 그다지 싫지만은 않은 듯한 모습으로 비정상적인 플레이를 즐기고있다…
		ELSE
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")%는 별다른 통증을 느끼지는 않았지만, 허리를 찌르는 부르르 소름 돋는듯한 뭐라고 말할 수 없는 감각에 불안스러운 표정을 짓고 있다…
		ENDIF
	ELSEIF PENIS(TARGET)
		PRINTFORMW 굵고 매끄러운 금속 막대는 %CALLNAME:TARGET%의 저항을 무릅쓰고 요도 깊숙이 침입하며 삽입할때마다, 좁은 점막의 길을 자극에 익숙해지게 만들고 확장하고 있다…
	ELSE
		PRINTFORMW 굵고 매끄러운 금속 막대는 %CALLNAME:TARGET%의 저항을 무릅쓰고 방광까지 침입하며 삽입할대마다, 오줌을 지리며 좁은 점막의 길을 확장하고 있다…
	ENDIF
ENDIF


@COM_ORDER_COM151, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:노출벽
IF ABL:노출벽
	LOCAL += ABL:노출벽 * 4
	LOCALS = %LOCALS% + %ABLNAME:7%LV{ABL:노출벽}({ABL:노출벽 * 4})
ENDIF
;ABL:마조끼
IF ABL:마조끼
	LOCAL += ABL:마조끼 * 3
	LOCALS = %LOCALS% + %ABLNAME:8%LV{ABL:마조끼}({ABL:마조끼 * 3})
ENDIF
;自制心
IF TALENT:냉정
	LOCAL -= 5
	LOCALS = %LOCALS% - %TALENTNAME:20%(5)
ENDIF
;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 15
	LOCALS = %LOCALS% - %TALENTNAME:35%(15)
ENDIF
;부끄럼없음
IF TALENT:부끄럼없음
	LOCAL += 2
	LOCALS = %LOCALS% + %TALENTNAME:36%(2)
ENDIF
;청초
IF TALENT:청초
	LOCAL -= 10
	LOCALS = %LOCALS% - %TALENTNAME:39%(10)
ENDIF
;쾌감에솔직
IF TALENT:쾌감에솔직
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:70%(5)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 5
	LOCALS = %LOCALS% - %TALENTNAME:71%(5)
ENDIF
;도착적
IF TALENT:도착적
	LOCAL += 10
	LOCALS = %LOCALS% + %TALENTNAME:80%(10)
ENDIF
IF TEQUIP:구속
	LOCAL += 30
	LOCALS = %LOCALS% + 구속中(30)
ENDIF
;미약
IF TEQUIP:미약
	LOCAL += 2
	LOCALS = %LOCALS% + %ITEMNAME:31%(2)
ENDIF
;ビデオ촬영
IF TEQUIP:비디오카메라
	LOCAL -= 20
	LOCALS = %LOCALS% - 촬영중(20)
ENDIF
;야외or관내노출
IF TEQUIP:야외플레이
	LOCAL -= 10
	LOCALS = %LOCALS% - 야외플레이(10)
ELSEIF TEQUIP:관내노출플레이
	LOCAL -= 10
	LOCALS = %LOCALS% - 관내노출(10)
ENDIF

;合計を表示(55이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 55

RETURN RESULT
