@COM150
;딥스로트
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 150

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1


@FLAG_COM150, ARG
;대상の입⇔조교자のＰの汚れが移動
CALL EQ_STAIN, TARGET, PLAYER, 0, 2

@SOURCE_COM150, ARG
VARSET LOCAL

;とりあえずは펠라と이라마치오の合わせ技
IF TEQUIP:딥스로트
	CALL SOURCE_COM91, 30
	CALL SOURCE_COM140, 50
ELSE
	CALL SOURCE_COM91, 20
	CALL SOURCE_COM140, 20
ENDIF

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM150
;딥스로트解除(今のところは死に分岐)
IF TEQUIP:딥스로트
	;前回は이라마치오
	IF PREVCOM == 140 && COND("前Ｔ조교자") == PLAYER && TEQUIP:넘어뜨리기 == 0
		PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% %CALLNAME:TARGET%의 
		IF RAND:2
			PRINTFORM 목을 범하는것을 멈추고 허리를 당겨, 
		ELSE
			;角がありますか？
			PRINTFORM \@ COND("뿔ハンドル") ? 뿔을 잡은 채로 # 머리를 누르면서 \@허리를 , 
		ENDIF
		;大きいと食道にまで行ってしまう
		IF SIZE("페니스", PLAYER) >= 3
			PRINTFORMW 식도까지 침투했던 페니스를 즈루즈루 끌어냈다…
		ELSE
			PRINTFORMW 입에서 페니스를 잡아끌었다…
		ENDIF
	ELSE
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 머리를 당겨 뿌리까지 삼키고 있던, %CALLNAME:PLAYER%의 페니스를 해방했다…
	ENDIF
	TEQUIP:딥스로트 = 0
	RETURN 0
ENDIF

PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
;前回펠라치오or이라마치오
IF (PREVCOM == 91 || PREVCOM == 140) && COND("前Ｔ조교자") == PLAYER
	IF RAND:2 && CHECK("喉姦") >= 2
		IF RAND:3 == 0 && SIZE("페니스", PLAYER) >= 3
			PRINTFORM 식도까지 사용하고
		ELSE
			PRINTFORM %TEXTR("주저없이", "혀를 미끄러하도록하고")%
		ENDIF
	ELSEIF TEQUIP:넘어뜨리기
		PRINTFORM 가랑이에 얼굴을 파뭇고
	ELSEIF PREVCOM == 140 && RAND:2
		PRINTFORM 의 허리 움직임에 맞추어 
	ELSE
		PRINTFORM 얼굴을 내밀어 
	ENDIF
	PRINTFORM %CALLNAME:PLAYER%의 
;押し倒されているとき
ELSEIF TEQUIP:넘어뜨리기
	;直前の조교と汚れで分岐
	IF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｖ", "Ａ")
		SIF (PREVCOM >= 70 && PREVCOM <= 79) && COND("前Ｔ조교자") == PLAYER && TEQUIP:아이마스크 == 0
			PRINT 자신의 항문에서 빼내 막 
		;젖기쉬움で追加
		SIF TALENT:젖기쉬움 && RAND:3 && TEQUIP:아이마스크 == 0
			PRINT 끈적끈적
		SIF (PREVCOM >= 60 && PREVCOM <= 69) && COND("前Ｔ조교자") == PLAYER && TEQUIP:아이마스크 == 0
			PRINT 자신의 애액으로
		PRINT 더러워진
	;음란or새드or새드끼Lv.5이상で「부끄럼쟁이」を持たない
	;ELSEIF (TALENT:음란 || TALENT:소악마 || ABL:새드끼 >= 5) && TALENT:부끄럼쟁이 == 0
	;	PRINT 침을 흘리면서, 
	ELSEIF RAND:3 == 0
		PRINTFORM 냄새나는
	ELSE
		PRINTFORM %TEXTS("페니스：視覚", PLAYER)%
	ENDIF
ELSE
	;식스나인
	IF TEQUIP:식스나인
		PRINTFORM %TEXTS("６９")%
	;아이마스크で分岐. ところで, Ｍ字開다리座りってもっといい名称無いですかね？
	ELSEIF TEQUIP:아이마스크 || TEQUIP:구속
		PRINTFORM 입술에 떠맡겨진
	ENDIF

	;直前の조교と汚れで分岐
	IF PREVEX("방뇨", PLAYER)
		IF RAND:2
			PRINTFORM 콕하고 코에 오는 구린 냄새를 발하고 있는
		ELSE
			PRINTFORM 소변을 더했기 때문에 냄새의 독한 
		ENDIF
	ELSEIF PREVCOM == 83 && CHECK_STAIN(STAIN:PLAYER:Ｐ, "尿")
		PRINTFORM 콕하고 코에 오는 구린 냄새를 발하고 있는
	ELSEIF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｖ", "Ａ")
		CALL TEXT, "추가地の文：穴から穴へ"
	ELSEIF RAND:3 == 0
		PRINTFORM 냄새나는
	ELSE
		PRINTFORM %TEXTS("페니스：視覚", PLAYER)%
	ENDIF
ENDIF

PRINTFORM 페니스를 뿌리까지 %TEXTR("단숨에"," 들이마시도록"," 쉽게")% 삼키면서
IF TALENT:연모 && RAND:4 == 0
	IF RAND:2 && COND("음모", PLAYER) >= 5 && TEQUIP:식스나인 == 0
		PRINTFORM 음모에 코끝을 묻어 문지르면서 
	ELSE
		PRINTFORM 황홀해하면서
	ENDIF
ELSEIF ABL:봉사정신 >= 3 && TEQUIP:넘어뜨리기 == 0 && TEQUIP:식스나인 == 0 && RAND:3 == 0
	PRINTFORM %TEXTR("눈을 치켜 뜨고", "물기를 띤 눈동자")%에서 바라보며
ELSEIF ABL:기교 >= 3 && TEQUIP:재갈 == 0 && RAND:2
	PRINTFORM %TEXTR("완급을 조절하", "입술에 바짝 조이")%며
ELSEIF TALENT:구토반사내성 == 0 && RAND:2
	PRINTFORM 올라오는 구토감에 눈을 글썽하면서
ELSE
	PRINTFORM 입가에 %TEXTR("거품의 침을 줄줄", "점도가 높은 대량의 침")% 흘리며
ENDIF
;기교４かつ봉사정신３
IF ABL:기교 >= 4 && ABL:봉사정신 >= 3
	IF RAND:2
		PRINTFORMW 목 전체를 이용하여 자극 해왔다 …
	ELSE
		PRINTFORMW 조인 목으로 빨기 시작했다…
	ENDIF
ELSEIF RAND:2
	PRINTFORMW 목구멍을 울리면서 비벼왔다 …
ELSE
	PRINTFORMW 목을 이용하여\@ TEQUIP:넘어뜨리기 ? 몰아세우고 # 봉사해\@온다…
ENDIF

;TEQUIP:딥스로트の値で誰のものを咥えているかを記録
TEQUIP:딥스로트 = PLAYER
;플래그とTEQUIPが分かれているのは, 사정後などにTEQUIP:딥스로트が０となっている時でも参照するため
CALL SETFLAG, "딥스로트플래그", TARGET

@COM_ORDER_COM150, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF
;ABL:마조끼
IF ABL:마조끼
	LOCAL += ABL:마조끼 * 3
	LOCALS = %LOCALS% + %ABLNAME:8%LV{ABL:마조끼}({ABL:마조끼 * 3})
ENDIF
;ABL:정액중독
IF ABL:정액중독
	LOCAL += ABL:정액중독 * 3
	LOCALS = %LOCALS% + %ABLNAME:11%LV{ABL:정액중독}({ABL:정액중독 * 3})
ENDIF
;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:35%(1)
ENDIF
;헌신的
IF TALENT:헌신
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF
;구토반사내성
IF TALENT:구토반사내성
	LOCAL += 20
	LOCALS = %LOCALS% + 구토반사내성(20)
ENDIF

LOCAL:1 = COM_ORDER_STAIN("Ｐ", "Ｍ")
;새드なら, 汚れていても, かまわずに突っ込みます
SIF ABL:PLAYER:새드끼 >= 3
	LOCAL:1 /= 3

;불결있음
IF LOCAL:1
	LOCAL -= LOCAL:1
	LOCALS = %LOCALS% - 불결있음
	;악취둔감
	SIF TALENT:악취둔감 || TALENT:악취민감
		LOCALS = %LOCALS%, \@TALENT:악취둔감 ? %TALENTNAME:61% # %TALENTNAME:62%\@
	LOCALS = %LOCALS%({LOCAL:1})
ENDIF

;合計を表示(60이상で実行. 이라마치오よりも高난이도)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 60

RETURN RESULT
