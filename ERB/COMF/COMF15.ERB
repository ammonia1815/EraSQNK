@COM15
;요도손가락넣기
;손가락はブジーに比べると非常に入れ辛いので, 확장効果があるような段階では入れることは出来ない
;あくまで요도の広がっている상대への애무커맨드. 消耗は低い
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

RETURN 1

@FLAG_COM15, ARG
;尿の汚れ発生
IF PENIS(TARGET)
	STAIN:Ｐ = SET_STAIN(STAIN:Ｐ, "尿")
ELSE
	STAIN:Ｖ = SET_STAIN(STAIN:Ｖ, "尿")
ENDIF
STAIN:PLAYER:손 = SET_STAIN(STAIN:PLAYER:손, "尿")
;팬티をずらす
SIF !CHECK_CLO("Ｖ노출") && INSERT("인서트") == 0
	CALL SETFLAG, "바텀을노출：텍스트없음", TARGET


@SOURCE_COM15, ARG
VARSET LOCAL

LOCAL:20 += 80
LOCAL:21 += 120

LOCAL:8 = 3000
LOCAL:12 = 300
LOCAL:11 = 500 + SIZE("손가락の直径", PLAYER)*100
LOCAL:13 = 5000
LOCAL:14 = 10000

;どっちの요도か
IF PENIS(TARGET)
	LOCALS = %CSTR:4%
	LOCAL:6 = 3000
	LOCAL:9 = 3000
ELSE
	LOCALS = %CSTR:3%
	LOCAL:6 = 1000
	LOCAL:9 = 1000
ENDIF

;ABL:Ｃ감각をみる
LOCAL:0 = CALCF("요도刺激", 1)
;조교자の손가락놀림をみる
LOCAL:0 = SOURCE_EXABL(LOCAL:0, 30, PLAYER)
CALL SETFLAG, "요도성감플래그", TARGET

;요도の方が손가락の太さよりも太いなら아픔が軽減
SELECTCASE MIN_STR(LOCALS) - SIZE("손가락の直径", PLAYER)
CASE IS >= 11
	TIMES LOCAL:6, 0.05
CASE IS >= 8
	TIMES LOCAL:6, 0.20
CASE IS >= 5
	TIMES LOCAL:6, 0.50
CASE IS >= 2
	TIMES LOCAL:6, 0.75
ENDSELECT

;요도확장경험が5, 20, 50, 150…と区切りを越えて大きいなら, 공포を軽減する
SELECTCASE GET_EXP_LV(EXP:요도확장경험)
CASE 1
	TIMES LOCAL:9, 0.50
CASE 2
	TIMES LOCAL:9, 0.30
CASE IS >= 3
	TIMES LOCAL:9, 0.15
ENDSELECT

SELECTCASE EXP:요도경험 + MIN(EXP:PLAYER:요도고문경험, 50)
CASE IS < 5
CASE IS < 20
	TIMES LOCAL:6, 0.80
	TIMES LOCAL:9, 0.80
CASE IS < 50
	TIMES LOCAL:0, 1.20
	TIMES LOCAL:6, 0.60
	TIMES LOCAL:9, 0.60
CASE IS < 150
	TIMES LOCAL:0, 1.35
	TIMES LOCAL:6, 0.40
	TIMES LOCAL:9, 0.40
CASEELSE
	TIMES LOCAL:0, 1.50
	TIMES LOCAL:6, 0.20
	TIMES LOCAL:9, 0.20
ENDSELECT

;PALAM:욕정をみる
SELECTCASE PALAM:욕정
CASE IS < PALAMLV:1
	TIMES LOCAL:14, 1.50
CASE IS < PALAMLV:2
	TIMES LOCAL:14, 1.00
CASE IS < PALAMLV:3
	TIMES LOCAL:14, 0.90
CASE IS < PALAMLV:4
	TIMES LOCAL:14, 0.70
CASE IS >= PALAMLV:4
	TIMES LOCAL:14, 0.50
ENDSELECT

;ABL:마조끼をみる
SELECTCASE ABL:마조끼
CASE 0
	TIMES LOCAL:11, 0.05
CASE 1
	TIMES LOCAL:11, 0.50
CASE 2

CASE 3
	TIMES LOCAL:11, 1.50
CASE 4
	TIMES LOCAL:11, 2.00
CASEELSE
	TIMES LOCAL:11, 3.00
ENDSELECT

;쾌Ｃについては既に参照済み
IF TALENT:요도광
	TIMES LOCAL:6, 0.10
	TIMES LOCAL:9, 0.10
	LOCAL:7 += 10000 + SIZE("페니스の直径", PLAYER)*200
	LOCAL:11 += LOCAL:14
	TIMES LOCAL:14, 0.05
ELSEIF TALENT:요도성감
	TIMES LOCAL:6, 0.20
	TIMES LOCAL:9, 0.20
	LOCAL:7 += 5000 + SIZE("페니스の直径", PLAYER)*100
	LOCAL:11 += LOCAL:14
	TIMES LOCAL:14, 0.20
ENDIF

SELECTCASE EXP:방뇨경험 + EXP:실금경험
CASE IS < EXPLV:1
	TIMES LOCAL:14, 3.00
CASE IS < EXPLV:2
	TIMES LOCAL:14, 2.50
CASE IS < EXPLV:3
	TIMES LOCAL:14, 2.00
CASE IS < EXPLV:4
	TIMES LOCAL:14, 1.00
CASE IS < EXPLV:5
	TIMES LOCAL:14, 0.80
CASEELSE
	TIMES LOCAL:14, 0.60
ENDSELECT

;근육이완제
SIF GETBIT(TEQUIP:근육이완제, 1)
	TIMES LOCAL:6, 0.20

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG



@TRAIN_MESSAGE_COM15
PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% 
IF TEQUIP:키스
	PRINTFORM %TEXTS("키스継続")%,
	CALL SETFLAG, "추가키스플래그", TARGET
ELSEIF TEQUIP:식스나인
	PRINTFORM %TEXTS("６９")%
ELSEIF INSERT("Ａ인서트")
	PRINTFORM 애널를 후벼파면서, 
ELSEIF PREVCOM == 1 && PENIS(TARGET) == 0 && CHECK_CLO("Ｖ노출")
	SIF PALAM:욕정 >= 3000 && ABL:Ｃ감각 >= 3
		PRINT 발기한
	PRINTFORM 클리토리스를 빨면서, 
ELSEIF PREVCOM == 13 && PENIS(TARGET) == 0
	PRINT 눈앞에서 노출된
ENDIF
PRINTFORM %CALLNAME:TARGET%의 

IF PREVCOM == SELECTCOM
	IF RAND:2 && PENIS(TARGET) == 0
		PRINTFORM 방광안에서 손가락를 휘어서 
		IF RAND:2
			PRINTFORMW 손톱을 세우지 않게 주의하면서 내부의 점막을 계속 어루만졌다…
		ELSE
			PRINTFORMW 안 요도구를 집중해 손가락의 끝으로 계속 간질였다…
		ENDIF
	ELSE
		IF RAND:2
			PRINTFORMW 에 손가락을 뿌리까지 삽입해, 천천히 출납하면서 계속 자극했다…
		ELSE
			PRINTFORMW 의 민감한 점막을 비비듯이 손가락을 움직였다…
		ENDIF
	ENDIF
;손가락
ELSE
	;前回요도で사정された
	IF PREVEX("요도内사정")
		PRINTFORM 정액를 쏟아진지 얼마 안된
	;前回방뇨
	ELSEIF PREVPLAY("방뇨")
		PRINTFORM 배뇨 한지 얼마 안된
	ELSEIF PREVCOM == 83
		PRINTFORM 섹스직후에 열릴 기미의
	ELSEIF PREVCOM == 151
		PRINTFORM 브지로 괴롭히고 뽑은 직후의
	ENDIF
	PRINTFORM \@ PENIS(TARGET) ? 페니스 # 요도 \@에
	IF CALCF("요도径") > SIZE("손가락の直径", PLAYER)*2 && RAND:2
		PRINTFORM \@ RAND:2 ? 중지와 약지 # 손가락두개 \@
	ELSE
		PRINTFORM \@ RAND:2 ? 가운데 # \@손가락
	ENDIF
	PRINTFORM 를\@ RAND:2 ? 뿌리까지 # 천천히 \@삽입

	IF RAND:3 == 0 && BASE:요의 >= 10000 && PENIS(TARGET) == 0
		PRINTFORMW 해, 모여 있는 오줌의 따뜻함을 즐겼다. 
	ELSEIF RAND:2
		PRINTFORMW 해, 손상시키지 않게 주의하면서 점막을 훑었다. 
	ELSE
		PRINTFORMW 해, 달라붙어 오는 점막을손가락의 배로 간질였다. 
	ENDIF

	;初めての손가락受け入れ
	IF FIRSTCOM(SELECTCOM) == 0
		IF TALENT:요도성감
			PRINTFORMW 요도성감의 포로가 되어 버린 %조사처리(CALLNAME:TARGET,"는")%, 허리를 내밀어 좀 더 격렬하게 넣고 빼라고 재촉하고 있다…
		ELSE
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 불안한 듯한 모습으로, 조금의 저항과 함께 출입하는 손가락의 움직임에 참고 있다…
		ENDIF
	;前回내사정
	ELSEIF PREVEX("요도内사정")
		PRINTFORM 요도로부터 뽑은 정액투성이의 손가락를 보여주자, 
		IF ABL:정액중독 > 3 && PALAM:욕정 >= 10000
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 갖고 싶어하는 듯이 혀를 내밀어 왔다…
		ELSEIF TALENT:연모
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 기쁜 듯한 미소를 보였다…
		ELSE
			IF RAND:2
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 부끄러운 것 같은 얼굴을 보였다…
			ELSE
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 부끄러운 듯이 시선을 피했다…
			ENDIF
		ENDIF
	ELSEIF TALENT:요도성감 && CHECK("요도플레이")
		SELECTCASE EXP:요도경험
		CASE IS <= 50
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 손가락의 감촉을 즐기고 있는 것 같다…
		CASE IS <= 80
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 손가락이 꿈틀거리는 감촉에 넋을 잃고 있다…
		CASE IS <= 120
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")%, 비밀의 장소를 휘저어지는 감각에 뜨거운 한숨을 토해 몸부림치고 있다…
		CASEELSE
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 군침을 흘려, 스스로 허리를 털어 쾌락을 탐내고 있다…
		ENDSELECT
	ELSE
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 숨을 정돈하면서, 움직이는 손가락의 감촉에 참고 있다…
	ENDIF
ENDIF
