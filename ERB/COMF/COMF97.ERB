@COM97
;팬티훑기
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 97

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;-------------------------------------------------
;사정체크
;-------------------------------------------------
IF CHECK_COM("Ａ자극손훑기")
	CALL SAMEN_CHECK, 70
	SELECTCOM = 96
	CALL SAMEN_CHECK, 50
	SELECTCOM = 97
ELSEIF CHECK_COM("파후파후손훑기")
	CALL SAMEN_CHECK, 70
	SELECTCOM = 104
	CALL SAMEN_CHECK, 50
	SELECTCOM = 97
ELSE
	CALL SAMEN_CHECK
ENDIF

RETURN 1

@FLAG_COM97, ARG
IF CHECK_COM("Ａ자극손훑기") == 97
	CALL FLAG_COM96, 50
ELSEIF CHECK_COM("파후파후손훑기") == 97
	CALL FLAG_COM104, 50
ENDIF
;대상の팬티⇔조교자のＰの汚れが移動
IF PENIS(PLAYER)
	CALL EQ_STAIN, TARGET, PLAYER, 10, 2
;대상の팬티⇔조교자のＶの汚れが移動
ELSE
	CALL EQ_STAIN, TARGET, PLAYER, 10, 3
ENDIF
;汚れは팬티でふき取る. 
CALL RESET_STAIN_EX, "Ｐ", PLAYER
;블루머と팬티はズレなどを戻す
FOR LOCAL, 60, 64
	CLEARBIT EQUIP:팬티, LOCAL
	CLEARBIT EQUIP:겉옷, LOCAL
NEXT

@SOURCE_COM97, ARG
IF CHECK_COM("Ａ자극손훑기") == 97
	CALL SOURCE_COM96, 50
ELSEIF CHECK_COM("파후파후손훑기") == 97
	CALL SOURCE_COM104, 50
ENDIF

VARSET LOCAL

LOCAL:20 += 10
LOCAL:21 += 100

LOCAL:8 = COM_ORDER_STAIN("Ｐ", "손")*20 + 60
LOCAL:11 = CALCF("실행치による욕정추가")
LOCAL:13 = 500
LOCAL:14 = 100

;汚れデータを元に, LOCAL:8は上で決定

;ABL:봉사정신をみる
SELECTCASE ABL:봉사정신
CASE 0
	LOCAL:4 = 250
	LOCAL:5 = 50
CASE 1
	LOCAL:4 = 300
	LOCAL:5 = 100
CASE 2
	LOCAL:4 = 350
	LOCAL:5 = 200
CASE 3
	LOCAL:4 = 400
	LOCAL:5 = 300
CASE 4
	LOCAL:4 = 450
	LOCAL:5 = 500
CASEELSE
	LOCAL:4 = 500
	LOCAL:5 = 750
ENDSELECT

;ABL:기교をみる
SELECTCASE ABL:기교
CASE 0
	TIMES LOCAL:4 , 0.50
	TIMES LOCAL:5 , 0.50
CASE 1
	TIMES LOCAL:4 , 0.80
	TIMES LOCAL:5 , 0.80
CASE 2
CASE 3
	TIMES LOCAL:4 , 1.20
	TIMES LOCAL:5 , 1.20
CASE 4
	TIMES LOCAL:4 , 1.50
	TIMES LOCAL:5 , 1.50
CASEELSE
	TIMES LOCAL:4 , 2.00
	TIMES LOCAL:5 , 2.00
ENDSELECT

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG



@TRAIN_MESSAGE_COM97
IF CHECK_COM("Ｗ봉사")
	;二人とも前回と同じ行動
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM == PREVCOM
		PRINTFORM 두사람은 
		IF TALENT:(TARGET:1):음란 + TALENT:(TARGET:1):연모 && TALENT:음란 + TALENT:연모 && RAND:3 == 0
			PRINT 황홀해하면서 
		ELSEIF RAND:2 == 0
			PRINT 정신없이 
		ENDIF
		PRINTFORMW %CALLNAME:MASTER%의 페니스를 팬티로 감싸 계속 훑고있다…
	ELSE
		PRINTFORM %타겟과()%%조사처리(CALLNAME:(TARGET:1),"는")%
		IF SELECTCOM:1 != PREVCOM:1 && SELECTCOM != PREVCOM && RAND:2 == 0
			PRINTFORMW %CALLNAME:MASTER%에게 과시하듯이 팬티를 벗고, 갓 벗은 팬티로 페니스를 훑기 시작했다…
		ELSE
			PRINTFORMW 두사람의 팬티로 페니스를 훑기 시작했다…
		ENDIF
	ENDIF
;==90で두개손훑기, == 91なら펠라＆손훑기, それ以外は犯されながら손훑기
ELSEIF CHECK_COM("３Ｐ봉사")
	CALL TRAIN_MESSAGE_3P
;同時넘어뜨리기
ELSEIF TEQUIP:Ｗ넘어뜨리기
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	;조수が前回と同じ行動(違う行動の時は, ここでなく, EQUIP_COM32で記述)
	SIF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM , %TEXTS("Ｗ넘어뜨리기 중")%

	PRINTFORM %CALLNAME:MASTER%의 페니스를, 
	IF CHECK_CLO("濡れ") && PREVCOM != SELECTCOM
		PRINT 애액으로 얼룩진 
	ELSEIF PREVCOM != SELECTCOM
		PRINT 갓 벗은 
		IF CHECK_CLO("濡れ") && RAND:2 == 0
			PRINT 축축한 
		ELSEIF CHECK_CLO("蒸れ")
			PRINT 열기를 띈 
		ENDIF
	ENDIF
	PRINTFORM %CLO("팬티훑기")%
	IF PREVCOM == SELECTCOM && COND("前Ｔ조교자") == PLAYER
		PRINTFORMW %조사만처리(CLO("팬티훑기"),"로")% 쓱쓱 문지르고있다…
	ELSE
		PRINTFORMW %조사만처리(CLO("팬티훑기"),"로")% 감싸서 훑고있다…
	ENDIF
ELSE
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	;押し倒されているとき
	IF TEQUIP:안면기승 && TEQUIP:넘어뜨리기
		IF PREVCOM == SELECTCOM && COND("前Ｔ조교자") == PLAYER
			PRINTFORM %CALLNAME:PLAYER%
			IF RAND:3 == 0 && ABL:욕망 + ABL:새드끼 >= 5
				PRINTFORM 의 얼굴에 %조사처리(TEXTS("보지：감촉"),"를")% 눌러붙였다가 떼었다가 하면서, 
			ELSEIF RAND:2 && ABL:욕망 + ABL:새드끼 >= 3
				PRINT 의 얼굴에 가랑이를 문지르면서, 
			ELSE
				PRINT 의 얼굴에 걸터앉은 자세인 채, 
			ENDIF
			PRINTFORMW %조사처리(CLO("팬티훑기"),"로")% 페니스를 쓱쓱 문지르고있다…
		ELSE
			IF EQUIP:겉옷 == 0 && CHECK_PANTIES("끈") && TALENT:부끄럼쟁이 == 0 && RAND:2 == 0
				PRINTFORM %CALLNAME:PLAYER%의 얼굴에 앉은 채로 
			ELSEIF EQUIP:겉옷 == 0 && CHECK_PANTIES("끈")
				PRINTFORM %CALLNAME:PLAYER%의 얼굴을 눌러서 뭉개고 있던 가랑이를 띄우고 
			ELSE
				PRINTFORM %CALLNAME:PLAYER%의 얼굴에 걸터앉은 자세로 
			ENDIF
			PRINTFORM %CLO("팬티훑기")%
			IF EQUIP:겉옷 == 0 && CHECK_PANTIES("끈")
				PRINT 의 끈을 풀고, 몸을 앞으로 기울여서, 
			ELSE
				PRINTFORMW %조사만처리(CLO("팬티훑기"),"를")% 벗고, 다시 얼굴에 허리를 내리면서 
			ENDIF
			IF CHECK_CLO("濡れ") && RAND:2 == 0
				PRINT 축축한 
			ELSEIF CHECK_CLO("蒸れ")
				PRINT 열기를 띈 
			ENDIF
			PRINTW 그것으로 페니스를 훑었다…
		ENDIF
	ELSE
		;식스나인
		SIF TEQUIP:식스나인
			PRINTFORM %TEXTS("６９")%
		IF TEQUIP:키스
			PRINTFORM %CALLNAME:PLAYER%%TEXTS("키스継続助詞付き")%,
			CALL SETFLAG, "추가키스플래그", TARGET
		ELSEIF CHECK_COM("Ａ責め손훑기")
			PRINTFORM %CALLNAME:PLAYER%의 엉덩이 구멍을 \@ RAND:2 ? 뾰족하게 세운  # \@ 혀로 
			IF RAND:3 == 0
				PRINT 희롱하면서 
			ELSEIF RAND:2
				SIF (ABL:봉사정신 >= 3 || ABL:새드끼 >= 3) && RAND:2
					PRINT 안쪽까지 
				PRINTFORM 핥으면서 
			ELSE
				PRINT 괴롭히면서 
			ENDIF
		ELSEIF CHECK_COM("파후파후손훑기")
			PRINTFORM %CALLNAME:PLAYER%의 얼굴을 
			IF RAND:3 == 0
				PRINT 풍만한 가슴
			ELSEIF RAND:2 == 0
				PRINT 실하게 여문 유방
			ELSE
				PRINT 부드러운 젖가슴
			ENDIF
			IF RAND:2 && TEQUIP:넘어뜨리기
				PRINTFORM 으로 뭉개면서, 
			ELSEIF RAND:2
				PRINTFORM 으로 감싸면서,  
			ELSE
				PRINTFORM 의 계곡에 묻은채, 
			ENDIF
		ELSEIF CHECK_CLO("濡れ") && PREVCOM != SELECTCOM
			PRINT 젖은 
		ELSEIF PREVCOM != SELECTCOM
			PRINT 갓 벗은 
			IF CHECK_CLO("濡れ") && RAND:2 == 0
				PRINT 축축한 
			ELSEIF CHECK_CLO("蒸れ")
				PRINT 열기를 띈 
			ENDIF
		ENDIF
		PRINTFORM %CLO("팬티훑기")%
		IF PREVCOM == SELECTCOM && COND("前Ｔ조교자") == PLAYER
			PRINTFORMW %조사만처리(CLO("팬티훑기"),"로")% 페니스를 쓱쓱 문지르고있다…
		ELSEIF RAND:3 == 0 && (TALENT:부끄럼쟁이 == 0 || TALENT:음란 || TALENT:연모)
			IF RAND:2 || CHECK_PANTIES("훈도시")
				PRINTW 의 그곳이 닿은 부분에 귀두를 문질렀다…
			ELSE
				PRINTW 의 가랑이로 귀두를 감싸, 떠오르는 얼룩을 핥으면서 문질렀다…
			ENDIF
		ELSEIF RAND:2 == 0
			PRINTFORMW %조사만처리(CLO("팬티훑기"),"를")%  페니스에 씌워서 문질렀다…
		ELSE
			PRINTFORMW %조사만처리(CLO("팬티훑기"),"로")%  페니스를 싸서, 쓱쓱 훑었다…
		ENDIF
	ENDIF
ENDIF
;모유체질とかだと모유を飲めるかも
CALL TEXT, "모유마시기"

@COM_ORDER_COM97, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF
;ABL:정액중독
IF ABL:정액중독
	LOCAL += ABL:정액중독 * 2
	LOCALS = %LOCALS% + %ABLNAME:11%LV{ABL:정액중독}({ABL:정액중독 * 2})
ENDIF
;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 3
	LOCALS = %LOCALS% - %TALENTNAME:35%(3)
ENDIF
;헌신的
IF TALENT:헌신
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF

;불결있음
IF COM_ORDER_STAIN("Ｐ", "손")/3
	LOCAL -= COM_ORDER_STAIN("Ｐ", "손")/3
	LOCALS = %LOCALS% - 불결있음
	;악취둔감
	SIF TALENT:악취둔감 || TALENT:악취민감
		LOCALS = %LOCALS%, \@TALENT:악취둔감 ? %TALENTNAME:61% # %TALENTNAME:62%\@
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｐ", "손")/3})
ENDIF

;合計を表示(20이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 20

RETURN RESULT
