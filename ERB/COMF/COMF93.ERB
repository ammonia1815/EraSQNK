@COM93
;스마타
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 93

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%
;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM93, ARG
;대상のＶ⇔조교자のＰの汚れが移動
SIF CHECK_CLO("Ｖ노출")
	CALL EQ_STAIN, TARGET, PLAYER, 3, 2
SIF ASSIPLAY == 0
	TFLAG:30 += 1

@SOURCE_COM93, ARG
VARSET LOCAL

LOCAL:20 += 40
LOCAL:21 += 130

LOCAL:8 = COM_ORDER_STAIN("Ｐ", "Ｖ")*50 + 60
LOCAL:11 = CALCF("실행치による욕정추가")
LOCAL:13 = 1200
LOCAL:14 = 50

;汚れデータを元に, LOCAL:8は上で決定

;ABL:봉사정신をみる
SELECTCASE ABL:봉사정신
CASE 0
	LOCAL:4 = 200
	LOCAL:5 = 100
CASE 1
	LOCAL:4 = 250
	LOCAL:5 = 180
CASE 2
	LOCAL:4 = 300
	LOCAL:5 = 250
CASE 3
	LOCAL:4 = 350
	LOCAL:5 = 350
CASE 4
	LOCAL:4 = 400
	LOCAL:5 = 500
CASEELSE
	LOCAL:4 = 450
	LOCAL:5 = 800
ENDSELECT

;ABL:기교をみる
SELECTCASE ABL:기교
CASE 0
	TIMES LOCAL:4 , 0.50
	TIMES LOCAL:5 , 0.50
CASE 1
	TIMES LOCAL:4 , 0.80
	TIMES LOCAL:5 , 0.80
CASE 2
CASE 3
	TIMES LOCAL:4 , 1.20
	TIMES LOCAL:5 , 1.20
CASE 4
	TIMES LOCAL:4 , 1.50
	TIMES LOCAL:5 , 1.50
CASEELSE
	TIMES LOCAL:4 , 2.00
	TIMES LOCAL:5 , 2.00
ENDSELECT

;ABL:Ｃ감각をみる
LOCAL:0 = CALCF("Ｃ刺激", 2)

;PALAM:Ｖ윤
SELECTCASE PALAM:Ｖ윤
CASE IS < PALAMLV:1
	TIMES LOCAL:0 , 0.10
	TIMES LOCAL:4 , 0.60
CASE IS < PALAMLV:2
	TIMES LOCAL:0 , 0.30
	TIMES LOCAL:4 , 0.80
CASE IS < PALAMLV:3
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:4 , 1.00
CASE IS < PALAMLV:4
	TIMES LOCAL:4 , 1.20
CASE IS < PALAMLV:5
	TIMES LOCAL:0 , 0.90
	TIMES LOCAL:4 , 1.40
CASEELSE
	TIMES LOCAL:0 , 0.80
	TIMES LOCAL:4 , 1.60
ENDSELECT

;보지が노출していないと쾌Ｃ減
IF CHECK_CLO("Ｖ노출") == 0 && COND("조교中")
	;바지等をはいていると쾌Ｃ激減
	IF CHECK_OUTER("바지") || CHECK_PANTIES("가죽")
		TIMES LOCAL:0 , 0.20
		TIMES LOCAL:12 , 0.20
	ELSE
		TIMES LOCAL:0 , 0.60
		TIMES LOCAL:12 , 0.50
	ENDIF
ENDIF

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM93
IF CHECK_COM("Ｗ봉사")
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM == PREVCOM
		PRINTFORM 두사람은 
		IF TALENT:(TARGET:1):음란 + TALENT:(TARGET:1):연모 && TALENT:음란 + TALENT:연모 && RAND:3 == 0
			PRINT 황홀해하면서 
		ELSEIF RAND:2 == 0
			PRINT 정신없이 
		ENDIF
		PRINTFORMW %CALLNAME:MASTER%의 페니스에 가랑이를 문질러붙여, 계속 자극하고있다…
	ELSE
		PRINTFORM %CALLNAME:TARGET%(와)과%CALLNAME:(TARGET:1)%하
		SIF TALENT:(TARGET:1):음란 + TALENT:(TARGET:1):연모 + TALENT:음란 + TALENT:연모 && RAND:2 == 0
			PRINTFORM 상기된 눈으로 %조사처리(CALLNAME:MASTER,"를")% 바라보면서, 
		PRINTFORMW 협력해서 페니스를 그곳에 끼우고 훑었다…
	ENDIF
ELSE
	IF TEQUIP:키스
		PRINTFORM %CALLNAME:TARGET%(은)는 %CALLNAME:PLAYER%%TEXTS("키스継続助詞付き")%페니스를
		CALL SETFLAG, "추가키스플래그", TARGET
	ELSE
		PRINTFORM %CALLNAME:TARGET%하페니스를
	ENDIF
	;보지剥き出し
	IF CHECK_CLO("Ｖ노출")
		IF RAND:2
			PRINT 부드럽게 부풀어 오른 
		ELSEIF COND("음모") >= 6
			PRINT 젖은 음모가 얽혀있는 
		ELSEIF COND("음모") == 5
			;PRINT 깨끗하게 깎아서 정돈된 
			PRINT 부드러운 
		;ELSEIF COND("음모") == 4
		;	PRINT 生えかけの
		ELSEIF COND("음모") && COND("음모") <= 3
			PRINT 맨질맨질한 
		ENDIF
	ENDIF
	PRINTFORM 가랑이 사이에 끼워, 
	SIF ABL:허리놀림 >= 3
		PRINT 허리를 음란하게 흔들어 
	PRINTFORMW 잡아당겼다…
ENDIF

@COM_ORDER_COM93, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF
;ABL:정액중독
IF ABL:정액중독
	LOCAL += ABL:정액중독 * 3
	LOCALS = %LOCALS% + %ABLNAME:11%LV{ABL:정액중독}({ABL:정액중독 * 3})
ENDIF

;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 3
	LOCALS = %LOCALS% - %TALENTNAME:35%(3)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF

;미약
IF TEQUIP:미약
	LOCAL += 6
	LOCALS = %LOCALS% + %ITEMNAME:31%(6)
ENDIF

;불결있음
IF COM_ORDER_STAIN("Ｐ", "Ｖ")/3
	LOCAL -= COM_ORDER_STAIN("Ｐ", "Ｖ")/3
	LOCALS = %LOCALS% - 불결있음
	;악취둔감
	SIF TALENT:악취둔감 || TALENT:악취민감
		LOCALS = %LOCALS%, \@TALENT:악취둔감 ? %TALENTNAME:61% # %TALENTNAME:62%\@
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｐ", "Ｖ")/3})
ENDIF

;合計を表示(20이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 20

RETURN RESULT
