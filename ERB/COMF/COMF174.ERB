@COM174
;관내노출플레이
;치정・욕망があがりやすい. 반감もやや高め. 
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;관객플래그初期化
VARSET TFLAG, 0, 151, 155
;終了時は修正なし
IF TEQUIP:관내노출플레이
	TEQUIP:관내노출플레이 = 0
	SIF ASSI
		TEQUIP:ASSI:관내노출플레이 = 0
	RETURN 1
ELSE
	TEQUIP:관내노출플레이 = 1
	SIF ASSI
		TEQUIP:ASSI:관내노출플레이 = 1
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM174
IF TEQUIP:관내노출플레이
	PRINTFORMW 방으로 돌아왔다…
ELSE
	PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% %조사처리(CALLNAME:TARGET,"를")% 
	IF RAND:3 == 0
		PRINT 복도에 
	ELSEIF RAND:2 == 0
		PRINT 객실에 
	ELSE
		PRINT 식당에 
	ENDIF
	PRINTFORMW 데려 갔다…
ENDIF


@EQUIP_COM35_2
;관내노출플레이
PRINTL ＜관내노출플레이＞

CALL AUDIENCE

CALL SOURCE_COM174, 100

EXP:노출경험 += TEQUIP:관내노출플레이
PRINTFORML %EXPNAME:34%＋{TEQUIP:관내노출플레이}

@FLAG_COM174, ARG

@SOURCE_COM174, ARG
VARSET LOCAL

LOCAL:20 += 50
LOCAL:21 += 100

;관객の数による보정あり
LOCAL:12 = 1000 + TEQUIP:관내노출플레이 * 1000
LOCAL:14 = TEQUIP:관내노출플레이 * 400

;옷の有無
IF CHECK_CLO("알몸")
	LOCAL:12 += 2000
	LOCAL:14 += 500
;팬티のみ
ELSEIF CHECK_CLO("下着姿")
	LOCAL:12 += 1200
	LOCAL:14 += 200
;노팬티
ELSEIF CHECK_CLO("노팬티")
	LOCAL:12 += 600
	LOCAL:14 += 100
ENDIF

;ABL:노출벽をみる
SELECTCASE ABL:노출벽
CASE 0
	TIMES LOCAL:14 , 1.20
CASE 1
	LOCAL:7 += 50
	TIMES LOCAL:12, 1.20
CASE 2
	LOCAL:7 += 150
	TIMES LOCAL:12, 1.40
	TIMES LOCAL:14 , 0.80
CASE 3
	LOCAL:7 += 400
	TIMES LOCAL:12, 1.60
	TIMES LOCAL:14 , 0.60
CASE 4
	LOCAL:7 += 750
	TIMES LOCAL:12, 2.00
	TIMES LOCAL:14 , 0.40
CASEELSE
	LOCAL:7 += 1300
	TIMES LOCAL:12, 3.00
	TIMES LOCAL:14 , 0.20
ENDSELECT

;겁쟁이
SIF TALENT:겁쟁이
	TIMES LOCAL:14 , 2.00
;부끄럼쟁이
SIF TALENT:부끄럼쟁이
	TIMES LOCAL:14 , 2.00

LOCAL:16 += LOCAL:12 / 4
LOCAL:9 += LOCAL:14 / 2

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@AUDIENCE
;集まった몽마たちをTFLAG:151～TFLAG:154で管理
;これはCHARA_NUM("관내노출관객数")で参照も가능
;参加できる人数
IF ASSI
	LOCAL:100 = CHARANUM - 4
ELSE
	LOCAL:100 = CHARANUM - 3
ENDIF

TEQUIP:관내노출플레이 = 1 + CHARA_NUM("관내노출관객数")

LOCAL:100 = MAX(LOCAL:100 - TEQUIP:관내노출플레이 + 1, 0)
LOCAL = RAND:(LOCAL:100 + 5)
IF CHARA_NUM("관내노출관객数") < 4 && LOCAL >= 5 && LOCAL:100
	LOCAL:1 = AUDIENCE_APPEAR()
	;今後バリエーションを増やそう
	PRINTPLAINFORM [{LOCAL:1}]%조사처리(CALLNAME:(LOCAL:1),"가")% 구경하러나왔다. 
	PRINTL
	CALL AUDIENCE_SPEAK, "관내조교：등장대사", LOCAL:1
ENDIF
IF CHARA_NUM("관내노출관객数")
	LASTWORD = [{TFLAG:151}]%CALLNAME:(TFLAG:151)%

	SELECTCASE CHARA_NUM("관내노출관객数")
	CASE IS >= 3
		LASTWORD = %LASTWORD% 일행
	CASE 2
		LASTWORD = %LASTWORD와()% [{TFLAG:152}]%CALLNAME:(TFLAG:152)%
	ENDSELECT

	;욕정依存で場の雰囲気を설정したい
	PRINTPLAINFORM %LASTWORD는()% 즐거운 듯이 조교광경을 구경하고있다…
	PRINTL
	IF CHARA_NUM("관내노출관객数") < 4 && LOCAL >= 5 && LOCAL:100
		CALL AUDIENCE_SPEAK, "관내조교：갤러리증가대사"
	ELSE
		CALL AUDIENCE_SPEAK, "관내조교：감상대사"
	ENDIF
ENDIF
IF LOCAL >= 5
	IF TFLAG:151 == 0
		TFLAG:151 = LOCAL:1
	ELSEIF TFLAG:152 == 0
		TFLAG:152 = LOCAL:1
	ELSEIF TFLAG:153 == 0
		TFLAG:153 = LOCAL:1
	ELSEIF TFLAG:154 == 0
		TFLAG:154 = LOCAL:1
	ENDIF
ENDIF

RETURN 1

@AUDIENCE_APPEAR()
#FUNCTION
$START_AUDIENCE_APPEAR
LOCAL = RAND:(CHARANUM - 2) + 2
SIF LOCAL == TARGET || LOCAL == ASSI || LOCAL == TFLAG:151 || LOCAL == TFLAG:152 || LOCAL == TFLAG:153
	GOTO START_AUDIENCE_APPEAR

RETURNF LOCAL

@AUDIENCE_SPEAK, ARGS, ARG
#DIM LCOUNT
#DIM MEMO_LINECOUNT

SELECTCASE ARGS
CASE "관내조교：등장대사"
	MEMO_LINECOUNT = LINECOUNT
	;台詞有る場合に, 台詞の名前表示をする？
	SIF CONFIG("대사の名前비표시") == 0
		PRINTFORM %CALLNAME:ARG%

	TRYCALLFORM GUEST_KOJO_K{NO:ARG}_{CFLAG:ARG:209}, ARGS, ARG

	;台詞が無いなら辻褄あわせ
	IF LINECOUNT == MEMO_LINECOUNT
		PRINTL 
		CLEARLINE 1
	ENDIF
CASEELSE
	SIF TEQUIP:관내노출플레이 < 2
		RETURN 0

	FOR LCOUNT, 0, 4
		LOCAL = TFLAG:(151 + LCOUNT)
		SIF LOCAL == 0
			BREAK
		;喋りすぎてもうざいかも？という訳で, ３回に一回程度で. 
		SIF RAND:3
			CONTINUE

		MEMO_LINECOUNT = LINECOUNT
		;台詞有る場合に, 台詞の名前表示をする？
		SIF CONFIG("대사の名前비표시") == 0
			PRINTFORM %CALLNAME:LOCAL%

		TRYCALLFORM GUEST_KOJO_K{NO:LOCAL}_{CFLAG:LOCAL:209}, ARGS, LOCAL

		;台詞が無いなら辻褄あわせ
		IF LINECOUNT == MEMO_LINECOUNT
			PRINTL 
			CLEARLINE 1
		ENDIF

	NEXT
ENDSELECT


RETURN 1
