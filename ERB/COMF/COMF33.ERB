@COM33
;애널바이브
;0bit細い, 1bit보통, 2bit太い, 3bit極太, 4bit腕並み, 5bit말잦이み, 
;10bit双頭タイプ, 11bitパールタイプ
IF TEQUIP:촉수
	PRINTFORML 애널촉수
ELSEIF TEQUIP:애널바이브
	IF COND("애널비즈")
		PRINTFORML 애널비즈를 뽑는다
	ELSE
		PRINTFORML 애널바이브를 뽑는다
	ENDIF
ELSE
	PRINTFORML 애널바이브\@ TALENT:요정 ? (요정용) # \@
ENDIF

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

IF TEQUIP:촉수
	STR:0 = 애널촉수
ELSEIF TEQUIP:애널바이브 == 0
	STR:0 = 애널바이브를뽑는다
ELSE
	IF COND("애널비즈")
		STR:0 = 애널비즈
	ELSE
		STR:0 = 애널바이브
	ENDIF
ENDIF

RETURN 1

;ARGに入れられるキャラ、ARGSは補발。
;現状だとARGS == "強制삽입"で[100] やめる を선택不可にできる
@SELECT_VIB_A, ARG, ARGS
#DIM LCOUNT
#DIM SIZE_DILDO

VARSET LOCAL, 1
;역Ａ바이브
IF ARG == PLAYER
	;大きな바이브
	SIF ABL:ARG:Ａ확장 == 0 || (ABL:ARG:Ａ확장 <= 1 && ABL:ARG:마조끼 < 3 && TALENT:새드 == 0)
		LOCAL:2 = 0
	;극태바이브
	SIF ABL:ARG:Ａ확장 <= 1 || (ABL:ARG:Ａ확장 <= 2 && ABL:ARG:마조끼 < 3 && TALENT:새드 == 0)
		LOCAL:3 = 0
	;腕並み바이브
	SIF ABL:ARG:Ａ확장 <= 2 || (ABL:ARG:Ａ확장 <= 3 && ABL:ARG:마조끼 < 3 && TALENT:새드 == 0)
		LOCAL:4 = 0
	;말잦이み바이브
	SIF ABL:ARG:Ａ확장 <= 3 || (ABL:ARG:Ａ확장 <= 4 && ABL:ARG:마조끼 < 3 && TALENT:새드 == 0)
		LOCAL:5 = 0
	LOCAL:11 = 0
ELSE
	;どの바이브を使えるかを判定（双方の새드마조具合で変化）
	FOR LCOUNT, 1, 6
		;바이브のサイズを代入
		SIZE_DILDO = LCOUNT

		SELECTCASE SIZE_DILDO - ABL:Ａ확장
		CASE IS >= 4
			;PLAYERが새드なら, どんな大きさのものだろうと무리やりねじ込める
			SIF TALENT:PLAYER:새드
				CONTINUE
		CASE 3
			;大きさが３上回っている時
			SIF TALENT:PLAYER:새드
				CONTINUE
			SIF (ABL:PLAYER:새드끼 >= 5 && ABL:마조끼 >= 3) || (TALENT:마조 && ABL:PLAYER:새드끼 >= 3)
				CONTINUE
		CASE 2
			;大きさが２上回っているなら, 새드끼５ＬＶor（새드끼３ＬＶに加えて상대の마조끼３ＬＶ）が필요
			SIF ABL:PLAYER:새드끼 >= 5 || MIN(ABL:마조끼, ABL:PLAYER:새드끼) >= 3
				CONTINUE
		CASE 1
			;大きさが１上回っているなら, 새드끼３ＬＶor（새드끼１ＬＶに加えて상대の마조끼１ＬＶ）が필요
			SIF ABL:PLAYER:새드끼 >= 3 || MIN(ABL:마조끼, ABL:PLAYER:새드끼) >= 1
				CONTINUE
		CASEELSE
			CONTINUE
		ENDSELECT

		LOCAL:LCOUNT = 0
	NEXT
ENDIF
SIF ITEM:애널비즈 == 0
	LOCAL:11 = 0

PRINTFORML 어느것을 \@ ARG == PLAYER ? 쓰게합니까 # 씁니까 \@？

VARSET LOCALS
LOCALS:0 = [ 0] 애널바이브
LOCALS:1 = [\@ LOCAL:1 ? {1, 2} # × \@] 보통 바이브　　　\@ ABL:ARG:Ａ확장 <= 0 - TALENT:ARG:요정 ? ※초심자 엄금!  # \@
LOCALS:2 = [\@ LOCAL:2 ? {2, 2} # × \@] 커다란 바이브　　\@ ABL:ARG:Ａ확장 <= 1 - TALENT:ARG:요정 ? ※숙련자용  # \@
LOCALS:3 = [\@ LOCAL:3 ? {3, 2} # × \@] 극태 바이브　　　　\@ ABL:ARG:Ａ확장 <= 2 - TALENT:ARG:요정 ? ※상급자 전용!  # \@
LOCALS:4 = [\@ LOCAL:4 ? {4, 2} # × \@] 피스트 바이브　　\@ ABL:ARG:Ａ확장 <= 3 - TALENT:ARG:요정 ? ※초상급자 전용!  # \@
LOCALS:5 = [\@ LOCAL:5 ? {5, 2} # × \@] 말잦이크기 바이브　　　\@ ABL:ARG:Ａ확장 <= 4 - TALENT:ARG:요정 ? ※갈데까지 간 사람들 용 # \@

LOCALS:11 = [\@ LOCAL:11 ? 11 # × \@] 애널비즈

FOR LCOUNT, 0, 20
	SIF STRLENS(LOCALS:LCOUNT) == 0
		CONTINUE

	SIF LOCAL:LCOUNT == 0
		SETCOLOR DEF_COLOR("회색")
	PRINTFORML  %LOCALS:LCOUNT%
	RESETCOLOR
NEXT

SIF ARGS != "強制삽입"
	PRINTFORML [100] 그만둔다

$INPUT_LOOP
INPUT

IF RESULT == 100 && ARGS != "強制삽입"
	TEQUIP:ARG:애널바이브 = 0
	TFLAG:14 = 1
	RETURN 0
ELSEIF RESULT == 0
	TEQUIP:ARG:애널바이브 = 1p0
ELSEIF RESULT == 1 && LOCAL:1
	TEQUIP:ARG:애널바이브 = 1p1
ELSEIF RESULT == 2 && LOCAL:2
	TEQUIP:ARG:애널바이브 = 1p2
ELSEIF RESULT == 3 && LOCAL:3
	TEQUIP:ARG:애널바이브 = 1p3
ELSEIF RESULT == 4 && LOCAL:4
	TEQUIP:ARG:애널바이브 = 1p4
ELSEIF RESULT == 5 && LOCAL:5
	TEQUIP:ARG:애널바이브 = 1p5
ELSEIF RESULT == 11 && LOCAL:11
	TEQUIP:ARG:애널바이브 = 1p0 + 1p11
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 1


@TRAIN_MESSAGE_COM33
;애널바이브の着脱
IF TEQUIP:애널바이브
	IF COND("애널비즈")
		PRINTFORMW %CALLNAME:TARGET%\@ TEQUIP:넘어뜨리기 ? %조사만처리(CALLNAME:TARGET,"는")% # 의 \@ 애널에서 애널비즈를 기세좋게 뽑아냈다…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%\@ TEQUIP:넘어뜨리기 ? %조사만처리(CALLNAME:TARGET,"는")% # 의 \@엉덩이구멍에서 %조사처리(NAMES("Ａ바이브", TARGET),"를")% 뽑아냈다…
	ENDIF
	TFLAG:16 = TEQUIP:애널바이브
	TEQUIP:애널바이브 = 0
	RETURN 0
ENDIF

;바이브の品揃えを見た, 몽마の反応です
CALL SELF_KOJO, "Ａ바이브추가구상"

$SELECT_VIB
;바이브선택
CALL SELECT_VIB_A, TARGET

SIF REFUSE_CHECK()
	RETURN 0

;使われる바이브が決定された時の, 몽마の反応です
CALL SELF_KOJO, "Ａ바이브추가구상"

IF TEQUIP:애널바이브 == 0
	TFLAG:16 = 1
	GOTO SELECT_VIB
ENDIF
;-------------------------------------------------
;ソースの計算(予測値)
;-------------------------------------------------
CALL CALC, "조교소스の試算"

PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% %CALLNAME:TARGET%의 
;애널が노출していないで, 팬티をずらしていないなら自動的にずらす
IF !CHECK_CLO("Ａ노출")
	CALL SETFLAG, "바텀을노출：텍스트有り", TARGET
	PRINT , 
ENDIF
IF (ABL:Ａ확장 >= 5 || (PREVCOM >= 70 && PREVCOM <= 79) || PREVCOM == 142 || PREVCOM == 143) && ABL:Ａ확장 >= 2 && RAND:3 == 0
	PRINT 야무지지 못하게 구멍을 열고있는 
ELSEIF ABL:Ａ확장 >= 2
	PRINT 완전히 부드러워진 
ELSEIF TALENT:연모 || RAND:3 == 0
	PRINT 귀여운 
ELSEIF EXP:Ａ경험 < 50 && RAND:2 == 0
	PRINT 경험이 적은 
ELSE
	PRINT 꾹 다물어진 
ENDIF
PRINT 애널
IF TALENT:연모
	PRINTFORM 을 비벼풀어서\@ COND("바텀을노출させた") ? # , \@ 
ELSE
	PRINTFORM 에 
ENDIF
IF COND("애널비즈")
	PRINTFORMW 애널비즈를 한개씩 삼키게 했다. 
ELSE
	SELECTCASE SIZE("Ａ바이브", TARGET)
	CASE 5
		PRINT 농담같은 크기의 
	CASE 4
		PRINT 남자의 팔뚝을 본뜬 
	CASE 3
		PRINT 극태 
	CASE 2
		PRINT 커다란 
	CASE 0
		PRINT 애널
	ENDSELECT
	PRINT 바이브를 
	;확장부족
	IF ABL:Ａ확장 < SIZE("Ａ바이브", TARGET)
		PRINTFORMW %TEXTR("힘껏", "억지로", "무리하게", "비집어", "고기를 밀어내는 감촉을 맛보면서", "저항에 아랑곳하지 않고")% 쑤셔 넣었다. 
	ELSE
		IF CHECK("Ａ바이브") == 0 && TALENT:PLAYER:새드 && RAND:2 == 0
			PRINT 힘껏
		ELSEIF (TALENT:연모 || ABL:새드끼 >= 3) && RAND:3 == 0
			PRINT 깊숙히
		ELSEIF TALENT:연모 && RAND:3 == 0
			PRINT 상냥하고
		ELSEIF (TALENT:연모 || ABL:새드끼 >= 3) && RAND:2 == 0
			PRINT 단번에
		ELSE
			PRINT 천천히
		ENDIF
		IF RAND:4 == 0
			PRINTW 삼키게 했다. 
		ELSEIF CHECK("Ａ바이브") == 0 && TALENT:PLAYER:새드 && RAND:2 == 0
			PRINTW 침입시켰다. 
		ELSEIF (TALENT:연모 || ABL:새드끼 >= 3) && RAND:3 == 0
			PRINTW 가라앉혔다. 
		ELSEIF (TALENT:연모 || ABL:새드끼 >= 3) && RAND:2 == 0
			PRINTW 꽂아넣었다. 
		ELSE
			PRINTW 삽입했다. 
		ENDIF
	ENDIF
ENDIF

;확장十分
IF CHECK("Ａ바이브") && RAND:3
	PRINTFORM %CALLNAME:TARGET%의 
	IF TALENT:작은몸집 && EXP:Ａ경험 < 100 && ABL:Ａ확장 <= 1
		PRINT 작은 
	ELSEIF TALENT:작은몸집 && ABL:Ａ확장 >= 2
		SIF ABL:Ａ확장 > SIZE("Ａ바이브", TARGET)
			PRINT 충분히 
		PRINT 확장된 
	ELSEIF EXP:Ａ경험 >= 100 && RAND:2 == 0
		PRINT 경험이 풍부한 
	ELSEIF ABL:Ａ확장 > SIZE("Ａ바이브", TARGET)
		PRINT 부드러운 
	ENDIF
	PRINT 애널은 
	IF SIZE("Ａ바이브", TARGET) >= 2 && ABL:Ａ확장 > SIZE("Ａ바이브", TARGET) && RAND:2
		IF RAND:3 == 0
			PRINT 아주 간단하게 
		ELSEIF RAND:2
			PRINT 아무런 저항도 없이 
		ELSE
			PRINT 빨아들이듯이 
		ENDIF
	ELSEIF ABL:Ａ확장 >= SIZE("Ａ바이브", TARGET) + 1 && RAND:2 == 0
		SIF TALENT:내성적 || TALENT:청초
			PRINT 얌전한 외견과는 반대로 
		PRINT 간단히 벌어져, 
	ELSEIF ABL:Ａ확장 >= SIZE("Ａ바이브", TARGET)
		PRINT 고무처럼 늘어나, 
	ENDIF
	IF COND("애널비즈")
		PRINT 애널비즈를 
	ELSE
		SELECTCASE SIZE("Ａ바이브", TARGET)
		CASE IS >= 3
			IF TALENT:남자 || TALENT:처녀
				PRINT 입에도 들어가지 않을 것같은 
			ELSEIF ABL:Ｖ확장 < SIZE("Ａ바이브", TARGET)
				PRINT 앞의 구멍에도 들어갈 것같지 않은 
			ENDIF
			PRINT 흉악한 
		CASE 2
			PRINT 두터운 
		CASE 0
			PRINT 애널
		ENDSELECT
		PRINT 바이브를 
	ENDIF
	PRINTW 직장 안쪽까지 삼켜넣었다…
ELSEIF CHECK("Ａ바이브") == 0 && ABL:Ａ확장 < SIZE("Ａ바이브", TARGET)
	PRINTFORM %CALLNAME:TARGET%의 
	SIF TALENT:작은몸집 && EXP:Ａ경험 < 100 && ABL:Ａ확장 == 0
		PRINT 작은 
	PRINT 항문은 
	IF SIZE("Ａ바이브", TARGET) >= 1 && ABL:Ａ확장 < SIZE("Ａ바이브", TARGET)
		PRINTW 바이브의 크기를 견디지 못하고, 비명을 지르고있다…
	ELSE
		PRINT 당장이라도 찢어질 것같은 정도로 벌어졌지만, 어떻게든 
		IF COND("애널비즈")
			PRINT 연결된 구체
		ELSEIF SIZE("Ａ바이브", TARGET) == 0
			PRINT 애널바이브
		ELSE
			PRINT 바이브
		ENDIF
		PRINTW 를 삼키고있다…
	ENDIF
ELSEIF CHECK("Ａ바이브")
	IF COND("애널비즈")
		PRINTFORMW 직장을 이물로 가득채우는 감각에, %조사처리(CALLNAME:TARGET,"는")% 뜨겁고 축축한 소리를 흘렸다…
	ELSE
		PRINTFORMW 직장에 떨리는 바이브가 가져오는 쾌감에, %조사처리(CALLNAME:TARGET,"는")% 뜨겁고 축축한 소리를 흘렸다…
	ENDIF
ELSEIF ABL:Ａ감각 >= 2 && CUP:고통 < 500
	PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 기묘한 감각에 당황하고있다…
ELSE
	PRINTFORM 익숙하지않은 
	IF COND("애널비즈")
		PRINT 애널비즈
	ELSE
		PRINT 바이브
	ENDIF
	PRINTFORMW 의 자극에 %CALLNAME:TARGET%의 항문은 단단히 긴장해, 강렬한 저항을 보인다…
ENDIF

@SELECT_SYOKUSYU_A
PRINTL 무엇으로 범합니까?
PRINTFORML  [ 0] 보통 촉수
PRINTFORML  [ 1] 커다란 촉수 \@ABL:Ａ확장 < 3 - TALENT:요정 ? ※상급자전용!  # \@
PRINTFORML  [ 2] 극태의 촉수 \@ABL:Ａ확장 < 4 - TALENT:요정 ? ※초상급자용!  # \@
PRINTFORML [100] 그만둔다

$INPUT_LOOP
INPUT

IF RESULT == 100
	TEQUIP:애널바이브 = 0
	TFLAG:14 = 1
	RETURN 0
ELSEIF RESULT == 0
	TEQUIP:애널바이브 = 1p1
ELSEIF RESULT == 1
	TEQUIP:애널바이브 = 1p2
ELSEIF RESULT == 2
	TEQUIP:애널바이브 = 1p3
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 1

@TRAIN_MESSAGE_COM202
IF TEQUIP:애널바이브
	PRINTFORMW 촉수는 %CALLNAME:TARGET%의 애널에서 뽑아졌다…
	TFLAG:16 = TEQUIP:애널바이브
	TEQUIP:애널바이브 = 0
ELSE
	CALL SELECT_SYOKUSYU_A

	SIF REFUSE_CHECK()
		RETURN 0
	SELECTCASE SIZE("Ａ바이브", TARGET)
	CASE IS >= 3
		PRINTFORMW 비틀려 얽힌 촉수의 덩어리가 %CALLNAME:TARGET%의 애널에 억지로 침입했다…
	CASE 2
		PRINTFORMW 장대한 촉수가 %CALLNAME:TARGET%의 애널에 질질 기어들어갔다…
	CASEELSE
		PRINTFORMW 미세한 돌기로 뒤덮힌 촉수가 %CALLNAME:TARGET%의 애널에 기어들어갔다…
	ENDSELECT
	CALL ADD_STAIN, TARGET, "Ａ", "촉수"
ENDIF


@FLAG_COM33, ARG
CALL FLAG_INSERTION, "Ａ바이브", ARG, 100
;애널が노출していないなら自動的にずらす
SIF CHECK_CLO("Ａ노출") == 0
	CALL SETFLAG, "바텀을노출：텍스트없음", TARGET

@SOURCE_COM33, ARG
CALL SOURCE_INSERTION, "Ａ바이브", ARG, 100


@EQUIP_COM13
;Ａ바이브삽입中
SIF SELECTCOM == 33 || SELECTCOM == 202
	RETURN 0

CALL SOURCE_COM33, 30
CALL FLAG_COM33, 30

RETURN 1

@EQUIP_COM13_2
;Ａ바이브삽입中
SIF SELECTCOM == 33 || SELECTCOM == 202
	RETURN 0

IF TEQUIP:촉수
	SELECTCASE SIZE("Ａ바이브", TARGET)
	CASE IS >= 3
		PRINTL ＜애널극태촉수삽입안＞
	CASEELSE
		PRINTL ＜애널촉수삽입안＞
	ENDSELECT
ELSE
	IF COND("애널비즈")
		PRINTL ＜애널비즈삽입안＞
	ELSE
		SELECTCASE SIZE("Ａ바이브", TARGET)
		CASE 5
			PRINTL ＜애널말잦이바이브삽입안＞
		CASE 4
			PRINTL ＜애널완형바이브삽입안＞
		CASE 3
			PRINTL ＜애널극태바이브삽입안＞
		CASEELSE
			PRINTL ＜애널바이브삽입안＞
		ENDSELECT
	ENDIF

ENDIF

LOCAL = 1
IF TEQUIP:촉수
	LOCAL += 1
;자위実行時は経験＋１
ELSEIF SELECTCOM == 3
	SIF ABL:Ａ확장 < SIZE("Ａ바이브", TARGET)
		LOCAL += 1
ENDIF

EXP:Ａ경험 += LOCAL
PRINTFORML Ａ경험＋{LOCAL}

VARSET LOCAL
;Ａ확장경험
IF TEQUIP:촉수 || SELECTCOM == 3 || SELECTCOM == 2
	SIF SIZE("Ａ바이브", TARGET) >= 2 - TALENT:요정
		LOCAL = MAX(SIZE("Ａ바이브", TARGET) - ABL:Ａ확장 + 1, SIZE("Ａ바이브", TARGET)/3)
ENDIF

IF LOCAL
	EXP:Ａ확장경험 += LOCAL
	PRINTFORML Ａ확장경험＋{LOCAL}
	;확장경험による追加の疲労
	DOWNBASE:체력 += LOCAL*50
	DOWNBASE:기력 += LOCAL*50
ENDIF

RETURN 1
