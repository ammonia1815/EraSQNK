@COM98
;컨닐링구스시킨다
CALL PRINT_TRAIN_NAME(SELECTCOM)
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 98

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL SETFLAG, "下半身を脱がす", PLAYER

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM98, ARG
;대상の입⇔조교자のＶの汚れが移動
CALL EQ_STAIN, TARGET, PLAYER, 0, 3
SIF CHECK_COM("청소컨닐링구스", 98)
	CALL SETFLAG, "청소컨닐링구스", TARGET


@SOURCE_COM98, ARG
VARSET LOCAL

LOCAL:20 += 10
LOCAL:21 += 50

LOCAL:8 = COM_ORDER_STAIN("Ｖ", "Ｍ")*100 + 100
LOCAL:11 = CALCF("실행치による욕정추가")
LOCAL:13 = 1000
LOCAL:14 = 500

;ABL:봉사정신をみる
SELECTCASE ABL:봉사정신
CASE 0
	LOCAL:4 = 420
	LOCAL:5 = 150
CASE 1
	LOCAL:4 = 500
	LOCAL:5 = 300
CASE 2
	LOCAL:4 = 580
	LOCAL:5 = 600
CASE 3
	LOCAL:4 = 660
	LOCAL:5 = 900
CASE 4
	LOCAL:4 = 740
	LOCAL:5 = 1500
CASEELSE
	LOCAL:4 = 820
	LOCAL:5 = 2200
ENDSELECT

;ABL:기교をみる
SELECTCASE ABL:기교
CASE 0
	TIMES LOCAL:4 , 0.50
	TIMES LOCAL:5 , 0.50
CASE 1
	TIMES LOCAL:4 , 0.80
	TIMES LOCAL:5 , 0.80
CASE 2
CASE 3
	TIMES LOCAL:4 , 1.20
	TIMES LOCAL:5 , 1.20
CASE 4
	TIMES LOCAL:4 , 1.50
	TIMES LOCAL:5 , 1.50
CASEELSE
	TIMES LOCAL:4 , 2.00
	TIMES LOCAL:5 , 2.00
ENDSELECT

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM98
;同時넘어뜨리기
IF TEQUIP:Ｗ넘어뜨리기
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")%
	;조수が前回と同じ行動(違う行動の時は, ここでなく, EQUIP_COM32で記述)
	SIF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM , %TEXTS("Ｗ넘어뜨리기 중")%

	IF PREVCOM == SELECTCOM
		PRINTFORMW %CALLNAME:PLAYER%의 그곳을 %NAMES("손가락")% 끝으로 벌리고, 열심히 계속 핥았다…
	ELSEIF TEQUIP:넘어뜨리기
		;前回역레이프
		IF PREVPLAY("Ｖ인서트", PLAYER)
			IF PREVEX("사정") && RAND:3
				PRINTFORM 정액이 눅진눅진하게 넘쳐흐르는 
			ELSEIF ABL:새드끼 >= 3 && RAND:2 == 0
				PRINTFORM 마음껏 즐긴 ㄴ
			ELSE
				PRINTFORM 범해져서 풀려있는 
			ENDIF
		ELSEIF RAND:3 == 0
			PRINTFORM 꼼짝도 할 수 없는 
		ELSE
			PRINTFORM 깔아 눕혀진 
		ENDIF
		PRINTFORMW %CALLNAME:PLAYER%의 음순을 입으로 공격했다…
	ENDIF
ELSEIF CHECK_COM("３Ｐ봉사")
	CALL TRAIN_MESSAGE_3P
ELSEIF CHECK_COM("치구청소컨닐링구스")
	PRINTFORM %타겟은()% %CALLNAME:PLAYER%의 그곳을 펴넓혀,
	PRINTFORM %TEXTR("음순에 남아있던 치구","질구에 맺힌 치구", "희고 물렁한 더러움", "치즈 같이 더러움", "점막을 물들인 부끄러운 더러움")%
	IF RAND:2 && PLAYER == MASTER && TALENT:냄새민감 && TALENT:연모
		PRINTFORMW 의 %조사처리(TEXTS("匂い"),"를")% 맡으면서, 혀로 깨끗하게 청소했다…
	ELSEIF RAND:2
		PRINTFORMW 를 혀로 구석구석까지 빨아냈다…
	ELSE
		PRINTFORMW 를 혀로 철저하게 청소했다…
	ENDIF
ELSEIF PREVCOM == SELECTCOM
	PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:PLAYER%의 그곳을 %NAMES("손가락")% 끝으로 벌리고, 열심히 계속 핥았다…
ELSEIF TEQUIP:넘어뜨리기
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	;前回역레이프
	IF PREVPLAY("Ｖ인서트", PLAYER)
		IF PREVEX("사정") && CHECK_STAIN(STAIN:PLAYER:Ｖ, "정액") && RAND:3
			PRINTFORM 정액이 눅진눅진하게 넘쳐흐르는 
		ELSEIF ABL:새드끼 >= 3 && RAND:2 == 0
			PRINTFORM 마음껏 즐긴 
		ELSE
			PRINTFORM 범해져서 풀려있는 
		ENDIF
	ELSEIF RAND:3 == 0
		PRINTFORM 꼼짝도 할 수 없는 
	ELSE
		PRINTFORM 깔아 눕혀진 
	ENDIF
	IF CHECK_COM("청소컨닐링구스")
		IF RAND:2
			PRINTFORMW %CALLNAME:PLAYER%의 보지를 벌리고, 더러운 것들을 혀로 깨끗하게 핥았다…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%의 질구에 혀를 찔러넣고, 애액이나 더러운 것을 들이마셨다…
		ENDIF
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%의 그곳을 입으로 괴롭히고있다…
	ENDIF
ELSE
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	;尿のお청소
	IF GETBIT(CHECK_COM("청소컨닐링구스"), 1)
		IF PREVPLAY("질내욕뇨", PLAYER)
			PRINTFORM 대량으로 퍼부어진 오줌이 역류하고 있는 
		ELSE
			PRINTFORM 배뇨 직후에 %TEXTR("지독한", "강한", "암모니아")% 냄새를 풍기는 
		ENDIF
	ELSEIF PREVPLAY("Ｖ인서트", PLAYER) || CHECK_COM("청소컨닐링구스")
		IF PREVEX("Ｖ내사정", PLAYER) && CHECK_STAIN(STAIN:PLAYER:Ｖ, "정액") && RAND:2
			PRINTFORM 갓 질내사정당해 정액을 흘리고 있는 
		ELSEIF RAND:2
			PRINTFORM 범해진 직후라 부들부들 꿈틀거리고 있는 
		ELSE
			PRINTFORM 휘저어져 거품같은 정액을 흘리고 있는 
		ENDIF
	ELSEIF TEQUIP:식스나인
		PRINTFORM %TEXTS("６９")%
	ELSEIF EXP_GAIN(2, PLAYER)
		PRINTFORM 절정의 여운에 와들와들 떨리고 있는 
	ELSEIF RAND:2
		PRINTFORM 봉사를 명령한 
	ELSE
		PRINTFORM 눈앞에서 다리를 벌리고 있는 
	ENDIF
	PRINTFORM %CALLNAME:PLAYER%의 보지
	IF COND("손가락 사용") && TCVAR:28 > 20
		PRINTFORM 를 벌리고, 
	ELSE
		PRINTFORM 에 얼굴을 붙이고, 
	ENDIF
	IF CHECK_COM("청소컨닐링구스")
		PRINTFORMW 열심히 청소하기 시작했다…
	ELSEIF TCVAR:28 > 20 && (TALENT:남자 || ABL:레즈끼)
		IF RAND:4 == 0
			PRINTFORMW 클리토리스에 달라붙었다…
		ELSEIF RAND:3 == 0
			PRINTFORMW 질구를 집중적으로 자극해왔다…
		ELSEIF RAND:2
			PRINTFORMW 주름의 구석구석을 혀로 핥았다…
		ELSE
			PRINTFORMW 쮸읍쮸읍 열정적인 키스를 퍼부었다…
		ENDIF
	ELSEIF RAND:2
		PRINTFORMW 혀로 봉사하기 시작했다…
	ELSE
		PRINTFORMW \@ (ABL:순종 >= 2 && RAND:2) ? 순종적으로  # 할짝할짝 \@ 핥기 시작했다…
	ENDIF

	SIF CHOSECOM(SELECTCOM) == 0 && TALENT:남자 == 0 && ABL:레즈끼 == 0 && CHECK_COM("청소컨닐링구스") == 0
		PRINTFORMW 동성의 성기에 입을 맞추는데 저항이 있는지, 그 표정에는 무척이나 복잡한 색을 띄고있다…
ENDIF


@COM_ORDER_COM98, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF
;ABL:레즈끼
IF ABL:레즈끼
	LOCAL += ABL:레즈끼 * 5
	LOCALS = %LOCALS% + %ABLNAME:9%LV{ABL:레즈끼}({ABL:레즈끼 * 5})
ENDIF
;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 3
	LOCALS = %LOCALS% - %TALENTNAME:35%(3)
ENDIF
;헌신的
IF TALENT:헌신
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF

;불결있음
IF COM_ORDER_STAIN("Ｖ", "Ｍ")
	LOCAL -= COM_ORDER_STAIN("Ｖ", "Ｍ")
	LOCALS = %LOCALS% - 불결있음
	;악취둔감
	SIF TALENT:악취둔감 || TALENT:악취민감
		LOCALS = %LOCALS%, \@TALENT:악취둔감 ? %TALENTNAME:61% # %TALENTNAME:62%\@
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｖ", "Ｍ")})
ENDIF
;남자
IF TALENT:남자
	LOCAL += 10
	LOCALS = %LOCALS% + %TALENTNAME:122%(10)
ENDIF

;合計を表示(30이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 30

RETURN RESULT
