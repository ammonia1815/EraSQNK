@COM178
;두사람에게넘어간다
;一人はTARGET、もう一人はTARGET:1で管理する。もしもTARGET:1が未설정の場合には조수がTARGET:1となる

PRINTL 두사람에게넘어간다

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 178

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = 두사람에게넘어간다

CALL SETFLAG, "Ｗ넘어뜨리기"

RETURN 1

@FLAG_COM178, ARG

@SOURCE_COM178, ARG
VARSET LOCAL

LOCAL:20 += 60
LOCAL:21 += 150

LOCAL:12 = 400

;ABL:봉사정신をみる
SELECTCASE ABL:봉사정신
CASE 0
	LOCAL:4 = 200
	LOCAL:5 = 50
CASE 1
	LOCAL:4 = 250
	LOCAL:5 = 200
CASE 2
	LOCAL:4 = 350
	LOCAL:5 = 550
CASE 3
	LOCAL:4 = 450
	LOCAL:5 = 900
CASE 4
	LOCAL:4 = 600
	LOCAL:5 = 1500
CASEELSE
	LOCAL:4 = 750
	LOCAL:5 = 2200
ENDSELECT

;ABL:Ｖ감각をみる
SELECTCASE ABL:Ｖ감각
CASE 0
	LOCAL:3 = 25
	TIMES LOCAL:4 , 0.50
CASE 1
	LOCAL:3 = 75
	TIMES LOCAL:4 , 0.80
CASE 2
	LOCAL:3 = 125
	TIMES LOCAL:4 , 1.00
CASE 3
	LOCAL:3 = 175
	TIMES LOCAL:4 , 1.20
CASE 4
	LOCAL:3 = 300
	TIMES LOCAL:4 , 1.50
CASEELSE
	LOCAL:3 = 425
	TIMES LOCAL:4 , 2.00
ENDSELECT

;PALAM:욕정
SELECTCASE PALAM:욕정
CASE IS < PALAMLV:1
	TIMES LOCAL:3 , 0.30
CASE IS < PALAMLV:2
	TIMES LOCAL:3 , 0.60
CASE IS < PALAMLV:3
CASE IS < PALAMLV:4
	TIMES LOCAL:3 , 1.50
CASE IS < PALAMLV:5
	TIMES LOCAL:3 , 1.80
CASEELSE
	TIMES LOCAL:3 , 2.20
ENDSELECT

;ABL:순종をみる
SELECTCASE ABL:순종
CASE 0
	TIMES LOCAL:3 , 0.60
	LOCAL:13 += 700
CASE 1
	TIMES LOCAL:3 , 0.80
	LOCAL:13 += 500
CASE 2
	LOCAL:13 += 300
CASE 3
	TIMES LOCAL:3 , 1.20
	LOCAL:13 += 100
CASE 4
	TIMES LOCAL:3 , 1.40
	LOCAL:13 += 30
CASEELSE
	TIMES LOCAL:3 , 1.60
ENDSELECT

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM178
IF TEQUIP:넘어뜨리기
	PRINTFORMW %CALLNAME:TARGET%의 권유에 응해,%조사처리(CALLNAME:(TARGET:1),"가")% %CALLNAME:PLAYER%에 다가간다…
	IF TARGET:1 == ASSI && TEQUIP:비디오카메라
		PRINTFORMW 아무래도,%조사처리(CALLNAME:(TARGET:1),"는")% 촬영을 중지해 함께 즐기기로 한 것 같다…
	ELSE
		PRINTFORMW 아무래도,%CALLNAME:(TARGET:1)%도 참가하는 것 같다…
	ENDIF
ELSE
	PRINTFORMW 요염하게 미소지으면서 %타겟과()% %조사처리(CALLNAME:(TARGET:1),"가")% %CALLNAME:PLAYER%에 다가간다…
	PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 두사람에게 밀려 넘어져버렸다…
ENDIF
TEQUIP:비디오카메라 = 0


;@EVENTCOMでSELECTCOMに応じて決定される. 
;몸부림, 몸을맡김の時にはそれぞれのCOMF中で決定するため, @EVENTCOMでは呼ばない. 
@SET_SELECTCOM_W
#DIM LCOUNT
;それぞれ、TARGET:0orTARGET:1が안면기승、インサート、봉사中かを見る
#DIM IS_FENDAMU, 2
#DIM IS_INSERT, 2
#DIM IS_SERVICE, 2
;二穴삽입に派生しやすい条件を満たしていたらＯＮ
#DIM IS_WPENE
;TARGET:1が커맨드実行가능かどうか
#DIM CANCOM, 160


CALL SET_FORBIDCOM, TARGET:1, SELECTCOM

SWAP TARGET, TARGET:1

VARSET CANCOM
FOR LCOUNT, 0, 110
	;체크필요な커맨드は実は少ない
	SELECTCASE LCOUNT
	CASE 1, 9, 65, 75, 83, 90 TO 95, 97, 104
		TRYCALLFORM COM_ABLE{LCOUNT}
		CANCOM:LCOUNT = RESULT
	ENDSELECT
NEXT

SWAP TARGET, TARGET:1

VARSET IS_FENDAMU
VARSET IS_INSERT
VARSET IS_SERVICE
VARSET IS_WPENE

SELECTCASE SELECTCOM
CASE 1, 9
	IS_FENDAMU = 1
CASE 60 TO 79, 83
	IS_INSERT = 1
CASE 90 TO 95, 97, 104
	IS_SERVICE = 1
CASE 120
	SIF PREVCOM:1 == 126
		IS_WPENE = 126
CASE 126
	SIF PREVCOM:1 == 120
		IS_WPENE = 120
ENDSELECT

SELECTCASE PREVCOM:1
CASE 1, 9
	IS_FENDAMU:1 = 1
CASE 60 TO 79, 83
	IS_INSERT:1 = 1
CASE 90 TO 95, 97, 104
	IS_SERVICE:1 = 1
ENDSELECT

;인서트中なら, 속행
IF IS_INSERT:1 && FORBIDCOM(TARGET:1, "섹스", "Ｐ下半身") == 0
	SELECTCOM:1 = PREVCOM:1
;거품춤はＷ거품춤
ELSEIF SELECTCOM == 94 && CANCOM:94
	SELECTCOM:1 = 94
;Ｗ기승中なら高확률で속행
ELSEIF RAND:3 && IS_INSERT && IS_FENDAMU:1
	SELECTCOM:1 = PREVCOM:1
ELSEIF RAND:3 && IS_FENDAMU && IS_INSERT:1
	SELECTCOM:1 = PREVCOM:1
;Ｗ기승を起き易く
ELSEIF IS_INSERT && CANCOM:1 + CANCOM:9 && RAND:3 == 0
	IF CANCOM:1 && RANDIF(1, CANCOM:9)
		SELECTCOM:1 = 1
	ELSE
		SELECTCOM:1 = 9
	ENDIF
ELSEIF IS_FENDAMU && CANCOM:65 + CANCOM:75 + CANCOM:83 && RAND:3 == 0
	IF CANCOM:83 && RANDIF(1, CANCOM:75, CANCOM:65)
		SELECTCOM:1 = 83
	ELSEIF CANCOM:65 && RANDIF(1, CANCOM:75)
		SELECTCOM:1 = 65
	ELSE
		SELECTCOM:1 = 75
	ENDIF
;同時봉사中なら속행
ELSEIF IS_SERVICE && SELECTCOM == PREVCOM && PREVCOM:1 == SELECTCOM
	SELECTCOM:1 = PREVCOM:1
;同時봉사を起き易く
ELSEIF IS_SERVICE && PREVCOM:1 == SELECTCOM
	SELECTCOM:1 = PREVCOM:1
ELSEIF IS_SERVICE && CANCOM:SELECTCOM && RAND:3 == 0
	SELECTCOM:1 = SELECTCOM
;양구멍삽입中なら, 속행
ELSEIF INSERT("양구멍삽입", PLAYER)
	SELECTCOM:1 = PREVCOM:1
;양구멍삽입になりやすくしてみる
ELSEIF IS_WPENE && RAND:2 == 0
	SELECTCOM:1 = IS_WPENE
ELSEIF FORBIDCOM(TARGET:1)
	SELECTCOM:1 = -1
ELSE
	;補佐役のABL:손～ABL:레이프を決定
	CALL CALC_COUNTER_ABL, TARGET:1

	;레이프の後は連続레이프
	IF (PREVCOM:1 == 120 || PREVCOM:1 == 126) && ABL:(TARGET:1):레이프 && RAND:4
		ABL:(TARGET:1):손 = 0
		ABL:(TARGET:1):입 = 0
		ABL:(TARGET:1):가슴 = 0
		ABL:(TARGET:1):발 = 0
		ABL:(TARGET:1):안면기승 = 0
		ABL:(TARGET:1):기승위 = 0
		ABL:(TARGET:1):키스 = 0
		ABL:(TARGET:1):엉덩이자극 = 0
		ABL:(TARGET:1):스마타 = 0
	ENDIF

	SELECTCOM:1 = SET_COUNTERCOM(TARGET:1)
ENDIF

;禁止커맨드消去
CALL SET_FORBIDCOM, TARGET:1, -1


@COM_ORDER_COM178, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF

;강기
IF TALENT:강기
	LOCAL += 10
	LOCALS = %LOCALS% + %TALENTNAME:18%(10)
ENDIF
;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 10
	LOCALS = %LOCALS% - %TALENTNAME:35%(10)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 5
	LOCALS = %LOCALS% - %TALENTNAME:71%(5)
ENDIF
;새드
IF TALENT:새드
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:83%(5)
ENDIF

;처녀
IF TALENT:처녀
	LOCAL -= 20
	LOCALS = %LOCALS% - %TALENTNAME:0%(20)
ELSEIF EXP:Ｖ경험 < EXPLV:2
	LOCAL -= 5
	LOCALS = %LOCALS% - %EXPNAME:0%부족(5)
ENDIF

;미약
IF TEQUIP:미약
	LOCAL += 6
	LOCALS = %LOCALS% + %ITEMNAME:31%(6)
ENDIF

;合計を表示(70이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 70

RETURN RESULT
