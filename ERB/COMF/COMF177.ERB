@COM177
;러브트랩
;実行後は조교자の선택가능커맨드が大きく制限される. 
;인서트系커맨드のみ선택가능である点が, 밀려넘어진다커맨드との違い. 
;実行時にＶ경험判定がつく. 

PRINTL 러브트랩

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 177

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = 러브트랩

;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM177, ARG
CALL FLAG_INSERTION, "Ｖ인서트", ARG, 100

@SOURCE_COM177, ARG
CALL SOURCE_INSERTION, "Ｖ인서트", ARG, 100
VARSET LOCAL

LOCAL:20 += 60
LOCAL:21 += 150

LOCAL:12 = 900

;ABL:봉사정신をみる
SELECTCASE ABL:봉사정신
CASE 0
	LOCAL:4 = 200
	LOCAL:5 = 50
CASE 1
	LOCAL:4 = 250
	LOCAL:5 = 200
CASE 2
	LOCAL:4 = 350
	LOCAL:5 = 550
CASE 3
	LOCAL:4 = 450
	LOCAL:5 = 900
CASE 4
	LOCAL:4 = 600
	LOCAL:5 = 1500
CASEELSE
	LOCAL:4 = 750
	LOCAL:5 = 2200
ENDSELECT

;ABL:Ｖ감각をみる
SELECTCASE ABL:Ｖ감각
CASE 0
	LOCAL:3 = 25
	TIMES LOCAL:4 , 0.50
CASE 1
	LOCAL:3 = 75
	TIMES LOCAL:4 , 0.80
CASE 2
	LOCAL:3 = 125
	TIMES LOCAL:4 , 1.00
CASE 3
	LOCAL:3 = 175
	TIMES LOCAL:4 , 1.20
CASE 4
	LOCAL:3 = 300
	TIMES LOCAL:4 , 1.50
CASEELSE
	LOCAL:3 = 425
	TIMES LOCAL:4 , 2.00
ENDSELECT

;PALAM:욕정
SELECTCASE PALAM:욕정
CASE IS < PALAMLV:1
	TIMES LOCAL:3 , 0.30
CASE IS < PALAMLV:2
	TIMES LOCAL:3 , 0.60
CASE IS < PALAMLV:3
	TIMES LOCAL:3 , 1.00
CASE IS < PALAMLV:4
	TIMES LOCAL:3 , 1.50
CASE IS < PALAMLV:5
	TIMES LOCAL:3 , 1.80
CASEELSE
	TIMES LOCAL:3 , 2.20
ENDSELECT

;ABL:순종をみる
SELECTCASE ABL:순종
CASE 0
	TIMES LOCAL:3 , 0.60
	LOCAL:13 += 700
CASE 1
	TIMES LOCAL:3 , 0.80
	LOCAL:13 += 500
CASE 2
	TIMES LOCAL:3 , 1.00
	LOCAL:13 += 300
CASE 3
	TIMES LOCAL:3 , 1.20
	TIMES LOCAL:15 , 0.80
	LOCAL:13 += 100
CASE 4
	TIMES LOCAL:3 , 1.40
	TIMES LOCAL:15 , 0.60
	LOCAL:13 += 30
CASEELSE
	TIMES LOCAL:3 , 1.60
	TIMES LOCAL:15 , 0.30
ENDSELECT

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM177
PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 다리를 크게 벌리고 유혹했다…
PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 솟구치는 열정을 주체하지 못하고, 비틀비틀 %CALLNAME:TARGET%에게 다가갔다…


@EQUIP_COM29
;러브트랩 중
CALL SOURCE_COM177, 20

RETURN 1

@EQUIP_COM29_2
;러브트랩 중
PRINTL ＜러브트랩＞

RETURN 1


@COM_ORDER_COM177, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:Ｖ감각
IF ABL:Ｖ감각
	LOCAL += ABL:Ｖ감각 * 2
	LOCALS = %LOCALS% + %ABLNAME:4%LV{ABL:Ｖ감각}({ABL:Ｖ감각 * 2})
ENDIF
;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF
;ABL:정액중독
IF ABL:정액중독
	LOCAL += ABL:정액중독 * 3
	LOCALS = %LOCALS% + %ABLNAME:11%LV{ABL:정액중독}({ABL:정액중독 * 3})
ENDIF
;PALAM:Ｖ윤（の부족）
IF PALAM:Ｖ윤 < PALAMLV:3
	LOCAL -= 5
	LOCALS = %LOCALS% - Ｖ윤부족(5)
ENDIF

;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 5
	LOCALS = %LOCALS% - %TALENTNAME:35%(5)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF
;미약
IF TEQUIP:미약
	LOCAL += 6
	LOCALS = %LOCALS% + %ITEMNAME:31%(6)
ENDIF

;불결있음
IF COM_ORDER_STAIN("Ｐ", "Ｖ")
	LOCAL -= COM_ORDER_STAIN("Ｐ", "Ｖ")
	LOCALS = %LOCALS% - 불결있음
	;악취둔감
	SIF TALENT:악취둔감 || TALENT:악취민감
		LOCALS = %LOCALS%, \@TALENT:악취둔감 ? %TALENTNAME:61% # %TALENTNAME:62%\@
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｐ", "Ｖ")})
ENDIF

;合計を表示(24이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 24

RETURN RESULT
