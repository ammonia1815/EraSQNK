@COM148
;피어싱
PRINTL 피어싱

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = 피어싱

RETURN 1

@TRAIN_MESSAGE_COM148
#DIM LCOUNT
;ピアスの形状
#DIMS PIERCES
;ピアスの場所
#DIMS PART_PIERCES

PRINTFORMW %CALLNAME:TARGET%의 몸을 피어스로 장식합니다. 
IF ABL:마조끼 < 3 && FIRSTCOM(SELECTCOM) == 0
	PRINTFORML 이 플레이는 아직 빠른 생각도 듭니다만……이번은 어떻게 합니까？
ELSE
	PRINTFORML 그런데, 이번에는 어떻게 합니까？
ENDIF

PRINTFORML  [ 0] \@ PENIS(TARGET) ? 페니스 # 클리토리스 \@\@ TALENT:클리피어스 ? 에서 피어스를 뺀다 # 에 피어스를 단다 \@
SIF TALENT:남자
	SETCOLOR DEF_COLOR("회색")
PRINTFORML  [ 1] 소음순\@ TALENT:라비아피어스 ? 에서 피어스를 뺀다 # 에 피어스를 단다 \@
RESETCOLOR
SIF COND("진성함몰유두")
	SETCOLOR DEF_COLOR("회색")
PRINTFORML  [ 2] 유두\@ TALENT:니플피어스 ? 에서 피어스를 뺀다 # 에 피어스를 단다 \@
RESETCOLOR
PRINTFORML  [ 3] 배꼽\@ TALENT:배꼽피어스 ? 에서 피어스를 뺀다 # 에 피어스를 단다 \@
PRINTFORML [100] 그만둔다

$INPUT_LOOP
INPUT

IF RESULT == 100
	TFLAG:14 = 1
	RETURN 0
ELSEIF RESULT == 0
	CALL SETFLAG, "클리피어스플래그", TARGET
	SIF TALENT:클리피어스 == 0
		CALL SETFLAG, "피어스ＯＮ플래그", TARGET
	CALL DEL_CEVENT_GROUP, "클리피어스"
	IF PENIS(TARGET)
		PART_PIERCES = 페니스
	ELSE
		PART_PIERCES = 클리토리스
	ENDIF
ELSEIF RESULT == 1 && TALENT:남자 == 0
	CALL SETFLAG, "라비아피어스플래그", TARGET
	SIF TALENT:라비아피어스 == 0
		CALL SETFLAG, "피어스ＯＮ플래그", TARGET
	CALL DEL_CEVENT_GROUP, "라비아피어스"
	PART_PIERCES = 小陰唇
ELSEIF RESULT == 2 && COND("진성함몰유두") == 0
	CALL SETFLAG, "니플피어스플래그", TARGET
	SIF TALENT:니플피어스 == 0
		CALL SETFLAG, "피어스ＯＮ플래그", TARGET
	CALL DEL_CEVENT_GROUP, "니플피어스"
	PART_PIERCES = 유두
ELSEIF RESULT == 3
	CALL SETFLAG, "배꼽피어스플래그", TARGET
	SIF TALENT:배꼽피어스 == 0
		CALL SETFLAG, "피어스ＯＮ플래그", TARGET
	CALL DEL_CEVENT_GROUP, "배꼽피어스"
	PART_PIERCES = おへそ
ELSE
	GOTO INPUT_LOOP
ENDIF

;ピアスの詳細を文字列で記録
PIERCES = 

;ピアスをつけた
IF COND("피어스ＯＮ플래그")
	PRINTFORML 어떤 피어스를 답니까？
	PRINTFORML  [ 0] 링형
	PRINTFORML  [ 1] 바벨형
	PRINTFORML  [ 2] 십자가형
	PRINTFORML  [ 3] ♂형태
	PRINTFORML  [ 4] ♀형태
	PRINTFORML  [90] 직접 입력한다
	PRINTFORML [100] 딱히 신경쓰지않는다（묘사안함）

	CALL INPUT_SELECT, 5, 90, 100

	SELECTCASE RESULT
	CASE 100
	CASE 0
		PIERCES = 린グ型
	CASE 1
		PIERCES = バーベル型
	CASE 2
		PIERCES = 十字架型
	CASE 3
		PIERCES = ♂型
	CASE 4
		PIERCES = ♀型
	CASE 90
		PRINTL 전각으로８문자이하（반각이라면16문자까지）의 캐릭터 라인을 입력해 주세요
		PRINTL ("～의 피어스"등과 같이 사용해도 위화감이 없는 것을 권장)
		PRINTL 또한, 아무것도 입력하지 않는 경우에는 딱히 신경쓰지않는다（묘사안함）를 선택한 것과 같습니다
		CALL INPUTS_SELECT, 16
		PIERCES = %RESULTS%
	ENDSELECT

	PRINTL 마취를 사용합니까? 
	PRINTL [0] 예
	PRINTL [1] 아니오
	CALL INPUT_SELECT, 2
	SIF RESULT == 0
		CALL SETFLAG, "鎮痛麻酔플래그", TARGET

	PRINTFORM %조사처리(CALLNAME:PLAYER,"는")%%CALLNAME:TARGET%의
	IF COND("클리피어스플래그")
		IF PENIS(TARGET)
			PRINT 페니스에
		ELSE
			IF TALENT:음핵
				PRINT 발기한
			ELSEIF TALENT:Ｃ민감
				PRINT 민감한
			ELSEIF TALENT:작은몸집
				PRINT 작은
			ENDIF
			PRINT 클리토리스에
		ENDIF
	ELSEIF COND("라비아피어스플래그")
		IF TALENT:음호
			PRINT 추잡하게 실룩거리는
		ELSEIF TALENT:Ｖ민감
			PRINT 민감한 
		ELSEIF TALENT:작은몸집 && TALENT:유치
			PRINT 어린
		ELSEIF TALENT:작은몸집
			PRINT 작은 
		ENDIF
		PRINT 소음순에
	ELSEIF COND("니플피어스플래그")
		IF COND("함몰유두")
			PRINTFORM 매몰 된\@ COND("유두비대") ? 거대한 # \@유두를노출시켜
		ELSE
			IF COND("유두비대")
				PRINT 비대한
			ELSEIF TALENT:음유
				PRINT 발기한
			ELSEIF TALENT:Ｂ민감
				PRINT 민감한
			ELSEIF TALENT:작은몸집 && TALENT:유치
				PRINT 어린
			ELSEIF TALENT:작은몸집
				PRINT 작은
			ENDIF
			PRINT 유두에
		ENDIF
	ELSEIF COND("배꼽피어스플래그")
		PRINT 배꼽에
	ENDIF
	SIF PIERCES != ""
		PRINTFORM %PIERCES%의
	PRINTFORMW 피어스를 붙였다

	IF COND("클리피어스플래그")
		IF PENIS(TARGET)

		ELSE
			PRINTFORMW %CALLNAME:TARGET%의 클리토리스는 피어스를 달은 덕분에 겉의 피부가 벗겨져, 항상 노출되어있는 상태가 되어버렸다…
		ENDIF
		PIERCES = 클리피어스：%PIERCES%
	ELSEIF COND("라비아피어스플래그")
		SIF AGE(TARGET) >= 3
			PRINTFORMW %CALLNAME:TARGET%의 소음순은 붙여진 피어스의 중량감에 끌려가 한눈에 알 정도로 대음순으로부터 넘쳐 나와 버리고 있다…
		PIERCES = 라비아피어스：%PIERCES%
	ELSEIF COND("니플피어스플래그")
		SIF COND("함몰유두")
			PRINTFORMW %CALLNAME:TARGET%의 유두는 달아진 피어스의 덕분에 돌아올 수 없게 되어, 상시노출로 고정되어 버렸다…
		PIERCES = 니플피어스：%PIERCES%
	ELSEIF COND("배꼽피어스플래그")
		PIERCES = 배꼽피어스：%PIERCES%
	ENDIF

	;ピアスの形状フラグセット
	SIF PIERCES != ""
		CALL SET_CEVENT, PIERCES
;ピアスを外した
ELSE
	PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% %CALLNAME:TARGET%의 %PART_PIERCES%로부터 피어스를 제외했다
ENDIF

;つけたり外したり
IF COND("클리피어스플래그")
	INVERTBIT TALENT:클리피어스, 0
ELSEIF COND("라비아피어스플래그")
	INVERTBIT TALENT:라비아피어스, 0
ELSEIF COND("니플피어스플래그")
	INVERTBIT TALENT:니플피어스, 0
ELSEIF COND("배꼽피어스플래그")
	INVERTBIT TALENT:배꼽피어스, 0
ENDIF


@FLAG_COM148, ARG
;クリor라비아피어스なら, 보지를노출させる
IF COND("클리피어스플래그") + COND("라비아피어스플래그") && !CHECK_CLO("Ｖ노출")
	CALL SET_PANTIES, TARGET
	CALL SET_BODYS, TARGET
	CALL SET_OUTER, TARGET
ENDIF

STAIN:PLAYER:손 = 0
IF COND("클리피어스플래그")
	STAIN:Ｖ = SET_STAIN(0, "Ｖ")
ELSEIF COND("라비아피어스플래그")
	STAIN:Ｖ = SET_STAIN(0, "Ｖ")
ELSEIF COND("니플피어스플래그")
	STAIN:Ｂ = 0
ENDIF

@SOURCE_COM148, ARG
VARSET LOCAL

LOCAL:20 += 100
LOCAL:21 += 200

;外した場合
SIF COND("피어스ＯＮ플래그") == 0
	RETURN 1

;基本は배꼽피어스の時
;初めてつける時は아픔のせいで, 체력기력소비が凄いことになります

;主なSOURCE
;LOCAL:6  아픔
;LOCAL:13 굴복
;LOCAL:14 일탈

LOCAL:6 = 3000
LOCAL:13 = 1000
LOCAL:14 = 3000

IF COND("클리피어스플래그")
	LOCAL:20 += 100
	LOCAL:21 += 300
	LOCAL:6 += 17000
	LOCAL:13 += 9000
	LOCAL:14 += 17000
ELSEIF COND("라비아피어스플래그")
	LOCAL:20 += 100
	LOCAL:21 += 200
	LOCAL:6 += 7000
	LOCAL:13 += 9000
	LOCAL:14 += 12000
ELSEIF COND("니플피어스플래그")
	LOCAL:20 += 50
	LOCAL:21 += 200
	LOCAL:6 += 12000
	LOCAL:13 += 9000
	LOCAL:14 += 12000
ELSEIF COND("배꼽피어스플래그")

ENDIF

;ABL:마조끼をみる
SELECTCASE ABL:마조끼
CASE 0
CASE 1
	TIMES LOCAL:13 , 1.20
	TIMES LOCAL:14 , 0.90
CASE 2
	TIMES LOCAL:13 , 1.40
	TIMES LOCAL:14 , 0.80
CASE 3
	TIMES LOCAL:13 , 1.60
	TIMES LOCAL:14 , 0.50
CASE 4
	TIMES LOCAL:13 , 1.80
	TIMES LOCAL:14 , 0.30
CASEELSE
	TIMES LOCAL:13 , 2.00
	TIMES LOCAL:14 , 0.10
ENDSELECT

;노출벽をみる
SELECTCASE ABL:노출벽
CASE 0, 1
CASE 2
	TIMES LOCAL:14 , 0.90
CASE 3
	TIMES LOCAL:14 , 0.80
CASE 4
	TIMES LOCAL:14 , 0.70
CASEELSE
	TIMES LOCAL:14 , 0.60
ENDSELECT

;麻酔を使ったら아픔激減
SIF COND("鎮痛麻酔플래그")
	TIMES LOCAL:6 , 0.01

;ビデオ촬영중
IF TEQUIP:비디오카메라
	TIMES LOCAL:13 , 1.50
	TIMES LOCAL:14 , 1.50
ENDIF

CALL SOURCE_SMPLAY, "피어싱", ARG

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG

