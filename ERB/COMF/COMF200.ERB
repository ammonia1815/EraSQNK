@COM200
;촉수소환
PRINTL 촉수소환

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = 촉수소환

RETURN 1

@TRAIN_MESSAGE_COM200
IF TEQUIP:촉수
	PRINTFORML 촉수를 돌려보내, %CALLNAME:TARGET%의 몸을 풀어주었다…
	TEQUIP:촉수 = 0

	;촉수삽입(바이브)
	TEQUIP:바이브 = 0
	;애널촉수(애널바이브)
	TEQUIP:애널바이브 = 0
	;촉수클리자극(클리캡)
	TEQUIP:클리캡 = 0
	;촉수유두자극(니플캡)
	TEQUIP:니플캡 = 0
	;촉수착유(착유기)
	TEQUIP:착유기 = 0
	;촉수페니스자극(オナホ)
	TEQUIP:오나홀 = 0
	;촉수긴박(縄)
	TEQUIP:구속 = 0
	;촉수관장(관장器)
	TEQUIP:관장 = 0
	;촉수구욕
	TEQUIP:촉수구욕 = 0

ELSE
	IF CONFIG("촉수は주인")
		PRINTFORML %조사처리(CALLNAME:PLAYER,"는")% 마법으로 이형의 촉수로 변신해, %CALLNAME:TARGET%의 전신에 얽혀들었다…
	ELSE
		PRINTFORML %조사처리(CALLNAME:PLAYER,"가")% 불러들인 이형의 촉수가, %CALLNAME:TARGET%의 몸을 휘감았다…
		;촉수用사정게이지
		BASE:MASTER:촉수 = 0
		MAXBASE:MASTER:촉수 = 30000
	ENDIF

	TEQUIP:촉수 = 1
ENDIF

@EQUIP_COM90
SIF SELECTCOM == 200
	RETURN 0

;촉수조교中
CALL SOURCE_COM200, 30

@FLAG_COM200, ARG

@SOURCE_COM200, ARG
VARSET LOCAL

SIF TEQUIP:촉수 == 0
	RETURN 0

LOCAL:30 = 50
SELECTCASE EXP:촉수경험
CASE IS < EXPLV:1
	TIMES LOCAL:30 , 3.00
CASE IS < EXPLV:2
	TIMES LOCAL:30 , 2.50
CASE IS < EXPLV:3
	TIMES LOCAL:30 , 2.00
CASE IS < EXPLV:4
	TIMES LOCAL:30 , 1.00
CASE IS < EXPLV:5
	TIMES LOCAL:30 , 0.80
CASEELSE
	TIMES LOCAL:30 , 0.60
ENDSELECT

;겁쟁이
SIF TALENT:겁쟁이
	TIMES LOCAL:30 , 2.00

;감정결핍
SIF TALENT:감정결핍
	TIMES LOCAL:30 , 0.60

LOCAL:20 += LOCAL:30
LOCAL:21 += LOCAL:30 * 2

LOCAL:9 += LOCAL:30 * 40
LOCAL:8 += LOCAL:30 * 20
LOCAL:14 += LOCAL:30 * 10

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@SYOKUSYU_SHOOT

SIF TEQUIP:촉수 == 0
	RETURN 0
;-------------------------------------------------
;사정체크
;-------------------------------------------------
LOCAL = 1000
;촉수삽입
SIF TEQUIP:바이브
	LOCAL += 2000 + ABL_V(TARGET)*50 + ABL:Ｖ확장*200
;애널촉수
SIF TEQUIP:애널바이브
	LOCAL += 2000 + ABL_A(TARGET)*50 + ABL:Ａ확장*200
;촉수클리자극
SIF TEQUIP:클리캡
	LOCAL += 1000
;촉수유두자극
SIF TEQUIP:니플캡
	LOCAL += 1000
;촉수착유
SIF TEQUIP:착유기
	LOCAL += 1000
;촉수페니스자극
SIF TEQUIP:오나홀
	LOCAL += 1000
;촉수긴박
SIF TEQUIP:구속
	LOCAL += 1500
;촉수관장
SIF TEQUIP:관장
	LOCAL += 1000
;촉수구욕
SIF TEQUIP:촉수구욕
	LOCAL += 1500 + ABL_T(TARGET)*50 + ABL:목확장*200
;ABL:기교と순종をみる
LOCAL = MULTIPLY(LOCAL, 100 + ABL:기교*10 + ABL:순종*5)

;PALAM:욕정をみる
SELECTCASE PALAM:욕정
CASE IS < PALAMLV:3
	TIMES LOCAL , 1.00
CASE IS < PALAMLV:4
	TIMES LOCAL , 1.10
CASE IS < PALAMLV:5
	TIMES LOCAL , 1.20
CASEELSE
	TIMES LOCAL , 1.30
ENDSELECT
;촉수피스톤
SIF SELECTCOM == 210
	TIMES LOCAL , 1.50

IF CONFIG("촉수は주인")
	BASE:MASTER:절정 += LOCAL
	TFLAG:15 = CALCF("절정", MASTER)
ELSE
	BASE:MASTER:촉수 += LOCAL
	SELECTCASE BASE:MASTER:촉수
	CASE IS >= MAXBASE:MASTER:촉수*4
		TFLAG:15 = 4
	CASE IS >= MAXBASE:MASTER:촉수*3
		TFLAG:15 = 3
	CASE IS >= MAXBASE:MASTER:촉수*2
		TFLAG:15 = 2
	CASE IS >= MAXBASE:MASTER:촉수
		TFLAG:15 = 1
	CASEELSE
		TFLAG:15 = 0
	ENDSELECT
ENDIF

;사정しないならここまで
SIF TFLAG:15 == 0
	RETURN 0

;-------------------------------------------------
;촉수사정処理＆텍스트
;-------------------------------------------------
PRINTL 
;촉수大量사정
IF TFLAG:15 >= 2
	PRINTFORMW %CALLNAME:TARGET%의 온몸에 엮여든 촉수가 일제히 끈적한 체액을 토해냈다…
	IF RAND:6 == 0
		PRINTL 『뷰븃, 부븃, 뷰릇, 부-왘』
	ELSEIF RAND:5 == 0
		PRINTL 『부붓, 브웃, 부부붓! 부와아아앜! 』
	ELSEIF RAND:4 == 0
		PRINTL 『드붓, 부부부붓!  비븃, 뷰븃, 뷰릇……브븝, 꿀럭, 꿀럭』
	ELSEIF RAND:3 == 0
		PRINTL 『부왘!  부르르르븟, 뷰브르븟, 뷰왘, 븃, 부아아……』
	ELSEIF RAND:2 == 0
		PRINTL 『부왘!  뷰븟, 븃, 부, 부와ㅋ……』
	ELSE
		PRINTL 『부부부붓, 부붓브부웃, 부와와앜! 』
	ENDIF
ELSE
	PRINTFORMW %CALLNAME:TARGET%에게 얽혀든 촉수가 체액을 토해냈다…
	IF RAND:3 == 0
		PRINTL 『뷰뷰뷰븟, 봐-앜』
	ELSEIF RAND:2 == 0
		PRINTL 『브부브붓, 브부왘! 』
	ELSE
		PRINTL 『브큥, 뷰큿! 부크왘! 』
	ENDIF
ENDIF

;애널애무
IF SELECTCOM == 2 || SELECTCOM == 207
	PRINTFORMW 촉수의 움직임이 살짝 둔해진 바로 다음 순간, %CALLNAME:TARGET%의 항문안으로 뜨거운 액체가 터져나왔다…
;가슴애무
ELSEIF SELECTCOM == 5 || SELECTCOM == 204 || SELECTCOM == 205
	PRINTFORMW 얽혀든 촉수가 일제히 백탁액을 토해내, %CALLNAME:TARGET%의 가슴을 새하얗게 물들였다…
;촉수바이브
ELSEIF TEQUIP:바이브 && (RANDIF(2, TEQUIP:애널바이브, TEQUIP:촉수구욕) || SELECTCOM == 201)
	PRINTFORMW %CALLNAME:TARGET%의 자궁은 촉수가 방출한 아기씨 즙으로 가득찼다…
;애널촉수, 촉수관장
ELSEIF TEQUIP:애널바이브 && (RANDIF(2, TEQUIP:촉수구욕) || SELECTCOM == 202)
	PRINTFORMW %CALLNAME:TARGET%의 항문 안은 촉수가 방출한 아기씨 즙으로 가득찼다…
;촉수클리자극
ELSEIF SELECTCOM == 203
	PRINTFORMW 얽혀든 촉수가 일제히 백탁액을 토해내, %CALLNAME:TARGET%의 가랑이를 새하얗게 물들였다…
;촉수긴박
ELSEIF SELECTCOM == 206
	PRINTFORMW 몸안에 감겨붙은 촉수에서 백탁액이 뿜어져, %조사처리(CALLNAME:TARGET,"는")% 질척질척하게 되었다…
;촉수구욕
ELSEIF TEQUIP:촉수구욕 && (RAND:2 || SELECTCOM == 208)
	IF RAND:2 == 0
		PRINTFORMW 얽혀든 촉수가 일제히 백탁액을 토해내, %CALLNAME:TARGET%의 안면을 새하얗게 물들였다…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%의 입안은 촉수가 발한 아기씨 즙으로 가득찼다…
	ENDIF
;촉수페니스자극
ELSEIF SELECTCOM == 209
	PRINTFORMW 얽혀든 촉수가 일제히 백탁액을 토해내, %CALLNAME:TARGET%의 페니스를 새하얗게 물들였다…
ELSE
	PRINTFORMW 얽혀든 촉수가 일제히 백탁액을 토해내, %조사처리(CALLNAME:TARGET,"는")% 새하얗게 물들었다…
ENDIF

LOCAL = 1
SIF TEQUIP:바이브
	LOCAL += 5
SIF TEQUIP:애널바이브
	LOCAL += 5
SIF TEQUIP:클리캡
	LOCAL += 1
SIF TEQUIP:니플캡
	LOCAL += 1
SIF TEQUIP:착유기
	LOCAL += 1
SIF TEQUIP:오나홀
	LOCAL += 1
SIF TEQUIP:구속
	LOCAL += 1
SIF TEQUIP:관장
	LOCAL += 1
SIF TEQUIP:촉수구욕
	LOCAL += 3

;사정処理
SIF CONFIG("촉수は주인")
	EXP:MASTER:사정경험 += TFLAG:15
EXP:정액경험 += TFLAG:15 * LOCAL
PRINT 촉수
SIF TFLAG:15 >= 3
	PRINT 초
SIF TFLAG:15 >= 2
	PRINT 대량
PRINTL 사정
PRINTFORMW 정액경험+{TFLAG:15 * LOCAL}

;게이지が限界超え・マイナスになっていたら修正
IF CONFIG("촉수は주인")
	BASE:MASTER:절정 -= MAXBASE:MASTER:절정*TFLAG:15
	SIF BASE:MASTER:절정 >= MAXBASE:MASTER:절정
		BASE:MASTER:절정 = MAXBASE:MASTER:절정-1
	SIF BASE:MASTER:절정 < 0
		BASE:MASTER:절정 = 0

	;정력を減少
	BASE:MASTER:정력 -= TFLAG:15 * 2
	PRINTFORMW 정력-{TFLAG:15 * 2}
ELSE
	BASE:MASTER:촉수 -= MAXBASE:MASTER:촉수*TFLAG:15
	SIF BASE:MASTER:촉수 >= MAXBASE:MASTER:촉수
		BASE:MASTER:촉수 = MAXBASE:MASTER:촉수-1
	SIF BASE:MASTER:촉수 < 0
		BASE:MASTER:촉수 = 0
ENDIF


;정액汚れ等を加える
SIF TEQUIP:촉수구욕
	CALL SETFLAG, "정음경험", TFLAG:15, TARGET, -5
SIF TEQUIP:오나홀
	CALL ADD_STAIN, TARGET, "Ｐ", "정액"
SIF TEQUIP:바이브
	CALL SETFLAG, "질내정액경험", TFLAG:15, TARGET, -5

SIF TEQUIP:애널바이브
	CALL SETFLAG, "장내정액경험", TFLAG:15, TARGET, -5

SIF TEQUIP:니플캡 || TEQUIP:착유기
	CALL ADD_STAIN, TARGET, "Ｂ", "정액"

;受精확률
CALL CHECK_NINSHIN

RETURN 1

@EQUIP_COM90_2
;촉수조교中
PRINTL ＜촉수조교中＞

;보지TEQUIP:바이브
;애널TEQUIP:애널바이브
;클리토리스TEQUIP:클리캡
;유두TEQUIP:니플캡
;搾乳TEQUIP:착유기
;페니스TEQUIP:오나홀
;緊縛TEQUIP:구속
;관장TEQUIP:관장
;입辱TEQUIP:촉수구욕

LOCAL = 1
SIF TEQUIP:바이브
	LOCAL += 2
SIF TEQUIP:애널바이브
	LOCAL += 2
SIF TEQUIP:클리캡
	LOCAL += 1
SIF TEQUIP:니플캡
	LOCAL += 1
SIF TEQUIP:착유기
	LOCAL += 1
SIF TEQUIP:오나홀
	LOCAL += 1
SIF TEQUIP:구속
	LOCAL += 1
SIF TEQUIP:관장
	LOCAL += 1
SIF TEQUIP:촉수구욕
	LOCAL += 3

EXP:촉수경험 += LOCAL
PRINTFORML 촉수경험+{LOCAL}

RETURN 1

;촉수바이브
@COM201
JUMP COM31
RETURN 1

@FLAG_COM201, ARG
CALL FLAG_COM31, ARG

@SOURCE_COM201, ARG
CALL SOURCE_COM31, ARG


;애널촉수
@COM202
JUMP COM33
RETURN 1

@FLAG_COM202, ARG
CALL FLAG_COM33, ARG

@SOURCE_COM202, ARG
CALL SOURCE_COM33, ARG


;촉수클리자극
@COM203
JUMP COM34
RETURN 1

@FLAG_COM203, ARG
CALL FLAG_COM34, ARG

@SOURCE_COM203, ARG
CALL SOURCE_COM34, ARG


;촉수유두자극
@COM204
JUMP COM35
RETURN 1

@FLAG_COM204, ARG
CALL FLAG_COM35, ARG

@SOURCE_COM204, ARG
CALL SOURCE_COM35, ARG


;촉수착유
@COM205
JUMP COM36
RETURN 1

@FLAG_COM205, ARG
CALL FLAG_COM36, ARG

@SOURCE_COM205, ARG
CALL SOURCE_COM36, ARG


;촉수긴박
@COM206
JUMP COM134
RETURN 1

@FLAG_COM206, ARG
CALL FLAG_COM134, ARG

@SOURCE_COM206, ARG
CALL SOURCE_COM134, ARG


;촉수관장
@COM207
JUMP COM136
RETURN 1

@FLAG_COM207, ARG
CALL FLAG_COM136, ARG

@SOURCE_COM207, ARG
CALL SOURCE_COM136, ARG


;촉수페니스자극
@COM209
JUMP COM37
RETURN 1

@FLAG_COM209, ARG
CALL FLAG_COM37, ARG

@SOURCE_COM209, ARG
CALL SOURCE_COM37, ARG



@SYOKUSYU_MILK
;［Ｂ感度］３이상, ［모유체질］無しかつ［小柄体系］で［빈유］無しかつ［남자］無し
IF ABL:Ｂ감각 >= 3 && TALENT:모유체질 == 0 && (TALENT:작은몸집 == 0 || TALENT:빈유 == 0 ) && TALENT:남자 == 0
	PRINTFORMW 촉수에게 젖을 빨리는 사이에 %조사처리(CALLNAME:TARGET,"는")% 모유가 나오게 되었다. 
	TALENT:모유체질 = 2
ENDIF
