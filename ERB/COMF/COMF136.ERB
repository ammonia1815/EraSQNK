@COM136
;관장기＋플러그
;배설した場合は NOWEX:배설 で記録. SOURCEとしては, 12：노출, 13：굴복, 14：일탈が特に高い. 
;배설を촬영すると, 初回は굴복が凄まじい事になる

IF COND("애널栓") || (TEQUIP:촉수 && TEQUIP:관장)
	IF COND("대량관장")
		PRINT 대량배설
	ELSEIF COND("액체관장")
		PRINT 배설
	ELSE
		PRINT 장내가스배설
	ENDIF
	SIF TEQUIP:비디오카메라
		PRINT （비디오 녹화중）
ELSEIF TEQUIP:촉수
	PRINT 촉수관장
ELSE
	PRINT 관장기
ENDIF
PRINTL 

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

IF NOWEX:배설
	IF TEQUIP:비디오카메라
		STR:0 = 배설（비디오 녹화중）
	ELSE
		STR:0 = 배설
	ENDIF
ELSEIF TEQUIP:촉수
	STR:0 = 촉수관장
ELSE
	IF COND("애널栓")
		STR:0 = 관장
	ELSE
		STR:0 = 관장（플러그 없음）
	ENDIF
ENDIF

RETURN 1

@FLAG_COM136, ARG
;팬티などをずらす
SIF !CHECK_CLO("Ａ노출")
	CALL SETFLAG, "바텀을노출：텍스트없음", TARGET

@SOURCE_COM136, ARG
VARSET LOCAL

;플레이としては, TEQUIP:관장…つまりCOND("액체관장")の値が増えるほどキツいこととする. 最高に厳しいのは배설플레이. 
LOCAL:20 += 60
LOCAL:21 += 150

;LOCAL:2  쾌Ａ
LOCAL:2 = ABL:Ａ감각*100
;LOCAL:6  고통
LOCAL:6 = COND("액체관장")*500
;LOCAL:8  불결
LOCAL:8 = 4000 + COND("액체관장")*1000
;LOCAL:12 노출
LOCAL:12 = 10000
;LOCAL:13 굴복
LOCAL:13 = 9000 + COND("액체관장")*1000
;LOCAL:14 일탈
LOCAL:14 = 4000 + COND("액체관장")*1000


;배설
IF NOWEX:배설
	LOCAL:8 += 20000
	LOCAL:12 += NOWEX:배설 * 5000 + 10000
	LOCAL:13 += NOWEX:배설 * 5000 + 10000
	LOCAL:14 += NOWEX:배설 * 5000 + 10000
	;初めて촬영したなら
	IF TEQUIP:비디오카메라 && GETBIT(CFLAG:12, 7) == 0
		LOCAL:13 *= 10
	;촬영したなら
	ELSEIF TEQUIP:비디오카메라
		LOCAL:13 *= 2
	ENDIF
ENDIF

;場所をみる
IF COND("마을") || TEQUIP:관내노출플레이
	TIMES LOCAL:12 , 1.50
	TIMES LOCAL:14 , 3.00
ELSEIF TEQUIP:야외플레이
	TIMES LOCAL:12 , 1.50
	TIMES LOCAL:14 , 2.00
ELSEIF TEQUIP:수치플레이
	TIMES LOCAL:12 , 1.20
	TIMES LOCAL:14 , 1.50
ELSEIF TEQUIP:목욕탕플레이
	TIMES LOCAL:14 , 0.50
ENDIF

;노출벽で일탈減少
SELECTCASE ABL:노출벽
CASE 0
CASE 1
	TIMES LOCAL:14 , 0.90
CASE 2
	TIMES LOCAL:14 , 0.75
CASE 3
	TIMES LOCAL:14 , 0.60
CASE 4
	TIMES LOCAL:14 , 0.40
CASEELSE
	TIMES LOCAL:14 , 0.20
ENDSELECT

;ABL8,마조끼で일탈減少＆굴복ＵＰ
SELECTCASE ABL:마조끼
CASE 0
CASE 1
	TIMES LOCAL:13 , 1.20
	TIMES LOCAL:14 , 0.80
CASE 2
	TIMES LOCAL:13 , 1.50
	TIMES LOCAL:14 , 0.60
CASE 3
	TIMES LOCAL:13 , 2.00
	TIMES LOCAL:14 , 0.40
CASE 4
	TIMES LOCAL:13 , 2.50
	TIMES LOCAL:14 , 0.20
CASEELSE
	TIMES LOCAL:13 , 3.00
	TIMES LOCAL:14 , 0.05
ENDSELECT

;Ａ확장LVをみる
SELECTCASE ABL:Ａ확장
CASE IS >= 3
	TIMES LOCAL:6 , 0.30
CASE 2
	TIMES LOCAL:6 , 0.50
CASE 1
CASEELSE
	TIMES LOCAL:6 , 2.00
ENDSELECT

;처녀で정조관념or엉덩이許可플래그
SIF (TALENT:처녀 == 1 && TALENT:정조관념) || CFLAG:100
	LOCAL:14 /= 2

CALL SOURCE_SMPLAY, "관장", ARG

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM136
; 0bit 공기관장
; 1bit 관장（少なめ）
; 2bit 관장（規定量）
; 3bit 관장（大量）
; 4bit 관장（超大量）
; 9bit 애널플러그
;10bit 정액관장
;11bit 온천관장

;관장플러그の着脱
IF COND("애널栓")
	CALL SETFLAG, "배설"
ELSE
	IF TEQUIP:관장
		IF COND("대량관장")
			PRINTFORMW %CALLNAME:TARGET%에게 한계까지 관장액을 추가로 주입했다
			PRINTFORMW 대략의 액채로 가득찬 %CALLNAME:TARGET%의 배는 개구리처럼 부풀어올라있다…
			TEQUIP:관장 = 1p1 + 1p2 + 1p3 + 1p4
		ELSE
			PRINTFORMW %CALLNAME:TARGET%의 애널에 관장액을 추가로 흘려넣었다
			PRINTFORMW 조금 더 여유가 있어 보이지만, %CALLNAME:TARGET%의 배는 팽팽하게 부풀어 상당히 괴로워보인다…
			TEQUIP:관장 = 1p1 + 1p2 + 1p3
		ENDIF
	ELSEIF ITEM:관장기＋플러그
		PRINTFORML 어느 걸로 %CALLNAME:TARGET%의 배를 부풀립니까? 
		PRINTFORML （주의：소형 이외의 것을 사용하는 경우에는 애널섹스등의 일부 커맨드를 실행할 수 없습니다）
		PRINTFORML  [ 0] 소형 관장기
		PRINTFORML  [ 1] 보통 관장기
		PRINTFORML  [ 2] 대형 관장기
		PRINTFORML  [ 3] 특대 관장기
		SIF ITEM:공기펌프
			PRINTFORML  [ 4] 공기펌프
		PRINTFORML [100] 그만둔다

		IF ITEM:공기펌프
			CALL INPUT_SELECT, 5, 100
		ELSE
			CALL INPUT_SELECT, 4, 100
		ENDIF

		IF RESULT == 100
			TFLAG:14 = 1
			RETURN 0
		ELSEIF RESULT == 0
			PRINTFORMW %CALLNAME:TARGET%의 엉덩이구멍에 관장액을 조금만 흘려넣었다
			TEQUIP:관장 = 1p1
		ELSEIF RESULT == 1
			PRINTFORMW %CALLNAME:TARGET%의 애널에 관장액을 흘려넣었다
			TEQUIP:관장 = 1p1 + 1p2
		ELSEIF RESULT == 2
			PRINTFORMW %CALLNAME:TARGET%의 애널에 대량의 관장액을 흘려넣었다
			TEQUIP:관장 = 1p1 + 1p2 + 1p3
		ELSEIF RESULT == 3
			PRINTFORMW 대량의 관장액을 주입당한 %CALLNAME:TARGET%의 배는 마치 임신한 것처럼 부풀어버렸다…
			TEQUIP:관장 = 1p1 + 1p2 + 1p3 + 1p4
		ELSEIF RESULT == 4 && ITEM:공기펌프
			PRINTFORMW %CALLNAME:TARGET%의 애널에, 펌프를 써서 배가 부풀어 오를 정도로 공기를 넣었다
			TEQUIP:관장 = 1p0 + 1p9
		ENDIF
	ELSE
		PRINTFORMW %CALLNAME:TARGET%의 애널에, 펌프를 써서 배가 부풀어 오를 정도로 공기를 넣었다
		TEQUIP:관장 = 1p0 + 1p9
	ENDIF
	IF COND("액체관장") && COND("애널栓") == 0
		PRINTFORML 　
		PRINTFORML 애널플러그를 사용합니까? 
		PRINTFORML [0] 사용한다
		PRINTFORML [1] 사용하지 않는다
		CALL INPUT_SELECT, 2
		IF RESULT == 0
			PRINTFORMW %CALLNAME:TARGET%의 애널을 플러그로 막았다…
			SETBIT TEQUIP:관장, 9
		ENDIF
	ENDIF
	;정액관장플래그消去
	CLEARBIT TEQUIP:관장, 10
ENDIF

@TRAIN_MESSAGE_COM207
IF COND("애널栓")
	CALL SETFLAG, "배설"
ELSE
	;액체量MAX＆플러그つき
	TEQUIP:관장 = 1p1 + 1p2 + 1p3 + 1p4 + 1p9
	PRINTFORMW 극태의 촉수가 %CALLNAME:TARGET%의 애널을 막고, 체액을 흘려넣었다…
	CALL ADD_STAIN, TARGET, "Ａ", "촉수"
ENDIF


@EQUIP_COM46
SIF SELECTCOM == 136
	RETURN 0
SIF COND("정액관장")
	RETURN 0
CALL SOURCE_COM136, 10

RETURN 1

@EQUIP_COM46_2
SIF SELECTCOM == 136
	RETURN 0

IF COND("정액관장")
	PRINTL ＜정액관장중＞
ELSEIF TEQUIP:촉수
	PRINTL ＜관장촉수삽입안＞
ELSE
	PRINTFORM ＜\@ COND("온천관장") ? 온천 # \@관장
	SELECTCASE COND("액체관장")
	CASE 0
		PRINT （공기）
	CASE 1
		PRINT （소량）
	CASE 3
		PRINT （대량）
	CASE 4
		PRINT （초대량）
	ENDSELECT
	IF TEQUIP:확장벌룬 == 2
		PRINT ＋애널확장벌룬
	ELSEIF INSERT("Ａ인서트")
		PRINT ＋Ａ인서트
	ELSEIF COND("애널栓")
		PRINT ＋애널플러그 삽입안
	ENDIF
	PRINTL ＞
ENDIF

;정액관장だと관장경험とかは無し
IF COND("정액관장")
	;ここは微々たる物
	BASE:배설욕 += EXP_GAIN(18)*20
	;限界まであとどれくらい？
	;SELECTCASE (BASE:배설욕 * 100) / MAXBASE:배설욕
	;CASE IS >= 90
	;	PRINTFORML %CALLNAME:TARGET%의 배는 정액이 들이부어진 자극때문인지, 꿀렁꿀렁 소리를 내고있다…
	;ENDSELECT
	RETURN 0
ELSEIF COND("액체관장")
	VARSET LOCAL
	;ターンごとに耐えるのがきつくなる
	LOCAL = 300 + (CUP:쾌Ｃ + CUP:쾌Ｖ + CUP:쾌Ａ + CUP:고통)/5 + NOWEX:Ｃ절정*200 + NOWEX:Ｖ절정*500 + NOWEX:Ａ절정*1000 + NOWEX:절정수합계*100

	;관장量による보정
	SELECTCASE COND("액체관장")
	CASE 2
		TIMES LOCAL, 1.50
	CASE 3
		TIMES LOCAL, 3.00
	CASE 4
		TIMES LOCAL, 5.00
	ENDSELECT

	;Ａ확장度合いによる보정
	SELECTCASE ABL:Ａ확장 - ABL:Ａ놀림
	CASE IS >= 4
		TIMES LOCAL, 30.00
	CASE 3
		TIMES LOCAL, 10.00
	CASE 2
		TIMES LOCAL, 5.00
	CASE 1
		TIMES LOCAL, 3.00
	ENDSELECT

	;커맨드などによる보정
	SELECTCASE SELECTCOM
	CASE 2
		TIMES LOCAL, 1.20
	CASE 9
		TIMES LOCAL, 1.10
	CASE 14
		TIMES LOCAL, 1.30
	CASE 33, 141
		TIMES LOCAL, 2.00
	CASE 142, 143
		TIMES LOCAL, 3.00
	CASE 60 TO 79
		TIMES LOCAL, 1.50
	ENDSELECT

	;LOCAL:1は下方修正. かかり過ぎないようにLOCALとは独立させて調整
	LOCAL:1 = 100

	;性格による보정(ツンやクーはがんばって堪えます. 他はシラネ)
	SELECTCASE TALENT:교만*50 + TALENT:꿋꿋함*40 + TALENT:강기*40 + TALENT:냉정*25 + TALENT:부끄럼쟁이*50 + TALENT:현명함*25
	CASE IS >= 150
		TIMES LOCAL:1, 0.30
	CASE IS >= 100
		TIMES LOCAL:1, 0.50
	CASE IS > 50
		TIMES LOCAL:1, 0.80
	ENDSELECT

	;能力による보정. 노출벽が高いと我慢しない. 마조끼が高いと我慢する. 
	SELECTCASE 100 - ABL:노출벽*30 + ABL:마조끼*10
	CASE IS >= 80
		TIMES LOCAL:1, 0.30
	CASE IS >= 50
		TIMES LOCAL:1, 0.50
	CASE IS >= 30
		TIMES LOCAL:1, 0.80
	CASE IS < -20
		TIMES LOCAL:1, 2.00
	CASE IS < 0
		TIMES LOCAL:1, 1.50
	ENDSELECT

	;どんなに耐えても30％はある
	LOCAL:1 = MAX(LOCAL:1, 30)

	;보정のかかった値を追加
	BASE:배설욕 += MULTIPLY(LOCAL, LOCAL:1)

	IF COND("애널栓")
		PRINTFORML 장내에 대량의 \@ COND("온천관장") ? 소변이 흘려넣어진 # 관장액이 들이부어진 \@ %조사처리(CALLNAME:TARGET,"는")%, 항문에 마개를 끼운채로 계속 괴로워하고있다. 
		SELECTCASE ABL:마조끼
		CASE 0
			PRINTFORML %조사처리(CALLNAME:TARGET,"는")% 고통스러운 표정을 짓고있다
		CASE 1
			PRINTFORML %조사처리(CALLNAME:TARGET,"는")% 비지땀을 흘리며, 배설감을 참고있다…
		CASE 2
			PRINTFORML %조사처리(CALLNAME:TARGET,"는")% 괴로워보이는 표정을 지으면서도, 얼굴을 붉게 물들이고 쭈삣쭈삣 엉덩이를 흔들고있다…
		CASE 3
			PRINTFORML %조사처리(CALLNAME:TARGET,"는")%, 배설감에 쫒기면서도, 이따금씩 황홀한 표정을 짓고있다…
		CASE 4
			PRINTFORML %조사처리(CALLNAME:TARGET,"는")%, 황홀한 표정을 지으며, 배설감과 복통을 즐기고있다…
		CASE IS >= 5
			PRINTFORML %조사처리(CALLNAME:TARGET,"는")%, 배설감을 마음껏 맛보고있다…
			PRINTFORML 그 표정은 쾌락에 칠칠맞게 풀어져, 입\@ TALENT:남자 == 0 ? 과 질구 # \@에서 똑똑 군침을 흘리고있다…
		ENDSELECT
		SIF COND("배설：限界")
			CALL SELF_KOJO, "플러그無し관장이벤트"
	ENDIF
ENDIF
EXP:관장경험 += 1
PRINTFORML 관장경험＋１
EXP:긴박경험 += 2
PRINTL 긴박경험＋２

RETURN 1



