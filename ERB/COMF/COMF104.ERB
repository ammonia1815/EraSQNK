@COM104
;파후파후
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 104

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%
;-------------------------------------------------
;사정체크
;-------------------------------------------------
IF CHECK_COM("파후파후손훑기")
	CALL SAMEN_CHECK, 70
	SELECTCOM = PREVCOM
	CALL SAMEN_CHECK, 50
	SELECTCOM = 104
ELSE
	CALL SAMEN_CHECK
ENDIF

RETURN 1

@FLAG_COM104, ARG
SIF CHECK_CLO("가슴골노출") == 0 && CHECK_CLO("はだけ可")
	CALL SETFLAG, "가슴을はだける", TARGET
SIF CHECK_COM("파후파후손훑기") == 104
	CALL FLAG_COM90, 50
;대상のＢ⇔조교자の입の汚れが移動
CALL EQ_STAIN, TARGET, PLAYER, 5, 0

@SOURCE_COM104, ARG
VARSET LOCAL

SIF CHECK_COM("파후파후손훑기") == 104
	CALL SOURCE_COM90, 50
SIF COND("모유마시기플래그", PLAYER)
	CALL SOURCE_COM5, 80

LOCAL:20 += 50
LOCAL:21 += 160

LOCAL:14 = 200

;거유
SIF TALENT:거유
	LOCAL:14 -= 200
;빈유
SIF TALENT:빈유
	LOCAL:14 += 400

;ABL:봉사정신をみる
SELECTCASE ABL:봉사정신
CASE 0
	LOCAL:4 = 420
	LOCAL:5 = 150
CASE 1
	LOCAL:4 = 500
	LOCAL:5 = 300
CASE 2
	LOCAL:4 = 580
	LOCAL:5 = 600
CASE 3
	LOCAL:4 = 660
	LOCAL:5 = 900
CASE 4
	LOCAL:4 = 740
	LOCAL:5 = 1500
CASEELSE
	LOCAL:4 = 820
	LOCAL:5 = 2200
ENDSELECT

;ABL:Ｂ감각をみる
LOCAL:17 = CALCF("Ｂ刺激", 2, 80)

;ABL:기교をみる
SELECTCASE ABL:기교
CASE 0
	TIMES LOCAL:4 , 0.50
	TIMES LOCAL:5 , 0.50
CASE 1
	TIMES LOCAL:4 , 0.80
	TIMES LOCAL:5 , 0.80
CASE 2
CASE 3
	TIMES LOCAL:4 , 1.20
	TIMES LOCAL:5 , 1.20
CASE 4
	TIMES LOCAL:4 , 1.50
	TIMES LOCAL:5 , 1.50
CASEELSE
	TIMES LOCAL:4 , 2.00
	TIMES LOCAL:5 , 2.00
ENDSELECT

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM104
IF CHECK_COM("Ｗ봉사")
	;二人とも前回と同じ行動
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM == PREVCOM
		PRINTFORM 두 사람은 
		IF TALENT:(TARGET:1):음란 + TALENT:(TARGET:1):연모 && TALENT:음란 + TALENT:연모 && RAND:3 == 0
			PRINTFORM 황홀해 하면서 
		ELSEIF RAND:2 == 0
			PRINTFORM 정신없이 
		ENDIF
		PRINTFORMW %CALLNAME:MASTER%의 얼굴을 가슴에 끼우고, 부드러운 유방을 맛보게 하고있다…
	ELSE
		PRINTFORM %타겟과()% %조사처리(CALLNAME:(TARGET:1),"는")%
		SIF TALENT:(TARGET:1):음란 + TALENT:(TARGET:1):연모 + TALENT:음란 + TALENT:연모 && RAND:2 == 0
			PRINTFORM 상기된 눈동자로 %조사처리(CALLNAME:MASTER,"를")% 바라보면서, 
		IF RAND:3 == 0
			PRINTFORMW 경쟁하듯이 얼굴에 가슴을 비볐다…
		ELSEIF RAND:2 == 0
			PRINTFORMW 가슴계곡에 머리를 끼우고 압박했다…
		ELSE
			PRINTFORMW 협력해서 얼굴을 가슴에 끼웠다…
		ENDIF
	ENDIF
;同時넘어뜨리기&連続
ELSEIF SELECTCOM == PREVCOM && SELECTCOM:1 >= 0 && TEQUIP:Ｗ넘어뜨리기
	PRINTFORMW %타겟과()% %조사처리(CALLNAME:(TARGET:1),"는")% %CALLNAME:MASTER%의 위에서, 쾌락을 탐내고 있다…
;前回と同じ
ELSEIF PREVCOM == SELECTCOM && COND("前Ｔ조교자") == PLAYER
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:PLAYER%의 
	IF TALENT:빈유
		IF TEQUIP:넘어뜨리기 && RAND:2
			PRINTFORMW 얼굴을 얄팍한 가슴으로 짓누르고 있다…
		ELSE
			PRINTFORMW 머리를 가슴에 껴안고, 작은 가슴으로 얼굴에 눌렀다 떼었다 하고 있다…
		ENDIF
	ELSE
		IF TEQUIP:넘어뜨리기 && RAND:2
			PRINTFORMW 얼굴을 유방으로 짓누르고 있다…
		ELSE
			PRINTFORMW 머리를 가슴에 껴안고, 부비적부비적 가슴사이에 얼굴을 파묻었다…
		ENDIF
	ENDIF
ELSE
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	IF CHECK_COM("파후파후손훑기")
		IF PREVCOM == 97
			LOCALS = %CLO("팬티훑기")%
			LOCALS:1 = %LOCALS%
		ELSE
			LOCALS = 손
			LOCALS:1 = 손바닥
		ENDIF
		IF TALENT:연모 && RAND:4 == 0
			PRINTFORM 페니스를 사랑스럽게 %조사처리(LOCALS,"로")% 훑으면서, 
		ELSEIF ABL:기교 >= 3 && ABL:봉사정신 >= 3 && RAND:3 == 0
			PRINTFORM 귀두를 %조사처리(LOCALS:1,"로")% 주물러 돌리면서, 
		ELSEIF ABL:기교 >= 3 && RAND:2 == 0
			PRINTFORM 페니스를 절묘한 힘조절로 자극하면서, 
		ELSEIF RAND:2 == 0
			PRINTFORM 페니스를 천천히 %조사처리(LOCALS,"로")% 훑으면서, 
		ELSE
			PRINTFORM 페니스를 %조사처리(LOCALS,"로")% 훑으면서, 
		ENDIF
	;인서트中
	ELSEIF INSERT("합체中")
		PRINTFORM %TEXTS("합체中", TARGET)%
	ENDIF
	;조수が前回と同じ行動(違う行動の時は, ここでなく, EQUIP_COM32で記述)
	SIF TEQUIP:Ｗ넘어뜨리기 && SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM %TEXTS("Ｗ넘어뜨리기 중")%

	PRINTFORM %CALLNAME:PLAYER%
	IF RAND:2 && TALENT:연모 && TEQUIP:넘어뜨리기
		IF CHECK_COM("파후파후손훑기")
			PRINTFORM 에게 꼬옥 안겨서 얼굴
		ELSE
			PRINTFORM 에게 꽉 달라붙어서, 얼굴
		ENDIF
	ELSE
		PRINTFORM 의 얼굴
	ENDIF
	IF TALENT:빈유
		IF RAND:2 || CALCF("Ｂ발정") == 0
			PRINTFORM 을 %TEXTR("작은 가슴", "겸손한 가슴", "평평한 가슴")%
			IF TEQUIP:넘어뜨리기 && RAND:2
				PRINTFORMW 로 꽉 눌러왔다…
			ELSE
				PRINTFORMW 로 애무해왔다…
			ENDIF
		ELSE
			PRINTFORMW 에 %TEXTR("작은 가슴을", "귀여운 유두를")% 문질러붙여왔다…
		ENDIF
	ELSE
		IF TALENT:거유 && RAND:3
			PRINTFORM 를 %TEXTR("중량감넘치는 가슴", "풍만한 가슴", "찰지게 여문 유방")%
		ELSE
			PRINTFORM 을 %TEXTR("부드러운 가슴", "감촉이 좋은 유방")%
		ENDIF
		;押し潰し
		IF TEQUIP:넘어뜨리기 && RAND:2
			IF TALENT:거유 && RAND:2 == 0
				PRINTFORMW 의 계곡에 묻었다…
			ELSE
				PRINTFORMW 으로 눌러붙였다…
			ENDIF
		ELSEIF TALENT:거유 && RAND:4 == 0
			PRINTFORMW 으로 감쌌다…
		ELSEIF RAND:3 == 0
			PRINTFORMW 으로 애무해왔다…
		ELSEIF RAND:2 == 0
			PRINTFORMW 으로 부드럽게 주물렀다…
		ELSE
			PRINTFORMW 에 끼워넣었다…
		ENDIF
	ENDIF
ENDIF

;모유체질とかだと모유を飲めるかも
CALL TEXT, "모유마시기"

@COM_ORDER_COM104, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF
;ABL:Ｂ감각
IF ABL:Ｂ감각
	LOCAL += ABL:Ｂ감각 * 3
	LOCALS = %LOCALS% + %ABLNAME:14%LV{ABL:Ｂ감각}({ABL:Ｂ감각 * 3})
ENDIF
;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 3
	LOCALS = %LOCALS% - %TALENTNAME:35%(3)
ENDIF
;악취둔감
IF TALENT:악취둔감
	LOCAL += 1
	LOCALS = %LOCALS% + %TALENTNAME:61%(1)
ENDIF
;헌신的
IF TALENT:헌신
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;거유
IF TALENT:거유
	LOCAL += TALENT:거유 * 5
	IF TALENT:거유 >= 3
		LOCALS = %LOCALS% + 초유(15)
	ELSEIF TALENT:거유 == 2
		LOCALS = %LOCALS% + 폭유(10)
	ELSE
		LOCALS = %LOCALS% + %TALENTNAME:110%(5)
	ENDIF
ENDIF

;合計を表示(40이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 40

RETURN RESULT
