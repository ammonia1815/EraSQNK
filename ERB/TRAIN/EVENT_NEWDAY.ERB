;-------------------------------------------------
;朝に起こる이벤트
;-------------------------------------------------
@EVENT_NEWDAY
#DIM LCOUNT
#DIM MEMO_EXP, 100

;変数CLEAR
VARSET LOCAL
;TAGETの経験増加の判定で使う
FOR LCOUNT, 0, 100
	MEMO_EXP:LCOUNT = EXP:LCOUNT
NEXT

;아침이벤트の記録初期化
CVARSET CFLAG, 90, 0

;条件によって이벤트の種類が増える
PLAYER = MASTER

;最初に새드끼이벤트の判定を行う(発生확률が低い)
IF RAND:5 == 0
	CALL BIND_AT_MORNING
	IF RESULT == 1
		BEGIN TURNEND
		RETURN 1
	ENDIF
ENDIF
;아침이벤트の決定
CALL PLAY_AT_MORNING
LOCALS = %STR:100%
SIF LOCALS == ""
	RETURN 0

;基本回数は정력依存
LOCAL = BASE:정력/20 + LIMIT(RAND:(BASE:정력/10 + 1), 1, BASE:정력/15)

DRAWLINE
SELECTCASE LOCALS
CASE "아침逆정상위"
	SIF TALENT:남자
		LOCAL += 1
	LOCAL = MAX(LOCAL + ABL:욕망/2, 1)

	CALL SIMULATE_TRAIN_FIRST
	CALL SIMULATE_TRAIN_COM, 120, LOCAL
	SIF EXP:사정경험 == MEMO_EXP:3
		CALL SIMULATE_TRAIN_COM, 120, LOCAL

	PRINTFORMW %마스터는()% 하복부\@ RAND:2 ? 를 덮치는 충격 # 에 활짝 퍼지는 쾌감 \@을 깨달아 눈을 떴다
	PRINTFORMW 아무래도,%타겟이()% 자고 있는 %마스터를()% 범해 즐기고 있는 한중간인 것 같다…
	IF TARGET == ASSI
		IF EXP:사정경험 > MEMO_EXP:3 && RAND:2
			PRINTFORMW 완전히 깨어난 %마스터는()% 허리를 내밀어 %CALLNAME:TARGET%에 확실히 봉사하도록 명령하고, 질 안쪽을 두드리는 정액의 감촉을 즐기면서 오늘의 스케줄을 보고시켰습니다…
		ELSE
			PRINTFORMW 주인이 눈을 뜬 것을 확인하면, %타겟은()% 유유히 허리를 움직여 %CALLNAME:MASTER%에 교성을 지르게 하면서 오늘의 스케줄을 보고하기 시작했다…
		ENDIF
	;ＭＡＳＴＥＲ主導権な展開はまだできてない
	;ELSEIF RAND:2
	;	PRINTFORML %CALLNAME:MASTER%が下半身の違和感に目を覚ますと、下着を剥ぎ取りいきり立った페니스を突き入れようとする%CALLNAME:TARGET%と目があった
	;	PRINTFORML しどろもどろになって言い訳するAへ당신は妖艶に笑いかけながら、발を開いてAを誘った…
	ELSE
		IF EXP:사정경험 > MEMO_EXP:3 && RAND:3 == 0
			PRINTFORM %마스터가()% 일어날 때까지 사정을 참고 있던 것 같은 %타겟은()%,
		ELSEIF RAND:2
			PRINTFORM 질의 야무짐의 변화로 %마스터가()% 눈을 뜸을 감지한 %타겟은()%,
		ELSE
			PRINTFORM 잠들어 있는동안 덮쳤던 것을 즐기게 된 %CALLNAME:MASTER%의 입을 키스로 막은 %타겟은()%,
			CALL SIMULATE_TRAIN_COM, 6, 1 + RAND:2
		ENDIF
		IF EXP:사정경험 > MEMO_EXP:3 && RAND:3 == 0
			PRINTFORMW 허리를 내밈과 동시에 대량의 정액을\@ COND("위험일", MASTER) ? 위험일의 자궁에 내 # 기분 좋은 듯이 사정하 \@면서 아침의 인사를 해 왔다…
		ELSEIF ABL:허리놀림 >= 3 && RAND:3 == 0
			PRINTFORMW 교묘하게 허리를 움직여 서로의 쾌감을 높이면서 아침의 인사를 해 왔다…
		ELSEIF ABL:새드끼 >= 3 && RAND:2
			PRINTFORMW 자다 일어나\@ RAND:2 ? 저항이 약한 # 움직임이 굼뜬 \@것을 구실로\@ (EXP:사정경험 > MEMO_EXP:3 && RAND:2) ? 듬뿍 씨를 내 # 더욱 격렬하게 범하 \@면서 아침의 인사를 해 왔다…
			CALL SIMULATE_TRAIN_COM, 120, 2
		ELSE
			PRINTFORMW 내던지듯 허리를 흔들어 기분 좋은 것 같은 얼굴을 보이면서 아침의 인사를 해 왔다…
		ENDIF
	ENDIF

	CALL SELF_KOJO, LOCALS

	;経験や구슬などの獲得量計算
	CALL SIMULATE_TRAIN_CALC
	;結果発表
	CALL SIMULATE_TRAIN_SHOW
	CALL CALC, "욕구불만解消", TARGET, LOCAL
CASE "아침컨닐링구스"
	SIF TALENT:남자
		LOCAL += 1
	LOCAL = MAX(LOCAL + (ABL:봉사정신 + ABL:레즈끼)/3, 1)
	PRINTFORML 아침, 눈을 뜨면 사타구니가 미지근하다…
	PRINTFORM 어쩌면, 이라고 생각해 힘차게 이불을 넘겨 보면,
	IF ABL:레즈끼 >= 3 && ABL:욕망 >= 5 && RAND:3 == 0
		PRINTFORM 상기 한 모습으로
	ELSEIF RAND:3 == 0
		PRINTFORM 사타구니에 얼굴을 묻어
	ELSEIF RAND:2
		PRINTFORM 가랑이를 밀어 열면서
	ELSE
		PRINTFORM 다리의 사이에 신체를 끼어들게 해
	ENDIF
	PRINTFORMW 저기를 빨고 있는 %타겟과()% 시선이 마주쳤다
	PRINTFORM %타겟은()% 날름 혀를 내밀어 웃는 얼굴을 띄워,
	IF ABL:봉사정신 >= 3 && RAND:2
		PRINTFORML 음순을 예쁘게 청소하면서 아침의 인사를 해 왔다…
		CALL SELF_KOJO, LOCALS
		CALL RESET_STAIN_EX, "Ｖ", MASTER
	ELSE
		PRINTFORML 아침의 인사를 해 왔다…
		CALL SELF_KOJO, LOCALS
	ENDIF
	CALL SIMULATE_TRAIN, 98, LOCAL
	IF ABL:레즈끼 >= 3 && COND("身嗜み：노팬티", MASTER) == 0 && RAND:2
		PRINTFORML %CALLNAME:TARGET%의 방을 나간 뒤로 거울의 앞에 서면, 무방비인 하반신이 눈에 들어온다
		PRINTFORML …아무래도, %타겟이()% 팬티를 가지고 가진 것 같다……
	ENDIF
	CALL CALC, "욕구불만解消", TARGET, 1
CASE "아침풋잡"
	LOCAL = MAX(LOCAL + (ABL:새드끼 + ABL:발놀림)/3, 1)

	PRINTFORML 아침,%CALLNAME:TARGET%\@ PENIS(MASTER) ? 의 풋잡으로 # 의 발로 그곳을 괴롭혀져 \@눈을 떴다
	
	CALL SELF_KOJO, LOCALS
	CALL SIMULATE_TRAIN, 95, LOCAL
	
	PRINTFORM %타겟은()%
	IF TALENT:심술쟁이 || TALENT:소악마
		IF EXP:정액경험 > MEMO_EXP:20
			PRINTFORM 발에 붙은 정액를 손가락으로 닦아 낼름 한번 빨면서,
		ELSEIF PENIS(MASTER) == 0
			PRINTFORM 발에 붙은 애액을 손가락으로 닦아 낼름 한번 빨면서,
		ENDIF
	ENDIF
	PRINTFORML 우월감에 젖은 듯한 얼굴로 아침 인사를 해왔다.

	CALL CALC, "욕구불만解消", TARGET, 1
CASE "아침の모유搾り"
	LOCAL = MAX(LOCAL + MIN(GET_EXP_LV(EXP:분유경험), 5), 1)
	;거유
	IF TALENT:거유
		PRINTFORML 아침, 부엌에 오자 %CALLNAME:TARGET%의  큰 가슴로부터 모유를 짜내고 있었다
		;임신または출산경험
		IF TALENT:임신 || TALENT:출산경험
			IF RAND:2 == 0
				PRINTFORML %타겟은()% 팡팡 긴장된 가슴로부터, 황홀한 표정으로 모유를 분출하고 있다…
			ELSE
				PRINTFORML %타겟은()% 자애로 가득 찬 표정으로, 힘차게 뿜어 나오는 모유를 용기에 짜내고 있다…
			ENDIF
		ENDIF
	ELSE
		PRINTFORML 아침, 부엌에 오자 %CALLNAME:TARGET%의 스스로의 모유를 용기에 짜내고 있었다
	ENDIF

	CALL SELF_KOJO, LOCALS
	;거유の場合、2倍の量が搾れ、半分を売りに出します
	IF TALENT:거유
		EXP:분유경험 += LOCAL * 2
		PRINTFORML %EXPNAME:54%＋{LOCAL*2}
		IF TALENT:헌신 || TALENT:호색
			IF RAND:2 == 0
				PRINTFORML %타겟은()% %마스터를()% 찾아내자, 생긋 미소지으며 손짓 해 왔다…
				PRINTFORML 가까워지자, %타겟은()% %CALLNAME:MASTER%의 머리를 감싸 안고, 아직도 모유가 방울져 떨어지는 유두를 %CALLNAME:MASTER%의 입에 밀어넣고 기쁜듯이 짜기 시작했다…
			ELSE
				PRINTFORML %타겟은()% 큰 한 잔의 농후한 모유를 웃는 얼굴로 내밀었다
			ENDIF
		ENDIF
		PRINTFORML %PALAMNAME:4%의 구슬＋{LOCAL*60}
		PRINTFORML %PALAMNAME:7%의 구슬＋{LOCAL*20}
		PRINTFORMW %PALAMNAME:14%의 구슬＋{LOCAL*50}
		JUEL:욕정 += LOCAL*60
		JUEL:습득 += LOCAL*20
		JUEL:쾌Ｂ += LOCAL*50
		PRINTFORML %마스터는()% %CALLNAME:TARGET%의 짜낸 {LOCAL*100*(TALENT:거유 + 1)}cc의 모유의 반을 병에 담아 팔았다
		;出した人のプロフィールが見れる安心品質
		PRINTFORML %CALLNAME:TARGET%의 1번 모유는 %MONEY_C(LOCAL*250*(TALENT:거유 + 1) )%으로 팔렸다
		PRINTFORML 소지금+%MONEY_C(LOCAL*250*(TALENT:거유 + 1) )%
		MONEY += LOCAL*250*(TALENT:거유 + 1)
	ELSE
		EXP:분유경험 += LOCAL
		PRINTFORML %EXPNAME:54%＋{LOCAL}
		PRINTFORML %타겟은()% 갓 짜낸 모유를 컵에 부어 건네주었다
		PRINTFORML %PALAMNAME:4%의 구슬＋{LOCAL*60}
		PRINTFORML %PALAMNAME:7%의 구슬＋{LOCAL*20}
		PRINTFORML %PALAMNAME:14%의 구슬＋{LOCAL*50}
		JUEL:욕정 += LOCAL*60
		JUEL:습득 += LOCAL*20
		JUEL:쾌Ｂ += LOCAL*50
	ENDIF
CASE "아침の새댁플레이"
	LOCAL = MAX(LOCAL + (ABL:봉사정신 + ABL:노출벽)/3, 1)
	PRINTFORML 아침에 일어 나면, 키친으로부터 좋은 냄새가 감돌아 왔다
	PRINTFORML 거기에 가자 %타겟이()% 알몸에이프런으로 아침 식사를 만들고 있었다…
	PRINTFORML ―――충동적으로 뒤로부터 덤벼 들었다

	CALL SELF_KOJO, LOCALS
	CALL SIMULATE_TRAIN, 61, LOCAL

	PRINTFORML 무심코 아침 식사전에 %타겟을()% {LOCAL}회 정도 받아 버렸다…
	SIF TALENT:부끄럼쟁이
		PRINTFORML %타겟은()% %CALLNAME:MASTER%의 행위에 불평하고 있지만, 분명하게 기쁜 듯하다…
	CALL CALC, "욕구불만解消", TARGET, LOCAL
CASE "아침파이즈리"
	LOCAL = MAX(LOCAL + (ABL:봉사정신 + ABL:정액중독)/3, 1)
		PRINTFORML 아침, 깨어나는 것과 동시에 하복부에 위화감이 있는 것을 눈치챘다
	;빈유の場合
	IF TALENT:빈유
		PRINTFORML 눈치채지지 않게 눈을 돌리자 ,%CALLNAME:TARGET%의 가슴으로 페니스를 훑고 있었다
		PRINTFORML 아무래도 파이즈리의 연습을 하고 있는 것 같다……
	;거유の場合
	ELSEIF TALENT:거유
		PRINTFORML 눈을 돌리자%CALLNAME:TARGET%의 큰가슴으로 페니스를 사이에 둬, 쓸어올리고 있다
	;それ以外
	ELSE
		PRINTFORML 눈을 돌리자%CALLNAME:TARGET%의 가슴으로 페니스를 쓸어올려, 끝으로부터 흘러넘치는 액체를 들이마시고 있다
	ENDIF

	CALL SELF_KOJO, LOCALS
	CALL SIMULATE_TRAIN, 92, LOCAL
	IF EXP:정액경험 > MEMO_EXP:20
		PRINTFORML %타겟은()%정액의 들러붙은 얼굴로 아침의 인사를 해 왔다
	ELSE
		PRINTFORML %타겟은()% 사타구니에 얼굴을 묻은 채로의 자세로 아침의 인사를 해 왔다
	ENDIF
	CALL CALC, "욕구불만解消", TARGET, 1
CASE "아침기승위"
	LOCAL = MAX(LOCAL + (ABL:Ｖ감각 + ABL:정액중독)/3, 1)

	PRINTFORML 아침에 일어 나자 허리의 근처에 중량감을 느꼈다
	PRINTFORML 눈을 돌리면 %타겟이()% 위에 걸쳐 허덕이고 있다……
	CALL SELF_KOJO, LOCALS
	IF TARGET == ASSI
		IF RAND:2
			PRINTFORMW %마스터가()% 눈을 뜬 것을 확인하고, %타겟은()% 유유히 허리를 움직여, 교성을 사이에 두면서, 오늘의 스케줄을 보고했다…
		ELSE
			PRINTFORMW 완전히 깨어난 %마스터는()%,%CALLNAME:TARGET%의 허리를 잡아 밀어올리면서, 오늘의 스케줄을 교성을 섞으며 보고시켰다…
		ENDIF
		LOCAL += 1
	ENDIF
	CALL SIMULATE_TRAIN, 65, LOCAL
	IF EXP:MASTER:사정경험 > MEMO_EXP:3
		IF TARGET != ASSI
			PRINTFORML %타겟은()% 질안에서 사정을 받아 들이면서, 아침의 인사를 해 왔다
		ELSE
			PRINTFORML 기승위로 %CALLNAME:TARGET%의 질안에 몽정 하면서 눈을 떴습니다
		ENDIF
	ELSE
		PRINTFORML 기승위로 눈을 떴습니다
	ENDIF
	CALL CALC, "욕구불만解消", TARGET, LOCAL
CASE "아침키스"
	LOCAL = LIMIT(LOCAL/2 + (ABL:순종 + ABL:봉사정신)/5, 1, 1 + TALENT:상사상애 + TALENT:음탕한입술)

	;봉사레벨が高いと、先に起きている확률が高い
	IF ABL:봉사정신 >= 3 && RANDF(2)
		PRINTFORML 아침,%CALLNAME:TARGET%의 \@ LOCAL >= 3 ? 농후한 # \@입맞춤으로 눈을 떴다
	ELSE
		IF AGE(TARGET) == 1 && RAND:2
			PRINTFORML 아침,%마스터는()% 갑자기 흔들림과 신체에 걸리는 중량감으로 눈을 떴다
			PRINTFORMW 아무래도, %CALLNAME:TARGET%의 가슴에 닿아 흔들흔들하고 있던 것 같다…
			PRINTFORMW %마스터가()% 눈을 뜸을 알아차린 %타겟은()% 얼굴을 접근해 모닝 키스를 해 왔다
		ELSEIF TALENT:거유 && RAND:2
			PRINTFORML 아침, 눈을 뜨자 베개가 묘하게 부드러워서 기분 좋다……
			IF TALENT:부끄럼없음 && RAND:3 == 0
				PRINT 전라로
			ELSEIF RAND:2
				PRINT 속옷 모습으로
			ENDIF
			PRINTFORMW 침대에 기어들어온 %타겟이()% ,%CALLNAME:MASTER%의 머리를 가슴에 껴안고 있는 것 같다
			PRINTFORMW %마스터가()% 눈을 뜸을 알아차린 %타겟이()% 얼굴을 접근해 모닝 키스를 해 왔다
		ELSE
			PRINTFORML 아침, 눈을 뜨면 신체가 무겁다……
			PRINTFORMW 어느새인가 침대에 기어들어온 %타겟이()% %마스터를()% 껴안는 베게로 하고 있는 것 같다
			IF TALENT:상사상애 && RAND:2
				PRINTFORML %마스터가()% 사랑스러움에 계속 참지 못하고 자고 있는%CALLNAME:TARGET%에 입맞춤을 하면,
				PRINTFORML %타겟은()% 꿈 꾸는 기분인 채로%CALLNAME:MASTER%의 뺨을 잡아, 깊고 깊은 키스를 돌려줬다…
			ELSE
				PRINTFORMW %마스터가()% 눈을 뜸을 알아차린 %타겟이()% 얼굴을 접근해 모닝 키스를 해 왔다
			ENDIF
		ENDIF
	ENDIF

	CALL SELF_KOJO, LOCALS

	CALL SIMULATE_TRAIN, 6, LOCAL

	IF ABL:봉사정신 >= 3 && RANDF(2)
		PRINTFORM %타겟은()%
		IF RAND:4 && TALENT:부끄럼쟁이 && TALENT:솔직 + TALENT:상사상애 == 0
			PRINTFORM 얼굴을 주홍에 물들이면서
		ELSE
			PRINTFORM %마스터를()% 껴안\@ TALENT:호색 ? 고 가랑이를 쓰다듬 # \@으면서,
		ENDIF
		PRINTFORML 아침의 인사를 해 왔다
	ENDIF
	CALL CALC, "욕구불만解消", TARGET, 1
CASE "아침펠라"
	LOCAL = MAX(LOCAL + (ABL:봉사정신 + ABL:정액중독)/3, 1)

	PRINTFORML 아침, 깨어나면 묘하게 하복부가 기분 좋다…
	CALL COM_ABLE99
	IF RESULT && COND("다리 사용") && ABL:욕망 >= 3 && ABL:봉사정신 >= 3 && CLO_ORI(TARGET,"겉옷") == 0 && RAND:2
		PRINTFORM 시선을 그 쪽으로 향하자…다리를 크게 벌린 자세로 신체에 걸친 %CALLNAME:TARGET%의
		IF (TALENT:음란 && ABL:노출벽 >= 5 && RAND:2) || CLO_ORI(TARGET,"スーツ","下着") == 0 || COND("身嗜み：노팬티")
			IF RAND:3 == 0
				PRINT 당장 애액이 늘어질 것 같은 그곳
			ELSEIF RAND:2
				PRINT 완전히 발정한 그곳
			ELSE
				PRINT 노출의 그곳
			ENDIF
		ELSEIF CLO_ORI(TARGET,"スーツ")
			IF (ABL:욕망 >= 4 || TALENT:젖기쉬움) && RAND:2
				PRINTFORM 얼룩이 뜬 팬티
			ELSEIF AGE(TARGET) >= 3
				PRINTFORM 사타구니에 먹혀든 팬티
			ELSE
				PRINTFORM 사랑스러운 팬티
			ENDIF
		ELSE
			IF (ABL:욕망 >= 4 || TALENT:젖기쉬움) && RAND:2
				PRINTFORM 얼룩이 뜬
			ELSEIF AGE(TARGET) >= 3
				PRINTFORM 사타구니에 먹혀든
			ELSE
				PRINTFORM 사랑스러운
			ENDIF
			IF EQUIP:팬티
				PRINTFORM %PANTIES(TARGET, 1)%
			ELSE
				PRINTFORM 팬티
			ENDIF
		ENDIF
		PRINTL 가 눈에 들어왔다
		PRINTFORML 아무래도, %타겟이()% 자고 있는 %CALLNAME:MASTER%의 얼굴에 사타구니를 문지르면서 펠라치오를 하고 있던 것 같다…
		SELECTCOM = 99
	ELSE
		PRINTFORML 아무래도, %타겟이()% 자고 있는 %CALLNAME:MASTER%에 펠라치오를 하고 있던 것 같다…
		SELECTCOM = 91
	ENDIF

	;식스나인かで구상分岐させるなら、SELECTCOM == 99で식스나인の時となる
	CALL SELF_KOJO, LOCALS

	CALL SIMULATE_TRAIN, SELECTCOM, LOCAL

	;청소펠라
	IF ABL:봉사정신 + TALENT:헌신 - (COND("포경", MASTER) > 0) >= 3
		;ＭＡＳＴＥＲが포경
		IF COND("포경", MASTER) && ABL:봉사정신 + TALENT:헌신 >= 4 && RAND:3
			PRINTFORM %타겟은()% %CALLNAME:MASTER%의
			IF COND("진성포경", MASTER)
				PRINTFORML 귀두를 싸는 가죽안에 혀를 넣어, 내부에 모인 치구를 혀로 긁어내면서 아침의 인사를 해 왔다
			ELSE
				PRINTFORML 포경페니스의 가죽을 꾹 뿌리쪽으로 밀어 귀두를 노출해, 자고 있는 동안에 모인 치구를 혀로 청소하면서 아침의 인사를 해 왔다
			ENDIF
		ELSE
			IF EXP:정액경험 > MEMO_EXP:20
				PRINTFORML %타겟은()% 정액을\@ ABL:정액중독 >= 3 ? 맛있게 # 남김없이 \@ 삼키고, 입으로 청소하면서 아침의 인사를 해 왔다
			ELSE
				PRINTFORML %타겟은()% %CALLNAME:MASTER%의 페니스를 입으로 청소하면서 아침의 인사를 해 왔다
			ENDIF
		ENDIF
		PRINTFORML 청소펠라경험＋１
		EXP:청소펠라경험 += 1
	ELSEIF (TALENT:호색 || ABL:봉사정신 >= 5) && COND("다리 사용") && CLO_ORI(TARGET,"下着") && COND("身嗜み：노팬티") == 0 && RAND:3 == 0
		PRINTFORM %타겟은()% 체온이 남은 팬티로 %CALLNAME:MASTER%의 페니스를 닦아 깨끗이
		IF RAND:2
			PRINTFORML 하면서 아침의 인사를 해 왔다
		ELSE
			PRINTFORML 해 눈앞에서 더러워진 그것을 입으면서 아침의 인사를 해 왔다
		ENDIF
	ELSEIF SELECTCOM == 99 && ABL:새드끼 && RAND:2
		PRINTFORML %타겟은()% %CALLNAME:MASTER%의 얼굴에 사타구니를 내려, 어깨 너머에서 아침의 인사를 해 왔다
	ELSEIF EXP:정액경험 > MEMO_EXP:20 && RAND:3
		IF ABL:새드끼 >= 3 && RAND:2
			PRINTFORML %타겟은()% 정액를 맛보면서 삼키면서 입술을 낼름 빨고 아침의 인사를 해 왔다
		ELSE
			PRINTFORML %타겟은()% 정액의 들러붙은 얼굴로 아침의 인사를 해 왔다
		ENDIF
	ELSE
		PRINTFORM %타겟은()% 사타구니에 얼굴을 묻고
		IF COND("손가락 사용") && RAND:4 == 0
			PRINTFORM 손가락의 점액을 빨아내면서, 웃는 얼굴로
		ELSEIF RAND:3 == 0
			PRINTFORM 페니스를 빨면서
		ELSEIF RAND:2
			PRINTFORM  입을 우물거리면서 우물거리는 소리로
		ELSE
			PRINTFORM 있는 그 자세로
		ENDIF
		PRINTFORML 아침의 인사를 해 왔다
	ENDIF
	CALL CALC, "욕구불만解消", TARGET, 1
ENDSELECT

;朝이벤트の記録
IF LOCALS != "" && LOCAL
	CFLAG:PLAYER:90 = NUM_EV(LOCALS)
	CFLAG:90 = NUM_EV(LOCALS)
	WAIT
ENDIF


@PLAY_AT_MORNING
#DIM LCOUNT

STR:100 = 
;반발각인が3あるとダメ
SIF MARK:반발각인 >= 3
	RETURN 0

VARSET LOCALS

CALL COM_ABLE6
SIF RESULT && TALENT:연모 && ABL:순종 >= 3 && CHECK_COM("키스")
	LOCALS:0 = 아침키스
SIF TALENT:연모 && TALENT:모유체질 && ABL:순종 >= 3 && ABL:봉사정신 >= 3 && EXP:분유경험 >= 20
	LOCALS:1 = 아침の모유搾り
CALL COM_ABLE95
SIF RESULT && ABL:새드끼 >= 3
	LOCALS:2 = 아침풋잡
CALL COM_ABLE98
SIF RESULT && (ABL:레즈끼 >= 3 || TALENT:남자) && TALENT:MASTER:남자 == 0
	LOCALS:3 = 아침컨닐링구스
CALL COM_ABLE120
SIF RESULT && PENIS(TARGET) && TALENT:동정 == 0 && EXP:Ｖ삽입경험 >= 20 && ABL:욕망 >= 3 && TALENT:MASTER:처녀 == 0 && TALENT:MASTER:임신 == 0
	LOCALS:4 = 아침逆정상위

CALL COM_ABLE91
SIF RESULT && PENIS(MASTER) && ABL:욕망 >= 2 && ABL:봉사정신 >= 2 && ABL:정액중독 >= 1
	LOCALS:5 = 아침펠라
CALL COM_ABLE65
SIF RESULT && PENIS(MASTER) && ABL:욕망 >= 4 && ABL:Ｖ감각 >= 3 && TALENT:처녀 + TALENT:남자 + TALENT:PLAYER:동정 == 0
	LOCALS:6 = 아침기승위
CALL COM_ABLE92
SIF RESULT && PENIS(MASTER) && ABL:욕망 >= 4 && ABL:봉사정신 >= 4 && TALENT:남자 == 0
	LOCALS:7 = 아침파이즈리
CALL COM_ABLE61
SIF RESULT && PENIS(MASTER) && ITEM:에이프런 && TALENT:연모 && TALENT:처녀 + TALENT:남자 + TALENT:PLAYER:동정 == 0
	LOCALS:8 = 아침の새댁플레이

LOCAL = 1
FOR LCOUNT, 0, 10
	SIF LOCALS:LCOUNT == ""
		CONTINUE
	SIF RAND:LOCAL == 0
		STR:100 = %LOCALS:LCOUNT%

	LOCAL += 1
NEXT

RETURN 0

;-------------------------------------------------
;구속されて弄ばれる이벤트
;욕구불만이벤트に変更してみる
;-------------------------------------------------
@BIND_AT_MORNING
#DIM LCOUNT
VARSET LOCAL
VARSET LOCALS
;そもそもこの種類の이벤트が起きるかの체크
CALL CHECK_BIND_AT_MORNING, TARGET, ""
LOCALS = %STR:100%
;TARGETがプレイの条件を満たしているか？
SIF LOCALS == ""
	RETURN 0

;複数キャラを使う이벤트用の대상フラグリセット
VARSET TARGET, 0, 1, 4

;仲間を呼ぶｗ
FOR LOCAL, 2, CHARANUM
	SIF LOCAL == TARGET || LOCAL == MASTER
		CONTINUE

	CALL CHECK_BIND_AT_MORNING, LOCAL, LOCALS
	LOCALS:9 = %STR:100%

	;선택されているプレイを許容できるなら욕구불만値を記録して、参加候補入り（ＭＡＸ二人）
	IF LOCALS:9 == LOCALS
		LOCAL:99 += 1
		IF TARGET:1 == 0
			TARGET:1 = LOCAL
		ELSEIF TARGET:2 == 0
			TARGET:2 = LOCAL
		ELSEIF RAND:(LOCAL:99) <= 1
			TARGET:(RAND:2 + 1) = LOCAL
		ENDIF
	ENDIF
NEXT

LOCALS:1 = %CALLNAME:TARGET%
LOCALS:2 = %CALLNAME:TARGET%
LOCAL = ABL:욕망 + RAND:(ABL:새드끼 + 5) + TALENT:음란*2 + TALENT:연모

IF TARGET:1 > 0
	LOCALS:1 = %CALLNAME:TARGET%と%CALLNAME:(TARGET:1)%
	LOCALS:2 = %CALLNAME:TARGET%達
	LOCALS:3 = 二人
	LOCAL:1 = ABL:(TARGET:1):욕망 + RAND:(ABL:(TARGET:1):새드끼 + 1) + TALENT:(TARGET:1):음란*2 + TALENT:(TARGET:1):연모
ENDIF
IF TARGET:2 > 0
	LOCALS:1 = %CALLNAME:TARGET%と%CALLNAME:(TARGET:1)%と%CALLNAME:(TARGET:2)%
	LOCALS:2 = %CALLNAME:TARGET%達
	LOCALS:3 = ３人
	LOCAL:2 = ABL:(TARGET:2):욕망 + RAND:(ABL:(TARGET:2):새드끼 + 1) + TALENT:(TARGET:2):음란*2 + TALENT:(TARGET:2):연모
ENDIF

LOCAL:3 = 1 + (TARGET:1 > 0) + (TARGET:2 > 0)
DRAWLINE

CVARSET TEQUIP, 30, 1
LOCAL:50 = ABL:MASTER:기교
ABL:MASTER:기교 = 2

CALL SIMULATE_TRAIN_FIRST, LOCAL:3

SELECTCASE LOCALS
CASE "아침ＳＭ"
	PRINTFORMW 아침, 깨어나면 신체가 움직이지 않는다……
	PRINTFORMW 어느새인가 알몸으로 침대에 얽매이고 있다
	PRINTFORMW 이쪽을 내려다 보고 있는 %조사처리(LOCALS:1,"가")%, 기학과 정욕에 탁해진 시선으로 인사해 왔다……
	CALL SELF_KOJO, LOCALS
	PRINTL 
	PRINTFORMW 그대로 밤이 될 때까지 %LOCALS:2%에 희롱해졌다……
	FOR LCOUNT, 0, LOCAL:3
		SWAP TARGET, TARGET:LCOUNT
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 1, MAX(LOCAL:LCOUNT/(2 + RAND:4), 1)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 6, MAX(LOCAL:LCOUNT/(3 + RAND:4), 1)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 9, MAX(LOCAL:LCOUNT/(2 + RAND:4), 1)
		IF RAND:2
			CALL COM_ABLE65
			SIF RESULT && TALENT:처녀 + TALENT:MASTER:동정 == 0 && ABL:순종+ABL:욕망+ABL:Ｖ감각+ABL:봉사정신 > 14 && PENIS(MASTER)
				CALL SIMULATE_TRAIN_COM, 65, MAX(LOCAL:LCOUNT/(5 + RAND:5), 1 + RAND:3)
		ENDIF
		IF RAND:3 == 0
			CALL COM_ABLE75
			SIF RESULT && TALENT:Ａ처녀 == 0 && EXP:PLAYER:Ａ삽입경험 && ABL:Ａ감각 >= MAX(3, ABL:Ｖ감각) && ABL:순종+ABL:욕망+ABL:Ａ감각+ABL:봉사정신 > 14 && PENIS(MASTER)
				CALL SIMULATE_TRAIN_COM, 75, MAX(LOCAL:LCOUNT/(5 + RAND:5), 1 + RAND:3)
		ENDIF
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 90, LOCAL:LCOUNT/(3 + RAND:3)
		CALL SIMULATE_TRAIN_COM, 91, LOCAL:LCOUNT/(4 + RAND:3)
		SIF RAND:2 && TALENT:거유
			CALL SIMULATE_TRAIN_COM, 92, MAX(LOCAL:LCOUNT/(4 + RAND:3), 1)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 95, LOCAL:LCOUNT/(3 + RAND:3)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 96, LOCAL:LCOUNT/(4 + RAND:3)
		SWAP TARGET, TARGET:LCOUNT
	NEXT
CASE "아침구속기승위"
	PRINTFORMW 아침, 깨어나면 신체가 무겁다……\@ TARGET:1 > 0 ? 뿐만 아니라 호흡도 어렵다… # \@
	IF TARGET:1
		PRINTFORM …상황의 파악에 시간이 걸렸지만, 아무래도 %CALLNAME:(TARGET:1)%의 얼굴에
		SIF TARGET:2
			PRINTFORM ,%CALLNAME:(TARGET:2)%의 팔에,
		PRINTFORMW 사타구니를 열중해 문지르고 있는 것 같다
		PRINTFORMW 게다가,%타겟이()% 기승위를 즐기고 있는 것 같고, 사타구니는 다양한 액체로 굉장한 모양이다…
		PRINTL 
		PRINTFORMW 깨어난 %마스터를()% 눈치챈 %LOCALS:3%의 기학과 정욕에 탁해진 시선을 보건데, 당분간 해방받을 수 없는 것 같다…
	ELSE
		PRINTFORMW 아무래도, 어느새 알몸으로 침대에 얽매이고 있는 것 같다…
		PRINTFORMW 무게가 걸리고 있는 장소를 바라보자, 허리에 걸치고 있는 %타겟과()% 시선이 마주쳐 버렸다
		PRINTFORMW 상당한 시간을 즐긴 것 처럼, 그 결합부는 거품이 일은 액체가 주르륵 늘어져, 질척질척 되어 있다…
		PRINTL 
		PRINTFORMW %CALLNAME:TARGET%의 기학과 정욕에 탁해진 시선을 보건데, 당분간 해방받을 수 없는 것 같다…
	ENDIF

	CALL SELF_KOJO, LOCALS
	PRINTL 
	PRINTFORMW 그대로 밤이 될 때까지%LOCALS:2%에 계속 짜여져, 농락당해졌다……
	FOR LCOUNT, 0, LOCAL:3
		SWAP TARGET, TARGET:LCOUNT
		CALL SIMULATE_TRAIN_COM, 1, LOCAL:LCOUNT/(2 + RAND:4)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 6, LOCAL:LCOUNT/(4 + RAND:4)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 9, LOCAL:LCOUNT/(2 + RAND:4)
		CALL SIMULATE_TRAIN_COM, 65, LOCAL:LCOUNT
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 91, LOCAL:LCOUNT/(3 + RAND:3)
		SIF RAND:2 && TALENT:거유
			CALL SIMULATE_TRAIN_COM, 92, LOCAL:LCOUNT/(4 + RAND:3)
		SIF RAND:4 == 0
			CALL SIMULATE_TRAIN_COM, 95, LOCAL:LCOUNT/(5 + RAND:3)
		SIF RAND:5 == 0
			CALL SIMULATE_TRAIN_COM, 96, LOCAL:LCOUNT/(5 + RAND:3)
		SWAP TARGET, TARGET:LCOUNT
	NEXT
CASE "아침구속Ａ기승위"
	PRINTFORMW 아침, 깨어나면 신체가 무겁다……\@ TARGET:1 > 0 ? 뿐만 아니라 호흡도 어렵다… # \@
	IF TARGET:1
		PRINTFORM …상황의 파악에 시간이 걸렸지만, 아무래도 %CALLNAME:(TARGET:1)%의 얼굴에
		SIF TARGET:2
			PRINTFORM ,%CALLNAME:(TARGET:2)%의 팔에,
		PRINTFORMW 사타구니를 열중해 문지르고 있는 것 같다
		PRINTFORMW 게다가,%타겟이()% 엉덩이의 구멍으로 즐기고 있는 것 같고, 사타구니는 다양한 액체로 굉장한 모양이다…
		PRINTL 
		PRINTFORMW 깨어난 %마스터를()% 눈치챈 %LOCALS:3%의 기학과 정욕에 탁해진 시선을 보건데, 당분간 해방받을 수 없는 것 같다…
	ELSE
		PRINTFORMW 아무래도, 어느새 알몸으로 침대에 얽매이고 있는 것 같다…
		PRINTFORMW 무게가 걸리고 있는 장소를 바라보자, 허리에 걸쳐 애널에 페니스를 물고 있는 %타겟과()% 시선이 마주쳐 버렸다
		PRINTFORMW 상당한 시간을 즐긴 것 처럼, 그 결합부는 거품이 일은 액체가 주르륵 늘어져, 질척질척 되어 있다…
		PRINTL 
		PRINTFORMW %CALLNAME:TARGET%의 기학과 정욕에 탁해진 시선을 보건데, 당분간 해방받을 수 없는 것 같다…
	ENDIF

	CALL SELF_KOJO, LOCALS
	PRINTL 
	PRINTFORMW 그대로 밤이 될 때까지%LOCALS:2%에 계속 짜여져, 농락당해졌다……
	FOR LCOUNT, 0, LOCAL:3
		SWAP TARGET, TARGET:LCOUNT
		CALL SIMULATE_TRAIN_COM, 1, MAX(LOCAL:LCOUNT/(2 + RAND:4), 1)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 6, LOCAL:LCOUNT/(4 + RAND:4)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 9, LOCAL:LCOUNT/(2 + RAND:4)
		CALL SIMULATE_TRAIN_COM, 75, LOCAL:LCOUNT
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 91, LOCAL:LCOUNT/(3 + RAND:3)
		SIF RAND:2 && TALENT:거유
			CALL SIMULATE_TRAIN_COM, 92, LOCAL:LCOUNT/(4 + RAND:3)
		SIF RAND:4 == 0
			CALL SIMULATE_TRAIN_COM, 95, LOCAL:LCOUNT/(5 + RAND:3)
		SIF RAND:5 == 0
			CALL SIMULATE_TRAIN_COM, 96, LOCAL:LCOUNT/(5 + RAND:3)
		SWAP TARGET, TARGET:LCOUNT
	NEXT
;今のところこの下は無し
CASE "아침구속역레이프"
	PRINTFORMW 아침, 깨어나면 신체가 움직이지 않는다……
	PRINTFORMW 어느새인가 알몸으로 침대에 얽매이고 있다
	PRINTFORMW 이쪽을 내려다 보고 있는 %조사처리(LOCALS:1,"가")%, 기학과 정욕에 탁해진 시선으로 인사해 왔다……
	CALL SELF_KOJO, LOCALS
	PRINTFORMW 그대로 밤이 될 때까지%LOCALS:2%에 희롱해졌다……
CASE "아침구속역애널"
	PRINTFORMW 아침, 깨어나면 신체가 움직이지 않는다……
	PRINTFORMW 어느새인가 알몸으로 침대에 얽매이고 있다
	PRINTFORMW 이쪽을 내려다 보고 있는 %조사처리(LOCALS:1,"가")%, 기학과 정욕에 탁해진 시선으로 인사해 왔다……
	CALL SELF_KOJO, LOCALS
	PRINTFORMW 그대로 밤이 될 때까지 %LOCALS:2%에 희롱해졌다……
ENDSELECT

CALL SIMULATE_TRAIN_CALC
CALL SIMULATE_TRAIN_SHOW

WAIT

;朝の구속이벤트の記録
CFLAG:MASTER:90 = NUM_EV(LOCALS)
CFLAG:90 = NUM_EV(LOCALS)
CALL CALC, "욕구불만解消", TARGET, LOCAL + LOCAL:1 + LOCAL:2
IF TARGET:1 > 0
	CFLAG:(TARGET:1):90 = NUM_EV(LOCALS)
	CALL CALC, "욕구불만解消", TARGET:1, LOCAL + LOCAL:1 + LOCAL:2
	;仲良くなる
	CALL CHANGE_RELATION, 5, TARGET, TARGET:1, TARGET:2
ENDIF
IF TARGET:2 > 0
	CFLAG:(TARGET:2):90 = NUM_EV(LOCALS)
	CALL CALC, "욕구불만解消", TARGET:2, LOCAL + LOCAL:1 + LOCAL:2
ENDIF

CVARSET TEQUIP, 30, 0

ABL:MASTER:기교 = LOCAL:50
RETURN 1


@CHECK_BIND_AT_MORNING, ARG, ARGS
#DIM LCOUNT

STR:100 = 
;욕구불만度が低いor욕망が3LV未満、好感度が500未満、반발각인あり
SIF COND("욕구불만", ARG) == 0 || ABL:ARG:욕망 < 3 || CFLAG:ARG:2 < 500 || MARK:ARG:반발각인 > 0
	RETURN 0

VARSET LOCALS

;새드or새드끼5LVならＳＭコース
IF TALENT:ARG:새드 || ABL:ARG:새드끼 >= 5
	IF ARGS == "아침ＳＭ"
		STR:100 = 아침ＳＭ
		RETURN 0
	ENDIF
	LOCALS:0 = 아침ＳＭ
ENDIF

CALL COM_ABLE65
IF RESULT && PENIS(MASTER) && TALENT:ARG:처녀 + TALENT:MASTER:동정 == 0 && ABL:ARG:Ｖ감각 >= 3 && EXP:ARG:Ｖ성교경험 >= 10 && EXP:ARG:허리사용경험 >= 20
	IF ARGS == "아침구속기승위"
		STR:100 = 아침구속기승위
		RETURN 0
	ENDIF
	LOCALS:1 = 아침구속기승위
ENDIF

CALL COM_ABLE75
IF RESULT && PENIS(MASTER) && EXP:MASTER:Ａ삽입경험 && COND("베테랑：Ａ섹스")
	IF ARGS == "아침구속Ａ기승위"
		STR:100 = 아침구속Ａ기승위
		RETURN 0
	ENDIF
	LOCALS:2 = 아침구속Ａ기승위
ENDIF

;욕구불만
SIF COND("욕구불만") && (LOCALS:1 == "아침구속기승위" || LOCALS:2 == "아침구속Ａ기승위")
	LOCALS:0 = 

LOCAL = 1
FOR LCOUNT, 0, 10
	SIF LOCALS:LCOUNT == ""
		CONTINUE
	SIF RAND:LOCAL == 0
		STR:100 = %LOCALS:LCOUNT%

	LOCAL += 1
NEXT

;今のところここまで
RETURN 0

