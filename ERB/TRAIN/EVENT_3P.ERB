;-------------------------------------------------
;多人数플레이で用いる共通処理はここで行う
;-------------------------------------------------
;-------------------------------------------------
;３Ｐにおける커맨드구상の범용部分
;-------------------------------------------------
@TRAIN_MESSAGE_3P
;同じ커맨드の組み合わせで連続実行
IF SELECTCOM:2 == PREVCOM:2 && SELECTCOM == PREVCOM
	IF RAND:3 == 0
		PRINTFORMW %조사처리(CALLNAME:PLAYER,"과")% %조사처리(CALLNAME:(PLAYER:1),"는")% %조사처리(CALLNAME:TARGET,"로")% 마음껏 쾌락을 탐하고있다…
		RETURN 1
	ENDIF
	SELECTCASE SELECTCOM
	;인서트
	CASE 60 TO 79, 83
		PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% %CALLNAME:(PLAYER:1)%
		SELECTCASE PREVCOM:2
		CASE 90, 97
			PRINTFORM 의 페니스를 \@ PREVCOM:2 == 97 ? %조사처리(CLO("팬티훑기"),"로")% 훑고있는 # 손으로 훑고있는 \@ 
		CASE 91
			PRINTFORM 의 페니스를 거친 기세로 빨고있는 
		CASE 92
			PRINTFORM 에게 파이즈리를 하고있는 
		CASE 96
			PRINTFORM 의 엉덩이구멍을 괴롭히고있는 
		CASE 98
			PRINTFORM 의 그곳을 혀로 맛보고있는 
		ENDSELECT
		PRINTFORMW %조사처리(CALLNAME:TARGET,"를")% 격렬하게 계속 범했다…
	;이라마치오
	CASE 140
		PRINTFORM %조사처리(CALLNAME:PLAYER,"과")% %조사처리(CALLNAME:(PLAYER:1),"는")%, 허리의 움직임을 맞춰 %CALLNAME:TARGET%의 
		SELECTCASE PREVCOM:2
		CASE 60 TO 69
			PRINTFORM 질
		CASE 70 TO 79
			PRINTFORM 애널
		CASE 83
			PRINTFORM 
		CASEELSE
			IF INSERT("Ｖ인서트", TARGET, PLAYER:1)
				PRINTFORM 질
			ELSEIF INSERT("Ａ인서트", TARGET, PLAYER:1)
				PRINTFORM 애널
			ELSEIF INSERT("요도인서트", TARGET, PLAYER:1)
				PRINTFORM 
			ENDIF
		ENDSELECT
		PRINTFORMW 과 목을 계속 범하고있다…
	CASEELSE
		;この値は조수側の行動での分岐(PREVCOM:2での分岐と思って問題ない)
		SELECTCASE CHECK_COM("３Ｐ봉사")
		;니플퍽
		CASE 82
			SELECTCASE SELECTCOM
			;左右니플퍽
			CASE 82
				PRINTFORMW %조사처리(CALLNAME:PLAYER,"와")%%조사처리(CALLNAME:(PLAYER:1),"는")%, 허리의 움직임을 맞추어%CALLNAME:TARGET%의 좌우의유두를 계속 범하고 있다…
			CASE 90
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:(PLAYER:1)%의 페니스를 가슴에서 받아들이면서, %CALLNAME:PLAYER%의 물건을 자신에게 돌려서 손에서 취급하고 있다.
			CASE 91
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:(PLAYER:1)%의 페니스를 가슴에서 받아들이면서, %CALLNAME:PLAYER%의 페니스에 혀를 얽어고 있다.
			CASE 97
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:(PLAYER:1)%의 페니스를 가슴에서 받아들이면서, %CALLNAME:PLAYER%의 물건을%CLO("팬티훑기")%로 취급하고 있다.
			ENDSELECT
		CASE 90
			SELECTCASE SELECTCOM
			CASE 82
				PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% , %CALLNAME:(PLAYER:1)%의 페니스를 손으로 쓰다듬고 있다%CALLNAME:TARGET%의 젖꼭지를 지어 갔다…
			CASE 90, 97
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:PLAYER%들 두 사람의 페니스를 자신의 얼굴로 향하게해서, 솜씨좋게 양손으로 손훑기를 계속하고있다…
			CASEELSE
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:(PLAYER:1)%의 페니스를 손으로 만지작거리면서, %CALLNAME:PLAYER%의 페니스에 혀를 기게하고있다…
			ENDSELECT
		CASE 91
			SELECTCASE SELECTCOM
			CASE 82
				PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% , %CALLNAME:(PLAYER:1)%의 페니스에 입에서 봉사하는%CALLNAME:TARGET%의 젖꼭지를 지어 갔다…
			CASE 90
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:(PLAYER:1)%의 페니스를 끈적끈적하게 핥으면서, %CALLNAME:PLAYER%의 물건을 자신을 향하게해서 손으로 계속 훑고있다…
			CASE 97
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:(PLAYER:1)%의 페니스를 끈적끈적하게 핥으면서, %CALLNAME:PLAYER%의 물건을 %조사처리(CLO("팬티훑기"),"로")% 계속 훑고있다…
			CASEELSE
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:PLAYER%들 두 사람의 페니스를 바쁘게 입으로 돌보고있다…
			ENDSELECT
		CASEELSE
			PRINTFORM %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:(PLAYER:1)%의 위에 걸터앉고 허리를 흔들면서, 
			SELECTCASE SELECTCOM
			;현재はこれは死に分岐
			CASE 82
				PRINTFORMW %CALLNAME:PLAYER%의 물건을 유선으로 받아들이고 있다.…
			CASE 90
				PRINTFORMW %CALLNAME:PLAYER%의 물건을 자신에게 향하게해서 손으로 계속 훑고있다…
			CASE 97
				PRINTFORMW %CALLNAME:PLAYER%의 물건을 %조사처리(CLO("팬티훑기"),"로")% 계속 훑고있다…
			CASE 91
				PRINTFORMW %CALLNAME:PLAYER%의 물건을 거친 기세로 계속 핥고있다…
			CASE 92
				PRINTFORMW %CALLNAME:PLAYER%의 물건을 젖 사이에 끼우고 계속 훑고있다…
			CASE 96
				PRINTFORMW %CALLNAME:PLAYER%의 애널에 계속 혀를 기고있다…
			CASE 98
				PRINTFORMW %CALLNAME:PLAYER%의 애액을 핥고 보지를 혀로 자극하고 있다…
			ENDSELECT
		ENDSELECT
	ENDSELECT
	RETURN 1
ENDIF

SELECTCASE SELECTCOM
;섹스系や이라마치오などPLAYER主導커맨드
CASE 60 TO 79, 82, 83, 140
	PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% %CALLNAME:(PLAYER:1)%
	SELECTCASE PREVCOM:2
	CASE 60 TO 69
		PRINTFORM 에게 격렬하게 범해지고있는 
	CASE 70 TO 79
		PRINTFORM 에게 격렬하게 애널을 범해지고있는 
	CASE 82
		PRINTFORM %TEXTR("에 젖꼭지를 저지르지", "의 물건을 가슴으로 받아들여")%고 있다.
	CASE 83
		PRINTFORM 에 격렬하게 범해지고 있는
	CASE 90, 97
		PRINTFORM 의 물건을 \@ PREVCOM:2 == 97 ? %조사처리(CLO("팬티훑기"),"로")% 훑고있는 # 손으로 훑고있는 \@ 
	CASE 91
		PRINTFORM 의 물건을 입으로 핥고 있는 
	CASE 92
		PRINTFORM 의 물건을 가슴으로 비비고 있는 
	CASE 96
		PRINTFORM 의 엉덩이구멍에 혀를 이용해 봉사하고 있는 
	CASE 98
		PRINTFORM 의 그곳에 혀로 봉사를 하고 있는 
	CASE 140
		PRINTFORM 에게 목구멍 안쪽까지 범해지고 있는 
	ENDSELECT

	PRINTFORM %CALLNAME:TARGET%

	SELECTCASE SELECTCOM
	CASE 60 TO 69
		PRINTFORM 의 %TEXTS("Ｖ삽입前")%
		SELECTCASE PREVCOM
		CASE 70 TO 79
			PRINTFORMW 의 애널에서 뽑아낸 %조사처리(NAMES("페니스", PLAYER),"를")% 쳐넣고 범하기시작했다…
		CASE 83
			PRINTFORMW 으로부터 뽑아 낸 %조사처리(NAMES("페니스", PLAYER),"를")% 쳐박아 범하기 시작했다…
		CASEELSE
			PRINTFORMW %조사만처리(TEXTS("Ｖ삽입前"),"를")% 범하기시작했다…
		ENDSELECT
	CASE 70 TO 79
		PRINTFORM 의 %TEXTS("애널")% 애널
		SELECTCASE PREVCOM
		CASE 60 TO 69
			PRINTFORMW 을 애액투성이의 %조사처리(NAMES("페니스", PLAYER),"로")% 범하기 시작했다…
		CASE 83
			PRINTFORMW 를 암모니아 냄새가 나는 %조사처리(NAMES("페니스", PLAYER),"로")% 범하기 시작했다…
		CASEELSE
			PRINTFORMW 을 범하기 시작했다…
		ENDSELECT
	CASE 82
		PRINTFORM 노, 사용되지 않은 (분)편의유두
		SELECTCASE PREVCOM
		CASE 60 TO 69
			PRINTFORMW 을 애액투성이의 %조사처리(NAMES("페니스", PLAYER),"로")% 범하기시작했…
		CASE 70 TO 79
			PRINTFORMW 의 애널에서 뽑아낸 %조사처리(NAMES("페니스", PLAYER),"를")% 쳐넣고 범하기시작했다…
		CASE 83
			PRINTFORMW 를 소변 투성이의 %조사처리(NAMES("페니스", PLAYER),"로")% 범하기 시작했다…
		CASEELSE
			PRINTFORMW 를 저지르기 시작했다.…
		ENDSELECT
	CASE 83
		PRINTFORMW 
	CASE 140
		PRINTFORMW 의 머리를 잡고, 몹시 거칠게 %조사처리(NAMES("페니스", PLAYER),"로")% 입을 범하기시작했다…
	ENDSELECT
CASEELSE
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:(PLAYER:1)%

	SELECTCASE CHECK_COM("３Ｐ봉사")
	;니플퍽
	CASE 82
		PRINTFORM %TEXTR("에게 젖꼭지를 범하", "의 물건을 가슴으로 받아들이")%며, 
	CASE 90
		PRINTFORM 의 물건을 \@ PREVCOM:2 == 97 ? %조사처리(CLO("팬티훑기"),"로")% # 손으로 \@ 훑으면서, 
	CASE 91
		SELECTCASE SELECTCOM
		CASE 91
			PRINTFORM 의 물건을 동시에, 
		CASEELSE
			PRINTFORM 의 물건을 입으로 빨면서, 
		ENDSELECT
	CASEELSE
		SELECTCASE PREVCOM:2
		CASE 60 TO 69
			PRINTFORM 의 물건을 질로 단단히 조이고 천천히 허리를 움직이면서, 
		CASE 70 TO 79
			PRINTFORM 의 물건을 직장으로 삼킨 채, 
		CASE 83
			PRINTFORM 의 것을 할 수 있는 만큼 단단히 조이면서,
		CASEELSE
			IF INSERT("Ｖ인서트", TARGET, PLAYER:1)
				PRINTFORM 의 물건을 질로 조이고 천천히 허리를 움직이면서, 
			ELSEIF INSERT("Ａ인서트", TARGET, PLAYER:1)
				PRINTFORM 의 물건을 직장으로 삼킨채, 
			ELSEIF INSERT("요도인서트", TARGET, PLAYER:1)
				PRINTFORM 의 것을 할 수 있는 만큼 단단히 조이면서,
			ENDIF
		ENDSELECT
	ENDSELECT

	SELECTCASE SELECTCOM
	CASE 90 TO 92, 97
		SELECTCASE PREVCOM
		CASE 60 TO 69
			PRINTFORM 조금전까지 자신을 범하고있던 
		CASE 70 TO 79
			PRINTFORM 애널에서 막 뽑혀져나온 
		CASE 83
			PRINTFORM 오줌의 구멍으로부터 뽑아 내진지 얼마 안된
		ENDSELECT
	ENDSELECT

	PRINTFORM %CALLNAME:PLAYER%의 
	SELECTCASE SELECTCOM
	CASE 90
		PRINTFORM 페니스를 남은 쪽의 손으로 
		IF RAND:3 == 0
			PRINTW 슥슥 문지르기시작했다…
		ELSEIF RAND:2
			PRINTW 훑기 시작했다…
		ELSE
			PRINTW 주무르기 시작했다…
		ENDIF
	CASE 97
		PRINTFORM 페니스를 %조사처리(CLO("팬티훑기"),"로")% 
		IF RAND:3 == 0
			PRINTW 비비기 시작했다…
		ELSEIF RAND:2
			PRINTW 훑기 시작했다…
		ELSE
			PRINTW 감싸서 주무르기 시작했다…
		ENDIF
	CASE 91
		PRINTFORM 페니스에 입을 대고 
		;봉사정신3이상か욕정4이상
		IF (ABL:봉사정신 >= 3  || PALAM:욕정 >= PALAMLV:4) && RAND:2 == 0
			PRINTFORMW 격렬하게 빨기 시작했다…
		ELSEIF (RAND:2 == 0 && ABL:기교 >= 2) || TFLAG:16
			PRINTFORMW 들이마시듯이 빨기 시작했다…
		ELSE
			PRINTFORMW 핥으며 돌렸다…
		ENDIF
	CASE 92
		PRINTFORM 페니스를 
		IF TALENT:빈유
			PRINTFORM 평평한 가슴으로 열심히 
		ELSEIF TALENT:거유
			PRINTFORM 풍만한 가슴에 묻고 
		ELSE
			PRINTFORM 가슴에 끼우고 
		ENDIF
		PRINTW 훑어주었다…
	CASE 96
		PRINTFORM 엉덩이구멍을 
		IF ABL:봉사정신 >= 3 && ABL:새드끼 >= 3 && RAND:2 == 0
			PRINTFORMW 뾰족하게 세운 혀로 안까지 핥기 시작했다…
		ELSEIF ABL:봉사정신 >= 3
			PRINTFORMW 꼼꼼하게 혀로 핥아서 닦기 시작했다…
			STAIN:PLAYER:Ａ = SET_STAIN(0, "Ａ")
		ELSEIF ABL:새드끼 >= 3 || TEQUIP:넘어뜨리기
			PRINTFORMW 혀로 괴롭혔다…
		ELSE
			PRINTFORMW 조심조심 핥았다…
		ENDIF
	CASE 98
		PRINTFORM 그곳을 
		IF ABL:봉사정신 >= 3 && ABL:새드끼 >= 3 && RAND:2 == 0
			PRINTFORMW 뾰족하게 세운 혀로 안의 벽까지 핥았다…
		ELSEIF ABL:봉사정신 >= 3
			PRINTFORMW 벌리고, 구석구석까지 혀로 핥기시작했다…
			STAIN:PLAYER:Ａ = SET_STAIN(0, "Ａ")
		ELSE
			PRINTFORMW 핥았다…
		ENDIF
	ENDSELECT
ENDSELECT

;-------------------------------------------------
;３Ｐにおける사정구상の범용部分
;-------------------------------------------------
@SAMEN_SHOOT_MESSAGE_3P, ARGS
PRINTFORM %CALLNAME:(PLAYER:1)%
SELECTCASE SELECTCOM:2
CASE 60 TO 79, 83
	IF COND("절정", PLAYER:1)
		PRINTFORM 에게 질내사정되어 부들부들 떨고있는 
	ELSEIF INSERT("기승위")
		PRINTFORM 에게 걸터앉고 허리를 흔들고있는 
	ELSE
		PRINTFORM 에게 범해지고있는 
	ENDIF
CASE 82
	IF COND("절정", PLAYER:1)
		PRINTFORM 에 정액을 가슴안에서 내 신체를 진동시키고 있다
	ELSE
		PRINTFORM 의 페니스가 가슴을 범하고 있다
	ENDIF
CASE 90, 97
	IF COND("절정", PLAYER:1)
		IF ARGS == "손"
			PRINTFORMW %조사만처리(CALLNAME:(PLAYER:1),"과")% 동시에 
		ELSE
			PRINTFORM 에게 정액세례를 받고있는 
		ENDIF
	ELSE
		IF ARGS == "손"
			PRINTFORM 두개의 페니스를 정신없이 훑고있는 
		ELSE
			PRINTFORM 의 페니스를 훑고있는 
		ENDIF
	ENDIF
CASE 91
	IF COND("절정", PLAYER:1)
		IF ARGS == "입"
			PRINTFORMW %조사만처리(CALLNAME:(PLAYER:1),"과")% 동시에 
		ELSE
			PRINTFORM 에게 구내사정당하고있는 
		ENDIF
	ELSE
		IF ARGS == "입"
			PRINTFORM 의 페니스에서 입을 뗀 
		ELSE
			PRINTFORM 의 페니스를 빨고있는 
		ENDIF
	ENDIF
CASE 92
	IF COND("절정", PLAYER:1)
		PRINTFORM 의 정액으로 가슴을 더럽혀진 
	ELSE
		PRINTFORM 의 페니스를 가슴으로 비비고있는 
	ENDIF
CASE 96
	IF COND("절정", PLAYER:1)
		PRINTFORMW %조사만처리(CALLNAME:(PLAYER:1),"를")% 엉덩이구멍의 쾌락으로 절정시키고 있는 
	ELSE
		PRINTFORM 의 애널에 혀를 후벼넣고있는 
	ENDIF
CASE 98
	IF COND("절정", PLAYER:1)
		PRINTFORMW %조사만처리(CALLNAME:(PLAYER:1),"를")% 컨닐링구스로 절정시키고있는 
	ELSE
		PRINTFORM 의 그곳에 혀로 봉사하고있는 
	ENDIF
CASE 140
	IF COND("절정", PLAYER:1)
		PRINTFORMW %조사만처리(CALLNAME:(PLAYER:1),"가")% 정액을 먹이고있는 중인 
	ELSE
		PRINTFORM 에게 거칠게 목구멍을 범해지고있는 
	ENDIF
ENDSELECT


;-------------------------------------------------
;３Ｐにおける補佐役（通常は조수）の커맨드決定（自由行動は今のところ無し）
;-------------------------------------------------
@SET_SELECTCOM_3P
SELECTCASE SELECTCOM
CASE 82, 90 TO 104, 140
	SIF CHECK_COM("Ｗ봉사")
		SELECTCOM:1 = SELECTCOM
	SIF CHECK_COM("３Ｐ봉사")
		SELECTCOM:2 = PREVCOM:2
CASE 60 TO 69
	IF CHECK_COM("Ｖ두개삽입")
		SELECTCOM:2 = 64
	ELSEIF CHECK_COM("양구멍삽입")
		SELECTCOM:2 = 74
	ELSEIF CHECK_COM("３Ｐ봉사")
		SELECTCOM:2 = PREVCOM:2
	ENDIF
CASE 70 TO 79
	IF CHECK_COM("Ａ두개삽입")
		SELECTCOM:2 = 75
	ELSEIF CHECK_COM("양구멍삽입")
		SELECTCOM:2 = 65
	ELSEIF CHECK_COM("３Ｐ봉사")
		SELECTCOM:2 = PREVCOM:2
	ENDIF
CASE 83
	SIF CHECK_COM("３Ｐ봉사")
		SELECTCOM:2 = PREVCOM:2
ENDSELECT

;-------------------------------------------------
;ARGS:1～5で손가락定された禁止ワードに応じたビットを설정する
;-------------------------------------------------
@BIT_FORBIDCOM(ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5)
#FUNCTION
#DIM LCOUNT

LOCAL = 0
FOR LCOUNT, 1, 6
	ARGS = %ARGS:LCOUNT%
	;補佐役がプレイヤーの動き（封印中）
	IF COND("조교側", PLAYER:1)
		SELECTCASE ARGS
		;TARGETに対する커맨드管理
		CASE "Ｔ얼굴"
			SETBIT LOCAL, 0
		CASE "Ｔ下半身"
			SETBIT LOCAL, 1
			SETBIT LOCAL, 2
			SETBIT LOCAL, 3
			SETBIT LOCAL, 4
		CASE "Ｔ페니스"
			SETBIT LOCAL, 2
		CASE "Ｔ보지"
			SETBIT LOCAL, 3
		CASE "Ｔ애널"
			SETBIT LOCAL, 4
		CASE "Ｔ가슴"
			SETBIT LOCAL, 5
		CASE "Ｔ손가락"
			SETBIT LOCAL, 6
		CASE "Ｔ前面"
			SETBIT LOCAL, 7
			SETBIT LOCAL, 5
			SETBIT LOCAL, 2
		CASE "Ｔ後面"
			SETBIT LOCAL, 8
			SETBIT LOCAL, 4
		ENDSELECT
	ELSE
		SELECTCASE ARGS
		;PLAYERに対する커맨드管理
		CASE "Ｐ얼굴"
			SETBIT LOCAL, 20
		CASE "Ｐ下半身"
			SETBIT LOCAL, 21
			SETBIT LOCAL, 22
			SETBIT LOCAL, 23
			SETBIT LOCAL, 24
		CASE "Ｐ페니스"
			SETBIT LOCAL, 22
		CASE "Ｐ보지"
			SETBIT LOCAL, 23
		CASE "Ｐ애널"
			SETBIT LOCAL, 24
		CASE "Ｐ가슴"
			SETBIT LOCAL, 25
		CASE "Ｐ손가락"
			SETBIT LOCAL, 26
		CASE "Ｐ前面"
			SETBIT LOCAL, 27
			SETBIT LOCAL, 25
			SETBIT LOCAL, 22
		CASE "Ｐ後面"
			SETBIT LOCAL, 28
			SETBIT LOCAL, 24
		;PLAYERに対する봉사커맨드
		CASE "손훑기"
			SETBIT LOCAL, 30
		CASE "손훑기以外"
			SETBIT LOCAL, 31
			SETBIT LOCAL, 32
			SETBIT LOCAL, 33
			SETBIT LOCAL, 34
			SETBIT LOCAL, 35
			SETBIT LOCAL, 36
			SETBIT LOCAL, 39
		CASE "펠라치오"
			SETBIT LOCAL, 31
		CASE "펠라치오以外"
			SETBIT LOCAL, 30
			SETBIT LOCAL, 32
			SETBIT LOCAL, 33
			SETBIT LOCAL, 34
			SETBIT LOCAL, 35
			SETBIT LOCAL, 36
			SETBIT LOCAL, 39
		CASE "파이즈리"
			SETBIT LOCAL, 32
		CASE "파이즈리以外"
			SETBIT LOCAL, 30
			SETBIT LOCAL, 31
			SETBIT LOCAL, 33
			SETBIT LOCAL, 34
			SETBIT LOCAL, 35
			SETBIT LOCAL, 36
			SETBIT LOCAL, 39
		CASE "풋잡"
			SETBIT LOCAL, 33
		CASE "풋잡以外"
			SETBIT LOCAL, 30
			SETBIT LOCAL, 31
			SETBIT LOCAL, 32
			SETBIT LOCAL, 34
			SETBIT LOCAL, 35
			SETBIT LOCAL, 36
			SETBIT LOCAL, 39
		CASE "스마타"
			SETBIT LOCAL, 34
		CASE "스마타以外"
			SETBIT LOCAL, 30
			SETBIT LOCAL, 31
			SETBIT LOCAL, 32
			SETBIT LOCAL, 33
			SETBIT LOCAL, 35
			SETBIT LOCAL, 36
			SETBIT LOCAL, 39
		CASE "거품춤"
			SETBIT LOCAL, 35
		CASE "거품춤以外"
			SETBIT LOCAL, 30
			SETBIT LOCAL, 31
			SETBIT LOCAL, 32
			SETBIT LOCAL, 33
			SETBIT LOCAL, 34
			SETBIT LOCAL, 36
			SETBIT LOCAL, 39
		CASE "팬티훑기"
			SETBIT LOCAL, 36
		CASE "팬티훑기以外"
			SETBIT LOCAL, 31
			SETBIT LOCAL, 32
			SETBIT LOCAL, 33
			SETBIT LOCAL, 34
			SETBIT LOCAL, 35
			SETBIT LOCAL, 39
		CASE "섹스"
			SETBIT LOCAL, 39
		ENDSELECT
	ENDIF
	SIF STRLENS(ARGS:(LCOUNT + 1) ) == 0
		RETURNF LOCAL
NEXT
RETURNF LOCAL

;-------------------------------------------------
;PLAYERでもTARGETでも無いARG(主に조수)に、他のキャラが選んだ커맨드ARG:1に応じた禁止커맨드を설정する
;-------------------------------------------------
@SET_FORBIDCOM, ARG, ARG:1
VARSET LOCAL

SELECTCASE ARG:1
;안면기승などPLAYERの입を責める
CASE 1
	LOCAL = BIT_FORBIDCOM("Ｐ얼굴", "Ｔ보지")
CASE 4
	LOCAL = BIT_FORBIDCOM("Ｐ얼굴", "Ｔ페니스")
CASE 5
	LOCAL = BIT_FORBIDCOM("Ｐ손가락", "Ｔ가슴")
CASE 6
	LOCAL = BIT_FORBIDCOM("Ｐ얼굴", "Ｔ얼굴")
CASE 9
	LOCAL = BIT_FORBIDCOM("Ｐ얼굴", "Ｔ애널")
;기승위などPLAYERの페니스を보지や애널で責める
CASE 60, 62, 63
	LOCAL = BIT_FORBIDCOM("Ｐ前面", "Ｔ下半身", "Ｔ前面")
CASE 61
	LOCAL = BIT_FORBIDCOM("Ｐ前面", "Ｔ下半身", "Ｔ後面")
CASE 64
	LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｐ前面", "Ｔ後面")
CASE 65
	LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｔ보지")
CASE 66 TO 69
	IF TEQUIP:넘어뜨리기
		LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｔ보지")
	ELSE
		LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｔ下半身")
	ENDIF
CASE 70, 72, 73
	LOCAL = BIT_FORBIDCOM("Ｐ前面", "Ｔ下半身", "Ｔ前面")
CASE 71
	LOCAL = BIT_FORBIDCOM("Ｐ前面", "Ｔ下半身", "Ｔ後面")
CASE 74
	LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｐ前面", "Ｔ後面")
CASE 75
	LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｔ보지", "Ｔ後面")
CASE 76 TO 79
	IF TEQUIP:넘어뜨리기
		LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｔ보지", "Ｔ後面")
	ELSE
		LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｔ下半身")
	ENDIF
;貝合わせ
CASE 80
	LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｔ下半身")
;요도삽입
CASE 83
	LOCAL = BIT_FORBIDCOM("Ｐ前面", "Ｔ下半身", "Ｔ前面")
;손훑기などPLAYERの페니스を손とかで責める
CASE 90
	LOCAL = BIT_FORBIDCOM("손훑기以外", "Ｔ손가락")
CASE 91
	LOCAL = BIT_FORBIDCOM("펠라치오以外", "Ｔ얼굴")
CASE 92
	LOCAL = BIT_FORBIDCOM("파이즈리以外", "Ｔ가슴")
CASE 93
	LOCAL = BIT_FORBIDCOM("스마타以外", "Ｔ下半身")
CASE 94
	LOCAL = BIT_FORBIDCOM("거품춤以外", "Ｔ前面")
CASE 95
	IF PENIS(MASTER)
		LOCAL = BIT_FORBIDCOM("풋잡以外", "Ｔ下半身")
	ELSE
		LOCAL = BIT_FORBIDCOM("풋잡以外", "Ｔ下半身")
	ENDIF
CASE 96
	LOCAL = BIT_FORBIDCOM("Ｐ애널", "Ｔ얼굴")
CASE 97
	LOCAL = BIT_FORBIDCOM("팬티훑기以外", "Ｔ손가락")
CASE 98
	LOCAL = BIT_FORBIDCOM("Ｐ보지", "Ｔ얼굴")
CASE 104
	LOCAL = BIT_FORBIDCOM("Ｐ얼굴", "Ｔ가슴", "Ｔ前面")
;역애널
CASE 125 TO 129
	LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｐ後面", "Ｔ페니스", "Ｔ前面")
;역레이프などPLAYERの보지를責める
CASE 120 TO 124
	LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｐ前面", "Ｔ페니스", "Ｔ前面")
CASE 140
	LOCAL = BIT_FORBIDCOM("Ｐ페니스", "Ｐ前面", "Ｔ얼굴")
CASE 141
	LOCAL = BIT_FORBIDCOM("Ｐ손가락", "Ｔ보지")
CASE 142
	LOCAL = BIT_FORBIDCOM("Ｐ손가락", "Ｔ애널")
CASE 143
	LOCAL = BIT_FORBIDCOM("Ｐ손가락", "Ｐ前面", "Ｔ下半身")
ENDSELECT
TCVAR:ARG:25 = LOCAL

;-------------------------------------------------
;ARGS:1～5が実際に禁止されているかを見る
;-------------------------------------------------
@FORBIDCOM(ARG, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5)
#FUNCTION
SIF TCVAR:ARG:25 & BIT_FORBIDCOM(ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5)
	RETURNF 1
RETURNF 0


;-------------------------------------------------
;３ＰやＷ넘어뜨리기などでの補佐役（通常は조수）の行動を決定する
;-------------------------------------------------
@SET_SELECTCOM_MULTI, ARGS
SELECTCASE ARGS
CASE "Ｗ넘어뜨리기"
	CALL SET_SELECTCOM_W
CASE "３Ｐ"
	CALL SET_SELECTCOM_3P
ENDSELECT

;-------------------------------------------------
;Ｗ넘어뜨리기の時には조수の커맨드구상とSOURCE等を管理する
;補佐役の取得経験は邪魔だから隠すことにした
;-------------------------------------------------
@EQUIP_COM_3P
#DIM MEMO_LINECOUNT
#DIM ACTOR

ACTOR = TARGET:1
VARSET LOCAL

;表示があったかを判定
MEMO_LINECOUNT = LINECOUNT

;最初のターンは何もしてこない
IF SELECTCOM:1 == -1
	IF PREVCOM:1 >= 0
		PRINTL 
		SIF TEQUIP:넘어뜨리기
			PRINTL ＜쓰러뜨려짐중＞
		PRINTFORMW %조사처리(CALLNAME:ACTOR,"은")% %CALLNAME:PLAYER%들의 플레이를 즐거운 듯이 바라보고 있다…
	ENDIF
	RETURN 0
;二人して同じことをしているor양구멍삽입
ELSEIF SELECTCOM:1 == SELECTCOM || (SELECTCOM == 120 && SELECTCOM:1 == 126) || (SELECTCOM == 126 && SELECTCOM:1 == 120)
	CALL GET_PALAM_MULTI, 1
	SIF LINECOUNT > MEMO_LINECOUNT
		PRINTL 
	SIF TEQUIP:넘어뜨리기
		PRINTL ＜쓰러뜨려짐중＞
;前回の조수, 今回のTARGETとも異なる行動or眺めている
ELSEIF SELECTCOM:1 != SELECTCOM && SELECTCOM:1 != PREVCOM:1
	PRINTL 
	SIF TEQUIP:넘어뜨리기
		PRINTL ＜쓰러뜨려짐중＞
	PRINTFORM %조사처리(CALLNAME:ACTOR,"은")%
	SELECTCASE SELECTCOM:1
	CASE 1
		PRINTFORMW %CALLNAME:PLAYER%의 얼굴에 올라타 그곳을 꽉 눌러붙였다…
		SIF INSERT("기승위") && ABL:ACTOR:레즈끼 + ABL:레즈끼 >= 3 && RAND:3 == 0
			PRINTFORMW 그리고, 눈앞에서 허리를 흔들고있는 %조사처리(CALLNAME:TARGET,"과")% 키스를 하기 시작했다…
	CASE 9
		PRINTFORMW %CALLNAME:PLAYER%의 얼굴에 걸터앉고 애널을 핥게했다…
		SIF INSERT("기승위") && ABL:ACTOR:레즈끼 + ABL:레즈끼 >= 3 && RAND:3 == 0
			PRINTFORMW 그리고, 눈앞에서 허리를 흔들고있는 %조사처리(CALLNAME:TARGET,"과")% 키스를 하기 시작했다…
	CASE 4
		PRINTFORMW %CALLNAME:PLAYER%의 입을 페니스로 범하기시작했다…
	CASE 6
		PRINTFORMW %CALLNAME:PLAYER%에게 부드럽게 키스했다…
	CASE 60 TO 79, 83
		PRINTFORMW %CALLNAME:PLAYER%의 페니스에 조준하고 허리를 내려앉았다…
		PRINTFORM 그리고, 
		SELECTCASE SELECTCOM:1
		CASE 60 TO 69
			PRINTFORM 질
		CASE 70 TO 79
			PRINTFORM 애널
		CASE 83
			PRINTFORM 
		ENDSELECT
		IF RAND:3 == 0
			PRINTFORM 을 교묘하게 꿈틀거리면서 
		ELSEIF RAND:2 == 0
			PRINTFORM 을 바짝 조이면서
		ELSE
			SELECTCASE SELECTCOM:1
			CASE 60 TO 69
				PRINTFORM 에서 애액을 흘리면서 
			CASE 70 TO 79
				PRINTFORM 에서 점액을 흘리면서 
			CASE 83
				PRINTFORM 의 틈새로부터 오줌을 흘리면서
			ENDSELECT
		ENDIF
		IF ABL:ACTOR:기교 >= 3 && RAND:3 == 0
			PRINTFORMW 격렬하게 허리를 흔들기 시작했다…
		ELSEIF RAND:2 == 0
			PRINTFORMW 허리를 흔들기시작했다…
		ELSE
			PRINTFORMW 긴 스트로크로 허리를 움직이기 시작했다…
		ENDIF
	CASE 90
		IF TALENT:ACTOR:연모 && RAND:4 == 0
			PRINTFORMW %CALLNAME:PLAYER%의 페니스를 사랑스러운 듯이 손으로 훑었다…
		ELSEIF ABL:ACTOR:기교 >= 3 && ABL:ACTOR:봉사정신 >= 3 && RAND:3 == 0
			PRINTFORMW %CALLNAME:PLAYER%의 귀두를 손바닥으로 주물러 돌렸다…
		ELSEIF ABL:ACTOR:봉사정신 >= 3 && RAND:2 == 0
			PRINTFORMW %CALLNAME:PLAYER%의 페니스를 천천히 주물렀다…
		ELSEIF ABL:ACTOR:기교 >= 3 && RAND:2 == 0
			PRINTFORMW %CALLNAME:PLAYER%의 페니스를 절묘한 힘 조절로 자극했다…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%의 페니스를 손으로 훑기 시작했다…
		ENDIF
	CASE 91
		IF TALENT:ACTOR:연모 && RAND:4 == 0
			PRINTFORMW %CALLNAME:PLAYER%의 페니스를 사랑스러운 듯이 입으로 물었다…
		ELSEIF ABL:ACTOR:기교 >= 4 && ABL:ACTOR:봉사정신 >= 3 && RAND:3 == 0
			PRINTFORMW %CALLNAME:PLAYER%의 페니스를 목구멍 안쪽까지 집어넣고, 긴 스트로크로 훑었다…
		ELSEIF ABL:ACTOR:봉사정신 >= 3 && RAND:2 == 0
			PRINTFORMW %CALLNAME:PLAYER%의 페니스를 맛있는 듯이 입으로 맛보았다…
		ELSEIF ABL:ACTOR:기교 >= 3 && RAND:2 == 0
			PRINTFORMW %CALLNAME:PLAYER%의 페니스에 혀를 섞으면서, 격렬하게 빨았다…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%의 페니스를 혀로 핥기시작했다…
		ENDIF
	CASE 92
		IF TALENT:ACTOR:거유
			PRINTFORM 커다란 
		ELSEIF TALENT:ACTOR:빈유
			PRINTFORM 작은 
		ENDIF
		PRINTFORM 가슴을 으로 %CALLNAME:PLAYER%의 페니스를 
		IF TALENT:ACTOR:거유
			PRINTFORMW 훑으면서, 가슴계곡에서 얼굴을 내민 귀두를 혀로 핥았다…
		ELSE
			PRINTFORMW 훑었다…
		ENDIF
	CASE 93
		PRINTFORMW %CALLNAME:PLAYER%의 페니스에 그곳을 문지러붙였다…
	CASE 94
		PRINTFORMW %CALLNAME:PLAYER%에 거품투성이인 온몸을 문질러붙였다…
	CASE 95
		PRINTFORM %CALLNAME:PLAYER%의 
		IF TALENT:MASTER:후타나리 + TALENT:MASTER:남자
			PRINTFORMW 페니스를 발로 훑었다…
		ELSE
			PRINTFORMW 가랑이를 발로 문질렀다…
		ENDIF
	CASE 96
		PRINTFORMW %CALLNAME:PLAYER%의 애널을 핥았다…
	CASE 97
		PRINTFORMW 팬티를 벗고, %CALLNAME:PLAYER%의 페니스를 팬티로 훑기시작했다…
	CASE 98
		PRINTFORMW %CALLNAME:PLAYER%의 그곳을 핥았다…
	CASE 104
		PRINTFORMW %CALLNAME:PLAYER%의 얼굴을 가슴으로 뭉갰다…
	CASE 120
		PRINTFORM %CALLNAME:PLAYER%의 
		IF TALENT:MASTER:처녀 == 1
			PRINTFORM 남자를 모르는 
		ELSE
			PRINTFORM 흠뻑 젖은 
		ENDIF
		IF TALENT:작은몸집
			PRINTFORM 어린 균열을 
		ELSE
			PRINTFORM 질을 
		ENDIF
		IF PENIS(ACTOR)
			PRINTFORM 페니스
		ELSE
			PRINTFORM 페니스밴드
		ENDIF
		PRINTFORMW 로 범했다…
	CASE 126
		PRINTFORM %CALLNAME:PLAYER%의 애널을 
		IF PENIS(ACTOR)
			PRINTFORM 페니스
		ELSE
			PRINTFORM 페니스밴드
		ENDIF
		PRINTFORMW 로 범했다…
	;CASE 997
	;	PRINTFORMW 절정の余韻を味わいながら, %CALLNAME:PLAYER%たちの플레이を眺めている…
	CASE 0
		IF SELECTCOM:1 == PREVCOM:1
			PRINTFORMW %CALLNAME:PLAYER%에게 몸을 문질러 붙이고 있다…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%에게 몸을 갖다대고, 손가락을 빨았다…
		ENDIF
	ENDSELECT
	SIF SELECTCOM:1 >= 0
		CALL GET_PALAM_MULTI, 1
;구상は発言させない
ELSE
	PRINTL 
	SIF TEQUIP:넘어뜨리기
		PRINTL ＜쓰러뜨려짐중＞
	CALL GET_PALAM_MULTI
ENDIF

;何か텍스트を表示したならもう一個改行
SIF LINECOUNT > MEMO_LINECOUNT
	PRINTL 

;-------------------------------------------------
;3Pの行動による補佐役のPALAM、EXP変化
;Ｗ넘어뜨리기の場合にはSELECTCOM:1が조수が선택した커맨드なので, それに応じて計算する
;-------------------------------------------------
@GET_PALAM_MULTI, ARG
#DIM MEMO_PLAYER
#DIM ACTOR

;何もやっていない場合
SIF SELECTCOM:1 == -1
	RETURN 0

;喋らないフラグON？
IF TFLAG:13
	ARG = 0
	;リセット
	TFLAG:13 = 0
ENDIF

;조수のBEGIN ABLUPをＯＮにする
SIF TARGET:1 == ASSI
	FLAG:22 = 1

SWAP SELECTCOM, SELECTCOM:1
SWAP PREVCOM, PREVCOM:1

IF COND("조교側", PLAYER:1)
	ACTOR = PLAYER:1

	MEMO_PLAYER = PLAYER
	PLAYER = PLAYER:1

	CALL SOURCE_CHECK_SIMPLE, SELECTCOM, ARG, 100, 1

	PLAYER = MEMO_PLAYER
ELSE
	ACTOR = TARGET:1
	SWAP TARGET, TARGET:1

	CALL SOURCE_CHECK_SIMPLE, SELECTCOM, ARG, 100, 1

	SWAP TARGET, TARGET:1
ENDIF

SWAP SELECTCOM, SELECTCOM:1
SWAP PREVCOM, PREVCOM:1

;ダウン判定
SIF BASE:ACTOR:체력 < 500 || BASE:ACTOR:정력 <= 0
	CALL ASSI_DOWN, ACTOR


;-------------------------------------------------
;텍스트を表示しない, 簡易커맨드선택
;ARGはSELECTCOMを代入し, ARG:1の値で구상等の出力を決定する. 
;ARG:2は強度でARG:3は行う回数
;-------------------------------------------------
@SOURCE_CHECK_SIMPLE, ARG, ARG:1, ARG:2, ARG:3
#DIM MEMO_FLAG8
#DIM MEMO_SELECTCOM

MEMO_FLAG8 = FLAG:8
MEMO_SELECTCOM = SELECTCOM

FLAG:8 = 0
SELECTCOM = ARG

;레벨리미터체크
CALL SETFLAG, "레벨리미터"

TRYCALLFORM SHOW_COM_ORDER_COM, SELECTCOM, 1
TRYCALLFORM FLAG_COM{SELECTCOM}, ARG:2*ARG:3
TRYCALLFORM SOURCE_COM{SELECTCOM}, ARG:2*ARG:3
CALL SETFLAG, "봉사嗜虐判定", SELECTCOM
CALL SAMEN_CHECK, ARG:2*ARG:3
CALL CALC_SOURCE, "조교커맨드"

;인서트解除（NEXTCOM等を考慮）
CALL SETFLAG, "体勢戻し"

;바이브など付けっぱなし아이템の체크(主にSOURCE計算. 快楽のSOURCEが増えないものは, ここにはなくても良い)
CALL EQUIP_1

;ソースの計算
CALL CALC_SOURCE

;절정処理_見込み
CALL ORGASM_ADD_A

;구상
SIF ARG:1
	CALL KOJO_MESSAGE_COM
;절정処理_本番
CALL ORGASM_ADD_A
CALL ORGASM_ADD_B

;各種経験の変動
CALL GET_EXP_COM, ARG:3

;커맨드による무드の獲得
CALL MOOD_COM, SELECTCOM

;인서트플래그獲得
CALL CHECK_INSERT

;ソースの計算
CALL CALC_SOURCE

;고통쾌락경험, 봉사쾌락경험等の체크
CALL EXP_GOT_CHECK

;이상경험체크
SIF ARG:1
	CALL EXP50_CHECK

;체력・기력の減少
CALL CALC, "체력기력減少"

;특수な소질などの判定
CALL GET_EXABL2, TARGET

FLAG:8 = MEMO_FLAG8
SELECTCOM = MEMO_SELECTCOM


;-------------------------------------------------
;３Ｐにおける절정구상の処理
;-------------------------------------------------
@KOJO_MESSAGE_SAMEN_AND_EXTACY_3P
;Ｗ넘어뜨리기などTARGETが二人の時
IF TEQUIP:Ｗ넘어뜨리기 || SELECTCOM:1 >= 0
	SIF COND("절정", PLAYER)
		PRINTL 
	;TARGET:1を一時的にTARGETに
	SWAP TARGET, TARGET:1
	SWAP SELECTCOM, SELECTCOM:1
	SWAP PREVCOM, PREVCOM:1
	;사정箇所を、TARGET:1の行動に対応したものに
	SWAP TCVAR:PLAYER:5, TCVAR:PLAYER:6

	CALL KOJO_MESSAGE_SAMEN_AND_EXTACY

	SWAP TARGET, TARGET:1
	SWAP SELECTCOM, SELECTCOM:1
	SWAP PREVCOM, PREVCOM:1
	SWAP TCVAR:PLAYER:5, TCVAR:PLAYER:6
ENDIF
;PLAYERが二人の, いわゆる３Ｐの時
IF COND("３Ｐ") && COND("절정", PLAYER:1) && COND("절정", PLAYER) == 0
	CALL SETFLAG, "조교자교대"

	CALL KOJO_MESSAGE_SAMEN_AND_EXTACY

	CALL SETFLAG, "조교자교대"
ENDIF

;-------------------------------------------------
;아르카나など, 커맨드途中でRETURN 0する커맨드での発言に適している. 
;-------------------------------------------------
@KOJO_MESSAGE_COM_MULTI
SIF TEQUIP:Ｗ넘어뜨리기 == 0
	RETURN 0

SWAP TARGET, TARGET:1
SWAP PREVCOM, PREVCOM:1

PRINTFORML 
CALL KOJO_MESSAGE_COM

SWAP TARGET, TARGET:1
SWAP PREVCOM, PREVCOM:1


