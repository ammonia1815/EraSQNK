;------------------------------------------------------------------------
;조교종료時の만족度체크
;------------------------------------------------------------------------
@CHECK_SATISFACTION, ARG
SIF ARG <= 0
	RETURN 0

CFLAG:ARG:99 = 0
;체력체크…체력500未満かつ기력が少ないor체력1000未満で기력０or정력０
SIF (BASE:ARG:체력 < 500 && BASE:ARG:기력 < 300) || (BASE:ARG:체력 < MIN(MAXBASE:ARG:체력 / 2, 1000) && BASE:ARG:기력 <= 0) || BASE:ARG:정력 <= 0
	SETBIT CFLAG:ARG:99, 0

;성욕체크
IF ABL:ARG:욕망
	CALL SETFLAG, "성욕게이지", ARG

	SELECTCASE BASE:ARG:성욕
	CASE 0
	;대만족
	CASE IS >= MAXBASE:ARG:성욕 * 2
		SETBIT CFLAG:ARG:99, 2
	CASE IS >= MAXBASE:ARG:성욕
		SETBIT CFLAG:ARG:99, 1
	ENDSELECT
ENDIF
;정액욕체크
IF ABL:ARG:정액중독 + TALENT:ARG:몽마*2 + TALENT:ARG:상위몽마*2 >= 3
	CALL SETFLAG, "정액욕게이지", ARG
	SELECTCASE BASE:ARG:정액욕
	CASE 0
	;대만족
	CASE IS >= MAXBASE:ARG:정액욕 * 2
		SETBIT CFLAG:ARG:99, 4
	CASE IS >= MAXBASE:ARG:정액욕
		SETBIT CFLAG:ARG:99, 3
	ENDSELECT
ENDIF

;レズ中毒체크
;PLAYER절정체크


;------------------------------------------------------------------------
;TARGETによる부정判定
;------------------------------------------------------------------------
@REFUSE_CHECK()
#FUNCTION
SIF TFLAG:14
	RETURNF 1
SIF NEXTCOM == 999
	RETURNF 1
RETURNF 0

;------------------------------------------------------------------------
;구상等で行われた사정게이지の増加を変数にも影響させる
;------------------------------------------------------------------------
@CHECK_BASEPLAYER2
TCVAR:101 = MAX(TCVAR:101, BASE:절정 - TCVAR:100)
TCVAR:MASTER:101 = MAX(TCVAR:MASTER:101, BASE:MASTER:절정 - TCVAR:MASTER:100)
SIF ASSI
	TCVAR:ASSI:101 = MAX(TCVAR:ASSI:101, BASE:ASSI:절정 - TCVAR:ASSI:100)


;-------------------------------------------------
;레벨리미터
;-------------------------------------------------
@LV_LIMITTER
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
REDRAW 0

PRINTFORML 
PRINTFORML □ 레벨리미터 □---------------------------------------------------------
PRINTFORML 당신의 ＬＶ(통상은 {FLAG:255}ＬＶ)을 제한할 수 있습니다. 
PRINTFORML 다음 중에서 마음에 드는 것을 골라주세요（같은 것을 선택하면 해제합니다）
PRINTFORML 
PRINTFORM  [ 1]～[{FLAG:255, 2}] 
IF FLAG:247 && FLAG:247 <= FLAG:255
	PRINTFORML 항상ＬＶ{FLAG:247}
ELSE
	PRINTFORML 0～{FLAG:255}까지의 범위에서 입력했다ＬＶ그리고 고정한다
ENDIF
PRINTFORML  [91] \@ CONFIG("레벨리미터：－10ＬＶ") ? ○ # － \@ 상대의ＬＶ－10
PRINTFORML  [92] \@ CONFIG("레벨리미터：－５ＬＶ") ? ○ # － \@ 상대의ＬＶ－５
PRINTFORML  [93] \@ CONFIG("레벨리미터：상대に合わせる") ? ○ # － \@ 상대의ＬＶ에 맞춘다
PRINTFORML  [94] \@ CONFIG("레벨리미터：＋５ＬＶ") ? ○ # － \@ 상대의ＬＶ＋５
PRINTFORML  [95] \@ CONFIG("레벨리미터：＋10ＬＶ") ? ○ # － \@ 상대의ＬＶ＋10
IF CONFIG("레벨리미터")
	PRINTFORML  [99] 사용을 그만둔다
	PRINTFORML [100] 이것으로 좋다
ELSE
	CALL PRINTL_COLORTEXT, " [99] 사용을 그만둔다", DEF_COLOR("파랑")
	PRINTFORML [100] 돌아온다
ENDIF


CALL INPUT_SELECT, 101

SELECTCASE RESULT
CASE 100
	CALL SETFLAG, "레벨리미터"
	CALL SETFLAG, "사정게이지", MASTER
	REDRAW 1
	RETURN 0
CASE 99
	FLAG:247 = 0
CASE 91
	IF GETBIT(FLAG:247, 11) == 0
		FLAG:247 = 1p11
	ELSE
		FLAG:247 = 0
	ENDIF
CASE 92
	IF GETBIT(FLAG:247, 12) == 0
		FLAG:247 = 1p12
	ELSE
		FLAG:247 = 0
	ENDIF
CASE 93
	IF GETBIT(FLAG:247, 13) == 0
		FLAG:247 = 1p13
	ELSE
		FLAG:247 = 0
	ENDIF
CASE 94
	IF GETBIT(FLAG:247, 14) == 0
		FLAG:247 = 1p14
	ELSE
		FLAG:247 = 0
	ENDIF
CASE 95
	IF GETBIT(FLAG:247, 15) == 0
		FLAG:247 = 1p15
	ELSE
		FLAG:247 = 0
	ENDIF
CASE 1 TO FLAG:255
	FLAG:247 = RESULT
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART


@EVENT_NEXTTIME
#DIM MEMO_LINECOUNT
SIF TARGET <= 0
	RETURN 0
SIF EXP:조교횟수 == 0
	RETURN 0

MEMO_LINECOUNT = LINECOUNT
;실금경험３０이상で오줌싸개버릇がつく
CALL EVENT_MORASI

;이벤트は一つまで
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 0

;[정액에약함]を得るかの체크. ただし通常は정액중독を上昇させた時に得るのであくまで補助的
IF TALENT:정액에약함 == 0
	CALL CALC, "정액에약함", TARGET
	;소질を得た
	IF TALENT:정액에약함
		PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 정액을 몇번이나 받아들이는 사이에, 
		SELECTCASE TALENT_NAME(49, TALENT:정액에약함)
		CASE "정음에약함"
			PRINT 정액을 마시는 것
		CASE "질내사정에약함"
			PRINT 질내에 사정당하는 것
		CASE "장내사정에약함"
			PRINT 애널에 사정당하는 것
		CASE "유内사정에약함"
			PRINT 니플퍽 중에 사정당하는 것
		CASE "요도内사정에약함"
			PRINT 요도를 범해지면서사정되는 일
		ENDSELECT
		PRINTFORMW 으로 쾌락을 얻는 몸이 되어버린 것 같다…
		PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
		LOCALS = [%TALENT_NAME(49, TALENT:정액에약함)%]
		CALL PRINT_COLORTEXT, LOCALS, DEF_COLOR("황색")
		PRINTFORMW 을 얻었다. 
	ENDIF
;정액에약함箇所の変更判定
ELSE
	LOCAL = TALENT:정액에약함
	CALL CALC, "정액에약함部位の変化", TARGET
	IF TALENT:정액에약함 != LOCAL
		PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 정액을 몇번이고 받아들이는 사이에, 
		SELECTCASE TALENT_NAME(49, TALENT:정액에약함)
		CASE "정음에약함"
			PRINT 정액을 마시는 것
		CASE "질내사정에약함"
			PRINT 질내에 사정당하는 것
		CASE "장내사정에약함"
			PRINT 애널에 사정당하는 것
		CASE "유内사정에약함"
			PRINT 니플퍽 중에 사정당하는 것
		CASE "요도内사정에약함"
			PRINT 요도를 범해지면서사정되는 일
		ENDSELECT
		PRINTFORMW 으로 쾌락을 얻는 몸이 되어버린 것 같다…
		PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
		LOCALS = [%TALENT_NAME(49, LOCAL)%]
		CALL PRINT_COLORTEXT, LOCALS, DEF_COLOR("황색")
		PRINTFORM 을 잃고, 
		LOCALS = [%TALENT_NAME(49, TALENT:정액에약함)%]
		CALL PRINT_COLORTEXT, LOCALS, DEF_COLOR("황색")
		PRINTFORMW 을 얻었다. 
	ENDIF
ENDIF

@EVENT_MORASI
SIF TIME == 0 || TALENT:오줌싸개버릇 || EXP:실금경험 < 30 + CFLAG:34
	RETURN 0
;おねしょ
PRINTFORMW （으으･･･ 뭐야 이거？）
PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
IF TALENT:유치
	PRINTFORM 오줌을 싸고만 모양이다
ELSE
	PRINTFORM 실례를 해버린 모양이다
ENDIF
PRINTFORMW …
PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% [오줌싸개버릇]이 붙었다. 
TALENT:오줌싸개버릇 = 1

@SELL_MILK
LOCAL = TFLAG:35 * 10
LOCAL:1 = LOCAL * 10
IF LOCAL > 0
	PRINTFORML 착유기를 써서 %CALLNAME:TARGET%에게서 {LOCAL}cc의 모유를 짜내었다. 
	SIF TALENT:처녀 == 1
		TIMES LOCAL:1 , 1.50
	SIF TALENT:인기
		TIMES LOCAL:1 , 1.50
	SIF TALENT:거유
		TIMES LOCAL:1 , 1.50
	LOCAL = MULTIPLY(LOCAL:1, 100 + (RAND:11 + RAND:11) * 10)
	PRINTFORML %CALLNAME:TARGET%의 모유에는 %MONEY_C(LOCAL:1)%의 가격이 붙었다. 
	MONEY += LOCAL:1
	PRINTFORMW 소지금 %MONEY_C(LOCAL:1)% 증가
ENDIF


;조수の조교종료時特典（여우で미약入手など）
@AFTERTRA_BONUS_ASSI
IF ASSI
	IF TALENT:ASSI:슬라임 && ITEM:로션 < 99
		PRINTL 
		PRINTFORMW %CALLNAME:ASSI%에게서 로션을 받았습니다. 
		ITEM:로션 += 1
	ENDIF
	IF TALENT:ASSI:요호 && ITEM:미약 < 99
		PRINTL 
		PRINTFORMW %CALLNAME:ASSI%에게서 미약을 받았습니다. 
		ITEM:미약 += 1
	ENDIF
	IF BASE:ASSI:종족 == 3
		IF TALENT:ASSI:상위몽마 && ITEM:라벤더 < 99
			PRINTL 
			PRINTFORMW %CALLNAME:ASSI%로부터라벤더를 받았습니다.
			ITEM:라벤더 += 1
		ELSEIF ITEM:재스민 < 99
			PRINTL 
			PRINTFORMW %CALLNAME:ASSI%로부터재스민를 받았습니다.
			ITEM:재스민 += 1
		ENDIF
	ENDIF
ENDIF

@EVENT_GROWTH
;조교종료後それぞれ2/5の확률で체력・기력の최대치が上昇
;ただし増加する値はそれぞれ＋30が限度に. 

;절정＆사정回数と절정시킨횟수, 정액경험で체력기력増加
LOCAL = EXP_GAIN(2) + EXP_GAIN(3) + EXP_GAIN(6)*2 + EXP_GAIN(20)/3
LOCAL:100 = RAND:5
SIF LOCAL:100 >= 3 || LOCAL <= RAND:20
	RETURN 0

SELECTCASE LOCAL
CASE IS < 5
	LOCAL:1 = 1 + RAND:5
CASE IS < 20
	LOCAL:1 = 3 + RAND:10
CASE IS < 50
	LOCAL:1 = 5 + RAND:15
CASEELSE
	LOCAL:1 = 10 + RAND:20
ENDSELECT
SIF TALENT:마왕의조각
	LOCAL:1 += 2
SIF !COND("성장끝")
	LOCAL:1 += 3
LOCAL:1 = MIN(LOCAL:1, 30)

IF LOCAL:100 == 0 || LOCAL:100 == 1
	IF MAXBASE:체력 >= CSVBASE(NO:TARGET, 0, 0)*3
		LOCAL:1 = MAX(LOCAL:1/4, 1)
	ELSEIF MAXBASE:체력 >= CSVBASE(NO:TARGET, 0, 0)*2
		LOCAL:1 = MAX(LOCAL:1/2, 1)
	ENDIF
	MAXBASE:체력 += LOCAL:1
	PRINTFORML 격렬한 조교에 의해 %조사처리(CALLNAME:TARGET,"는")% 견디는 것의 쾌감을 깨달았다. 
	PRINTFORML %CALLNAME:TARGET%의 체력 최대치가 {LOCAL:1} 올라갔습니다. 
ENDIF
IF LOCAL:100 == 0 || LOCAL:100 == 2
	LOCAL:2 = MAX(LOCAL:1 + RAND:5 - RAND:5, 1)
	IF MAXBASE:기력 >= CSVBASE(NO:TARGET, 1, 0)*3
		LOCAL:2 = MAX(LOCAL:2/4, 1)
	ELSEIF MAXBASE:기력 >= CSVBASE(NO:TARGET, 1, 0)*2
		LOCAL:2 = MAX(LOCAL:2/2, 1)
	ENDIF
	MAXBASE:기력 += LOCAL:2
	PRINTFORML 격렬한 조교에 의해 %CALLNAME:TARGET%의 의지가 강해졌다. 
	PRINTFORML %CALLNAME:TARGET%의 기력 최대치가 {LOCAL:2} 올라갔습니다. 
ENDIF
WAIT

@RANKING_MUMA
FLAG:8 = 0

REPEAT CHARANUM
	SIF COUNT <= 1
		CONTINUE

	TARGET = COUNT
	PRINTFORML ～%CALLNAME:TARGET%～

	;状況は最高
	PALAM:Ｖ윤 = PALAMLV:5
	PALAM:Ａ윤 = PALAMLV:5
	PALAM:욕정 = PALAMLV:5

	;정상위
	SELECTCOM = 60
	CALL SAMEN_CHECK_PLAYER
	PRINTFORML %TRAINNAME:SELECTCOM%의 기술 = {B}
	;기승위
	SELECTCOM = 65
	CALL SAMEN_CHECK_PLAYER
	PRINTFORML %TRAINNAME:SELECTCOM%의 기술 = {B}
	;애널섹스
	SELECTCOM = 71
	CALL SAMEN_CHECK_PLAYER
	PRINTFORML %TRAINNAME:SELECTCOM%의 기술 = {B}
	;애널기승위
	SELECTCOM = 75
	CALL SAMEN_CHECK_PLAYER
	PRINTFORML %TRAINNAME:SELECTCOM%의 기술 = {B}
	;수음
	SELECTCOM = 90
	CALL SAMEN_CHECK_PLAYER
	PRINTFORML %TRAINNAME:SELECTCOM%의 기술 = {B}
	;펠라치오
	SELECTCOM = 91
	CALL SAMEN_CHECK_PLAYER
	PRINTFORML %TRAINNAME:SELECTCOM%의 기술 = {B}
	;파이즈리
	SELECTCOM = 92
	CALL SAMEN_CHECK_PLAYER
	PRINTFORML %TRAINNAME:SELECTCOM%의 기술 = {B}
	;풋잡
	SELECTCOM = 95
	CALL SAMEN_CHECK_PLAYER
	PRINTFORML %TRAINNAME:SELECTCOM%의 기술 = {B}
	;애널봉사
	SELECTCOM = 96
	CALL SAMEN_CHECK_PLAYER
	PRINTFORML %TRAINNAME:SELECTCOM%의 기술 = {B}
	;역애널
	SELECTCOM = 126
	CALL SAMEN_CHECK_PLAYER
	PRINTFORML %TRAINNAME:SELECTCOM%의 기술 = {B}

	PALAM:Ｖ윤 = 0
	PALAM:욕정 = 0

	WAIT
REND

BASE:PLAYER:절정 = 0
FLAG:8 = 1

;------------------------------------------------------------------------
;ARGは記憶を失う
;------------------------------------------------------------------------
@MEMORY_LOST, ARG
FOR LOCAL, 0, 999
	;名前(딸), 名前の長さ, 몽마のランク, 이상경험, 경험치寄与, 임신, 家系や종족などは忘れない
	SIF (LOCAL >= 4 && LOCAL <= 16) || LOCAL == 33 || LOCAL == 209 || LOCAL == 468 || LOCAL == 469
		CONTINUE

	CFLAG:ARG:LOCAL = 0
NEXT
FOR LOCAL, 0, 11
	MARK:ARG:LOCAL = 0
NEXT
FOR LOCAL, 0, 20
	JUEL:ARG:LOCAL = 0
NEXT

ABL:ARG:순종 = MIN(ABL:ARG:순종, 1)

TALENT:ARG:연모 = 0

;------------------------------------------------------------------------
;ARG:1に몽마にされたARGが, ARG:1に受ける影響を체크
;------------------------------------------------------------------------
@CHANGE_SUCCUBUS, ARG, ARG:1
SIF TALENT:ARG:몽마 || TALENT:ARG:상위몽마
	RETURN 0

TALENT:ARG:몽마 = 1
CALL SETFLAG, "몽마化플래그", ARG
CALL EXP50_CHECK, ARG

;「変化しない」場合は戻る
SIF CONFIG("몽마化時の特徴変化") == 0
	RETURN 0

;身体特徴を変える（逆の特徴を持つ場合は打ち消し）
CALL SELECT_TALENT, ARG, ARG:1, "아픔에약함"
CALL SELECT_TALENT, ARG, ARG:1, "회복빠름"
CALL SELECT_TALENT, ARG, ARG:1, "일광욕"
CALL SELECT_TALENT, ARG, ARG:1, "흡혈귀"
CALL SELECT_TALENT, ARG, ARG:1, "濡れやすさ"
CALL SELECT_TALENT, ARG, ARG:1, "가슴 크기"

;몽마でない人々は外見などにも変化が(마법사や드라군は生き方なので変化しない)
IF CSVTALENT(NO:ARG, 200, 0) + CSVTALENT(NO:ARG, 201, 0) == 0
	CALL SELECT_TALENT, ARG, ARG:1, "신장"
	CALL SELECT_TALENT, ARG, ARG:1, "고양이혀"
	CALL SELECT_TALENT, ARG, ARG:1, "요호"
	CALL SELECT_TALENT, ARG, ARG:1, "동물귀"
	CALL SELECT_TALENT, ARG, ARG:1, "꼬리"
	CALL SELECT_TALENT, ARG, ARG:1, "용의혈통"
	CALL SELECT_TALENT, ARG, ARG:1, "뿔"
	CALL SELECT_TALENT, ARG, ARG:1, "날개"
	CALL SELECT_TALENT, ARG, ARG:1, "장사"
	CALL SELECT_TALENT, ARG, ARG:1, "요정"
	CALL SELECT_TALENT, ARG, ARG:1, "라미아"
	CALL SELECT_TALENT, ARG, ARG:1, "머메이드"
	CALL SELECT_TALENT, ARG, ARG:1, "하피"
	CALL SELECT_TALENT, ARG, ARG:1, "구지배자의자취"
	CALL SELECT_TALENT, ARG, ARG:1, "슬라임"
	CALL SELECT_TALENT, ARG, ARG:1, "팔많음"

	PRINTFORMW %조사처리(CALLNAME:ARG,"는")% %조사처리(CALLNAME:(ARG:1),"과")% 똑같은 모습이 되었다…
	;眷族となる
	TALENT:ARG:권속 = BASE:(ARG:1):식별번호
	BASE:ARG:권속 = BASE:(ARG:1):종족
	SETCOLOR DEF_COLOR("하트핑크")
	PRINTFORMW %CALLNAME:ARG%하%CALLNAME:(ARG:1)%의 [권속]이 되었습니다
	RESETCOLOR
ENDIF

;性格を変えないならここまで
SIF CONFIG("몽마化時の特徴変化：全部") == 0
	RETURN 0

;それぞれ, 33%程で変化する
CALL SELECT_TALENT, ARG, ARG:1, "気の強さ"
CALL SELECT_TALENT, ARG, ARG:1, "솔직さ"
CALL SELECT_TALENT, ARG, ARG:1, "プライドの高さ"
CALL SELECT_TALENT, ARG, ARG:1, "호기심"
CALL SELECT_TALENT, ARG, ARG:1, "양기さ"
CALL SELECT_TALENT, ARG, ARG:1, "정조관념"
CALL SELECT_TALENT, ARG, ARG:1, "억압"
CALL SELECT_TALENT, ARG, ARG:1, "부끄럼쟁이"
CALL SELECT_TALENT, ARG, ARG:1, "惚れっぽさ"
CALL SELECT_TALENT, ARG, ARG:1, "쾌감에솔직"
CALL SELECT_TALENT, ARG, ARG:1, "바이"
CALL SELECT_TALENT, ARG, ARG:1, "자위しやすさ"
CALL SELECT_TALENT, ARG, ARG:1, "헌신"
CALL SELECT_TALENT, ARG, ARG:1, "중독되기쉬움"
CALL SELECT_TALENT, ARG, ARG:1, "호색"
CALL SELECT_TALENT, ARG, ARG:1, "도착적"
CALL SELECT_TALENT, ARG, ARG:1, "소악마"


@SELECT_TALENT, ARG, ARG:1, ARGS
VARSET LOCAL
;소질:%ARGS%について, ARGのものをARG:1のもので優性的に変化させる
;相対変化
SELECTCASE ARGS
CASE "아픔에약함"
	LOCAL = 40
	LOCAL:1 = 41
CASE "신장"
	LOCAL = 99
	LOCAL:1 = 100
CASE "회복빠름"
	LOCAL = 111
	LOCAL:1 = 112
CASE "일광욕"
	LOCAL = 115
	LOCAL:1 = 116
;絶対変化(外見)
CASE "고양이혀"
	LOCAL = 54
CASE "요호"
	LOCAL = 90
CASE "흡혈귀"
	LOCAL = 114
CASE "동물귀"
	LOCAL = 124
CASE "꼬리"
	LOCAL = 128
CASE "용의혈통"
	LOCAL = 129
CASE "뿔"
	LOCAL = 160
CASE "날개"
	LOCAL = 161
CASE "장사"
	LOCAL = 162
CASE "요정"
	LOCAL = 150
CASE "라미아"
	LOCAL = 152
CASE "人魚", "머메이드"
	LOCAL = 153
CASE "하피"
	LOCAL = 155
CASE "구지배자의자취"
	LOCAL = 156
CASE "슬라임"
	LOCAL = 158
CASE "팔많음"
	LOCAL = 159
;この二つは남자ではダメ
CASE "濡れやすさ"
	IF TALENT:ARG:남자 == 0
		LOCAL = 42
		LOCAL:1 = 43
	ENDIF
CASE "가슴 크기"
	IF TALENT:ARG:남자 == 0
		LOCAL = 109
		LOCAL:1 = 110
	ENDIF
CASEELSE
	;性格については, それぞれ33%程度の変化率とする
	SIF RAND:3
		RETURN 0
	SELECTCASE ARGS
	CASE "気の強さ"
		LOCAL = 10
		LOCAL:1 = 12
	CASE "솔직さ"
		LOCAL = 11
		LOCAL:1 = 13
	CASE "プライドの高さ"
		LOCAL = 15
		LOCAL:1 = 17
	CASE "호기심"
		LOCAL = 23
		LOCAL:1 = 24
	CASE "양기さ"
		LOCAL = 25
		LOCAL:1 = 26
	CASE "정조관념"
		LOCAL = 30
		LOCAL:1 = 31
	CASE "억압", "해방"
		LOCAL = 32
		LOCAL:1 = 33
	CASE "부끄럼쟁이"
		LOCAL = 35
		LOCAL:1 = 36
	CASE "惚れっぽさ"
		LOCAL = 38
		LOCAL:1 = 27
	CASE "쾌감에솔직"
		LOCAL = 70
		LOCAL:1 = 71
	CASE "바이"
		LOCAL = 81
		LOCAL:1 = 82
	;絶対変化(性格)
	CASE "자위しやすさ"
		LOCAL = 60
	CASE "헌신"
		LOCAL = 63
	CASE "중독되기쉬움"
		LOCAL = 72
	CASE "호색"
		LOCAL = 75
	CASE "도착적"
		LOCAL = 80
	CASE "소악마"
		LOCAL = 87
	CASEELSE
		RETURN 0
	ENDSELECT
ENDSELECT

;比較変化
IF LOCAL:1 > 0
	TALENT:ARG:LOCAL = LIMIT(TALENT:ARG:LOCAL + TALENT:(ARG:1):LOCAL - TALENT:(ARG:1):(LOCAL:1), 0, 1)
	TALENT:ARG:(LOCAL:1) = LIMIT(TALENT:ARG:(LOCAL:1) + TALENT:(ARG:1):(LOCAL:1) - TALENT:(ARG:1):LOCAL, 0, 1)
;絶対変化
ELSEIF TALENT:ARG:LOCAL == 0
	TALENT:ARG:LOCAL = TALENT:(ARG:1):LOCAL
ENDIF
