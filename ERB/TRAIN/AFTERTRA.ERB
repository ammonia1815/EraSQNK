@EVENTEND
#PRI
REDRAW 1
;二人で넘어뜨리기の時には, きちんと二回ここに来るのに注意

;PLAYERをMASTERに直す
SIF PLAYER != MASTER
	CALL SETFLAG, "조교자교대", 1
;消滅予定キャラはどかす(フラグや分岐の問題で居た方が便利なのかもしれないので残してみる)
SIF CEVENT("消滅予定")
	CALL SETFLAG, "대상교대"

;연모を外した時に戻し忘れているなら, ここで復帰
CALL SETFLAG, "조수상대の연모外し종료"

CALL CHECK_SATISFACTION, TARGET
CALL CHECK_SATISFACTION, ASSI

CALL SETFLAG, "조교종료", TARGET
CALL SETFLAG, "조교종료", MASTER
SIF ASSI
	CALL SETFLAG, "조교종료", ASSI


@EVENTEND
#LATER
;조교中플래그OFF
FLAG:99 = 0
IF FLAG:22 != 2
	;상대が상위몽마の場合には공략각인（굴복각인取得に요정する파라미터量を減らす）を得る
	;만족＋공략각인が一定値이하
	IF COND("성욕대만족") && MARK:공략각인 < 4
		IF TALENT:상위몽마 && MARK:굴복각인 < 3
			DRAWLINE
			PRINTFORMW ☆★☆이벤트 달성・굴복각인 습득의 패널티가 경감되었습니다☆★☆
			DRAWLINE
		ENDIF
		MARK:공략각인 = 4
	ELSEIF COND("성욕만족") && MARK:공략각인 < 2
		IF TALENT:상위몽마 && MARK:굴복각인 < 3
			DRAWLINE
			PRINTFORMW ☆★☆이벤트 달성・굴복각인 습득의 패널티가 경감되었습니다☆★☆
			DRAWLINE
		ENDIF
		MARK:공략각인 = 2
	ENDIF

	PRINTL 
	PRINTL 조교를 종료했습니다.

	;NTR이벤트
	CALL EVENT_DRAGON_NTR, "寝取られ判定"

	;조교후 행위의 체크
	CALL SELF_CHECK

	;짜낸 모유의 매각
	CALL SELL_MILK

	;조교시 촬영한 비디오 매각
	CALL SELL_VIDEO
	IF ASSI
		SWAP TARGET, ASSI
		CALL SELL_VIDEO
		SWAP TARGET, ASSI
	ENDIF

	;조수의 조교종료시 특전（여우가 미약주는등）
	CALL AFTERTRA_BONUS_ASSI

	;소모품 자동구입
	CALL CALC, "소모품の自動購入"
ENDIF

CALL EVENT_AFTER_TRAIN

BEGIN ABLUP


@EVENT_AFTER_TRAIN
;기력・체력의 상승  체크
CALL EVENT_GROWTH

;어떤 구슬을 얻었는지
CALL JUEL_CHECK

;사정플래그, 처녀상실플래그などをリセット
VARSET TFLAG, 0, 0, 101

;조교경험을 늘림
EXP:조교횟수 += 1

;조교한 날짜를 기록
CFLAG:97 = DAY

@EVENT_AFTER_TRAIN_COS, ARG
#DIM LCOUNT
;더러움을 초기화
CALL RESET_STAIN_EX, "全部", ARG

SELECTCASE CONFIG("조교종료時の복장")
;종료시본인의 옷으로 갈아입는다면
CASE 0
	CALL COORDINATE_MUMA, ARG
;종료시마음에드는옷으로 갈아입는다면
CASE 2
	;ここでは밤の衣装にのみ着替えるのに注意. 낮の衣装については@EVENTSHOPの早아침이벤트後に改めて着替える
	IF GETBIT(IS_FAVORITE_DRESS(ARG), 6)
		CALL SET_FAVORITE_DRESS, ARG, 6
	ELSEIF GETBIT(IS_FAVORITE_DRESS(ARG), 0)
		CALL SET_FAVORITE_DRESS, ARG, 0
	ENDIF
;종료시마음에드는옷중 어느것으로 갈아입는다면
CASE 3
	IF IS_FAVORITE_DRESS(ARG) == 0
		;팬티の着用禁止
		SIF COND("身嗜み：노팬티", ARG)
			CALL SET_PANTIES, ARG
		RETURN 0
	ENDIF

	;가끔은 못 입을지도 모르잖아! 
	FOR LCOUNT, 0, 100
		LOCAL = RAND:10
		SIF GETBIT(IS_FAVORITE_DRESS(ARG), LOCAL) == 0
			CONTINUE

		CALL SET_FAVORITE_DRESS, ARG, LOCAL
		BREAK
	NEXT
;종료시に薄着なら옷を着てもらう
CASE 4
	SIF EQUIP:ARG:웃옷 + EQUIP:ARG:보디슈츠 == 0
		CALL COORDINATE_MUMA, ARG
ENDSELECT

;팬티 착용 금지
SIF COND("身嗜み：노팬티", ARG)
	CALL SET_PANTIES, ARG

@JUEL_CHECK
#DIM LCOUNT
#DIM BONUS
#DIM MEMO_LV
#DIM MEMO_TEC
#DIM MEMO_LINECOUNT

DRAWLINE
VARSET LOCAL
BONUS = 0
;성욕등 만족보너스
IF COND("성욕대만족")
	PRINT 성욕대만족
	BONUS += MAX(ABL:욕망 * 4, 10)
ELSEIF COND("성욕만족")
	PRINT 성욕만족
	BONUS += MAX(ABL:욕망 * 2, 5)
ENDIF
IF COND("정액욕대만족")
	SIF BONUS
		PRINT ＆
	PRINT 정액욕대만족
	BONUS += MAX(ABL:정액중독 * 4, 10)
ELSEIF COND("정액욕만족")
	SIF BONUS
		PRINT ＆
	PRINT 정액욕만족
	BONUS += MAX(ABL:정액중독 * 2, 5)
ENDIF
IF EX:연속절정 >= 3
	SIF BONUS
		PRINT ＆
	PRINTFORM 연속절정{EX:연속절정}COMBO
	BONUS += LIMIT(EX:연속절정 - 2, 1, 100)
ENDIF

IF BONUS
	EXP:경험치 += MULTIPLY(EXP_GAIN(5), BONUS)
	PRINTL 보너스! 
	PRINTFORML 획득 구슬과 경험치가 {BONUS}％ 증가합니다
	PRINTL 
ENDIF
IF EXP_GAIN(58)
	SETCOLOR DEF_COLOR("노랑")
	PRINTL ※미약 사용으로 인해, 획득 구슬의 양이 감소합니다※
	RESETCOLOR
	PRINTL 
ENDIF

VARSET J
FOR LCOUNT, 0, 16
	SELECTCASE LCOUNT
	CASE 3, 12, 13
		CONTINUE
	CASE 11
		GOTJUEL:11 = GET_JUEL(11)
		J:11 = JUEL:11 + GOTJUEL:11
	CASEELSE
		GOTJUEL:LCOUNT = MULTIPLY(GET_JUEL(LCOUNT), 100 + BONUS)
		J:LCOUNT = JUEL:LCOUNT + GOTJUEL:LCOUNT
	ENDSELECT
NEXT

;반감의 구슬の打ち消し체크
CALL DENIAL_CHECK

PRINTL 조교 결과：
FOR LCOUNT, 0, 13
	;파라미터を表示する順番を並べ替える・ＭＢＣＶＡ欲恭屈恥痛恐習反不鬱・Ｖ윤Ａ윤は無し
	SELECTCASE LCOUNT
	CASE  0
		LOCAL = 0
	CASE  1
		LOCAL = 1
	CASE  2
		LOCAL = 2
	CASE  3
		LOCAL = 14
	CASE  4
		LOCAL = 15
	CASE  5
		LOCAL = 4
	CASE  6
		LOCAL = 5
	CASE  7
		LOCAL = 6
	CASE  8
		LOCAL = 7
	CASE  9
		LOCAL = 8
	CASE  10
		LOCAL = 9
	CASE  11
		LOCAL = 10
	CASE  12
		LOCAL = 11
	ENDSELECT

	PRINTFORM %PALAMNAME:LOCAL%의 구슬：({JUEL:LOCAL, 10} \@ GOTJUEL:LOCAL >= 0 ? + # - \@ 
	SIF GOTJUEL:LOCAL
		SETCOLOR DEF_COLOR("황색")
	PRINTFORM {ABS(GOTJUEL:LOCAL), 10} 
		RESETCOLOR
	PRINTFORML )
	JUEL:LOCAL += GOTJUEL:LOCAL
NEXT

PRINTW 이상의 구슬을 얻었습니다.

PRINTL 
MEMO_LINECOUNT = LINECOUNT

FOR LCOUNT, 0, 100
	SELECTCASE LCOUNT
	CASE 4 TO 7
		CONTINUE
	ENDSELECT

	SIF EXP_GAIN(LCOUNT)
		PRINTFORML %TEXT_RJ(EXPNAME:LCOUNT, 16)%＋{EXP_GAIN(LCOUNT), 3}
NEXT

IF LINECOUNT > MEMO_LINECOUNT
	PRINTFORMW 이상의 경험\@ EXP_GAIN(5) ? 과, 경험치를 {EXP_GAIN(5)}Ｐ # 을 \@얻었습니다
ELSEIF EXP_GAIN(5)
	PRINTFORMW 경험치를{EXP_GAIN(5)}Ｐ얻었습니다.
ELSE
	CLEARLINE 1
ENDIF

;정액의 주고받음
PRINTL 

IF EX:절대착정량 && EX:주인에대한절대사정량
	PRINTFORMW %타겟은()% 이번조교에서 {EX:절대착정량}ml의 정액를 그 몸으로 받아 들여,{EX:주인에대한절대사정량}ml의 정액를 %CALLNAME:MASTER%에 따랐습니다.
ELSEIF EX:절대착정량
	PRINTFORMW %타겟은()% 이번조교에서 {EX:절대착정량}ml의 정액를 그 몸으로 받아 들였습니다.
ELSEIF EX:주인에대한절대사정량
	PRINTFORMW %타겟은()% 이번조교에서 {EX:주인에대한절대사정량}ml의 정액를 %CALLNAME:MASTER%에 따랐습니다.
ELSE
	CLEARLINE 1
ENDIF

;욕구불만의 해소
LOCAL = 0
SELECTCASE BASE:성욕
CASE IS >= MAXBASE:성욕 * 2
	LOCAL += 15
CASE IS >= MAXBASE:성욕
	LOCAL += 10
CASE IS >= 1
	LOCAL += 3
CASEELSE
	LOCAL += 1
ENDSELECT

SELECTCASE BASE:정액욕
CASE IS >= MAXBASE:정액욕 * 2
	LOCAL += 10
CASE IS >= MAXBASE:정액욕
	LOCAL += 5
CASE IS >= 1
	LOCAL += 1
ENDSELECT

;욕망과 정액욕구를 동시에 만족시키면, 욕구불만이 큰폭으로 해소된다
SIF COND("성욕만족") && COND("정액욕만족")
	LOCAL += 10

;욕구불만의 해소
IF BASE:욕구불만 && COND("성욕만족") && COND("정액욕만족")
	PRINTL 
	MEMO_LINECOUNT = LINECOUNT

	SETCOLOR DEF_COLOR("황색")
	IF COND("성욕대만족") && COND("정액욕대만족")
		PRINTFORMW %CALLNAME:TARGET%의 욕구불만이 완전히 해소되었습니다! 
	ELSEIF COND("성욕만족") && COND("정액욕만족")
		PRINTFORMW %CALLNAME:TARGET%의 욕구불만이 큰 폭으로 해소되었습니다. 
	ENDIF
	RESETCOLOR

	SIF LINECOUNT == MEMO_LINECOUNT
		CLEARLINE 1
ENDIF

CALL CALC, "욕구불만解消", TARGET, LOCAL

;절정보너스는 여기서 리셋트
JUEL:20 = 0
JUEL:21 = 0
JUEL:22 = 0
JUEL:23 = 0
JUEL:24 = 0

;HARDだと自動で幾つかの레벨が上がる
SIF CONFIG("ＨＡＲＤ") == 0
	RETURN 0

;上昇前の기교とＬＶを記録
MEMO_TEC = ABL:기교
MEMO_LV = ABL:ＬＶ

;기교の自動레벨アップ! 
FOR LCOUNT, 0, 10
	CALL ISABLUP, "기교"
	IF RESULT && ABL:기교 < NUM("최대레벨", 2)
		JUEL:습득 -= A
		ABL:기교 += 1
	ELSE
		BREAK
	ENDIF
NEXT

;ＬＶの自動레벨アップ! 
FOR LCOUNT, 0, NUM("최대레벨", 90)
	CALL ISABLUP, "ＬＶ"
	IF RESULT && ABL:ＬＶ < NUM("최대레벨", 90)
		EXP:경험치 -= A
		ABL:ＬＶ += 1
	ELSE
		BREAK
	ENDIF
NEXT

IF ABL:기교 > MEMO_TEC || ABL:ＬＶ > MEMO_LV
	DRAWLINE
	PRINTFORMW 능력의 자동 레벨업이 발생했습니다…
	SIF ABL:기교 > MEMO_TEC
		PRINTFORMW %CALLNAME:TARGET%의 기교가{ABL:기교 - MEMO_TEC}LV상승해{ABL:기교}LV이 되었습니다! 
	IF ABL:ＬＶ > MEMO_LV
		PRINTFORMW %CALLNAME:TARGET%의 ＬＶ이{ABL:ＬＶ - MEMO_LV}LV상승해{ABL:ＬＶ}LV이 되었습니다! 
		;ＬＶが10이상になると상위몽마화の가능性あり. LV14だと確実に상위몽마になる
		IF ABL:ＬＶ >= 10 && ABL:ＬＶ + ABL:기교/2 >= 10 + RAND:5 && TALENT:몽마 && TALENT:상위몽마 == 0
			PRINTFORMW %타겟은()% 상위 몽마가 되었습니다! 
			TALENT:몽마 = 0
			TALENT:상위몽마 = 1
		ENDIF
		SIF ABL:ＬＶ/5 > MAX(MEMO_LV/5, 1)
			CALL GET_TALENT, ABL:ＬＶ/5 - MEMO_LV/5
	ENDIF
ENDIF

@DENIAL_CHECK
#DIM LCOUNT
SIF J:11 == 0
	RETURN 0

PRINTL 반감의 구슬과 다른 구슬의 상쇄가 발생하였습니다.

VARSET LOCAL

FOR LCOUNT, 0, 100
	;욕정・순종・굴복의 구슬が無いなら, 치정・고통・공포의 구슬を減らす
	IF J:4 + J:5 + J:6
		IF J:4 && RANDIF(1, J:5, J:6)
			LOCAL = 4
		ELSEIF J:5 && RANDIF(1, J:6)
			LOCAL = 5
		ELSE
			LOCAL = 6
		ENDIF
	ELSEIF J:8 + J:9 + J:10
		IF J:8 && RANDIF(1, J:9, J:10)
			LOCAL = 8
		ELSEIF J:9 && RANDIF(1, J:10)
			LOCAL = 9
		ELSE
			LOCAL = 10
		ENDIF
	ELSE
		BREAK
	ENDIF

	LOCAL:1 = LIMIT(J:11 / 2, 1, J:LOCAL)

	LOCAL:LOCAL += LOCAL:1
	J:LOCAL -= LOCAL:1
	GOTJUEL:LOCAL -= LOCAL:1
	J:11 -= LOCAL:1
	GOTJUEL:11 -= LOCAL:1

	SIF J:11 <= 0
		BREAK
NEXT
;감소량 표시
FOR LCOUNT, 4, 11
	SIF LCOUNT == 7 || LOCAL:LCOUNT == 0
		CONTINUE
	PRINTFORML %PALAMNAME:LCOUNT%의 구슬×{LOCAL:LCOUNT}개 감소
NEXT

PRINTL 

