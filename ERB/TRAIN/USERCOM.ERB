@SHOW_USERCOM
#DIM LCOUNT
#DIM MEMO_LINECOUNT
;存在している조교のみを数えていって몇번째か
#DIM NUM_TRAIN
#DIM NEXT_LINE
#DIM NUM_COLUMN
#DIMS COM_NAMES
#DIMS COM_NAMES_HIDDEN

REDRAW 0
;主導権が無い플래그を一時的に消去
CLEARBIT FLAG:99, 11

;갈아입히기部分の高速化플래그消去
TCVAR:26 = 0

IF FLAG:238 || CONFIG("조교컨피그모드")
	FLAG:238 = 0

	NUM_TRAIN = -1

	NEXT_LINE = 0
	NUM_COLUMN = 3 + (CONFIG("画面표시：조교커맨드４列") > 0)

	;直接선택できる커맨드は499番までだが, 現状は320までで十分すぎる
	FOR LCOUNT, 0, 320
		SIF STRLENS(TRAINNAME:LCOUNT) == 0
			CONTINUE

		NUM_TRAIN += 1

		IF CONFIG("조교컨피그모드：全커맨드표시") == 0
			TRYCALLFORM COM_ABLE{LCOUNT}
			SIF RESULT == 0
				CONTINUE
		ENDIF

		RESETCOLOR
		SIF CONFIG("画面표시：未実行커맨드の色") && FIRSTCOM(LCOUNT) == 0
			SETCOLOR DEF_COLOR("明회색")

		IF CONFIG("조교컨피그모드") == 0
			;커맨드표시필터
			SELECTCASE DITEMTYPE:0:LCOUNT
			;비표시설정
			CASE 1
				SIF CONFIG("비표시커맨드の표시") == 0
					CONTINUE
				SETCOLOR DEF_COLOR("파랑")
			;禁止설정
			CASE 2
				CONTINUE
			ENDSELECT
		ELSE
		 	SELECTCASE TRAINNAME:LCOUNT
			CASE "～예비～"
				CONTINUE
			ENDSELECT
		ENDIF

		IF CONFIG("ＨＡＲＤ") && TEQUIP:넘어뜨리기
			SELECTCASE LCOUNT
			CASE 178, 280, 281
				PRINTFORMC %TRAIN_NAME_SELECT(LCOUNT)%[{NUM_TRAIN, 3}]
			CASEELSE
				SETCOLOR DEF_COLOR("회색")
				PRINTFORMC %TRAIN_NAME_SELECT(LCOUNT)%[XXX]
				RESETCOLOR
			ENDSELECT
		ELSE

			IF CONFIG("조교컨피그모드") == 0
				;커맨드名が通常と違う場合には色を変える(넘어뜨리기状態での基本커맨드は色を変えない)
				SIF TRAIN_NAME_SELECT(LCOUNT) != TRAIN_NAME_STANDARD(LCOUNT)
					SETCOLOR DEF_COLOR("하트핑크")

				;플레이を終わらせられる時には色を変える
				SELECTCASE LCOUNT
				CASE 31, 201
					SIF TEQUIP:바이브
						SETCOLOR DEF_COLOR("황색")
				CASE 33, 202
					SIF TEQUIP:애널바이브
						SETCOLOR DEF_COLOR("황색")
				CASE 34, 203
					SIF TEQUIP:클리캡
						SETCOLOR DEF_COLOR("황색")
				CASE 35, 204
					SIF TEQUIP:니플캡
						SETCOLOR DEF_COLOR("황색")
				CASE 36, 205
					SIF TEQUIP:착유기
						SETCOLOR DEF_COLOR("황색")
				CASE 38
					SIF TEQUIP:샤워
						SETCOLOR DEF_COLOR("황색")
				CASE 39
					SIF TEQUIP:로터삽입
						SETCOLOR DEF_COLOR("황색")
				CASE 99
					SIF TEQUIP:식스나인
						SETCOLOR DEF_COLOR("황색")
				CASE 133
					SIF TEQUIP:아이마스크
						SETCOLOR DEF_COLOR("황색")
				CASE 134
					SIF TEQUIP:구속
						SETCOLOR DEF_COLOR("황색")
				CASE 135
					SIF TEQUIP:재갈
						SETCOLOR DEF_COLOR("황색")
				CASE 136
					SIF COND("애널栓")
						SETCOLOR DEF_COLOR("황색")
				CASE 137
					SIF TEQUIP:확장벌룬
						SETCOLOR DEF_COLOR("황색")
				CASE 149
					SIF TEQUIP:카테터
						SETCOLOR DEF_COLOR("황색")
				CASE 170
					SIF TEQUIP:비디오카메라
						SETCOLOR DEF_COLOR("황색")
				CASE 171
					SIF TEQUIP:야외플레이
						SETCOLOR DEF_COLOR("황색")
				CASE 172
					SIF TEQUIP:목욕탕플레이
						SETCOLOR DEF_COLOR("황색")
				CASE 173
					SIF TEQUIP:새댁플레이
						SETCOLOR DEF_COLOR("황색")
				CASE 174
					SIF TEQUIP:관내노출플레이
						SETCOLOR DEF_COLOR("황색")
				CASE 179
					SIF TEQUIP:수치플레이
						SETCOLOR DEF_COLOR("황색")
				CASE 182
					SIF TEQUIP:PLAYER:넘어뜨리기
						SETCOLOR DEF_COLOR("황색")
				CASE 200
					SIF TEQUIP:촉수
						SETCOLOR DEF_COLOR("황색")
				CASE 208
					SIF TEQUIP:촉수구욕
						SETCOLOR DEF_COLOR("황색")
				CASE 209
					SIF TEQUIP:오나홀
						SETCOLOR DEF_COLOR("황색")
				CASE 301
					SIF TEQUIP:손대중
						SETCOLOR DEF_COLOR("황색")
				CASE 302
					SIF TEQUIP:연주중
						SETCOLOR DEF_COLOR("황색")
				CASE 306
					SIF STATE("하드스킨", PLAYER)
						SETCOLOR DEF_COLOR("황색")
				ENDSELECT
			ELSE
				SELECTCASE DITEMTYPE:0:LCOUNT
				;通常状態
				CASE 0
					SELECTCASE LCOUNT
					CASE 0 TO 9, 60, 61, 65, 71, 75, 80 TO 95, 98, 99, 230 TO 289
						SETCOLOR DEF_COLOR("暗황색")
					CASEELSE
						RESETCOLOR
					ENDSELECT
				;비표시설정
				CASE 1
					SIF CONFIG("조교컨피그모드：비표시커맨드を숨김")
						CONTINUE
					SETCOLOR DEF_COLOR("파랑")
				;禁止설정
				CASE 2
					SIF CONFIG("조교컨피그모드：금지커맨드を숨김")
						CONTINUE
					SETCOLOR DEF_COLOR("초록")
				ENDSELECT
			ENDIF

			IF GETCOLOR() != DEF_COLOR("황색") && GETCOLOR() != DEF_COLOR("하트핑크")
				;표시するかどうか
				SELECTCASE LCOUNT
				CASE 0 TO 29
					SIF CONFIG("애무系비표시") == 0
						CONTINUE
				CASE 30 TO 59
					SIF CONFIG("도구系비표시") == 0
						CONTINUE
				CASE 60 TO 69
					SIF CONFIG("Ｖ섹스系비표시") == 0
						CONTINUE
				CASE 70 TO 79
					SIF CONFIG("Ａ섹스系비표시") == 0
						CONTINUE
				CASE 80 TO 89
					SIF CONFIG("특수섹스系비표시") == 0
						CONTINUE
				CASE 90 TO 119
					SIF CONFIG("봉사系비표시") == 0
						CONTINUE
				CASE 120 TO 129
					SIF CONFIG("조교자に삽입系비표시") == 0
						CONTINUE
				CASE 130 TO 159
					SIF CONFIG("ＳＭ・하드코어系비표시") == 0
						CONTINUE
				CASE 160 TO 169
					SIF CONFIG("조수系비표시") == 0
						CONTINUE
				CASE 170 TO 199
					SIF CONFIG("특수系비표시") == 0
						CONTINUE
				CASE 200 TO 229
					SIF CONFIG("촉수系비표시") == 0
						CONTINUE
				CASE 230 TO 259
					SIF CONFIG("아르카나系비표시") == 0
						CONTINUE
				CASE 260 TO 279
					SIF CONFIG("소비아이템系비표시") == 0
						CONTINUE
				CASEELSE
					SIF CONFIG("その他비표시") == 0
						CONTINUE
				ENDSELECT
			ENDIF

			PRINTFORMC %TRAIN_NAME_SELECT(LCOUNT)%[{NUM_TRAIN, 3}]
		ENDIF

		NEXT_LINE += 1

		SIF NEXT_LINE % NUM_COLUMN == 0
			PRINTL 
	NEXT
	SIF NEXT_LINE % NUM_COLUMN
		PRINTL 
ENDIF
RESETCOLOR

CALL SETFLAG, "조수상대の연모外し종료"

;-------------------------------------------------
;특수な커맨드
;-------------------------------------------------
PRINTL 

;HARDで押し倒されているなら, 主導権が無い플래그をセット
IF CONFIG("ＨＡＲＤ") && TEQUIP:넘어뜨리기
	SETBIT FLAG:99, 11
ELSE
	CLEARBIT FLAG:99, 11
ENDIF


NEXT_LINE = 0
VARSET COM_NAMES
VARSET COM_NAMES_HIDDEN

;조교컨피그모드가 켜져 있거나, 항목표시필터가 A이거나,
;비표시컨피그가 모두 꺼져 있으면(GETBIT(FLAG:210, 0) 에서 GETBIT(FLAG:210, 13)까지 0이면)  
IF CONFIG("조교컨피그모드") || (CONFIG("항목표시필터：Ｂ") == 0 && CONFIG("항목표시필터：Ｃ") == 0) || (FLAG:210 & (1p14 - 1)) == 0
    SIF CONFIG("애무系비표시")
		SETCOLOR DEF_COLOR("황색")
	PRINTC 애무계[300]
	RESETCOLOR
	SIF CONFIG("도구系비표시")
		SETCOLOR DEF_COLOR("황색")
	PRINTC 도구계[301]
	RESETCOLOR
	SIF CONFIG("Ｖ섹스系비표시")
		SETCOLOR DEF_COLOR("황색")
	PRINTC Ｖ섹스계[302]
	RESETCOLOR
	PRINTL 

	SIF CONFIG("Ａ섹스系비표시")
		SETCOLOR DEF_COLOR("황색")
	PRINTC Ａ섹스계[303]
	RESETCOLOR
	SIF CONFIG("특수섹스系비표시")
		SETCOLOR DEF_COLOR("황색")
	PRINTC 특수섹스계[304]
	RESETCOLOR
	SIF CONFIG("봉사系비표시")
		SETCOLOR DEF_COLOR("황색")
	PRINTC 봉사계[305]
	RESETCOLOR
	PRINTL 

	SIF CONFIG("조교자に삽입系비표시")
		SETCOLOR DEF_COLOR("황색")
	PRINTC 조교자에삽입계[306]
	RESETCOLOR
	SIF CONFIG("ＳＭ・하드코어系비표시")
		SETCOLOR DEF_COLOR("황색")
	PRINTC ＳＭ·하드코어계[307]
	RESETCOLOR
	SIF CONFIG("조수系비표시")
		SETCOLOR DEF_COLOR("황색")
	;メイド長系とか言われても困るし조수系のまま
	PRINTFORMC 조수계[308]
	RESETCOLOR
	PRINTL 

	SIF CONFIG("특수系비표시")
		SETCOLOR DEF_COLOR("황색")
	PRINTC 특수계[309]
	RESETCOLOR
	SIF CONFIG("촉수系비표시")
		SETCOLOR DEF_COLOR("황색")
	PRINTC 촉수계[310]
	RESETCOLOR
	SIF CONFIG("아르카나系비표시")
		SETCOLOR DEF_COLOR("황색")
	PRINTC 아르카나계[311]
	RESETCOLOR
	PRINTL 

	SIF CONFIG("소비아이템系비표시")
		SETCOLOR DEF_COLOR("황색")
	PRINTC 소비아이템계[312]
	RESETCOLOR
	SIF CONFIG("その他비표시")
		SETCOLOR DEF_COLOR("황색")
	PRINTC 그 외[313]
	RESETCOLOR
	SIF (CONFIG("항목표시필터：Ｂ") == 0 && CONFIG("항목표시필터：Ｃ") == 0)
	    PRINTC 표시커맨드군의반전[320]
	PRINTL

;켜져있는 메뉴만 표시
ELSE
	SETCOLOR DEF_COLOR("황색")
	SIF CONFIG("애무系비표시")
		PRINTC 애무계[300]
	SIF CONFIG("도구系비표시")
		PRINTC 도구계[301]
	SIF CONFIG("Ｖ섹스系비표시")
		PRINTC Ｖ섹스계[302] 

	SIF CONFIG("Ａ섹스系비표시")
		PRINTC Ａ섹스계[303]
	SIF CONFIG("특수섹스系비표시")
		PRINTC 특수섹스계[304]
	SIF CONFIG("봉사系비표시")
		PRINTC 봉사계[305]

	SIF CONFIG("조교자に삽입系비표시")
		PRINTC 조교자에삽입계[306]
	SIF CONFIG("ＳＭ・하드코어系비표시")
		PRINTC ＳＭ·하드코어계[307]
	SIF CONFIG("조수系비표시")
	    PRINTFORMC 조수계[308]

	SIF CONFIG("특수系비표시")
		PRINTC 특수계[309]
	SIF CONFIG("촉수系비표시")
		PRINTC 촉수계[310]
	SIF CONFIG("아르카나系비표시")
		PRINTC 아르카나계[311]

	SIF CONFIG("소비아이템系비표시")
		PRINTC 소비아이템계[312]
	SIF CONFIG("その他비표시")
		PRINTC 그 외[313]
	PRINTL 
	RESETCOLOR
ENDIF

IF CONFIG("조교컨피그모드") == 0
	CALL COM_ABLE500
	SIF RESULT == 0
		SETCOLOR DEF_COLOR("회색")
	PRINTC 아르카나일람[340]
	RESETCOLOR

	CALL COM_ABLE501
	SIF RESULT == 0
		SETCOLOR DEF_COLOR("회색")
	PRINTC 소비아이템일람[341]
	RESETCOLOR

	IF CONFIG("비표시커맨드の표시") == 0
		PRINTC 비표시커맨드를표시[350]
	ELSE
		SETCOLOR DEF_COLOR("파랑")
		PRINTC 비표시커맨드를숨긴다[350]
		RESETCOLOR
	ENDIF
	PRINTL 

	PRINTC 능력표시[400]

	IF CONFIG("항목표시필터：Ｃ") == 0
		IF TEQUIP:넘어뜨리기
			PRINTC 더러움표시[410]
		ELSE
			PRINTC 더러움표시＆관리[410]
		ENDIF

		IF PLAYER == MASTER
			SIF TALENT:연모 == 0
				SETCOLOR DEF_COLOR("회색")
			SIF CONFIG("ＨＡＲＤ")
					CFLAG:9 = 0
			IF CFLAG:9
				PRINTC 서로 사랑한다[420]
			ELSE
				PRINTC 귀여워한다[420]
			ENDIF
			RESETCOLOR
		ENDIF
		PRINTL 

		SIF CHECK_EXCHANGE("조교자") == 0
			SETCOLOR DEF_COLOR("회색")
		IF ASSIPLAY
			PRINTFORMC 주인과 교대[430]
		ELSE
			PRINTFORMC %조사처리(TEXTS("조수の명칭"), "와")% 교대[430]
		ENDIF
		RESETCOLOR

		SIF CHECK_EXCHANGE("조교대상") == 0
			SETCOLOR DEF_COLOR("회색")
		IF PLAYER == MASTER
			IF TEQUIP:Ｗ넘어뜨리기
				PRINTFORMC 주도측을 교대한다[431]
			ELSEIF TEQUIP:넘어뜨리기
				PRINTFORMC %조사처리(TEXTS("조수の명칭"), "와")% 교대한다[431]
			ELSE
				PRINTFORMC %조사처리(TEXTS("조수の명칭"), "를")% 상대한다[431]
			ENDIF
		ELSE
			PRINTFORMC 입장을 역전한다[431]
		ENDIF
		RESETCOLOR

		PRINTC 구슬취득예정개수표시[440]

		PRINTL 

		PRINTC 조교컨피그모드[888]
	ENDIF

	IF CONFIG("항목표시필터：Ｂ")
		PRINTC 항목표시필터：Ｂ[900]
	ELSEIF CONFIG("항목표시필터：Ｃ")
		PRINTC 항목표시필터：Ｃ[900]
	ELSE
		PRINTC 항목표시필터：Ａ[900]
	ENDIF

	SIF COND("조교종료가능") == 0
		SETCOLOR DEF_COLOR("회색")
	PRINTC 조교종료[999]
	RESETCOLOR
ELSE
	IF CONFIG("Ｖ조교필터")
		PRINTC Ｖ조교필터：○[360]
	ELSE
		PRINTC Ｖ조교필터：✕[360]
	ENDIF
	IF CONFIG("Ａ조교필터")
		PRINTC Ａ조교필터：○[361]
	ELSE
		PRINTC Ａ조교필터：✕[361]
	ENDIF
	IF CONFIG("실행치필터")
		PRINTC 실행치필터：○[362]
	ELSE
		PRINTC 실행치필터：✕[362]
	ENDIF
	PRINTL 

	IF CONFIG("脱동정필터")
		PRINTC 탈동정필터：○[363]
	ELSE
		PRINTC 탈동정필터：✕[363]
	ENDIF
	IF CONFIG("脱처녀필터")
		PRINTC 탈처녀필터：○[364]
	ELSE
		PRINTC 탈처녀필터：✕[364]
	ENDIF
	IF CONFIG("페니스밴드필터")
		PRINTC 페니스밴드필터：○[365]
	ELSE
		PRINTC 페니스밴드필터：✕[365]
	ENDIF
	PRINTL 

	SELECTCASE GETBIT(FLAG:98, 0) + GETBIT(FLAG:98, 4)*1p4 + GETBIT(FLAG:98, 5)*1p5
	CASE 0
		PRINTC 게이지：단색Ａ[370]
	CASE 1p5
		PRINTC 게이지：단색Ｂ[370]
	CASE 1p4 + 1p5
		PRINTC 게이지：단색Ｃ[370]
	CASE 1p0 + 1p5
		PRINTC 게이지：컬러Ａ[370]
	CASE 1p0 + 1p4 + 1p5
		PRINTC 게이지：컬러Ｂ[370]
	ENDSELECT

	IF CONFIG("画面표시：파라미터４列")
	 	PRINTC 파라미터：４열[371]
	ELSE
	 	PRINTC 파라미터：３열[371]
	ENDIF
	IF CONFIG("画面표시：조교커맨드４列")
		PRINTC 커맨드표시：４열[372]
	ELSE
		PRINTC 커맨드표시：３열[372]
	ENDIF
	PRINTL 

	IF CONFIG("画面표시：未実行커맨드の色")
		PRINTC 미실행커맨드：암색[373]
	ELSE
		PRINTC 미실행커맨드：통상[373]
	ENDIF
	IF CONFIG("画面표시：요의게이지")
		PRINTC 요의게이지：표시[374]
	ELSE
		PRINTC 요의게이지：숨김[374]
	ENDIF
	IF CONFIG("画面표시：배설욕게이지")
		PRINTC 배설욕게이지：표시[375]
	ELSE
		PRINTC 배설욕게이지：숨김[375]
	ENDIF
	PRINTL 

	PRINTC 사정모드선택[380]
	PRINTL 
	PRINTC 조교로 돌아온다[888]
	IF CONFIG("조교컨피그모드：全커맨드표시")
		PRINTC 실행불가커맨드 숨김[890]
	ELSE
		PRINTC 조교커맨드의 전표시[890]
	ENDIF
	IF CONFIG("조교컨피그모드：비표시커맨드を숨김")
		PRINTC 청록색커맨드：파랑숨김[891]
	ELSEIF CONFIG("조교컨피그모드：금지커맨드を숨김")
		PRINTC 청록색커맨드：초록숨김[891]
	ELSE
		PRINTC 청록색커맨드：전표시[891]
	ENDIF
ENDIF

PRINTL 

MEMO_LINECOUNT = LINECOUNT

SIF MAX(PREVCOM, PREVCOM:2) >= 0 && CONFIG("조교컨피그모드") == 0
	CALL SHOW_PREVCOM

;選んだ커맨드次第で軽い説明표시。原則１行
SELECTCASE TFLAG:103
CASE 360
	SIF CONFIG("Ｖ조교필터")
		PRINTFORML  Ｖ조교필터ＯＮ. 처녀의 경우, 질을 사용하는 조교를 실행불가로 합니다
CASE 361
	SIF CONFIG("Ａ조교필터")
		PRINTFORML  Ａ조교필터ＯＮ. 애널을 사용하는 조교를 실행불가로 합니다
CASE 362
	SIF CONFIG("실행치필터")
		PRINTFORML  실행치필터ＯＮ. 실행치가 충분하지 않은 조교를 실행불가로 합니다
CASE 363
	SIF CONFIG("脱동정필터")
		PRINTFORML  탈동정필터ＯＮ. 동정을 잃는 조교를 실행불가로 합니다
CASE 364
	SIF CONFIG("脱처녀필터")
		PRINTFORML  탈처녀필터ＯＮ. 사입 시에 처녀를 빼앗는지 확인합니다
CASE 365
	SIF CONFIG("페니스밴드필터")
		PRINTFORML  페니스밴드필터ＯＮ. 페니스밴드의 사용을 금지합니다
CASE 372
	SIF CONFIG("画面표시：조교커맨드４列") && GETCONFIG("ウィンドウ幅") < 1000
		PRINTFORML  ４열표시의 경우에는, 윈도우 폭은 1000정도가 추천입니다
CASE 373
	IF CONFIG("画面표시：未実行커맨드の色")
		PRINTFORM  아직 선택했던 적이 없는 커맨드는
		CALL PRINT_COLORTEXT, "회색", DEF_COLOR("明회색")
		PRINTFORML 이 됩니다
	ENDIF
CASE 890
	IF CONFIG("조교컨피그모드：全커맨드표시")
		PRINTPLAINFORM  [{TARGET, 2}]%타겟이()% 실행할 수 없는 커맨드도 모두 표시합니다
	ELSE
		PRINTPLAINFORM  [{TARGET, 2}]%타겟이()% 실행할 수 있는 커맨드만 표시합니다
	ENDIF
	PRINTL
CASE 888
	IF CONFIG("조교컨피그모드")
		PRINT  선택한 조교커맨드를 
		CALL PRINT_COLORTEXT, "비표시", DEF_COLOR("파랑")
		PRINT , 
		CALL PRINT_COLORTEXT, "금지", DEF_COLOR("초록")
		PRINTFORM  상태로 바꿉니다（
			CALL PRINT_COLORTEXT, "이 색", DEF_COLOR("暗황색")
		PRINTFORML 의 것은 금지 불가)
	ENDIF
CASE 891
	IF CONFIG("조교컨피그모드：비표시커맨드を숨김")
		CALL PRINT_COLORTEXT, " 비표시", DEF_COLOR("파랑")
		PRINTFORML 의 커맨드를 숨깁니다（조교컨피그모드에 한정）
	ELSEIF CONFIG("조교컨피그모드：금지커맨드を숨김")
		CALL PRINT_COLORTEXT, " 금지", DEF_COLOR("초록")
		PRINTFORML 의 커맨드를 숨깁니다
	ENDIF
CASE 900
	;前回커맨드がない時に限り説明出す
	IF MEMO_LINECOUNT == LINECOUNT
		IF CONFIG("항목표시필터：Ｂ")
			PRINTPLAIN  [300]～[320]
			PRINTFORML 비표시 커맨드군 필터를 숨깁니다
		ELSEIF CONFIG("항목표시필터：Ｃ")
			PRINTFORML  사용 빈도가 낮은 항목를 모두 숨깁니다
		ENDIF
	ENDIF
ENDSELECT



SIF MEMO_LINECOUNT == LINECOUNT
	PRINTL 


;ARGSには"조교자"or"조교대상"が入る
@CHECK_EXCHANGE(ARGS)
#FUNCTION

SELECTCASE ARGS
;PLAYERとPLAYER:1を交代させる。（조교開始時にPLAYER = MASTER、PLAYER:1 = ASSIとなっている）
CASE "조교자"
	SIF PLAYER <= 0 || PLAYER:1 <= 0
		RETURNF 0
	SIF TEQUIP:PLAYER:넘어뜨리기 || TEQUIP:러브트랩 || TEQUIP:넘어뜨리기 || TEQUIP:손대중 || TEQUIP:새댁플레이 || TEQUIP:연주중 || TEQUIP:촉수 || TEQUIP:비디오카메라
		RETURNF 0
	;撃沈していたら交代できない
	SIF STATE("撃沈", PLAYER) || STATE("撃沈", PLAYER:1)
		RETURNF 0
	RETURNF 1
;TARGETと(TARGET:1 or ASSI)間でTARGETを交代
CASE "조교대상"
	;넘어뜨리기中は、Ｗ넘어뜨리기でないと交代できない
	SIF TEQUIP:넘어뜨리기 && TEQUIP:Ｗ넘어뜨리기 == 0
		RETURNF 0
	SIF TEQUIP:PLAYER:넘어뜨리기 || TEQUIP:러브트랩 || TEQUIP:구속 || TEQUIP:비디오카메라 || TEQUIP:연주중 || TEQUIP:촉수 || TEQUIP:비디오카메라
		RETURNF 0
	;TARGET:1が居る(主にＷ넘어뜨리기中)
	IF TARGET:1
		SIF BASE:정력 <= 0 || BASE:(TARGET:1):정력 <= 0
			RETURNF 0
	 	SIF STATE("撃沈", TARGET:1) || STATE("흐물흐물", TARGET) || STATE("흐물흐물", TARGET:1)
			RETURNF 0
		RETURNF 1
	;조수は居る（交代＝조수と조교대상の立場交換）
	ELSEIF ASSI
		SIF BASE:정력 <= 0 || COND("조수가능") == 0
			RETURNF 0
	 	SIF STATE("撃沈", ASSI) || STATE("흐물흐물", TARGET)
			RETURNF 0
		RETURNF 1
	ENDIF
ENDSELECT

RETURNF 0

@SHOW_PREVCOM
PRINTFORM < 전회조교커맨드 : %TEXTS("前回조교커맨드")%
SIF NUM("턴수") == 0 && PREVCOM:2 >= 0
	PRINTFORM \@ PLAYER == MASTER ? (%TEXTS("조수の명칭")%) # (주인) \@
SETCOLOR DEF_COLOR("노랑")
PRINTFORM %STR:1%
RESETCOLOR
PRINTFORML >

@USERCOM

LOCAL = RESULT

;HARDで押し倒されているなら, 主導権が無い플래그をセット
IF CONFIG("ＨＡＲＤ") && TEQUIP:넘어뜨리기
	SETBIT FLAG:99, 11
ELSE
	CLEARBIT FLAG:99, 11
ENDIF

TFLAG:103 = LOCAL

;커맨드선택
IF LOCAL >= 0 && LOCAL <= 299
	IF CONFIG("조교컨피그모드") == 0
		CALL NO_SELECTCOM, LOCAL

		SIF RESULT == 999
			RETURN 0

		SELECTCOM = RESULT
		TRYCALLFORM COM_ABLE{SELECTCOM}

		SIF RESULT == 0
			RETURN 0

		;主導権が無いフラグを解除
		CLEARBIT FLAG:99, 11

		REDRAW 1

		DOTRAIN SELECTCOM
	ELSE
		CALL NO_SELECTCOM, LOCAL
		LOCAL = RESULT

		SELECTCASE LOCAL
		CASE 999
			CLEARLINE LINECOUNT - TFLAG:31
			RETURN 0
		CASEELSE
			IF STRLENS(TRAINNAME:LOCAL) == 0
				CLEARLINE LINECOUNT - TFLAG:31
				RETURN 0
			ENDIF

		 	SELECTCASE TRAINNAME:LOCAL
			CASE "～예비～"
				CLEARLINE LINECOUNT - TFLAG:31
				RETURN 0
			ENDSELECT
		ENDSELECT

		DITEMTYPE:0:LOCAL = (DITEMTYPE:0:LOCAL + 1)%3

		SELECTCASE LOCAL
		CASE 0 TO 9, 60, 61, 65, 71, 75, 80 TO 95, 98, 99, 230 TO 289
			SIF DITEMTYPE:0:LOCAL == 2
				DITEMTYPE:0:LOCAL = 0
		ENDSELECT
	ENDIF
;조교커맨드の種類ごとの표시ＯＮＯＦＦ
ELSEIF LOCAL >= 300 && LOCAL <= 313
	INVERTBIT FLAG:210, LOCAL - 300
	CLEARLINE LINECOUNT - TFLAG:31
	RETURN 1
;표시커맨드群の반전[320]
ELSEIF LOCAL == 320
	FLAG:210 = INVERTBITS(FLAG:210, 0, 13)
	CLEARLINE LINECOUNT - TFLAG:31
	RETURN 1
;조교컨피그모드へ[888]
ELSEIF LOCAL == 888
	TFLAG:103 = LOCAL
	INVERTBIT FLAG:210, 20
;항목표시필터[900]
ELSEIF LOCAL == 900
	IF CONFIG("항목표시필터：Ｂ")
		CLEARBIT FLAG:210, 30
		SETBIT FLAG:210, 31
	ELSEIF CONFIG("항목표시필터：Ｃ")
		CLEARBIT FLAG:210, 31
	ELSE
		SETBIT FLAG:210, 30
	ENDIF
;조교종료[999]
ELSEIF LOCAL == 999 && COND("조교종료가능")
	REDRAW 1
	BEGIN AFTERTRAIN
ELSEIF CONFIG("조교컨피그모드") == 0
	;아르카나[340]
	IF LOCAL == 340
		DOTRAIN 500
	;아이템[341]
	ELSEIF LOCAL == 341
		DOTRAIN 501
	;비표시커맨드を표시[350]
	ELSEIF LOCAL == 350
		INVERTBIT FLAG:210, 21
		CLEARLINE LINECOUNT - TFLAG:31
	;能力표시[400]
	ELSEIF LOCAL == 400
		CALL NEW_PRINT_STATE, TARGET, "通常"
	;더러움표시[410]
	ELSEIF LOCAL == 410
		CALL STAIN_INFO
	;反撃するか선택[420]
	ELSEIF LOCAL == 420 && PLAYER == MASTER
		IF CONFIG("ＨＡＲＤ")
			PRINTFORMW 난이도가 HARD면 선택할 수 없습니다
			RETURN 0
		ELSEIF TALENT:연모 == 0
			PRINTFORMW 현재의 조교대상이 [연모]인 경우에 한정해 선택할 수 있습니다
			RETURN 0
		ELSEIF CFLAG:9 == 1
			PRINTFORMW %조사처리(CALLNAME:TARGET, "과")% 서로 서로 사랑하기로 했다.
		ELSEIF CFLAG:9 == 0
			PRINTFORMW %조사처리(CALLNAME:TARGET, "를")% 마음가는 대로 귀여워하기로 했다.
		ENDIF
		INVERTBIT CFLAG:9, 0
	;交代[430]
	ELSEIF LOCAL == 430 && CHECK_EXCHANGE("조교자")
		CALL SETFLAG, "조교자교대", 1
		CLEARLINE LINECOUNT - TFLAG:31
	;TARGETとTARGET:1(いないならASSI)を交換[431]
	ELSEIF LOCAL == 431 && CHECK_EXCHANGE("조교대상")
		CALL SETFLAG, "대상교대"
		CLEARLINE LINECOUNT - TFLAG:31
	;宝구슬取得予定数표시[440]
	ELSEIF LOCAL == 440
		CALL SHOW_WILLGET_JUEL
	ENDIF
ELSE
	TFLAG:103 = LOCAL
	;[360]～は各種조교필터
	IF LOCAL == 360
		INVERTBIT FLAG:209, 0
	ELSEIF LOCAL == 361
		INVERTBIT FLAG:209, 1
	ELSEIF LOCAL == 362
		INVERTBIT FLAG:209, 4
	ELSEIF LOCAL == 363
		INVERTBIT FLAG:209, 5
	ELSEIF LOCAL == 364
		INVERTBIT FLAG:209, 6
	ELSEIF LOCAL == 365
		INVERTBIT FLAG:209, 7
	;[370]～は조교画面표시の설정
	ELSEIF LOCAL == 370
		SELECTCASE GETBIT(FLAG:98, 0) + GETBIT(FLAG:98, 4)*1p4 + GETBIT(FLAG:98, 5)*1p5
		CASE 0
			SETBIT FLAG:98, 5
		CASE 1p5
			SETBIT FLAG:98, 4
		CASE 1p4 + 1p5
			SETBIT FLAG:98, 0
			CLEARBIT FLAG:98, 4
		CASE 1p0 + 1p5
			SETBIT FLAG:98, 4
		CASE 1p0 + 1p4 + 1p5
			CLEARBIT FLAG:98, 0
			CLEARBIT FLAG:98, 4
			CLEARBIT FLAG:98, 5
		ENDSELECT
	ELSEIF LOCAL == 371
		INVERTBIT FLAG:98, 2
	ELSEIF LOCAL == 372
		INVERTBIT FLAG:98, 3
	ELSEIF LOCAL == 373
		INVERTBIT FLAG:98, 1
	ELSEIF LOCAL == 374
		INVERTBIT FLAG:98, 6
	ELSEIF LOCAL == 375
		INVERTBIT FLAG:98, 7
	;사정모드선택[380]
	ELSEIF LOCAL == 380
		CALL SETCONFIG, "사정箇所の변경"
	;조교커맨드の全표시[890]
	ELSEIF LOCAL == 890
		INVERTBIT FLAG:210, 45
	;青緑커맨드：全표시[891]
	ELSEIF LOCAL == 891
		IF CONFIG("조교컨피그모드：비표시커맨드を숨김")
			CLEARBIT FLAG:210, 40
			SETBIT FLAG:210, 41
		ELSEIF CONFIG("조교컨피그모드：금지커맨드を숨김")
			CLEARBIT FLAG:210, 41
		ELSE
			SETBIT FLAG:210, 40
		ENDIF
	ENDIF
ENDIF

SIF CONFIG("조교컨피그모드")
	CLEARLINE LINECOUNT - TFLAG:31
