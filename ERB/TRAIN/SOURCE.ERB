@SOURCE_CHECK
#DIM LCOUNT
;-------------------------------------------------
;조교커맨드の플래그取得
;-------------------------------------------------
CALL CALC, "조교の플래그취득"

;-------------------------------------------------
;嗜虐or봉사쾌락경험発生플래그
;-------------------------------------------------
CALL SETFLAG, "봉사嗜虐判定", SELECTCOM

;-------------------------------------------------
;인서트解除（NEXTCOM等を考慮）
;-------------------------------------------------
CALL SETFLAG, "体勢戻し"

;-------------------------------------------------
;바이브など付けっぱなし아이템の체크(主にSOURCE計算. 快楽のSOURCEが増えないものは, ここにはなくても良い)
;-------------------------------------------------
CALL EQUIP_1

;-------------------------------------------------
;SOURCE計算
;-------------------------------------------------
CALL CALC_SOURCE

;조수が조교側として参加している３Ｐの場合
IF COND("３Ｐ")
	CALL SETFLAG, "조교자교대"
	TRYCALLFORM SOURCE_COM{SELECTCOM}, 50
	TRYCALLFORM FLAG_COM{SELECTCOM}, 50
	CALL CALC_SOURCE
	CALL SETFLAG, "조교자교대"
ENDIF

;-------------------------------------------------
;절정処理(見込み)
;-------------------------------------------------
CALL ORGASM_ADD_A

;-------------------------------------------------
;刻印取得の체크(見込み)
;-------------------------------------------------
CALL MARK_GOT_CHECK

;-------------------------------------------------
;커맨드実行時の구상
;-------------------------------------------------
;刻印取得구상を喋ったなら, 커맨드구상は飛ばす
IF GROUPMATCH(2, CHOSE_EV("고통각인Lv3취득"), CHOSE_EV("쾌락각인Lv3취득"), CHOSE_EV("반발각인Lv3취득") )
ELSE
	CALL KOJO_MESSAGE_COM
ENDIF

;-------------------------------------------------
;各種경험の変動
;-------------------------------------------------
CALL GET_EXP_COM

;-------------------------------------------------
;受精확률の変動を表示
;-------------------------------------------------
CALL CHECK_NINSHIN_ALLACTOR

;-------------------------------------------------
;구상(@KOJO_MESSAGE_COM)による強制終了
;-------------------------------------------------
SIF NEXTCOM == 999
	RETURN 0
;-------------------------------------------------
;커맨드による무드の獲得
;-------------------------------------------------
CALL MOOD_COM, SELECTCOM

;-------------------------------------------------
;인서트플래그等の설정
;-------------------------------------------------
CALL CHECK_INSERT
;안면기승플래그などのON
CALL CHECK_EX_EQUIP
;-------------------------------------------------
;이상경험獲得_1
;-------------------------------------------------
CALL EXP50_CHECK_ALLACTOR

;-------------------------------------------------
;ラグジャラス判定 （EVENT_M.ERBに記述）
;-------------------------------------------------
SIF STATE("황홀", PLAYER) == 0
	CALL LUXURIOUS_HIT, "커맨드", PLAYER
;PLAYERが황홀になってしまったらTARGETは황홀にならない
SIF STATE("황홀", PLAYER) == 0 && STATE("황홀", TARGET) == 0
	CALL LUXURIOUS_HIT, "커맨드", TARGET

;-------------------------------------------------
;조수の行動
;-------------------------------------------------
CALL EVENT_ASSI_AUTO, "조교中"
;-------------------------------------------------
;바이브など付けっぱなし아이템の체크(텍스트と경험)
;-------------------------------------------------
CALL EQUIP_2
;-------------------------------------------------
;おもな경험の増加が一段落下ところで技能ＬＶ체크
;-------------------------------------------------
CALL GET_EXABL2, TARGET
CALL GET_EXABL2, MASTER
CALL GET_EXABL2, ASSI
;-------------------------------------------------
;이상경험獲得_2
;-------------------------------------------------
CALL EXP50_CHECK_ALLACTOR

;-------------------------------------------------
;カウンターの反撃구상・処理
;-------------------------------------------------
CALL COUNTER_SYS_1
;구상による強制終了
SIF NEXTCOM == 999
	RETURN 0
;-------------------------------------------------
;절정処理, 조교대상の사정＆분유체크（후타나리・남자）
;-------------------------------------------------
;손대중している時には止めるか선택させる？
CALL ORGASM_ADD_A, "数値표시"
CALL ORGASM_ADD_B
;-------------------------------------------------
;이상경험獲得_3
;-------------------------------------------------
CALL EXP50_CHECK_ALLACTOR

;-------------------------------------------------
;NEW_UPCHECKの直接記述커맨드対応用
;-------------------------------------------------
FOR LCOUNT, 1, CHARANUM
	SELECTCASE LCOUNT
	CASE PLAYER, PLAYER:1, MASTER, ASSI
		;정력が減少している
		SIF BASE:LCOUNT:정력 < TCVAR:LCOUNT:108
			TCVAR:LCOUNT:109 = TCVAR:LCOUNT:108 - BASE:LCOUNT:정력
	ENDSELECT
NEXT

;-------------------------------------------------
;절정メッセージの表示
;-------------------------------------------------
CALL TRAIN_MESSAGE_A

;-------------------------------------------------
;파라미터変動による구상(TARGETの절정など)
;-------------------------------------------------
CALL KOJO_MESSAGE_PALAMCNG
;구상による強制終了
SIF NEXTCOM == 999
	RETURN 0

;-------------------------------------------------
;조수による청소
;-------------------------------------------------
CALL EVENT_ASSI_AUTO, "청소"

;-------------------------------------------------
;二人で넘어뜨리기てる時の조수の절정や사정の텍스트
;-------------------------------------------------
IF TEQUIP:Ｗ넘어뜨리기
	CALL ORGASM_ADD_TEXT, TARGET:1
	CALL TARGET_EJAC_CHECK_TEXT, TARGET:1
	CALL TARGET_MILK_CHECK_TEXT, TARGET:1
	;사정or절정(こらえた時には発生しない)
	IF NOWEX:(TARGET:1):절정수합계 + NOWEX:(TARGET:1):사정
		PRINTFORM %조사처리(CALLNAME:(TARGET:1),"는")%
		IF NOWEX:(TARGET:1):사정
			IF SELECTCOM:1 == 121
				PRINTFORM %CALLNAME:MASTER%의 장내에 정액을 토하
			ELSEIF SELECTCOM:1 == 4
				PRINTFORM %CALLNAME:MASTER%의 입에 사정하
			ELSE
				PRINTFORM 페니스에서 정액을 분출하
			ENDIF
		ELSE
			IF NOWEX:(TARGET:1):절정수합계 >= 2 && TALENT:(TARGET:1):남자 == 0 && RAND:2
				PRINT 음순에서 투명한 분수를 퓻퓻하고 귀엽게 분출하
			ELSE
				PRINT 등을 크게 뒤로 젖히
			ENDIF
		ENDIF
		PRINTW 면서, 전신을 떨며 절정에 달했다
		PRINTL 
	ENDIF
ENDIF
;-------------------------------------------------
;촉수の사정체크
;-------------------------------------------------
CALL SYOKUSYU_SHOOT
;-------------------------------------------------
;드래곤の사정체크
;-------------------------------------------------
CALL DRAGON_SHOOT
;-------------------------------------------------
;사정・절정判定
;-------------------------------------------------
CALL SAMEN_AND_EXTACY

;-------------------------------------------------
;SOURCE計算
;-------------------------------------------------
CALL CALC_SOURCE
;-------------------------------------------------
;PLAYER, 드래곤, 촉수の사정＆절정をトリガーにした구상
;-------------------------------------------------
CALL KOJO_MESSAGE_SAMEN_AND_EXTACY
;구상による強制終了
SIF NEXTCOM == 999
	RETURN 0
;-------------------------------------------------
;PLAYER, 드래곤, 촉수の사정＆절정をトリガーにした구상（３Ｐ）
;-------------------------------------------------
CALL KOJO_MESSAGE_SAMEN_AND_EXTACY_3P
;구상による強制終了
SIF NEXTCOM == 999
	RETURN 0

;-------------------------------------------------
;조교대상の状態（陥没유두の勃起など）체크
;-------------------------------------------------
CALL TARGET_FLAG_CHECK
;-------------------------------------------------
;고통쾌락경험, 봉사쾌락경험等の체크
;-------------------------------------------------
CALL EXP_GOT_CHECK
;-------------------------------------------------
;주인による조교の경험치
;-------------------------------------------------
CALL MASTER_FLAG_CHECK
;-------------------------------------------------
;刻印取得の체크(@KOJO_MESSAGE_MARKCNG は@MARK_GOT_CHECK 内にある)
;-------------------------------------------------
CALL MARK_GOT_CHECK

;구상による強制終了
SIF NEXTCOM == 999
	RETURN 0

;-------------------------------------------------
;이상경험獲得_4
;-------------------------------------------------
CALL EXP50_CHECK_ALLACTOR

;-------------------------------------------------
;カウンターによる황홀・押し倒され, 撃沈判定
;-------------------------------------------------
CALL COUNTER_SYS_2

;-------------------------------------------------
;조교ソースの表示
;-------------------------------------------------
CALL SHOW_SOURCE

;-------------------------------------------------
;数値変動の表示
;-------------------------------------------------
;상성
IF RELATION:(NO:PLAYER) != 100 && RELATION:(NO:PLAYER) > 0
	PRINTFORM ＜상성 {RELATION:(NO:PLAYER)/100}.
	SIF RELATION:(NO:PLAYER)%100 < 10
		PRINT 0
	PRINTFORML {RELATION:(NO:PLAYER)%100}배＞
ENDIF

;同じ커맨드の連続実行
SIF COND("連続実行")
	PRINTL ＜동일커맨드 연속실행＞

;-------------------------------------------------
;체력・기력の減少
;-------------------------------------------------
IF TALENT:Ｖ손상 || TALENT:Ａ손상
	SETCOLOR DEF_COLOR("황색")
	PRINTL ＜체내손상중＞
	RESETCOLOR
ENDIF
CALL CALC, "체력기력減少"
;체력・기력の変動を表示
IF DOWNBASE:체력 || DOWNBASE:기력
	PRINTFORM 체력\@ DOWNBASE:체력 > 0 ? - # + \@{ABS(DOWNBASE:체력)} 
	PRINTFORML 기력\@ DOWNBASE:기력 > 0 ? - # + \@{ABS(DOWNBASE:기력)}
ENDIF
;기력０
SIF BASE:기력 <= 0
	PRINTL ★기력０★

;-------------------------------------------------
;非参加者の기력の회복
;-------------------------------------------------
FOR LCOUNT, 2, CHARANUM
	SIF DOWNBASE:LCOUNT:체력 > 0 || DOWNBASE:LCOUNT:기력 > 0
		CONTINUE

	BASE:LCOUNT:기력 = LIMIT(BASE:LCOUNT:기력 + 20 + RAND:81, 0, MAXBASE:LCOUNT:기력)
NEXT
;-------------------------------------------------
;텍스트の表示
;-------------------------------------------------
CALL TEXT_TURNEND

;-------------------------------------------------
;SOURCE計算
;-------------------------------------------------
CALL CALC_SOURCE

;-------------------------------------------------
;刻印取得の체크
;-------------------------------------------------
CALL MARK_GOT_CHECK

;-------------------------------------------------
;파라미터の上昇＆表示（CDOWNもここで）
;-------------------------------------------------
CALL NEW_UPCHECK

;-------------------------------------------------
;フラグに使う現実시간を取得
;-------------------------------------------------
CALL GET_REALTIMEFORFLAG

;-------------------------------------------------
;조수と一緒にＷ넘어뜨리기に移行
;-------------------------------------------------
IF RAND:3 == 0 && BASE:체력 >= 1200 && BASE:기력 >= 800 && ASSI && STATE("撃沈", ASSI) == 0 && (CONFIG("ＨＡＲＤ") || RELATION:ASSI >= 120 ) && (TALENT:연모 == 0 || TALENT:음란) && TEQUIP:넘어뜨리기 && TEQUIP:Ｗ넘어뜨리기 == 0
	CALL COM_ABLE178
	SIF RESULT
		NEXTCOM = 178
ENDIF


;追加処理及びサブ이벤트
;-------------------------------------------------
;조교대상の状態（陥没유두の勃起など）체크
;-------------------------------------------------
@TARGET_FLAG_CHECK
#DIM ERECT_B_BEFORE
#DIM ERECT_B
#DIM MEMO_PALAM5
#DIM MEMO_EXB

;유두の勃起に関係あるのはPALAM:욕정(CUP:욕정はまだPALAMには転化されていない)と
;EX:Ｂ절정(この関数内ではNOWEXはEXに変換済み)なので、このターンの開始時の유두の状況と현재を比較する
MEMO_PALAM5 = PALAM:욕정
MEMO_EXB = EX:Ｂ절정

PALAM:욕정 += CUP:욕정
ERECT_B = CALCF("Ｂ발정")

PALAM:욕정 = MEMO_PALAM5
EX:Ｂ절정 -= NOWEX:Ｂ절정
ERECT_B_BEFORE = CALCF("Ｂ발정")

;陥没유두が勃起状態になった
IF ERECT_B && ERECT_B_BEFORE == 0
	PRINTL 
	;授乳済み
	IF EXP_GAIN(73)
		CALL PRINT_STR, "하트핑크_CALLNAME:TARGET_의 함몰되어 있던 유두가, 흥분과 모유의 자극으로 부스스 일어났다!_W"
	ELSEIF COND("유두비대")
		CALL PRINT_STR, "하트핑크_CALLNAME:TARGET_의 함몰되어 있던 큰 유두가, 흥분으로 노출되었다!_W"
	ELSE
		CALL PRINT_STR, "하트핑크_CALLNAME:TARGET_의 함몰되어 있던 유두가 흥분으로 노출되었다!_W"
	ENDIF
ENDIF

PALAM:욕정 = MEMO_PALAM5
EX:Ｂ절정 = MEMO_EXB

;-------------------------------------------------
;주인による조교の경험치
;-------------------------------------------------
@MASTER_FLAG_CHECK
#DIM MOOD_UP
#DIM FAV_UP

;このターンに上昇した무드を算出
MOOD_UP = MOOD() - TFLAG:34

IF (MOOD_UP < 5 && MOOD_HEART() < 3) || MOOD_UP < 0
	IF TALENT:음란 + TALENT:호색 && SEXUAL_PLAY()
		TFLAG:33 += 3
	ELSEIF TALENT:음란 + TALENT:도착적 && ABNORMAL_PLAY()
		TFLAG:33 += 3
	ENDIF
ENDIF

;악세사리効果（무드アップ）：조수조교時にも効果発揮
TFLAG:33 += TALENT:MASTER:무드

;무드の上昇
MOOD_UP = MOOD() - TFLAG:34

;무드変化有り
IF MOOD_UP
	PRINTFORM 무드:{MOOD()}
	IF MOOD_UP > 0
		SETCOLOR DEF_COLOR("핑크")
		PRINTFORML  ↑{MOOD_UP}
	ELSE
		SETCOLOR DEF_COLOR("파랑")
		PRINTFORML  ↓{-MOOD_UP}
	ENDIF
	RESETCOLOR
ENDIF


SIF PLAYER != MASTER || TEQUIP:촉수
	RETURN 1

FAV_UP = TFLAG:30 + NOWEX:동시절정
SIF NOWEX:절정수합계
	FAV_UP += 1
;절정箇所に対応する敏感系の소질があるなら、係数ＵＰ
SIF NOWEX:Ｃ절정 > 0 && (TALENT:음핵 || TALENT:Ｃ민감)
	FAV_UP += 2
SIF NOWEX:Ｖ절정 > 0 && (TALENT:음호 || TALENT:Ｖ민감)
	FAV_UP += 2
SIF NOWEX:Ａ절정 > 0 && (TALENT:애널광 || TALENT:Ａ민감)
	FAV_UP += 2
SIF NOWEX:Ｂ절정 > 0 && (TALENT:음유 || TALENT:Ｂ민감)
	FAV_UP += 2
SIF NOWEX:Ｍ절정 > 0 && (TALENT:음탕한입술 || TALENT:Ｍ민감)
	FAV_UP += 2
SIF NOWEX:요도절정 > 0 && TALENT:요도광
	FAV_UP += 2
;사정+(도착적or남자）
SIF NOWEX:사정 > 0 && TALENT:도착적 + TALENT:남자
	FAV_UP += 2
;분유+도착적
SIF NOWEX:분유 > 0 && TALENT:도착적
	FAV_UP += 2
;お漏らし+도착적
SIF NOWEX:절정실금 > 0 && TALENT:도착적
	FAV_UP += 2

;幸せな처녀상실をした
SIF GETBIT(NOWEX:처녀상실, 1)
	FAV_UP += 50

;소질による増加
;반하기쉬움
SIF TALENT:반하기쉬움
	FAV_UP += 2
;망신
SIF TALENT:망신
	FAV_UP += 1
;主人の매혹or매력
SIF TALENT:MASTER:매혹 || TALENT:MASTER:매력
	FAV_UP += 1
;主人の수수께끼의매력
SIF TALENT:MASTER:수수께끼의매력
	FAV_UP += 1
;마음에듬の相手には優しくします
SIF TALENT:마음에듬
	FAV_UP += 1
;시길군
SIF STATE("시길군", PLAYER)
	FAV_UP += 1
;主人の인기
SIF TALENT:MASTER:인기
	FAV_UP += 1

;고통각인によるペナルティ
SIF MARK:고통각인 > ABL:마조끼 && TALENT:도착적 == 0
	FAV_UP -= MARK:고통각인 - ABL:마조끼
;고통각인によるボーナス
SIF MARK:고통각인 == 3 && (ABL:마조끼 >= 3 || TALENT:도착적)
	FAV_UP += 1

;반감と不快と공포によるペナルティ
SIF CUP:공포 + CUP:반감 >= 100
	FAV_UP -= POWER(2, GET_PALAMLV(CUP:공포 + CUP:반감) )

;순종によるボーナス
SIF CUP:순종 >= 500
	FAV_UP += GET_PALAMLV(CUP:순종)

;무드アップ인센스
SIF GETBITS(TEQUIP:MASTER:인센스, 4, 6)
	FAV_UP += 1
;무드
SIF MOOD_HEART() >= 3
	FAV_UP += 1

;미약
IF TEQUIP:미약
	FAV_UP -= TEQUIP:미약/50
	;쾌감에솔직、호색、음란、도착적、마조のどれでもない
	SIF TALENT:쾌감에솔직 + TALENT:호색 + TALENT:음란 + TALENT:도착적 + TALENT:마조 == 0
		FAV_UP -= 2
	;쾌감을부정(쾌감을부정持ちは、累計で-7の修正)
	SIF TALENT:쾌감을부정
		FAV_UP -= 2
ENDIF
;이뇨제
SIF TEQUIP:이뇨제 && TALENT:도착적 + TALENT:마조 == 0
	FAV_UP -= 3

;악세사리効果（매력）
SIF TALENT:PLAYER:매력강화 >= 20
	FAV_UP += TALENT:PLAYER:매력강화 / 20

;소질による減少
;반항적
SIF TALENT:반항적 && FAV_UP > 0
	FAV_UP -= 1
;自制心
SIF TALENT:냉정 && FAV_UP > 0
	FAV_UP -= 1
;무관심
SIF TALENT:무관심 && FAV_UP > 0
	FAV_UP -= 1
;감정결핍
SIF TALENT:감정결핍 && FAV_UP > 0
	FAV_UP -= 1
;일선을넘지않음(二段階減少)
SIF TALENT:일선을넘지않음 && FAV_UP > 0
	FAV_UP -= 1
SIF TALENT:일선을넘지않음 && FAV_UP > 10
	FAV_UP -= 1
;저항
SIF TALENT:저항 && FAV_UP > 0
	FAV_UP -= 1
;쾌감을부정
SIF TALENT:쾌감을부정 && FAV_UP > 0
	FAV_UP -= 1
;남성혐오
SIF TALENT:남성혐오 && TALENT:MASTER:남자 && FAV_UP > 0
	FAV_UP -= 1

;好感度のキャップ
IF FAV_UP > 0
	SELECTCASE CFLAG:2
	CASE IS >= 100000000
		FAV_UP /= 64
	CASE IS >= 10000000
		FAV_UP /= 32
	CASE IS >= 1000000
		FAV_UP /= 16
	CASE IS >= 100000
		FAV_UP /= 8
	CASE IS >= 10000
		FAV_UP /= 4
	CASE IS >= 5000
		FAV_UP /= 2
	ENDSELECT
	FAV_UP = MAX(FAV_UP, 1)
ENDIF

SELECTCASE FAV_UP
CASE IS >= 200
	FAV_UP = 60 + (FAV_UP - 200)/16
;MAX60
CASE IS >= 100
	FAV_UP = 48 + (FAV_UP - 100)/8
;MAX47
CASE IS >= 50
	FAV_UP = 35 + (FAV_UP - 50)/4
;MAX35
CASE IS >= 20
	FAV_UP = 20 + (FAV_UP - 20)/2
ENDSELECT

CFLAG:2 += FAV_UP

;好感度変化有り
IF FAV_UP
	PRINTFORM 호감도:{CFLAG:2}
	IF FAV_UP > 0
		SETCOLOR DEF_COLOR("핑크")
		PRINTFORML  ↑{FAV_UP}
	ELSE
		SETCOLOR DEF_COLOR("파랑")
		PRINTFORML  ↓{-FAV_UP}
	ENDIF
	RESETCOLOR
ENDIF

;言葉での寝取り返し
CALL EVENT_DRAGON_NTR, "言葉での寝取り返し"



;-------------------------------------------------
;경험取得と表示の管理
;ARGに２이상の数字が代入されている場合には獲得경험をARG倍する
;ただし, 확장경험など一部のものは경험を積むたびに上昇が鈍るので, ２回までは単純にARG倍させたりはしない
;-------------------------------------------------
@GET_EXP_COM, ARG
#DIM LCOUNT
#DIM V_SONSHOU, 2
#DIM A_SONSHOU, 2

VARSET LOCAL

IF FLAG:8
	V_SONSHOU = TALENT:Ｖ손상
	A_SONSHOU = TALENT:Ａ손상

	V_SONSHOU:1 = TALENT:PLAYER:Ｖ손상
	A_SONSHOU:1 = TALENT:PLAYER:Ａ손상
ENDIF

;식스나인だけは특수
IF SELECTCOM == 99 && TEQUIP:식스나인
	SELECTCOM = TEQUIP:식스나인
	CALL CALC, "경험취득", SELECTCOM
	IF ARG >= 2
		FOR LCOUNT, 0, ARG-1
			SELECTCASE LCOUNT
			CASE 0
				CALL CALC, "경험취득", SELECTCOM
			CASEELSE
				CALL CALC, "경험취득", SELECTCOM, ARG - 2
				BREAK
			ENDSELECT
		NEXT
	ENDIF

	SELECTCOM = TEQUIP:PLAYER:식스나인
	CALL CALC, "경험취득", SELECTCOM
	IF ARG >= 2
		FOR LCOUNT, 0, ARG-1
			SELECTCASE LCOUNT
			CASE 0
				CALL CALC, "경험취득", SELECTCOM
			CASEELSE
				CALL CALC, "경험취득", SELECTCOM, ARG - 2
				BREAK
			ENDSELECT
		NEXT
	ENDIF

	SELECTCOM = 99
ELSE
	CALL CALC, "경험취득", SELECTCOM
	IF ARG >= 2
		FOR LCOUNT, 0, ARG-1
			SELECTCASE LCOUNT
			CASE 0
				CALL CALC, "경험취득", SELECTCOM
			CASEELSE
				CALL CALC, "경험취득", SELECTCOM, ARG - 2
				BREAK
			ENDSELECT
		NEXT
	ENDIF
ENDIF

IF COND("３Ｐ")
	SWAP SELECTCOM, SELECTCOM:2
	SWAP PLAYER, PLAYER:1
	CALL CALC, "경험취득", SELECTCOM
	IF ARG >= 2
		FOR LCOUNT, 0, ARG-1
			SELECTCASE LCOUNT
			CASE 0
				CALL CALC, "경험취득", SELECTCOM
			CASEELSE
				CALL CALC, "경험취득", SELECTCOM, ARG - 2
				BREAK
			ENDSELECT
		NEXT
	ENDIF
	SWAP SELECTCOM, SELECTCOM:2
	SWAP PLAYER, PLAYER:1
ENDIF

SIF FLAG:8 == 0
	RETURN 0

;LOCAL:LCOUNTにはTARGETの該当경험の増加量を代入し, LOCAL:(LCOUNT + 100)にはPLAYERの該当경험の増加量を代入
FOR LCOUNT, 0, 100
	LOCAL:LCOUNT = EXP:LCOUNT - TCVAR:(LCOUNT + 700)

	LOCAL:(LCOUNT + 100) = EXP:PLAYER:LCOUNT - TCVAR:PLAYER:(LCOUNT + 700)
NEXT
;"경험취득"内では増加していないが、実際には増えている

;表示するべき경험があるならPRINTL ないならここで終わり
FOR LCOUNT, 0, 100
	SELECTCASE LCOUNT
	;表示しない경험
	CASE 5 TO 7
		CONTINUE
	CASEELSE
		SELECTCASE LOCAL:LCOUNT + LOCAL:(LCOUNT + 100)
		CASE IS > 0
			BREAK
		ENDSELECT
	ENDSELECT
NEXT
PRINTL 

FOR LCOUNT, 0, 100
	SIF LOCAL:LCOUNT == 0
		CONTINUE

	SELECTCASE LCOUNT
	;表示しない경험
	CASE 5 TO 7
		CONTINUE
	ENDSELECT

	PRINTFORML %EXPNAME:LCOUNT%＋{LOCAL:LCOUNT}
NEXT
;-------------------------------------------------
;무리な확장による체내의 손상체크
;-------------------------------------------------
SETCOLOR DEF_COLOR("핑크")
SIF TALENT:Ｖ손상 > V_SONSHOU
	PRINTFORMW %CALLNAME:TARGET%의 질이 삽입된 것의 크기를 견디지 못하고, 부서져 버렸다! 
SIF TALENT:Ａ손상 > A_SONSHOU
	PRINTFORMW %CALLNAME:TARGET%의 애널이 삽입된 것의 크기를 견디지 못하고, 부서져 버렸다! 
RESETCOLOR


FOR LCOUNT, 0, 100
	SIF LOCAL:(LCOUNT + 100) == 0
		CONTINUE
	PRINTFORML %EXPNAME:LCOUNT%＋{LOCAL:(LCOUNT + 100)} (%CALLNAME:PLAYER%)
NEXT

SETCOLOR DEF_COLOR("핑크")
SIF TALENT:PLAYER:Ｖ손상 > V_SONSHOU:1
	PRINTFORMW %CALLNAME:PLAYER%의 질이 삽입된 것의 크기를 견디지 못하고, 부서져 버렸다! 
SIF TALENT:PLAYER:Ａ손상 > A_SONSHOU:1
	PRINTFORMW %CALLNAME:PLAYER%의 애널이 삽입된 것의 크기를 견디지 못하고, 부서져 버렸다! 
RESETCOLOR


@EQUIP_1
;손대중中
SIF TEQUIP:손대중
	CALL EQUIP_COM31
;조수も一緒に넘어뜨리기 중
IF TEQUIP:넘어뜨리기 && TEQUIP:Ｗ넘어뜨리기
	CALL EQUIP_COM30
;쓰러뜨려짐중
ELSEIF TEQUIP:넘어뜨리기
	CALL EQUIP_COM30
	SIF TEQUIP:안면기승
		CALL EQUIP_COM1
;넘어뜨리기 중
ELSEIF TEQUIP:PLAYER:넘어뜨리기
	CALL EQUIP_COM27
ENDIF

;자위중독判定
SELECTCASE SELECTCOM
CASE 60 TO 79, 139
	SIF ABL:자위중독
		CALL PLAY_ONANIE
ENDSELECT

;追加키스
SIF TEQUIP:키스 && COND("추가키스플래그")
	CALL ADD_KISS

SIF TEQUIP:PLAYER:자위한다
	CALL EQUIP_COM37

;식스나인
SIF TEQUIP:식스나인
	CALL EQUIP_COM69

;인서트中(커맨드によっては, @INSERTIONのなかでRETURN 0)
CALL INSERTION

;하드스킨
SIF STATE("하드스킨", PLAYER)
	CALL EQUIP_COM306

;비젼
CALL EQUIP_COM243

;바이브装着中
SIF TEQUIP:바이브
	CALL EQUIP_COM11
;카테터装着中
SIF TEQUIP:카테터
	CALL EQUIP_COM12
;애널바이브装着中
SIF TEQUIP:애널바이브
	CALL EQUIP_COM13
;클리캡装着中
SIF TEQUIP:클리캡
	CALL EQUIP_COM14
;니플캡装着中
SIF TEQUIP:니플캡
	CALL EQUIP_COM15
;착유기装着中
SIF TEQUIP:착유기
	CALL EQUIP_COM16
;오나홀装着中
SIF TEQUIP:오나홀
	CALL EQUIP_COM17
;샤워사용중
SIF TEQUIP:샤워
	CALL EQUIP_COM18
;로터삽입中
SIF TEQUIP:로터삽입
	CALL EQUIP_COM19
;러브트랩 중
SIF TEQUIP:러브트랩
	CALL EQUIP_COM29
;아이마스크装着中
SIF TEQUIP:아이마스크
	CALL EQUIP_COM43
;밧줄・구속中
SIF TEQUIP:구속
	CALL EQUIP_COM44
;볼개그装着中
SIF TEQUIP:재갈
	CALL EQUIP_COM45
;관장
SIF TEQUIP:관장
	CALL EQUIP_COM46
;확장벌룬装着中
SIF TEQUIP:확장벌룬
	CALL EQUIP_COM48
;ビデオ촬영중
SIF TEQUIP:비디오카메라
	CALL EQUIP_COM53_1
;야외플레이중
SIF TEQUIP:야외플레이
	CALL EQUIP_COM54_1
;수치플레이중
SIF TEQUIP:수치플레이
	CALL EQUIP_COM57
;목욕탕플레이중
SIF TEQUIP:목욕탕플레이
	CALL EQUIP_COM58
;새댁플레이중
SIF TEQUIP:새댁플레이
	CALL EQUIP_COM59

;인센스
SIF TEQUIP:MASTER:인센스
	CALL INCENSE

;촉수조교中
SIF TEQUIP:촉수
	CALL EQUIP_COM90
;촉수구욕中
SIF TEQUIP:촉수구욕
	CALL EQUIP_COM98

@EQUIP_2
;바늘다루기獲得이벤트
SIF SELECTCOM == 132
	CALL EVENT_COM132
;손대중中
SIF TEQUIP:손대중
	CALL EQUIP_COM31_2
;3P中
IF TEQUIP:Ｗ넘어뜨리기 || SELECTCOM:1 >= 0
	CALL EQUIP_COM_3P
;쓰러뜨려짐중
ELSEIF TEQUIP:넘어뜨리기
	CALL EQUIP_COM30_2
	SIF TEQUIP:안면기승
		CALL EQUIP_COM1_2
;넘어뜨리기 중
ELSEIF TEQUIP:PLAYER:넘어뜨리기
	CALL EQUIP_COM27
ENDIF

;자위중독判定
SELECTCASE SELECTCOM
CASE 60 TO 79, 139
	SIF ABL:자위중독
		CALL PLAY_ONANIE_2
ENDSELECT

;追加키스
SIF TEQUIP:키스 && COND("추가키스플래그")
	CALL ADD_KISS_2

SIF TEQUIP:PLAYER:자위한다
	CALL EQUIP_COM37_2

;식스나인
SIF TEQUIP:식스나인
	CALL EQUIP_COM69_2
;플라트
SIF TEQUIP:플라트
	CALL EQUIP_COM34_2
;칭찬하기
SIF TEQUIP:칭찬하기
	CALL EQUIP_COM24_2
;달콤한말
SIF TEQUIP:달콤한말
	CALL EQUIP_COM33_2
;인서트中
CALL INSERTION_2
;하드스킨
SIF STATE("하드스킨", PLAYER)
	CALL EQUIP_COM306_2
;비젼
CALL EQUIP_COM243_2
;바이브装着中
SIF TEQUIP:바이브
	CALL EQUIP_COM11_2
;카테터装着中
SIF TEQUIP:카테터
	CALL EQUIP_COM12_2
;애널바이브装着中
SIF TEQUIP:애널바이브
	CALL EQUIP_COM13_2
;클리캡装着中
SIF TEQUIP:클리캡
	CALL EQUIP_COM14_2
;니플캡装着中
SIF TEQUIP:니플캡
	CALL EQUIP_COM15_2
;착유기装着中
SIF TEQUIP:착유기
	CALL EQUIP_COM16_2
;오나홀装着中
SIF TEQUIP:오나홀
	CALL EQUIP_COM17_2
;샤워사용중
SIF TEQUIP:샤워
	CALL EQUIP_COM18_2
;로터삽입中
SIF TEQUIP:로터삽입
	CALL EQUIP_COM19_2
;러브트랩 중
SIF TEQUIP:러브트랩
	CALL EQUIP_COM29_2
;관내노출플레이
SIF TEQUIP:관내노출플레이
	CALL EQUIP_COM35_2
;아이마스크装着中
SIF TEQUIP:아이마스크
	CALL EQUIP_COM43_2
;밧줄・구속中
SIF TEQUIP:구속
	CALL EQUIP_COM44_2
;볼개그装着中
SIF TEQUIP:재갈
	CALL EQUIP_COM45_2
;관장
SIF TEQUIP:관장
	CALL EQUIP_COM46_2
;확장벌룬装着中
SIF TEQUIP:확장벌룬
	CALL EQUIP_COM48_2
;ビデオ촬영중
SIF TEQUIP:비디오카메라
	CALL EQUIP_COM53_1_2
;야외플레이중
SIF TEQUIP:야외플레이
	CALL EQUIP_COM54_2
;수치플레이중
SIF TEQUIP:수치플레이
	CALL EQUIP_COM57_2
;목욕탕플레이중
SIF TEQUIP:목욕탕플레이
	CALL EQUIP_COM58_2
;새댁플레이중
SIF TEQUIP:새댁플레이
	CALL EQUIP_COM59_2
;인센스
SIF TEQUIP:MASTER:인센스
	CALL INCENSE_2
;촉수조교中
SIF TEQUIP:촉수
	CALL EQUIP_COM90_2
;촉수구욕中
SIF TEQUIP:촉수구욕
	CALL EQUIP_COM98_2
;歌
SIF TEQUIP:연주중
	CALL EQUIP_COM302


;-------------------------------------------------
;절정処理
;ARGSが"数値표시"の時に컨피그を弄ってあるなら확률がみられる
;-------------------------------------------------
@ORGASM_ADD_A, ARGS
#DIM LCOUNT

IF FLAG:217 && ARGS == "数値표시"
	SIF CUP:쾌Ｃ+CUP:쾌Ｖ+CUP:쾌Ａ+CUP:쾌Ｂ+CUP:쾌Ｍ
		PRINTFORML 쾌감/허용치：{BASE:성감허용치여자}/{MAXBASE:성감허용치여자}
	SIF NOWEX:절정수합계 + NOWEX:가는것을참았다
		PRINTFORML 절정을 견딜 확률：{LIMIT(30*MAXBASE:성감허용치여자/(BASE:성감허용치여자 + 1), 20, 90)}％
ENDIF

SIF NOWEX:가는것을참았다 && NOWEX:럭셔리어스히트 == 0
	RETURN 1

NOWEX:가는것을참았다 = 0

;快感を受け入れられる量は１万とする

VARSET LOCAL
FOR LCOUNT, 0, 16
	SELECTCASE LCOUNT
	CASE 0 TO 2, 14, 15
	CASEELSE
		CONTINUE
	ENDSELECT

	;손대중中
	IF TEQUIP:손대중 || TEQUIP:플라트 == 1
		SELECTCASE UP_PALAM(LCOUNT)
		CASE IS >= 20000
			NOWEX:(LOCAL + 6) = 2
		CASE IS >= 10000
			NOWEX:(LOCAL + 6) = 1
		ENDSELECT
	;イくのを堪えたor손대중で게이지が1万超えたままのは, 他の절정が発生した時に同時に절정する. (ここを2回見るのでOK)
	ELSEIF CUP:LCOUNT || NOWEX:절정수합계 || NOWEX:럭셔리어스히트
		SELECTCASE UP_PALAM(LCOUNT)
		CASE IS >= 100000
			NOWEX:LOCAL = 5
			CDOWN:LCOUNT = 99000
		CASE IS >= 60000
			NOWEX:LOCAL = 4
			CDOWN:LCOUNT = 59000
		CASE IS >= 30000
			NOWEX:LOCAL = 3
			CDOWN:LCOUNT = 29000
		CASE IS >= 20000
			NOWEX:LOCAL = 2
			CDOWN:LCOUNT = 19000
		CASE IS >= 10000
			NOWEX:LOCAL = 1
			CDOWN:LCOUNT = 9000
		ENDSELECT
		SIF UP_PALAM(LCOUNT) - CDOWN:LCOUNT >= 10000 && NOWEX:LOCAL
			CDOWN:LCOUNT = UP_PALAM(LCOUNT) - 10000 + 1000*NOWEX:LOCAL
		SIF LCOUNT == 0 && CDOWN:0 && PENIS(TARGET)
			CDOWN:0 = UP_PALAM(LCOUNT)
	ENDIF
	SIF NOWEX:LOCAL
		NOWEX:(LOCAL + 6) = 0
	SIF NOWEX:(LOCAL + 6) || NOWEX:LOCAL == 0
		CDOWN:LCOUNT = 0

	LOCAL += 1
NEXT

;NOWEX:절정수합계とNOWEX:절정직전멈춤수합계を설정。요도절정직전멈춤は用意してはあるもののＣ절정직전멈춤の方が使いやすいので現状死に절정
NOWEX:절정수합계 = NOWEX:Ｃ절정 + NOWEX:Ｖ절정 + NOWEX:Ａ절정 + NOWEX:Ｂ절정 + NOWEX:Ｍ절정 + NOWEX:요도절정
NOWEX:절정직전멈춤수합계 = NOWEX:Ｃ절정직전멈춤 + NOWEX:Ｖ절정직전멈춤 + NOWEX:Ａ절정직전멈춤 + NOWEX:Ｂ절정직전멈춤 + NOWEX:Ｍ절정직전멈춤 + NOWEX:요도절정직전멈춤

;MAXは12万
MAXBASE:성감허용치여자 = MIN(ABL:욕망*5000+ABL:ＬＶ*2000+TALENT:상위몽마*5000+(JUEL:욕정+JUEL:쾌Ｃ+JUEL:쾌Ｖ+JUEL:쾌Ａ+JUEL:쾌Ｂ+JUEL:쾌Ｍ)/200, 120000)
;연속절정には弱い
SIF PALAM:연속절정COMBO수
	MAXBASE:성감허용치여자 = DIVIDE(MAXBASE:성감허용치여자, 100 + PALAM:연속절정COMBO수*10)
BASE:성감허용치여자 = UP_PALAM(0)+UP_PALAM(1)+UP_PALAM(2)+UP_PALAM(14)+UP_PALAM(15)
SIF PENIS(TARGET) && UP_PALAM(0) >= EJAC:TARGET
	BASE:성감허용치여자 *= 2

SIF ARGS == "数値표시" || NOWEX:절정수합계 == 0
	RETURN 0

;연모の人は당신の절정に合わせてくれます
IF TALENT:연모 && PLAYER == MASTER && COND("절정", PLAYER) >= MAX(2, 1 + RAND:5) && RAND:3 == 0
	EX:가는것을참았다 = 0
;럭셔리어스히트の時にはこらえることが出来ない
ELSEIF NOWEX:럭셔리어스히트 || NOWEX:정액절정
	EX:가는것을참았다 = 0
;배설していたらこらえることはできない
ELSEIF NOWEX:방뇨 || NOWEX:배설 || NOWEX:사정
	EX:가는것을참았다 = 0
;堪える확률は20%～90%＆快感が許容量を越えているなら, 堪えることが出来ない
ELSEIF MAXBASE:성감허용치여자 >= BASE:성감허용치여자 && LIMIT(30*MAXBASE:성감허용치여자/(BASE:성감허용치여자 + 1), 20, 90) > RAND:100
	EX:가는것을참았다 += 1
	NOWEX:가는것을참았다 = 1
	FOR LCOUNT, 0, 6
		NOWEX:LCOUNT = 0
	NEXT
	VARSET CDOWN
ELSE
	EX:가는것을참았다 = 0
ENDIF


@CHECK_ECST(ARG)
#FUNCTION
#DIM LCOUNT
;何重절정したかを返す
LOCAL = 0

SIF NOWEX:ARG:Ｃ절정
	LOCAL += 1
SIF NOWEX:ARG:Ｖ절정
	LOCAL += 1
SIF NOWEX:ARG:Ａ절정
	LOCAL += 1
SIF NOWEX:ARG:Ｂ절정
	LOCAL += 1
SIF NOWEX:ARG:Ｍ절정
	LOCAL += 1
SIF NOWEX:ARG:요도절정
	LOCAL += 1

RETURNF LOCAL

@ORGASM_ADD_TEXT, ARG
#DIM LCOUNT
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

LOCALS = 
FOR LCOUNT, 0, 5
	IF NOWEX:ARG:LCOUNT + NOWEX:ARG:(LCOUNT + 6)
		LOCALS = 
		SIF NOWEX:ARG:LCOUNT >= 4
			LOCALS = 초
		SIF NOWEX:ARG:LCOUNT + NOWEX:ARG:(LCOUNT + 6) >= 2
			LOCALS = %LOCALS%강

		;今回のターンの절정具合の表示で使用する
		;특수な절정
		IF LCOUNT == 0 && NOWEX:ARG:요도절정
			LOCALS = %LOCALS%절정\@NOWEX:ARG:(LCOUNT + 6) ? 직전멈춤 # \@
		ELSE
			LOCALS = %SUBSTRING(EXNAME:LCOUNT, 0, 2)%%LOCALS%절정\@NOWEX:ARG:(LCOUNT + 6) ? 직전멈춤 # \@
		ENDIF
		PRINTFORML %LOCALS% \@ ARG == ASSI ? (%TEXTS("조수の명칭")%) # \@
	ENDIF
NEXT

IF NOWEX:절정직전멈춤수합계
	LOCALS = 절정직전멈춤
;多重절정
ELSEIF CHECK_ECST(ARG) >= 2
	SELECTCASE CHECK_ECST(ARG)
	CASE 5
		LOCALS = 오 중 절 정
		SETCOLOR DEF_COLOR("하트핑크")
	CASE 4
		LOCALS = 사 중 절 정
		SETCOLOR DEF_COLOR("하트핑크")
	CASEELSE
		LOCALS = 
		IF NOWEX:ARG:요도절정
			LOCALS += "요도＆"
		ELSEIF NOWEX:ARG:Ｃ절정
			LOCALS += "Ｃ＆"
		ENDIF
		SIF NOWEX:ARG:Ｖ절정
			LOCALS += "Ｖ＆"
		SIF NOWEX:ARG:Ａ절정
			LOCALS += "Ａ＆"
		SIF NOWEX:ARG:Ｂ절정
			LOCALS += "Ｂ＆"
		SIF NOWEX:ARG:Ｍ절정
			LOCALS += "Ｍ＆"
		LOCALS = %SUBSTRING(LOCALS, 0, STRLENS(LOCALS) - 2)%절정
		SETCOLOR DEF_COLOR("황색")
	ENDSELECT

	PRINTFORML %LOCALS% \@ ARG == ASSI ? (%TEXTS("조수の명칭")%) # \@ (각 {CHECK_ECST(ARG)}배의 구슬을 얻습니다)

	RESETCOLOR
ENDIF

IF ARG == ASSI
	STR:6 = %LOCALS%
ELSE
	STR:5 = %LOCALS%
ENDIF

;堪える
IF NOWEX:ARG:가는것을참았다
	PRINTL 
	IF NOWEX:ARG:사정직전멈춤 && COND("요도사용中", ARG)
		PRINTFORML %CALLNAME:ARG%는 치밀어 오르는 정액를 어찌할 도리가 없이, 몸을 굳어지게 해 필사적으로 견디고 있다…
	ELSEIF EX:ARG:가는것을참았다 == 1 && SELECTCOM == 3
		PRINTFORML %조사처리(CALLNAME:ARG,"는")%, 더욱 더 절정을 찾으며 자신을 안달나게 하고있다…
	ELSEIF EX:ARG:가는것을참았다 == 1 && RAND:3 == 0
		PRINTFORML %조사처리(CALLNAME:ARG,"는")% 절정을 견뎌냈다! 
	ELSE
		IF EXP_GAIN(6, ARG) == 0 && EX:ARG:절정수합계 == 0 && INSERT("인서트", ARG) && TALENT:ARG:강기 && RAND:3 == 0
			PRINTFORML %조사처리(CALLNAME:ARG,"는")% %조사처리(CALLNAME:PLAYER,"를")% 먼저 가게 해주겠다며 쾌감을 견디고있다…
		ELSEIF INSERT("인서트", ARG) && RANDIF(3, COND("視力", ARG) )
			IF INSERT("Ｖ인서트", ARG)
				PRINTFORML %조사처리(CALLNAME:ARG,"는")% 질을 꽈악 조이면서, 절정에 저항하고있다…
			ELSEIF INSERT("Ａ인서트", ARG)
				PRINTFORML %조사처리(CALLNAME:ARG,"는")% 애널에서 내뿜어져 나오는 쾌감에 필사적으로 견디고있다…
			ELSEIF INSERT("요도인서트", ARG)
				PRINTFORML %CALLNAME:ARG%는 몸의 깊은 곳을 덮치는 금단의 쾌락에 필사적으로 참고 있다…
			ENDIF
		ELSEIF COND("視力", ARG) && RAND:3 == 0
			IF RAND:2
				PRINTFORML %조사처리(CALLNAME:ARG,"는")% 고통스러운 표정으로 절정을 견디고있다…
			ELSE
				PRINTFORML %조사처리(CALLNAME:ARG,"는")% 눈동자를 글썽이면서, 쾌감을 견디고있다…
			ENDIF
		ELSEIF ABL:ARG:욕망 >= 3 && RAND:3 == 0
			PRINTFORML %조사처리(CALLNAME:ARG,"는")% 뺨을 붉히면서, 한계 직전까지 절정을 견디고있다…
		ELSEIF RAND:2
			PRINTFORML %조사처리(CALLNAME:ARG,"는")% 호흡을 가다듬고, 밀려오는 절정의 파도를 견디고있다…
		ELSE
			PRINTFORML %조사처리(CALLNAME:ARG,"는")% 입가에서 군침을 흘리면서, 쾌감을 견디고있다…
		ENDIF
	ENDIF
ELSE
	LOCAL = LIMIT(MAX(MAX(NOWEX:ARG:Ｃ절정, NOWEX:ARG:Ｖ절정, NOWEX:ARG:Ａ절정, NOWEX:ARG:Ｂ절정, NOWEX:ARG:Ｍ절정) + CHECK_ECST(ARG) - TALENT:ARG:몽마 - TALENT:ARG:상위몽마, 0), 0, BASE:ARG:정력)
	SIF LOCAL > 0
		PRINTFORML 정력-{LOCAL} \@ ARG == ASSI ? (%TEXTS("조수の명칭")%) # \@
ENDIF

;表示があった場合にはWAITを入れる
SIF LINECOUNT > MEMO_LINECOUNT
	WAIT

@ORGASM_ADD_C
#DIM LCOUNT

IF NOWEX:Ｃ절정
	SOURCE:노출 += 300 * NOWEX:Ｃ절정
	SIF TALENT:Ｃ민감
		SOURCE:굴복 += 1000
	;에이스오브스페이드で절정
	SIF SELECTCOM == 80
		CALL SETFLAG, "선호체위", 80, NOWEX:Ｃ절정
	;요도절정はＣ절정を上書きする
	IF TALENT:요도성감 && COND("요도성감플래그") && CHECK("요도플레이")
		NOWEX:요도절정 = NOWEX:Ｃ절정
		NOWEX:Ｃ절정 = 0
		;조교中ならば선호체위に影響
		IF COND("조교中")
			SIF INSERT("요도인서트")
				CALL SETFLAG, "선호체위", 83, NOWEX:요도절정
		ENDIF
	ENDIF
ENDIF
IF NOWEX:Ｖ절정
	SOURCE:노출 += 500 * NOWEX:Ｖ절정
	SOURCE:굴복 += 500
	SIF TALENT:Ｖ민감
		SOURCE:굴복 += 1000
	SOURCE:욕정추가 += 500
	DOWNBASE:체력 += 10
	;조교中ならば선호체위に影響
	IF COND("조교中")
		IF INSERT("Ｖ정상위")
			CALL SETFLAG, "선호체위", 60, NOWEX:Ｖ절정
		ELSEIF INSERT("Ｖ후배위")
			CALL SETFLAG, "선호체위", 61, NOWEX:Ｖ절정
		ELSEIF INSERT("Ｖ굴곡위")
			CALL SETFLAG, "선호체위", 62, NOWEX:Ｖ절정
		ELSEIF INSERT("Ｖ대면좌위")
			CALL SETFLAG, "선호체위", 63, NOWEX:Ｖ절정
		ELSEIF INSERT("Ｖ배면좌위")
			CALL SETFLAG, "선호체위", 64, NOWEX:Ｖ절정
		ELSEIF INSERT("Ｖ기승위")
			CALL SETFLAG, "선호체위", 65, NOWEX:Ｖ절정
		ENDIF
	ENDIF
ENDIF
IF NOWEX:Ａ절정
	SOURCE:노출 += 1000 * NOWEX:Ａ절정
	SOURCE:굴복 += 2000
	SIF TALENT:Ａ민감
		SOURCE:굴복 += 1000
	SOURCE:욕정추가 += 500
	DOWNBASE:체력 += 20
	DOWNBASE:기력 += 10
	IF COND("조교中")
		IF INSERT("Ａ정상위")
			CALL SETFLAG, "선호체위", 70, NOWEX:Ａ절정
		ELSEIF INSERT("Ａ후배위")
			CALL SETFLAG, "선호체위", 71, NOWEX:Ａ절정
		ELSEIF INSERT("Ａ굴곡위")
			CALL SETFLAG, "선호체위", 72, NOWEX:Ａ절정
		ELSEIF INSERT("Ａ대면좌위")
			CALL SETFLAG, "선호체위", 73, NOWEX:Ａ절정
		ELSEIF INSERT("Ａ배면좌위")
			CALL SETFLAG, "선호체위", 74, NOWEX:Ａ절정
		ELSEIF INSERT("Ａ기승위")
			CALL SETFLAG, "선호체위", 75, NOWEX:Ａ절정
		ENDIF
	ENDIF
ENDIF
IF NOWEX:Ｂ절정
	SOURCE:노출 += 300 * NOWEX:Ｂ절정
	SIF TALENT:Ｂ민감
		SOURCE:굴복 += 1000
ENDIF
IF NOWEX:Ｍ절정
	SOURCE:정애 += 500 * NOWEX:Ｍ절정
	SIF TALENT:Ｍ민감
		SOURCE:굴복 += 1000
ENDIF

;절정した時の基本値
IF NOWEX:절정수합계
	SOURCE:굴복 += 1000 * CHECK_ECST(TARGET)
	SOURCE:욕정추가 += 300 * CHECK_ECST(TARGET)
	SOURCE:순종추가 += 500 * CHECK_ECST(TARGET)
	DOWNBASE:체력 += 10 * CHECK_ECST(TARGET)
	DOWNBASE:기력 += 10 * CHECK_ECST(TARGET)
ENDIF

;多重절정した時의 구슬の処理
IF CHECK_ECST(TARGET) >= 2
	;EASY모드で無いなら, 미약を使った場合には보너스を減らす
	IF CONFIG("ＥＡＳＹ") == 0 && TEQUIP:미약
		LOCAL = (CHECK_ECST(TARGET) - 1) * 100
	ELSE
		LOCAL = (CHECK_ECST(TARGET) - 1) * 1000
	ENDIF
	JUEL:20 += (NOWEX:Ｃ절정 + NOWEX:요도절정) * LOCAL
	JUEL:21 += NOWEX:Ｖ절정 * LOCAL
	JUEL:22 += NOWEX:Ａ절정 * LOCAL
	JUEL:23 += NOWEX:Ｂ절정 * LOCAL
	JUEL:24 += NOWEX:Ｍ절정 * LOCAL
ENDIF

;절정횟수を増やす
EX:Ｃ절정 += NOWEX:Ｃ절정
EX:Ｖ절정 += NOWEX:Ｖ절정
EX:Ａ절정 += NOWEX:Ａ절정
EX:Ｂ절정 += NOWEX:Ｂ절정
EX:Ｍ절정 += NOWEX:Ｍ절정
EX:요도절정 += NOWEX:요도절정

;절정횟수に応じて호감도増加に보너스
FOR LCOUNT, 0, 5
	;その部位の절정５回目
	SIF EX:LCOUNT >= 5 && EX:LCOUNT - NOWEX:LCOUNT < 5
		TFLAG:30 += 10
	;その部位の절정10回目
	SIF EX:LCOUNT >= 10 && EX:LCOUNT - NOWEX:LCOUNT < 10
		TFLAG:30 += 20
	;その部位の절정20回目
	SIF EX:LCOUNT >= 20 && EX:LCOUNT - NOWEX:LCOUNT < 20
		TFLAG:30 += 30
NEXT

;절정경험を増やす
EXP:절정경험 += NOWEX:절정수합계
;절정の内訳を記録
;CALL SETFLAG, "절정の記録"
;정력소비
LOCAL = LIMIT(MAX(MAX(NOWEX:Ｃ절정, NOWEX:Ｖ절정, NOWEX:Ａ절정, NOWEX:Ｂ절정, NOWEX:Ｍ절정, NOWEX:요도절정) + CHECK_ECST(TARGET) - TALENT:몽마 - TALENT:상위몽마, 0), 0, BASE:정력)
BASE:정력 -= LOCAL
TCVAR:109 += LOCAL

;절정횟수の合計も記録
EX:절정수합계 = EX:Ｃ절정 + EX:Ｖ절정 + EX:Ａ절정 + EX:Ｂ절정 + EX:Ｍ절정 + EX:요도절정
;무드に보너스
SIF NOWEX:절정수합계
	TFLAG:33 += LIMIT(NOWEX:절정수합계/2, 1, 5)


@ORGASM_ADD_B
;절정した時の処理
CALL ORGASM_ADD_C

;快感総量を増やす
PALAM:Extacy += CUP:쾌Ｃ + CUP:쾌Ｖ + CUP:쾌Ａ + CUP:쾌Ｂ + CUP:쾌Ｍ
;オバフロ対策
SIF PALAM:Extacy >= 10000000000000000
	PALAM:Extacy = 10000000000000000

;남자でないなら, 快楽に応じたＶ윤が発生
IF TALENT:남자 == 0 && CUP:쾌Ｃ+CUP:쾌Ｖ+CUP:쾌Ａ+CUP:쾌Ｂ+CUP:쾌Ｍ > 0
	SELECTCASE CUP:쾌Ｃ+CUP:쾌Ｖ+CUP:쾌Ａ+CUP:쾌Ｂ+CUP:쾌Ｍ
	CASE IS >= 10000
		LOCAL = 500
	CASE IS >= 3000
		LOCAL = 200
	CASE IS >= 500
		LOCAL = 50
	CASE IS >= 100
		LOCAL = 20
	CASEELSE
		LOCAL = 5
	ENDSELECT
	LOCAL += LIMIT(NOWEX:절정수합계*200, 0, 600)
	;Ｖ윤アップの50％を노출のソースに加える(濡れて恥ずかしい, というやつ)
	IF TALENT:젖기쉬움
		SOURCE:액체추가 += UP_PALAMLV(LOCAL)
		SOURCE:노출 += UP_PALAMLV(LOCAL) / 2
	ELSEIF TALENT:젖기어려움
		SOURCE:액체추가 += DOWN_PALAMLV(LOCAL)
		SOURCE:노출 += DOWN_PALAMLV(LOCAL) / 2
	ELSE
		SOURCE:액체추가 += LOCAL
		SOURCE:노출 += LOCAL / 2
	ENDIF
ENDIF

SIF FLAG:8
	CALL ORGASM_ADD_TEXT, TARGET

;절정による욕망ＬＶアップ
LOCAL = 0
SIF NOWEX:절정수합계
	LOCAL = 1
SIF NOWEX:Ｖ절정
	LOCAL = 2
SIF NOWEX:Ａ절정
	LOCAL = 3
SIF NOWEX:Ｃ절정 + NOWEX:요도절정 >= 4 && NOWEX:Ａ절정 >= 4
	LOCAL = 3
SIF NOWEX:Ｃ절정 + NOWEX:요도절정 >= 4 && NOWEX:Ｖ절정 >= 4 && NOWEX:Ａ절정 >= 4
	LOCAL = 4
;自制心があると１下がる
SIF TALENT:냉정 && LOCAL
	LOCAL -= 1

;多重절정等による욕망の上昇や쾌감을부정の消滅
SETCOLOR DEF_COLOR("황색")
IF ABL:욕망 < LOCAL
	ABL:욕망 = LOCAL
	PRINTFORML 그리고 욕망이 LV{LOCAL}가 되었다
	;욕망の上昇による[억압][저항]の消滅を체크
	CALL YOKUBO_UP_CHECK, "욕망"
ENDIF
IF TALENT:쾌감을부정 && NOWEX:절정수합계 >= 3
	PRINTFORML %조사처리(CALLNAME:TARGET,"는")% [%TALENTNAME:71%]을 잃었다
	TALENT:쾌감을부정 = 0

	SIF FLAG:8 && JUEL:반감 > 0
		PRINTL 반감의 구슬이 절반이 되었다
	JUEL:반감 /= 2
ENDIF
RESETCOLOR

;요도성감の獲得…めんどくさい感覚なので弄りながらのＣ절정で良いこととする
IF TALENT:요도성감 == 0 && COND("요도성감플래그") && NOWEX:Ｃ절정 + NOWEX:요도절정
	TALENT:요도성감 = 1
	PRINTFORML ……%타겟은()% 요도를 괴롭혀지면서 절정해, 새로운 경지를 열어 버린 것 같다
	PRINTFORM %타겟은()% 
	CALL PRINT_COLORTEXT, "[요도성감]", DEF_COLOR("황색")
	PRINTFORML 를 얻었다
	FORCEWAIT
ENDIF

;-------------------------------------------------
;요의게이지の増加
;1万이상だと실금の危険あり. とはいえ, 실금癖がない場合には이뇨제がないと早々漏らさない
;CV절정は溜まり易い. 
;-------------------------------------------------
LOCAL = 100 + (NOWEX:Ｃ절정 + NOWEX:Ｖ절정)*300 + NOWEX:요도절정*500 + (NOWEX:절정수합계 + NOWEX:Ｃ절정직전멈춤 + NOWEX:Ｖ절정직전멈춤 + NOWEX:요도절정직전멈춤 + NOWEX:절정직전멈춤수합계)*100

;Gスポ刺激
SIF CHECK_COM("Ｇ스팟자극") || SELECTCOM == 66
	LOCAL += 500
;실금癖（快感由来の増加量が増える）
SIF TALENT:오줌싸개버릇
	LOCAL *= 3
;질弄り
SIF CUP:쾌Ｖ
	LOCAL += 100
;クリ弄り
SIF CUP:쾌Ｃ
	LOCAL += 200
;이뇨제
SIF TEQUIP:이뇨제
	LOCAL += 2000
;弄り
SIF COND("요도성감플래그")
	LOCAL += 500

;방뇨済み
IF EXP_GAIN(31) + EXP_GAIN(33)
	LOCAL /= 5
ELSEIF STATE("드렁크", TARGET)
	LOCAL += 1000
ENDIF

BASE:요의 = MIN(BASE:요의 + MIN(LOCAL, 3000), 45000)

;PLAYERの요의増加は簡単な感じで
LOCAL = 100
SIF COND("절정", PLAYER)
	LOCAL = 2000
SIF COND("イきそう", PLAYER)
	LOCAL = 1000
;이뇨제
SIF TEQUIP:PLAYER:이뇨제
	LOCAL += 2000
;방뇨済み
IF EXP_GAIN(31, PLAYER) + EXP_GAIN(33, PLAYER)
	LOCAL /= 5
ELSEIF STATE("드렁크", PLAYER)
	LOCAL += 1000
ENDIF

BASE:PLAYER:요의 = MIN(BASE:PLAYER:요의 + MIN(LOCAL, 3000), 20000)

CALL TARGET_EJAC_CHECK
CALL TARGET_MILK_CHECK

;-------------------------------------------------
;이상경험取得
;CFLAG:11の0～19bitは처녀상실関連, 20～29bitは동정喪失関連, 30～39bitはＡ처녀상실関連, 40～49bitは처녀にかかわる이상경험
;CFLAG:12はその他色々な이상경험ごった煮
;EXP50_CHECK_ALLACTORは全部のPLAYER:0～1とTARGET:0～3について行う
;-------------------------------------------------
@EXP50_CHECK_ALLACTOR
#DIM LCOUNT

FOR LCOUNT, 1, CHARANUM
	SELECTCASE LCOUNT
	CASE PLAYER, PLAYER:1, TARGET, TARGET:1, TARGET:2, TARGET:3
		CALL EXP50_CHECK, LCOUNT
	ENDSELECT
NEXT


@EXP50_CHECK, ARG
#DIM LCOUNT
#DIM MEMO_LINECOUNT

SIF ARG < 0
	RETURN 0
SIF ARG == 0 && TARGET
	ARG = TARGET

;色は황색
SETCOLOR DEF_COLOR("노랑")

;LOCAL:50 の値ぶん, 이상경험を得る
VARSET LOCAL

MEMO_LINECOUNT = LINECOUNT

;初めて처녀상실
IF TALENT:ARG:처녀 == 1 && COND("처녀상실플래그", ARG) && GETBITS(CFLAG:ARG:11, 0, 19) == 0
	TALENT:ARG:처녀 = 0
	PRINT ＜
	LOCAL = 0
	IF TEQUIP:ARG:촉수
		PRINT 촉수로 
		LOCAL += 3
		SETBIT CFLAG:ARG:11, 8
	ELSEIF TEQUIP:ARG:바이브
		IF SIZE("바이브", ARG) >= 3
			PRINT 극태 
			LOCAL += 1
			SETBIT CFLAG:ARG:11, 0
		ENDIF
		PRINT 바이브로 
		LOCAL += 1
		SETBIT CFLAG:ARG:11, 1
	;3P
	;ELSEIF 
	;	PRINT ３Ｐ로 
	;	LOCAL += 1
	;	SETBIT CFLAG:ARG:11, 2
	ELSEIF INSERT("Ｖ인서트", ARG)
		IF PENIS(INSERT("Ｖ인서트상대", ARG) ) == 0
			IF INSERT("Ｖ인서트상대", ARG) == ASSI
				PRINTFORM %TEXTS("조수の명칭")%의 페니반
				SETBIT CFLAG:ARG:11, 3
			ELSE
				PRINT 페니스밴드
				SETBIT CFLAG:ARG:11, 4
			ENDIF
			LOCAL += 1
		ENDIF
		;기승위で처녀상실
		IF INSERT("기승위", ARG)
			IF PENIS(INSERT("Ｖ인서트상대", ARG) ) == 0
				PRINT 에 올라타 
			ELSE
				PRINT 기승위로 
			ENDIF
			LOCAL += 1
			SETBIT CFLAG:ARG:11, 5
		ELSEIF PENIS(INSERT("Ｖ인서트상대", ARG) ) == 0
			PRINT 로 
			SETBIT CFLAG:ARG:11, 6
		ENDIF
	ENDIF
	PRINT 처녀상실

	;처녀상실
	NOWEX:ARG:처녀상실 = 1
	;연모状態で주인との섹스で처녀상실した
	IF ARG == TARGET && TALENT:연모 && PLAYER == MASTER && MARK:반발각인 < 3
		IF (SELECTCOM >= 60 && SELECTCOM <= 69) || SELECTCOM == 177
			SETBIT NOWEX:처녀상실, 1
			;무드大幅アップ
			TFLAG:33 += 50
		ENDIF
	ENDIF
	EX:ARG:처녀상실 = NOWEX:ARG:처녀상실
	
	;처녀상실플래그(ビデオ촬영)
	IF TEQUIP:ARG:비디오카메라
		PRINT (촬영)
		LOCAL += 1
		SETBIT CFLAG:ARG:11, 7
	ENDIF
	PRINTL ＞
	IF LOCAL
		LOCAL:50 += LOCAL
	ELSE
		CFLAG:ARG:11 = 0
	ENDIF
	FOR LCOUNT, 1, CHARANUM
		SIF COND("처녀탈취플래그", LCOUNT) == 0
			CONTINUE

		EXP:LCOUNT:처녀탈취경험 += 1
		SETCOLOR DEF_COLOR("노랑")
		IF ARG == MASTER
			EXP:LCOUNT:경험치 += 1000
			SETBIT TALENT:LCOUNT:첫사람, 1
			PRINTFORML %조사처리(CALLNAME:LCOUNT,"는")% %CALLNAME:MASTER%의 %조사처리(TALENTNAME:94,"가")% 되었다
			PRINTFORML 경험치＋1000
		ELSE
			EXP:LCOUNT:경험치 += 50
			PRINTFORML 경험치＋50
		ENDIF
		RESETCOLOR
	NEXT
;再生처녀상실
ELSEIF TALENT:ARG:처녀 == 2 && COND("처녀상실플래그", ARG)
	TALENT:ARG:처녀 = 0
	PRINT ＜처녀상실
	;처녀상실
	NOWEX:ARG:처녀상실 = 1
	;연모状態で주인との섹스で처녀상실した
	IF ARG == TARGET && TALENT:연모 && PLAYER == MASTER && MARK:반발각인 < 3
		SIF (SELECTCOM >= 60 && SELECTCOM <= 69) || SELECTCOM == 177
			SETBIT NOWEX:처녀상실, 1
	ENDIF
	EX:ARG:처녀상실 = NOWEX:ARG:처녀상실

	;처녀상실플래그(ビデオ촬영)
	SIF TEQUIP:ARG:비디오카메라
		PRINT (촬영)
	PRINTL ＞
ENDIF

;동정喪失
IF TALENT:ARG:동정 && COND("동정喪失플래그", ARG)
	PRINT ＜동정상실
	SELECTCASE ARG
	CASE MASTER
		PRINTFORM (%CALLNAME:MASTER%)
	CASE ASSI
		PRINTFORM (%TEXTS("조수の명칭")%)
	CASE IS > 0
		PRINTFORM (%CALLNAME:ARG%)
	ENDSELECT
	PRINTL ＞
	TALENT:ARG:동정 = 0
	FOR LCOUNT, 1, CHARANUM
		SIF COND("동정狩り플래그", LCOUNT) == 0
			CONTINUE

		EXP:LCOUNT:동정탈취경험 += 1
		SETCOLOR DEF_COLOR("노랑")
		IF ARG == MASTER
			EXP:LCOUNT:경험치 += 1000
			SETBIT TALENT:LCOUNT:첫사람, 0
			PRINTFORML %조사처리(CALLNAME:LCOUNT,"는")% %CALLNAME:MASTER%의 %조사처리(TALENTNAME:94,"가")% 되었다
			PRINTFORML 경험치＋1000
		ELSE
			EXP:LCOUNT:경험치 += 50
			PRINTFORML 경험치＋50
		ENDIF
		RESETCOLOR
	NEXT
ENDIF

;동정なのに애널섹스
IF TALENT:ARG:동정 && EXP_GAIN(16, ARG) && GETBIT(CFLAG:ARG:11, 20) == 0
	PRINTL ＜이상경험：동정인데 애널섹스경험＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:11, 20
ENDIF
;애널섹스
IF TALENT:ARG:Ａ처녀 && COND("Ａ처녀상실플래그", ARG) + EXP_GAIN(13, ARG) && GETBIT(CFLAG:ARG:11, 32) == 0
	TALENT:ARG:Ａ처녀 = 0
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:11, 32
	;애널섹스を先に경험
	IF TALENT:ARG:처녀 == 1 && EXP:ARG:Ｖ성교경험 == 0 && GETBIT(CFLAG:ARG:11, 30) == 0
		LOCAL:50 += 1
		SETBIT CFLAG:ARG:11, 30
	ENDIF
	;애널섹스を先に경험 その２
	IF (TALENT:ARG:남자 || TALENT:ARG:후타나리 == 1) && TALENT:ARG:동정 && GETBIT(CFLAG:ARG:11, 31) == 0
		LOCAL:50 += 1
		SETBIT CFLAG:ARG:11, 31
	ENDIF
	PRINTFORM ＜이상경험：
	IF GETBIT(CFLAG:ARG:11, 30) || GETBIT(CFLAG:ARG:11, 31)
		SIF GETBIT(CFLAG:ARG:11, 30)
			PRINTFORM 처녀
		SIF GETBIT(CFLAG:ARG:11, 30) && GETBIT(CFLAG:ARG:11, 31)
			PRINTFORM , 
		SIF GETBIT(CFLAG:ARG:11, 31)
			PRINTFORM 동정
		PRINTFORM 보다 먼저 
	ENDIF
	PRINTFORML Ａ처녀상실＞
ENDIF

;공개자위
IF TEQUIP:ARG:비디오카메라 && COND("자위플래그", ARG) && GETBIT(CFLAG:ARG:12, 0) == 0
	PRINTL ＜이상경험：공개자위＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 0
ENDIF

;야외で알몸
IF TEQUIP:ARG:야외플레이 && CHECK_CLO("알몸", ARG) && GETBIT(CFLAG:ARG:12, 1) == 0 && CLO_ORI(ARG, "옷", "슈츠", "겉옷", "下着")
	PRINTL ＜이상경험：야외전라＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 1
ENDIF
;公開제모
IF TEQUIP:ARG:비디오카메라 && COND("제모플래그", ARG) && GETBIT(CFLAG:ARG:12, 2) == 0
	PRINTL ＜이상경험：공개제모＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 2
ENDIF
;公開방뇨
IF TEQUIP:ARG:비디오카메라 && NOWEX:ARG:방뇨 + NOWEX:ARG:절정실금 && GETBIT(CFLAG:ARG:12, 3) == 0
	PRINTL ＜이상경험：공개방뇨＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 3
ENDIF
;性器ピアス
IF COND("피어스ＯＮ플래그", ARG) && COND("클리피어스플래그", ARG) + COND("라비아피어스플래그", ARG) + COND("니플피어스플래그", ARG) && GETBIT(CFLAG:ARG:12, 4) == 0
	PRINTL ＜이상경험：성기 피어싱＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 4
ENDIF
;배설
IF NOWEX:ARG:배설
	LOCAL = 0
	;배설
	IF GETBIT(CFLAG:ARG:12, 5) == 0
		LOCAL += 1
		SETBIT CFLAG:ARG:12, 5
	ENDIF
	;야외배설
	IF GETBIT(CFLAG:ARG:12, 6) == 0 && TEQUIP:야외플레이
		LOCAL += 1
		SETBIT CFLAG:ARG:12, 6
	ENDIF
	;배설記録
	IF GETBIT(CFLAG:ARG:12, 7) == 0 && TEQUIP:비디오카메라
		LOCAL += 1
		SETBIT CFLAG:ARG:12, 7
	ENDIF
	SIF LOCAL
		PRINTFORML ＜이상경험：\@TEQUIP:ARG:야외플레이 ? 야외 # \@배설\@TEQUIP:ARG:비디오카메라 ? 기록 # \@＞
	LOCAL:50 += LOCAL
ENDIF
;애널봉사
IF COND("Ａ봉사플래그", ARG) && GETBIT(CFLAG:ARG:12, 8) == 0
	PRINTL ＜이상경험：애널봉사＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 8
ENDIF
;애널の汚れをお청소
IF COND("Ａ청소펠라플래그", ARG) && GETBIT(CFLAG:ARG:12, 9) == 0
	PRINTL ＜이상경험：청소펠라（Ａ）＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 9
ENDIF

;初めての２穴責め
IF INSERT("양구멍삽입", ARG) && GETBIT(CFLAG:ARG:12, 10) == 0
	PRINTL ＜이상경험：양구멍삽입＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 10
ENDIF
;Ｖ확장
IF EXP_GAIN(53, ARG) && ABL:ARG:Ｖ확장 - (TALENT:ARG:출산경험 > 0) + TALENT:ARG:요정 >= 3 && GETBIT(CFLAG:ARG:12, 11) == 0
	PRINTL ＜이상경험：Ｖ확장＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 11
ENDIF
;Ａ확장
IF EXP_GAIN(57, ARG) && ABL:ARG:Ａ확장 + TALENT:ARG:요정 >= 3 && GETBIT(CFLAG:ARG:12, 12) == 0
	PRINTL ＜이상경험：Ａ확장＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 12
ENDIF
;피스트퍽
IF EXP_GAIN(66, ARG) + EXP_GAIN(67, ARG) && GETBIT(CFLAG:ARG:12, 13) == 0
	PRINTL ＜이상경험：피스트퍽＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 13
ENDIF
;용간
IF EXP_GAIN(61, ARG) && GETBIT(CFLAG:ARG:12, 14) == 0
	PRINTL ＜이상경험：용간＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 14
ENDIF
;多重절정
IF CHECK_ECST(ARG) == 5 && GETBIT(CFLAG:ARG:12, 16) == 0
	PRINTL ＜이상경험：오중절정＞
	LOCAL:50 += 1
	SIF GETBIT(CFLAG:ARG:12, 15) == 0
		LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 15
	SETBIT CFLAG:ARG:12, 16
ELSEIF CHECK_ECST(ARG) == 4 && GETBIT(CFLAG:ARG:12, 15) == 0
	PRINTL ＜이상경험：사중절정＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 15
ENDIF
;자궁절정(多重절정の時にも起きてしまうが, 良い事とする)
IF COND("자궁責め플래그", ARG) && TALENT:ARG:자궁성감 && NOWEX:ARG:Ｖ절정 >= 3 && GETBIT(CFLAG:ARG:12, 17) == 0
	PRINTL ＜이상경험：자궁절정＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 17
ENDIF
;질よりも애널の方が경험豊富
IF INSERT("Ａ인서트", ARG) && EXP:ARG:Ａ경험 >= MAX(EXP:ARG:Ｖ경험, 150) && EXP:ARG:Ａ성교경험 >= MAX(EXP:ARG:Ｖ성교경험, 50) && ABL:ARG:Ａ감각 >= MAX(ABL:ARG:Ｖ감각, 3) && GETBIT(CFLAG:ARG:12, 18) == 0
	PRINTL ＜이상경험：애널퍽커＞
	LOCAL:50 += 1
	SIF TALENT:ARG:처녀 == 1
		LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 18
ENDIF
;요정に미니멈なしで인서트
IF TALENT:ARG:요정 && INSERT("인서트", ARG) && SIZE("페니스", INSERT("인서트상대", ARG) ) && GETBIT(CFLAG:ARG:12, 19) == 0
	PRINTL ＜이상경험：미니멈불필요＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 19
ENDIF
;새드の本領発揮
;IF CUP:ARG:고통 >= 4500 && GETBIT(CFLAG:ARG:12, 20) == 0
;	PRINTL ＜이상경험：【ＳＭ】＞
;	LOCAL:50 += 1
;	SETBIT CFLAG:ARG:12, 20
;ENDIF
;바늘다루기
IF COND("바늘다루기플래그", ARG) && GETBIT(CFLAG:ARG:12, 21) == 0
	PRINTL ＜이상경험：【바늘다루기】＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 21
ENDIF
;耐え難い고통
IF PALAM:ARG:고통 + CUP:ARG:고통 >= 50000 && GETBIT(CFLAG:ARG:12, 22) == 0
	PRINTL ＜이상경험：견디기 힘든 고통＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 22
ENDIF
;望まぬ임신…연모で無いときに, 반발각인Lv3or반항적or촉수で임신の時
IF TALENT:ARG:임신 && TALENT:ARG:연모 == 0 && GETBIT(CFLAG:ARG:12, 23) == 0
	IF COND("정액의우세", ARG) == -3 && MARK:ARG:용간각인 == 2
	ELSEIF MARK:ARG:반발각인 == 3 || TALENT:ARG:반항적 || (COND("정액의우세", ARG) == -5 && TALENT:ARG:구지배자의자취 == 0)
		PRINTL ＜이상경험：원치 않은 임신＞
		LOCAL:50 += 1
		SETBIT CFLAG:ARG:12, 23
	ENDIF
ENDIF
;촉수
IF EXP_GAIN(55, ARG) && TALENT:ARG:구지배자의자취 == 0 && GETBIT(CFLAG:ARG:12, 24) == 0
	PRINTL ＜이상경험：촉수경험＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 24
ENDIF
;후타나리(処理はSHOPでも行う)
IF TALENT:ARG:후타나리 == 2 && TALENT:ARG:구현 == 0 && GETBIT(CFLAG:ARG:12, 25) == 0
	PRINTL ＜이상경험：후타나리화＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 25
ENDIF
;初めての분유で, 임신由来の분유体質で無い
IF EXP_GAIN(54, ARG) && TALENT:ARG:모유체질 != 1 && TALENT:ARG:임신 == 0 && GETBIT(CFLAG:ARG:12, 26) == 0
	PRINTL ＜이상경험：분유경험＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 26
ENDIF
;몽마화
IF COND("몽마化플래그", ARG) && GETBIT(CFLAG:ARG:12, 27) == 0
	PRINTL ＜이상경험：몽마화＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 27
ENDIF
;성전환
IF COND("성전환플래그", ARG) && GETBIT(CFLAG:ARG:12, 28) == 0
	PRINTL ＜이상경험：성반전＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 28
ENDIF
;호접지몽
IF COND("호접지몽플래그", ARG) && GETBIT(CFLAG:ARG:12, 29) == 0
	PRINTL ＜이상경험：호접지몽＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 29
ENDIF
;자궁
IF COND("자궁삽입플래그", ARG) && EXP_GAIN(68, ARG) && GETBIT(CFLAG:ARG:12, 30) == 0
	PRINTL ＜이상경험：자궁삽입＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 30
ENDIF
;니플퍽
IF EXP_GAIN(45, ARG) && GETBIT(CFLAG:ARG:12, 31) == 0
	PRINTL ＜이상경험：니플퍽＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 31
ENDIF
;요도확장
IF EXP_GAIN(77, ARG) && GETBIT(CFLAG:ARG:12, 32) == 0
	PRINTL ＜이상경험：요도확장＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 32
ENDIF
;요도처녀상실
IF EXP_GAIN(78, ARG) && GETBIT(CFLAG:ARG:12, 33) == 0
	PRINTL ＜이상경험：요도처녀상실＞
	LOCAL:50 += 1
	SETBIT CFLAG:ARG:12, 33
ENDIF

IF LOCAL:50
	PRINTFORML 이상경험＋{LOCAL:50}\@ ARG != TARGET ? （%CALLNAME:ARG%） # \@
	EXP:ARG:이상경험 += LOCAL:50
ENDIF

SIF LINECOUNT > MEMO_LINECOUNT
	FORCEWAIT

RESETCOLOR

;-------------------------------------------------
;조교ソースの表示
;-------------------------------------------------
@SHOW_SOURCE
VARSET LOCALS
SIF TCVAR:120 > 0
	LOCALS = %LOCALS%쾌Ｃ({TCVAR:120})
SIF TCVAR:121 > 0
	LOCALS = %LOCALS%쾌Ｖ({TCVAR:121})
SIF TCVAR:122 > 0
	LOCALS = %LOCALS%쾌Ａ({TCVAR:122})
SIF TCVAR:137 > 0
	LOCALS = %LOCALS%쾌Ｂ({TCVAR:137})
SIF TCVAR:138 > 0
	LOCALS = %LOCALS%쾌Ｍ({TCVAR:138})
SIF TCVAR:123 > 0
	LOCALS = %LOCALS%정애({TCVAR:123})
SIF TCVAR:124 > 0
	LOCALS = %LOCALS%성행위({TCVAR:124})
SIF TCVAR:125 > 0
	LOCALS = %LOCALS%달성감({TCVAR:125})
SIF TCVAR:126 > 0
	LOCALS = %LOCALS%아픔({TCVAR:126})
SIF TCVAR:127 > 0
	LOCALS = %LOCALS%중독충족({TCVAR:127})
SIF TCVAR:128 > 0
	LOCALS = %LOCALS%불결({TCVAR:128})
SIF TCVAR:129 > 0
	LOCALS = %LOCALS%공포({TCVAR:129})
SIF TCVAR:130 > 0
	LOCALS = %LOCALS%액체추가({TCVAR:130})
SIF TCVAR:139 > 0
	LOCALS = %LOCALS%Ａ액체추가({TCVAR:139})
SIF TCVAR:131 > 0
	LOCALS = %LOCALS%욕정추가({TCVAR:131})
SIF TCVAR:136 > 0
	LOCALS = %LOCALS%순종추가({TCVAR:136})
SIF TCVAR:132 > 0
	LOCALS = %LOCALS%노출({TCVAR:132})
SIF TCVAR:133 > 0
	LOCALS = %LOCALS%굴복({TCVAR:133})
SIF TCVAR:134 > 0
	LOCALS = %LOCALS%일탈({TCVAR:134})
SIF TCVAR:135 > 0
	LOCALS = %LOCALS%반감추가({TCVAR:135})

SIF LOCALS != ""
	PRINTFORML %LOCALS%

