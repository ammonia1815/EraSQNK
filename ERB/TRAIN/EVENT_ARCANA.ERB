@COM_ARCANA, ARG
;아르카나の영창

CALL PRINT_TRAIN_NAME(ARG)

CALL MAGIC_CASTER_SELECT, ARG

SIF REFUSE_CHECK()
	RETURN 0

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

CALL KOJO_MESSAGE_COM
;Ｗ넘어뜨리기等での구상処理
CALL KOJO_MESSAGE_COM_MULTI

CALL SETFLAG, "今回조교커맨드"

;-------------------------------------------------
;아르카나の이벤트一括処理
;ARG=아르카나番号、ARG:1=詠唱者、ARG:2=대상
;조교커맨드で呼ばれた際には@MAGIC_CASTER_SELECTを経るのでARG:1とARG:2は自動的に決定される(TCVAR:22とTCVAR:23から逆算)が、他の場所で呼ぶ際にはARG:1とARG:2は必ず指定すること
;텍스트出力にはPRINT_STRを用いる
;-------------------------------------------------
@TRAIN_MESSAGE_ARCANA, ARG, ARG:1, ARG:2
#DIM LCOUNT
;詠唱者
#DIM CASTER
;대상
#DIM MAGIC_TARGET
;페니스サイズの変化をみる
#DIM MEMO_PSIZE
;出力する텍스트
#DIMS MESSAGE
#DIMS MESSAGE_RESIST

;아르카나詠唱者フラグ（ARGは아르카나番号）
IF ARG:1 > 0
	TCVAR:(ARG:1):23 = ARG
ELSE
	ARG:1 = FINDCHARA(TCVAR:23, ARG)
ENDIF
;아르카나대상フラグ（ARGは아르카나番号）
IF ARG:2 > 0
	TCVAR:(ARG:2):22 = ARG
ELSE
	ARG:2 = FINDCHARA(TCVAR:22, ARG)
ENDIF

;括弧を多用するのを避けるために代入
CASTER = ARG:1
MAGIC_TARGET = ARG:2


MESSAGE = 
MESSAGE_RESIST = 

SELECTCASE ARG
;아르카나
CASE 230 TO 259
	MESSAGE = %MESSAGE%＜%TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)%＞_L_
CASE 312
	MESSAGE = %MESSAGE%＜구현＞_
ENDSELECT

SELECTCASE ARG
;바인드
CASE 240
	SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
	CASE "트레일루트"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%는 땅바닥에 뿌리박았다！_L_%CALLNAME:MAGIC_TARGET%의 신체에 뿌리가 얽힌다！_W_
	CASE "꼬리감기"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%는 꼬리로 %CALLNAME:MAGIC_TARGET%의 전신을 휘감아 왔다！_W_
	CASE "퀸즈본디지"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%는 %CALLNAME:MAGIC_TARGET%에게 채찍을 옭아매 왔다！_W_
	CASE "와인딩웹"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%는 %CALLNAME:MAGIC_TARGET%에게 실을 옭아매 왔다！_W_
	CASE "파워바인드"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%는 힘주어 %CALLNAME:MAGIC_TARGET%를 붙잡았다！_L_%CALLNAME:MAGIC_TARGET%의 몸이 끈으로 구속되어 간다！_W_
	CASE "페트로아이즈"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%는 요사한 시선으로 %CALLNAME:MAGIC_TARGET%을 노려봤다！_W_
	CASE "바인드코드"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%는 무수한 코드를 쐈다！_L_%CALLNAME:MAGIC_TARGET% 몸이 구속되어 간다！_W_
	CASE "원더트립"
		MESSAGE = %MESSAGE%「吹・ｖ黴・ゆ・ｯり等啓ｍ・凍・麾槙・・A金-黴・痘u・hｕ操÷逐枕装・br>哩循魔…！」_W_
		MESSAGE = %MESSAGE%이상한 목소리로 %CALLNAME:CASTER%는 큰 소리를 질렀다！_L_눈앞의 경치가 일그러져 간다！_W_
	CASEELSE
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%는 %TRAINNAME:ARG%를 주창했다…_W_
	ENDSELECT

	SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
	CASE "바인드", "페트로아이즈"
		MESSAGE_RESIST = %CALLNAME:MAGIC_TARGET%에게는 효과가 없었다！
	CASEELSE
		MESSAGE_RESIST = %CALLNAME:MAGIC_TARGET%는 재빠르게 뿌리쳤다！
	ENDSELECT
;비젼
CASE 243
	;TARGETが使った際にはＷ넘어뜨리기へ
	SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
	CASE "분신"
		MESSAGE = %MESSAGE%「ふっ……」_L_%CALLNAME:CASTER%は분신した！_W_
		SIF CASTER == TARGET
			MESSAGE = %MESSAGE%二人がかりで%CALLNAME:MAGIC_TARGET%を翻弄しようとしてくる！_W_
		MESSAGE_RESIST = %CALLNAME:MAGIC_TARGET%は素早く振り払った！
	CASE "分裂"
		MESSAGE = %MESSAGE%「え～い、ぷるるん분신の術～～っ！」_L_なんと%CALLNAME:CASTER%は分裂した！_W_
		SIF CASTER == TARGET
			MESSAGE = %MESSAGE%二体の%CALLNAME:CASTER%がニコニコ笑顔で%CALLNAME:MAGIC_TARGET%に抱きついてくる！_W_
		MESSAGE_RESIST = %CALLNAME:MAGIC_TARGET%は素早く振り払った！
	CASEELSE
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%は%TRAINNAME:ARG%を唱えた…_W_
		SIF CASTER == TARGET
			MESSAGE = %MESSAGE%作り出された虚像と共に%CALLNAME:CASTER%が襲い掛かってくる！_W_
		MESSAGE_RESIST = %CALLNAME:MAGIC_TARGET%には効かなかった！
	ENDSELECT

;아르카나
CASE 230 TO 259
	MESSAGE = %MESSAGE%%CALLNAME:CASTER%は%TRAINNAME:ARG%を唱えた…_W_
	MESSAGE_RESIST = %CALLNAME:MAGIC_TARGET%には効かなかった！
CASEELSE
	MESSAGE = %MESSAGE%%CALLNAME:CASTER%は%TRAINNAME:ARG%の能力を使った…_W_
	MESSAGE_RESIST = %CALLNAME:MAGIC_TARGET%には効かなかった！
ENDSELECT

IF CONDS("카운터", CASTER) == "아르카나"
	SELECTCASE ARG
	CASE 230, 231, 240, 243
		CALL PRINT_STR, MESSAGE
		MESSAGE = 
		PRINTFORML 저항를 시도합니까？
		PRINTFORML [ 0] 네
		PRINTFORML [ 1] 아니오
		IF CONFIG("제한시간철폐")
			CALL INPUT_SELECT, 2
			IF RESULT == 0
				PRINTFORML ＜레지스터 성공！＞
				PRINTFORMW %MESSAGE_RESIST%
				MESSAGE_RESIST = レジスト
			ENDIF
		ELSE
			TINPUT MAX(4000 + 200*(ABL:MAGIC_TARGET:ＬＶ - ABL:CASTER:ＬＶ), 2000), 1, 1
			IF RESULT == 0 && LIMIT(70 + 3*(ABL:MAGIC_TARGET:ＬＶ - ABL:CASTER:ＬＶ), 50, 95) > RAND:100
				PRINTFORML ＜레지스터 성공！＞
				PRINTFORMW %MESSAGE_RESIST%
				MESSAGE_RESIST = レジスト
			ELSEIF RESULT == 0
				PRINTFORML ＜레지스터 실패＞
			ENDIF
		ENDIF
	ENDSELECT
ENDIF

IF MESSAGE_RESIST != "レジスト"
	SELECTCASE ARG
	CASE 230
		IF STATE("인라지", MAGIC_TARGET)
			IF TALENT:MAGIC_TARGET:항상인라지 == 0
				MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の페니스の大きさが元に戻った！_W_
				CALL REMOVE_STATE, MAGIC_TARGET, "인라지"
			ENDIF
		ELSE
			MESSAGE = %MESSAGE%なんと%CALLNAME:MAGIC_TARGET%の페니스が小さくなってしまった！_W_
			CALL ADD_STATE, MAGIC_TARGET, "미니멈"
		ENDIF
	CASE 231
		MESSAGE = %MESSAGE%なんと%CALLNAME:MAGIC_TARGET%の身体は敏感になってしまった！_W_
		CALL ADD_STATE, MAGIC_TARGET, "센시브"
	CASE 232
		;페니스サイズの記憶
		MEMO_PSIZE = SIZE("페니스", MAGIC_TARGET)
		CALL ADD_STATE, MAGIC_TARGET, "우르즈군"
		IF PENIS(MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の정력が極限まで強められ、硬度を増した페니스が天を衝く！_W_
			;페니스もでかくなった！
			SIF SIZE("페니스", MAGIC_TARGET) > MEMO_PSIZE
				MESSAGE = %MESSAGE%その大きさは先程までとは比べ物にならない！！_W_
		ELSE
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の정력が極限まで強められる！_W_
		ENDIF
	CASE 233
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の매력が高まる！_W_
		CALL ADD_STATE, MAGIC_TARGET, "시길군"
	CASE 234
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の精神力が極限まで強められる！_W_
		CALL ADD_STATE, MAGIC_TARGET, "에올군"
	CASE 235
		SIF BASE:MASTER:스톡 < MAXBASE:MASTER:스톡
			MESSAGE = %MESSAGE%%CALLNAME:MASTER%の스톡が１つ회복した！_W_

		SELECTCASE MAXBASE:MASTER:정력 - BASE:MASTER:정력
		CASE IS >= 20
			MESSAGE = %MESSAGE%%CALLNAME:MASTER%の정력が20회복した！_W_
		CASE IS != 0
			MESSAGE = %MESSAGE%%CALLNAME:MASTER%の정력が全快した！_W_
		ENDSELECT
		CALL CALC, "정력회복", 20, MASTER
		BASE:MASTER:스톡 = MIN(BASE:MASTER:스톡 + 1, MAXBASE:MASTER:스톡)
	CASE 236
		SELECTCASE MAXBASE:체력 - BASE:체력
		;회복するまでもない
		CASE IS <= 0
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%には効果がなかった！_W_
		CASE IS <= 300
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の체력が全회복した！_W_
			CALL CALC, "체력회복", 300, MAGIC_TARGET
		CASEELSE
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の체력が300회복した！_W_
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の체력+300 ({BASE:MAGIC_TARGET:체력} → {BASE:MAGIC_TARGET:체력 + 300} /{MAXBASE:MAGIC_TARGET:체력})_W_
		ENDSELECT
		CALL CALC, "체력회복", 300, MAGIC_TARGET
	CASE 237
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は連続行動ができるようになった！_W_
		CALL ADD_STATE, MAGIC_TARGET, "투와이스"
	CASE 238
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の高められた能力が元に戻った！_W_
		CALL REMOVE_STATE, MAGIC_TARGET, "군"
	CASE 239
		SIF STATE("바인드", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の体の구속が解けた！_W_
		SIF TALENT:MAGIC_TARGET:항상미니멈 == 0 && STATE("미니멈", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の페니스が元のサイズに戻った！_W_
		SIF TALENT:MAGIC_TARGET:항상센시브 == 0 && STATE("센시브", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の感じやすくなった神経が鎮まった！_W_
		SIF STATE("비젼", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は幻覚から해방された！_W_
		IF STATE("황홀", MAGIC_TARGET) || STATE("드렁크", MAGIC_TARGET) || STATE("술주정", MAGIC_TARGET) || STATE("흐물흐물", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は我を取り戻した！_W_
			;MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は押し倒され状態から復帰した！_W_
		ENDIF
		SIF STATE("피블", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は本来の実力を取り戻した！_W_
		CALL REMOVE_STATE, MAGIC_TARGET, "バッド"
	CASE 240
		IF CONDS("카운터", CASTER) == "아르카나"
			SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
			CASE "원더트립"
				MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は体の自由が利かなくなってしまった！_W_
				CALL ADD_STATE, MAGIC_TARGET, "바인드：손"
				CALL ADD_STATE, MAGIC_TARGET, "바인드：다리"
			CASEELSE
				IF COND("손가락 사용", MAGIC_TARGET) && STATE("바인드：손", MAGIC_TARGET) == 0
					MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は両손を封じられた！_W_
					CALL ADD_STATE, MAGIC_TARGET, "바인드：손"
				ENDIF
				IF COND("다리 사용", MAGIC_TARGET) && STATE("바인드：다리", MAGIC_TARGET) == 0
					MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は両발を封じられた！_W_
					CALL ADD_STATE, MAGIC_TARGET, "바인드：다리"
				ENDIF
				IF ABL:CASTER:새드끼 && ABL:CASTER:욕망 >= 3 && PENIS(MAGIC_TARGET) && STATE("바인드：Ｐ", MAGIC_TARGET) == 0 && TEQUIP:MAGIC_TARGET:31 == 0
					MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は페니스を封じられた！_W_

					SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
					CASE "바인드"
						MESSAGE = %MESSAGE%見えない締め付けが사정するのをせき止める！_W_
					CASE "페트로아이즈"
						MESSAGE = %MESSAGE%見えない力が사정するのをせき止める！_W_
						MESSAGE = %MESSAGE%加えて、%CALLNAME:MAGIC_TARGET%の페니스は石のように硬くされてしまった！_W_
						CALL ADD_STATE, MAGIC_TARGET, "하드스킨"
					CASEELSE
						MESSAGE = %MESSAGE%締め付けのせいで、사정する事ができない！_W_
					ENDSELECT

					CALL ADD_STATE, MAGIC_TARGET, "바인드：Ｐ"
				ENDIF
			ENDSELECT
		ENDIF
	CASE 241
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%のあらゆる能力が高まった！_W_
		CALL ADD_STATE, MAGIC_TARGET, "우르즈군"
		CALL ADD_STATE, MAGIC_TARGET, "시길군"
		CALL ADD_STATE, MAGIC_TARGET, "에올군"
	CASE 242
		IF STATE("미니멈", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の페니스の大きさが元に戻った！_W_
			CALL REMOVE_STATE, MAGIC_TARGET, "미니멈"
		ELSE
			MESSAGE = %MESSAGE%なんと%CALLNAME:MAGIC_TARGET%の페니스が大きくなってしまった！_W_
			CALL ADD_STATE, MAGIC_TARGET, "인라지"
		ENDIF
	CASE 243
		;TARGETが使った際にはＷ넘어뜨리기へ
		IF CASTER == TARGET
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は%CALLNAME:CASTER%達に押し倒されてしまった！_W_

			;本来の비젼は終了
			CALL REMOVE_STATE, MAGIC_TARGET, "비젼"

			;虚像作成
			ADDCOPYCHARA TARGET
			TARGET:1 = CHARANUM - 1
			;虚像は後で消える
			CALL SET_CEVENT, "消滅予定", TARGET:1
			CALLNAME:(TARGET:1) = %CALLNAME:(TARGET:1)%Ｂ
			NAME:(TARGET:1) = %NAME:(TARGET:1)%Ｂ

			SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
			CASE "분신"
				CALL SET_CEVENT, "분신", TARGET:1
			CASE "分裂"
				CALL SET_CEVENT, "分裂", TARGET:1
			CASEELSE

			ENDSELECT

			CALL SETFLAG, "Ｗ넘어뜨리기"
		ELSE
			SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
			CASE "분신"
				MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は%CALLNAME:CASTER%の분신に襲われた！_W_
			CASE "分裂"
				MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は%CALLNAME:CASTER%の分裂体に襲われた！_W_
			CASEELSE
				MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は몽마の幻覚に襲われた！_W_
			ENDSELECT

			CALL ADD_STATE, MAGIC_TARGET, "비젼"
		ENDIF
	CASE 312
		IF STATE("구현", MAGIC_TARGET)
			CALL REMOVE_STATE, MAGIC_TARGET, "구현"
			MESSAGE = %MESSAGE%구현の能力で生やした%CALLNAME:MAGIC_TARGET%の페니스が消滅した…_W_
			CALL SETFLAG, "구현化ＯＦＦ플래그", MAGIC_TARGET
		ELSE
			CALL ADD_STATE, MAGIC_TARGET, "구현"
			MESSAGE = %MESSAGE%なんと%CALLNAME:MAGIC_TARGET%の가랑이から
			SELECTCASE SIZE("페니스", MAGIC_TARGET)
			CASE IS >= 3
				MESSAGE = %MESSAGE%凶悪な大きさの
			CASE 2
				MESSAGE = %MESSAGE%ほれぼれする大きさの
			ENDSELECT
			MESSAGE = %MESSAGE%페니스が凄い勢いで生えてきた！_W_
			CALL EXP50_CHECK, MAGIC_TARGET
			CALL SETFLAG, "구현化ＯＮ플래그", MAGIC_TARGET
		ENDIF
	ENDSELECT
ENDIF

BASE:CASTER:정력 -= MAGIC_EP(ARG, CASTER)
TCVAR:CASTER:109 += MAGIC_EP(ARG, CASTER)
MESSAGE = %MESSAGE%%CALLNAME:CASTER%の정력-{MAGIC_EP(ARG, CASTER)}   ({BASE:CASTER:정력}/{MAXBASE:CASTER:정력})_W_

CALL PRINT_STR, MESSAGE

;-------------------------------------------------
;아르카나や특수能力を管理する
;-------------------------------------------------
@MAGIC_CASTER_SELECT, ARG
SELECTCASE MAGIC_USER(ARG, "커맨드")
;唱えられる人は一人だけ
CASE 1, 2, 4
	IF ASSI && USE_MAGIC(ARG, "커맨드", ASSI)
		LOCAL = ASSI
	ELSEIF USE_MAGIC(ARG, "커맨드", MASTER)
		LOCAL = MASTER
	ELSEIF USE_MAGIC(ARG, "커맨드")
		LOCAL = TARGET
	ENDIF

	TCVAR:LOCAL:23 = ARG

	;唱える人がTARGET＆넘어뜨리기でない時には止められる様にしてみる
	IF LOCAL == TARGET && TEQUIP:LOCAL:넘어뜨리기 == 0
		PRINTFORML %CALLNAME:LOCAL% 정력({BASE:LOCAL:정력}/{MAXBASE:LOCAL:정력})하지만%TALENT_NAME(ARG, TALENT:LOCAL:ARG)% 소비정력:{MAGIC_EP(ARG, LOCAL)}를\@ ARG >= 230 && ARG <= 259 ? 唱え # 使い \@.
		PRINTFORML 좋습니까? 
		PRINTFORML  [ 0] 예
		PRINTFORML  [ 1] 역시 관둔다
		CALL INPUT_SELECT, 2
		IF RESULT == 1
			TFLAG:14 = 1
			RETURN 0
		ENDIF
	ENDIF
CASEELSE
	PRINTFORML 누가 %TRAINNAME:ARG% %조사처리(TEXTS("아르카나解説", ARG),"를")% \@ ARG >= 230 && ARG <= 259 ? 영창합 # 사용합 \@니까? 
	PRINTL 

	IF ASSI && USE_MAGIC(ARG, "커맨드", ASSI)
		LOCALS = %CALLNAME:ASSI%(조수)
		PRINTFORML  [ 0] %TEXT_LJ(LOCALS, 22)% 정력({BASE:ASSI:정력}/{MAXBASE:ASSI:정력}) 소비정력:{MAGIC_EP(ARG, ASSI)}
	ENDIF
	IF USE_MAGIC(ARG, "커맨드", MASTER)
		LOCALS = %CALLNAME:MASTER%(주인)
		PRINTFORML  [ 1] %TEXT_LJ(LOCALS, 22)% 정력({BASE:MASTER:정력}/{MAXBASE:MASTER:정력}) 소비정력:{MAGIC_EP(ARG, MASTER)}
	ENDIF
	SIF USE_MAGIC(ARG, "커맨드")
		PRINTFORML  [ 2] %TEXT_LJ(CALLNAME:TARGET, 22)% 정력({BASE:정력}/{MAXBASE:정력}) 소비정력:{MAGIC_EP(ARG, TARGET)}
	PRINTL [100] 돌아간다

	CALL INPUT_SELECT, 3, 100
	IF RESULT == 100
		TFLAG:14 = 1
		RETURN 0
	ELSEIF RESULT == 0 && ASSI && USE_MAGIC(ARG, "커맨드", ASSI)
		TCVAR:ASSI:23 = ARG
	ELSEIF RESULT == 1 && USE_MAGIC(ARG, "커맨드", MASTER)
		TCVAR:MASTER:23 = ARG
	ELSEIF RESULT == 2 && USE_MAGIC(ARG, "커맨드")
		TCVAR:23 = ARG
	ELSE
		RESTART
	ENDIF
ENDSELECT

CALL MAGIC_TARGET_SELECT, ARG


;-------------------------------------------------
;誰に使用するかを선택
;使い손はTCVAR:23 == ARG となっている
;-------------------------------------------------
@MAGIC_TARGET_SELECT, ARG
SELECTCASE TRAINNAME:ARG
;MASTERのみ
CASE "스톡"
	TCVAR:MASTER:22 = ARG
;TARGETのみ
CASE "로긴", "이사군"
	TCVAR:22 = ARG
;使い손により変化
CASE "비젼"
	;TARGETが使う時
	IF TCVAR:23 == ARG
		TCVAR:PLAYER:22 = ARG
	ELSE
		TCVAR:22 = ARG
	ENDIF
CASE "미니멈"
	VARSET LOCAL

	;페니스有り＆인라지がかかっていない＆항상인라지でない ことが条件
	SIF ASSI && PENIS(ASSI) && STATE("미니멈", ASSI) == 0 && TALENT:ASSI:항상인라지 == 0
		SETBIT LOCAL, 0
	SIF PENIS(MASTER) && STATE("미니멈", MASTER) == 0 && TALENT:MASTER:항상인라지 == 0
		SETBIT LOCAL, 1
	SIF PENIS(TARGET) && STATE("미니멈", TARGET) == 0 && TALENT:항상인라지 == 0
		SETBIT LOCAL, 2

	PRINTL 누구를 대상으로 합니까? 
	IF GETBIT(LOCAL, 0)
		LOCALS = %CALLNAME:ASSI%(조수)
		PRINTFORML  [ 0] %TEXT_LJ(LOCALS, 22)% 현재의 크기：%CONDS("페니스사이즈", ASSI)% \@ STATE("인라지", ASSI) ? 인라지影響中 # \@
	ENDIF
	IF GETBIT(LOCAL, 1)
		LOCALS = %CALLNAME:MASTER%(주인)
		PRINTFORML  [ 1] %TEXT_LJ(LOCALS, 22)% 현재의 크기：%CONDS("페니스사이즈", MASTER)% \@ STATE("인라지", MASTER) ? 인라지影響中 # \@
	ENDIF
	SIF GETBIT(LOCAL, 2)
		PRINTFORML  [ 2] %TEXT_LJ(CALLNAME:TARGET, 22)% 현재의 크기：%CONDS("페니스사이즈")% \@ STATE("인라지", TARGET) ? 인라지影響中 # \@
	PRINTL [100] 돌아간다
	CALL INPUT_SELECT, 3, 100
	IF RESULT == 100
		TFLAG:14 = 1
		RETURN 0
	ELSEIF RESULT == 0 && GETBIT(LOCAL, 0)
		TCVAR:ASSI:22 = ARG
	ELSEIF RESULT == 1 && GETBIT(LOCAL, 1)
		TCVAR:MASTER:22 = ARG
	ELSEIF RESULT == 2 && GETBIT(LOCAL, 2)
		TCVAR:22 = ARG
	ELSE
		RESTART
	ENDIF
CASE "인라지"
	VARSET LOCAL

	SIF ASSI && PENIS(ASSI) && STATE("인라지", ASSI) == 0 && TALENT:ASSI:항상미니멈 == 0
		SETBIT LOCAL, 0
	SIF PENIS(MASTER) && STATE("인라지", MASTER) == 0 && TALENT:MASTER:항상미니멈 == 0
		SETBIT LOCAL, 1
	SIF PENIS(TARGET) && STATE("인라지", TARGET) == 0 && TALENT:항상미니멈 == 0
		SETBIT LOCAL, 2

	PRINTL 누구를 대상으로 합니까? 
	IF GETBIT(LOCAL, 0)
		LOCALS = %CALLNAME:ASSI%(조수)
		PRINTFORML  [ 0] %TEXT_LJ(LOCALS, 22)% 현재의 크기：%CONDS("페니스사이즈", ASSI)% \@ STATE("미니멈", ASSI) ? 미니멈影響中 # \@
	ENDIF
	IF GETBIT(LOCAL, 1)
		LOCALS = %CALLNAME:MASTER%(주인)
		PRINTFORML  [ 1] %TEXT_LJ(LOCALS, 22)% 현재의 크기：%CONDS("페니스사이즈", MASTER)% \@ STATE("미니멈", MASTER) ? 미니멈影響中 # \@
	ENDIF
	SIF GETBIT(LOCAL, 2)
		PRINTFORML  [ 2] %TEXT_LJ(CALLNAME:TARGET, 22)% 현재의 크기：%CONDS("페니스사이즈")% \@ STATE("미니멈", TARGET) ? 미니멈影響中 # \@
	PRINTL [100] 돌아간다
	CALL INPUT_SELECT, 3, 100
	IF RESULT == 100
		TFLAG:14 = 1
		RETURN 0
	ELSEIF RESULT == 0 && GETBIT(LOCAL, 0)
		TCVAR:ASSI:22 = ARG
	ELSEIF RESULT == 1 && GETBIT(LOCAL, 1)
		TCVAR:MASTER:22 = ARG
	ELSEIF RESULT == 2 && GETBIT(LOCAL, 2)
		TCVAR:22 = ARG
	ELSE
		RESTART
	ENDIF

CASE "구현"
	VARSET LOCAL
	SIF ASSI && (TALENT:ASSI:여자 || STATE("구현", ASSI) )
		SETBIT LOCAL, 0
	SIF TALENT:MASTER:여자 || STATE("구현", MASTER)
		SETBIT LOCAL, 1
	SIF TALENT:여자 || STATE("구현", TARGET)
		SETBIT LOCAL, 2
	PRINTL 누구를 대상으로 합니까? 
	IF GETBIT(LOCAL, 0)
		LOCALS = %CALLNAME:ASSI%(조수)
		PRINTFORML  [ 0] %TEXT_LJ(LOCALS, 22)% \@ TALENT:ASSI:여자 ? 여자 # 구현중 \@
	ENDIF
	IF GETBIT(LOCAL, 1)
		LOCALS = %CALLNAME:MASTER%(주인)
		PRINTFORML  [ 1] %TEXT_LJ(LOCALS, 22)% \@ TALENT:MASTER:여자 ? 여자 # 구현중 \@
	ENDIF
	SIF GETBIT(LOCAL, 2)
		PRINTFORML  [ 2] %TEXT_LJ(CALLNAME:TARGET, 22)% \@ TALENT:여자 ? 여자 # 구현중 \@
	PRINTL [100] 돌아간다
	CALL INPUT_SELECT, 3, 100
	IF RESULT == 100
		TFLAG:14 = 1
		RETURN 0
	ELSEIF RESULT == 0 && GETBIT(LOCAL, 0)
		TCVAR:ASSI:22 = ARG
	ELSEIF RESULT == 1 && GETBIT(LOCAL, 1)
		TCVAR:MASTER:22 = ARG
	ELSEIF RESULT == 2 && GETBIT(LOCAL, 2)
		TCVAR:22 = ARG
	ELSE
		RESTART
	ENDIF
;それ以外はPLAYER
CASEELSE
	TCVAR:PLAYER:22 = ARG
ENDSELECT


;-------------------------------------------------
;아르카나の커맨드番号をARGで손가락定して, それを誰が使えるのかを체크する
;0bit ASSI, 1bit MASTER, 2bit TARGET
;ARGSは判定する時の状況で, 커맨드선택, カウンター時で区別する
;-------------------------------------------------
@MAGIC_USER(ARG, ARGS)
#FUNCTION
#DIM LCOUNT
VARSET LOCAL

;誰が唱えられるかを체크
;0bitでASSI영창가능, 1bit でMASTER영창가능, 2bitでTARGET영창가능
FOR LCOUNT, 1, CHARANUM
	SELECTCASE LCOUNT
	CASE MASTER, TARGET, ASSI
	CASEELSE
		CONTINUE
	ENDSELECT
	SIF USE_MAGIC(ARG, ARGS, LCOUNT) == 0
		CONTINUE

	SELECTCASE LCOUNT
	CASE ASSI
		SETBIT LOCAL, 0
	CASE MASTER
		SETBIT LOCAL, 1
	CASE TARGET
		SETBIT LOCAL, 2
	ENDSELECT
NEXT

RETURNF LOCAL

;-------------------------------------------------
;캐릭터번호ARG:1が아르카나の커맨드番号ARGを使えるのかを체크する
;ARGSは判定する時の状況で, 커맨드선택, カウンター時で区別する
;-------------------------------------------------
@USE_MAGIC(ARG, ARGS, ARG:1)
#FUNCTION
SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET
SIF TEQUIP:(ARG:1):아이마스크 || TEQUIP:(ARG:1):구속 || TEQUIP:(ARG:1):재갈 || TEQUIP:(ARG:1):연주중
	RETURNF 0
;입か目が制限されてるとダメ
SIF COND("입의 사용", ARG:1) == 0 || COND("視力", ARG:1) == 0
	RETURNF 0
;소비정력
SIF BASE:(ARG:1):정력 <= MAGIC_EP(ARG, ARG:1)
	RETURNF 0

SELECTCASE ARG
CASE 312
	SIF TALENT:(ARG:1):구현 == 0
		RETURNF 0
CASEELSE
	SIF TALENT:(ARG:1):ARG == 0 && TALENT:(ARG:1):마법사 != 2
		RETURNF 0
ENDSELECT

SELECTCASE ARG:1
;使用者が主人や조수
CASE ASSI, MASTER
	RETURNF 1
;조교相手が使用する場合
CASEELSE
	;반발각인が1이하かつ순종３ＬＶor굴복각인２ＬＶ
	SELECTCASE ARGS
	CASE "커맨드"
		SELECTCASE TRAINNAME:ARG
		CASE "비젼"
			SIF TEQUIP:(ARG:1):Ｗ넘어뜨리기
				RETURNF 0
			RETURNF 1
		CASE "시길군", "에올군", "투와이스", "아르스군"
			RETURNF 0
		CASE "인라지"
			IF COND("베테랑：Ｖ섹스", ARG:1) && SIZE("페니스", PLAYER) <= ABL:(ARG:1):Ｖ확장
			ELSEIF COND("베테랑：Ａ섹스", ARG:1) && SIZE("페니스", PLAYER) <= ABL:(ARG:1):Ａ확장
			ELSE
				RETURNF 0
			ENDIF
		CASE "아르스이사"
			SIF TEQUIP:(ARG:1):넘어뜨리기 && MARK:(ARG:1):굴복각인 <= 2
				RETURNF 0
		ENDSELECT
		SIF MARK:(ARG:1):반발각인 >= 2
			RETURNF 0
		SIF ABL:(ARG:1):순종 < 3 && MARK:(ARG:1):굴복각인 < 2
			RETURNF 0
		RETURNF 1
	CASE "카운터"
		;정력には余裕を５程度残す
		SIF BASE:(ARG:1):정력 < 5 + MAGIC_EP(ARG, ARG:1)
			RETURNF 0
		SELECTCASE TRAINNAME:ARG
		CASE "미니멈"
			SIF SIZE("페니스", PLAYER) >= MAX(2, ABL:(ARG:1):Ｖ확장) && TALENT:(ARG:1):호색 == 0 && ABL:(ARG:1):순종 < 3
				RETURNF 1
		CASE "센시브"
			SIF STATE("센시브", PLAYER) == 0
				RETURNF 1
		CASE "우르즈군"
			SIF STATE("우르즈군", ARG:1) < 3
				RETURNF 1
		CASE "시길군"
			SIF STATE("시길군", ARG:1) < 3
				RETURNF 1
		CASE "에올군"
			SIF STATE("에올군", ARG:1) < 3
				RETURNF 1
		CASE "로긴"
			SIF BASE:(ARG:1):체력 < MIN(1500, MAXBASE:(ARG:1):체력 - 300)
				RETURNF 1
		CASE "투와이스"
			SIF STATE("투와이스", ARG:1) == 0
				RETURNF 1
		CASE "이사군"
			SIF STATE("군", PLAYER) && TALENT:(ARG:1):호색 == 0 && ABL:(ARG:1):순종 < 3 && MARK:(ARG:1):반발각인
				RETURNF 1
		CASE "바인드"
			SIF STATE("바인드", PLAYER) == 0
				RETURNF 1
		CASE "아르스군"
			SIF STATE("우르즈군", ARG:1) < 3
				RETURNF 1
			SIF STATE("시길군", ARG:1) < 3
				RETURNF 1
			SIF STATE("에올군", ARG:1) < 3
				RETURNF 1
		CASE "인라지"
			SIF COND("베테랑：Ｖ섹스", ARG:1) && SIZE("페니스", PLAYER) < ABL:(ARG:1):Ｖ확장
				RETURNF 1
			SIF COND("베테랑：Ａ섹스", ARG:1) && SIZE("페니스", PLAYER) < ABL:(ARG:1):Ａ확장
				RETURNF 1
		CASE "비젼"
			SIF TEQUIP:(ARG:1):Ｗ넘어뜨리기
				RETURNF 0
			RETURNF 1
		ENDSELECT
	ENDSELECT
ENDSELECT
RETURNF 0

;---------------------------------------------------------------------
;ARG:1がARG番の아르카나を唱えるのに필요な정력
;ARG:1が省略されている場合には基本소비量を返すが, ARG:1 > 0ならばARG:1が마법사かどうかを見て소비量を減らす
;---------------------------------------------------------------------
@MAGIC_EP(ARG, ARG:1)
#FUNCTION
VARSET LOCAL
SELECTCASE ARG
CASE 230
	LOCAL = 2
CASE 231
	LOCAL = 4
CASE 232
	LOCAL = 3
CASE 233
	LOCAL = 4
CASE 234
	LOCAL = 3
CASE 235
	LOCAL = 30
CASE 236
	LOCAL = 8
CASE 237
	LOCAL = 4
CASE 238
	LOCAL = 3
CASE 239
	LOCAL = 7
CASE 240
	LOCAL = 8
CASE 241
	LOCAL = 9
CASE 242
	LOCAL = 4
CASE 243
	LOCAL = 8
CASE 312
	LOCAL = 8
ENDSELECT

SIF ARG:1 <= 0
	RETURNF LOCAL

;마법사だと아르카나使用時の疲労減少
SELECTCASE ARG
CASE 230 TO 259
	SELECTCASE TALENT:(ARG:1):마법사
	CASE IS >= 2
		LOCAL -= MIN((LOCAL+1)/2, 10)
	CASE 1
		LOCAL = MAX(MULTIPLY(LOCAL, 75), 1)
	ENDSELECT
ENDSELECT

RETURNF LOCAL


@LEARNING_MAGIC, ARG, ARGS
#DIM LCOUNT

SELECTCASE ARGS
CASE "個人授業"
	PRINTL 
	PRINTFORML 어느 것을 배우도록 합니까? 
CASEELSE
	PRINTL 
	PRINTFORML 어느것을 배우도록 합니까？ - 습득의 구슬×{JUEL:ARG:습득}/{NUM("宝구슬：아르카나습득", ARG)}
ENDSELECT

VARSET LOCAL
FOR LCOUNT, 230, 230 + NUM("存在する아르카나")
	SIF TALENT:ARG:LCOUNT || TALENTNAME:LCOUNT == ""
		CONTINUE
	SELECTCASE LCOUNT
	CASE 235, 240
		SIF ARG == MASTER
			CONTINUE
	ENDSELECT
	PRINTFORML  [{LCOUNT - 230, 2}] %TEXT_LJ(TALENTNAME:LCOUNT, 14)% %TEXTS("아르카나解説", LCOUNT)%
	LOCAL:LCOUNT = 1
NEXT

PRINTFORML [100] 기억하지 않는다

$INPUT_LOOP
INPUT

LOCAL = RESULT + 230

IF RESULT == 100
	RETURN 0
ELSEIF RESULT >= 0 && RESULT < NUM("存在する아르카나") && LOCAL:LOCAL

ELSE
	GOTO INPUT_LOOP
ENDIF

SELECTCASE ARGS
CASE "個人授業"
	TALENT:ARG:LOCAL = 1
CASEELSE
	PRINTFORMW %조사처리(NAME:ARG,"는")% %조사처리(TALENTNAME:LOCAL,"를")% 기억했다. 
	JUEL:ARG:습득 -= NUM("宝구슬：아르카나습득", ARG)
	TALENT:ARG:LOCAL = 1
	SIF COND("아르카나습득가능", ARG)
		RESTART
ENDSELECT

RETURN LOCAL

@COM230
CALL COM_ARCANA, 230
RETURN 0

@TRAIN_MESSAGE_COM230
CALL TRAIN_MESSAGE_ARCANA, 230

@COM231
CALL COM_ARCANA, 231
RETURN 0

@TRAIN_MESSAGE_COM231
CALL TRAIN_MESSAGE_ARCANA, 231

@COM232
CALL COM_ARCANA, 232
RETURN 0

@TRAIN_MESSAGE_COM232
CALL TRAIN_MESSAGE_ARCANA, 232

@COM233
CALL COM_ARCANA, 233
RETURN 0

@TRAIN_MESSAGE_COM233
CALL TRAIN_MESSAGE_ARCANA, 233

@COM234
CALL COM_ARCANA, 234
RETURN 0

@TRAIN_MESSAGE_COM234
CALL TRAIN_MESSAGE_ARCANA, 234

@COM235
CALL COM_ARCANA, 235
RETURN 0

@TRAIN_MESSAGE_COM235
CALL TRAIN_MESSAGE_ARCANA, 235

@COM236
CALL COM_ARCANA, 236
RETURN 0

@TRAIN_MESSAGE_COM236
CALL TRAIN_MESSAGE_ARCANA, 236

@COM237
CALL COM_ARCANA, 237
RETURN 0

@TRAIN_MESSAGE_COM237
CALL TRAIN_MESSAGE_ARCANA, 237

@COM238
CALL COM_ARCANA, 238
RETURN 0

@TRAIN_MESSAGE_COM238
CALL TRAIN_MESSAGE_ARCANA, 238

@COM239
CALL COM_ARCANA, 239
RETURN 0

@TRAIN_MESSAGE_COM239
CALL TRAIN_MESSAGE_ARCANA, 239

@COM240
CALL COM_ARCANA, 240
RETURN 0

@TRAIN_MESSAGE_COM240
CALL TRAIN_MESSAGE_ARCANA, 240

@COM241
CALL COM_ARCANA, 241
RETURN 0

@TRAIN_MESSAGE_COM241
CALL TRAIN_MESSAGE_ARCANA, 241

@COM242
CALL COM_ARCANA, 242
RETURN 0

@TRAIN_MESSAGE_COM242
CALL TRAIN_MESSAGE_ARCANA, 242

@COM243
CALL COM_ARCANA, 243
RETURN 0

@TRAIN_MESSAGE_COM243
CALL TRAIN_MESSAGE_ARCANA, 243

@EQUIP_COM243
SIF STATE("비젼")
	CALL SOURCE_COM0, 80
IF ASSI && STATE("비젼", ASSI) && PLAYER != ASSI
	SWAP TARGET, ASSI
	CALL SOURCE_COM0, 80
	SWAP TARGET, ASSI
ENDIF
IF STATE("비젼", PLAYER)
	BASE:PLAYER:절정 += MAXBASE:PLAYER:절정/5
	TCVAR:PLAYER:101 += MAXBASE:PLAYER:절정/5
ENDIF
RETURN 1

@EQUIP_COM243_2
LOCALS = 
SIF STATE("비젼")
	LOCALS = %CALLNAME:TARGET%
SIF STATE("비젼", PLAYER)
	LOCALS = %LOCALS%\@ STATE("비젼") ? と # \@%CALLNAME:PLAYER%
SIF ASSI && STATE("비젼", ASSI) && PLAYER != ASSI
	LOCALS = %LOCALS%\@ STATE("비젼") + STATE("비젼", PLAYER) ? と # \@%CALLNAME:ASSI%

SIF LOCALS == ""
	RETURN 0

PRINTL ＜비젼＞
IF STATE("비젼", PLAYER)
	PRINTFORML %조사처리(LOCALS,"는")% 환각에 사로잡혀있다! 
ELSE
	PRINTFORML %조사처리(LOCALS,"는")% 환각에 사로잡혀 쾌감에 허덕이고있다! 
ENDIF

RETURN 1

