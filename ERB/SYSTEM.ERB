@TARGET_DOWN
;조교자が몽마or상위몽마で몽마화ＯＮ
IF (BASE:체력 <= 0 || BASE:정력 <= 0) && COND("몽마", PLAYER) && COND("몽마化가능")
	PRINTFORMW 체력의 한계를 넘은 %조사처리(CALLNAME:TARGET,"는")%, 몽마가 되었다…
	IF CONFIG("대상の몽마化：여체화") && TALENT:남자
		PRINTFORMW 몽마가 된 영향으로, %조사처리(CALLNAME:TARGET,"는")% 여자가 되었다…
		CALL CHANGE_SEX, TARGET
	ENDIF
	CALL CHANGE_SUCCUBUS, TARGET, PLAYER
;조교대상の체력이 0이하일 때
ELSEIF BASE:체력 <= 0
	IF TEQUIP:넘어뜨리기
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 심한 피로로 어깨를 들썩이고 있다…
		PRINTFORMW 조금 무리를 해버린 것 같다…
	;몽마or상위몽마or마왕의조각 있음
	ELSEIF TALENT:몽마 || TALENT:상위몽마 || TALENT:마왕의조각
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 심한 피로로 숨이 끊어질 것만 같다…
		PRINTFORMW 조금 무리를 과하게 한 것 같다…
	;それ以外はゲームオーバー
	ELSE
		PRINTFORMW ………
		PRINTFORMW ……
		PRINTFORMW …
		PRINTFORMW ★★★%조사처리(CALLNAME:TARGET,"는")% 사라져 버렸다★★★
		PRINTFORMW 
		PRINTFORMW 
		PRINTFORMW 
		CALL GAMEOVER
		RETURN 0
	ENDIF
	IF TEQUIP:넘어뜨리기
	;마조끼が3이상
	ELSEIF ABL:마조끼 >= 3
		PRINTFORMW 그러나, %CALLNAME:TARGET%의 얼굴에는 어딘가 기쁜 표정이 감돌고 있다…. 
	;それ以外で순종Lvが1이상なら1下げる. 넘어뜨리기のときは除外
	ELSEIF ABL:순종 > 0
		ABL:순종 -= 1
		JUEL:순종 = 0
		JUEL:굴복 = 0
		PRINTFORML %CALLNAME:TARGET%의 순종이 １내려갔다. 
	ENDIF
	BASE:체력 = 1
ENDIF

;체력が少ない状態で続行する気が有って로긴を使えるなら使う
IF BASE:체력 < LIMIT(MAXBASE:체력/3, 500, 1000) && TALENT:연모 + TALENT:음란 + TALENT:호색 && USE_MAGIC(236, "커맨드")
	PRINTFORML 
	CALL TRAIN_MESSAGE_ARCANA, 236, TARGET, TARGET
ENDIF

;それでも체력が500未満なら조교を強制終了する
IF BASE:체력 < 500
	;비젼使用中などで発生する消滅予定キャラかどうか
	IF CEVENT("消滅予定")
		CALL ASSI_DOWN, TARGET
	ELSE
		PRINTFORMW ★\@ BASE:체력 < 500 ? 체 # 정 \@력이 한계에 도달해, 조교를 종료합니다. ★
		BEGIN AFTERTRAIN
	ENDIF
ENDIF


@EVENTTURNEND_FLAG, ARG
#DIM LCOUNT
SELECTCASE ARG
CASE 1
	FOR LCOUNT, 1, CHARANUM
		;状態変化や永続装備の解除
		VARSET EQUIP:LCOUNT:0, 0, 0, 10
	NEXT

	;조교中の装備の解除
	VARSET TEQUIP:MASTER:0, 0, 0, 100
	IF TARGET
		VARSET TEQUIP:TARGET:0, 0, 0, 100
		;服装の状態を初期化
		CALL RESETFLAG_CLO, TARGET
		;服装や装備を初期化
		CALL EVENT_AFTER_TRAIN_COS, TARGET
	ENDIF
	IF ASSI
		VARSET TEQUIP:ASSI:0, 0, 0, 100
		CALL RESETFLAG_CLO, ASSI
		CALL EVENT_AFTER_TRAIN_COS, ASSI
	ENDIF

	;包茎の皮を戻す
	FOR LCOUNT, 1, CHARANUM
		CALL SETFLAG, "포경の皮を戻す", LCOUNT
	NEXT

	;一時的なCEVENTフラグの操作
	FOR LCOUNT, 1, CHARANUM
		CALL DEL_CEVENT_GROUP, "조수で성처리", LCOUNT
		IF CEVENT("조수설정：下がらせる", LCOUNT)
			ASSI = LCOUNT
			CALL DEL_CEVENT, "조수설정：下がらせる", LCOUNT
		ENDIF
		SIF CEVENT("消滅予定", LCOUNT)
			DELCHARA LCOUNT
	NEXT

	;달콤한말回数のリセット
	CVARSET CFLAG, 268, 0

	;마음에드는인물설정不가능일数を減らす
	SIF COND("밤") && FLAG:208 >= 1
		FLAG:208 -= 1
	;조교지식설정不가능일数を減らす
	SIF COND("밤") && FLAG:240 >= 1
		FLAG:240 -= 1
	;お腹を減らす
	FLAG:40 = 0

	FOR LCOUNT, 1, CHARANUM
		;조교で得た経験などをここで一段落
		CALL SET_FIRSTEXP, LCOUNT
		CALL RESET_EXP_GAIN, LCOUNT
		;特技LVに還元
		CALL GET_EXABL2, LCOUNT
	NEXT

	;랜덤팬티
	IF CONFIG("팬티を몽마の好きにさせる")
		FOR LCOUNT, 2, CHARANUM
			SIF COND("밤") || (ABL:LCOUNT:욕망 >= 3 && TALENT:LCOUNT:젖기쉬움 + TALENT:LCOUNT:오줌싸개버릇 && RAND:2)
				CALL RAND_PANTU, LCOUNT
		NEXT
	ENDIF
CASE 2
	IF COND("밤")
		FOR LCOUNT, 1, CHARANUM
			;하룻밤개방の悪名の軽減処理
			SELECTCASE CFLAG:LCOUNT:52
			CASE IS <= 0
			CASE IS < DAY
				TIMES CFLAG:LCOUNT:55, 0.95
				CFLAG:LCOUNT:55 = MAX(CFLAG:LCOUNT:55, CFLAG:LCOUNT:51*CFLAG:LCOUNT:52/DAY )
			ENDSELECT
		NEXT

		FOR LCOUNT, 1, CHARANUM
			;욕구불만게이지（一일で２+α程増加）. イかせたり, 상대することで減ります
			IF TALENT:LCOUNT:호색 || TALENT:LCOUNT:음란 || TALENT:LCOUNT:질투 || TALENT:LCOUNT:연모 || ABL:LCOUNT:욕망 >= 3 || ABL:LCOUNT:정액중독 >= 3
				LOCAL = 0
				SIF TALENT:LCOUNT:연모
					LOCAL += 1
				SIF TALENT:LCOUNT:상사상애
					LOCAL += 2
				SIF TALENT:LCOUNT:호색
					LOCAL += 1
				SIF TALENT:LCOUNT:음란
					LOCAL += 3
				SIF TALENT:LCOUNT:질투
					LOCAL += 2
				IF MARK:LCOUNT:정액각인 && TALENT:LCOUNT:남자 == 0
					SELECTCASE COND("태内の정액량", LCOUNT)
					CASE IS <= 5000
						LOCAL += MARK:LCOUNT:정액각인
					CASE IS <= 20000
						LOCAL += MAX(MARK:LCOUNT:정액각인 - 1, 0)
					CASE IS <= 50000
						LOCAL += MAX(MARK:LCOUNT:정액각인 - 2, 0)
					CASE IS <= 80000
						LOCAL += MAX(MARK:LCOUNT:정액각인 - 3, 0)
					ENDSELECT
				ENDIF
				SELECTCASE MAX(ABL:LCOUNT:욕망, ABL:LCOUNT:정액중독)
				CASE IS >= 5
					LOCAL += 1 + RAND:3
				CASE IS >= 3
					LOCAL += RAND:3
				CASE IS >= 1
					LOCAL += RAND:2
				ENDSELECT
				SIF COND("발정기", LCOUNT)
					LOCAL += 3
				BASE:LCOUNT:욕구불만 = MIN(BASE:LCOUNT:욕구불만 + LOCAL, 30)
			ENDIF
		NEXT

		;일々の身嗜みや음모겨털処理
		IF CONFIG("음모と겨털")
			FOR LCOUNT, 1, CHARANUM
				CALL CALC, "일々の음모＆겨털処理", LCOUNT
			NEXT
		ENDIF

		FOR LCOUNT, 1, CHARANUM
			;常時삽입체크
			TALENT:LCOUNT:상시바이브 = COND("身嗜み：Ｖ확장", LCOUNT)
			IF TALENT:LCOUNT:상시바이브
				SIF EXP:LCOUNT:Ｖ경험 < EXP_LV(TALENT:LCOUNT:상시바이브 + 3)
					EXP:LCOUNT:Ｖ경험 += EXP_LV(TALENT:LCOUNT:상시바이브) / 5
				SIF TALENT:LCOUNT:상시바이브 >= 2 && EXP:LCOUNT:Ｖ확장경험 < EXP_LV(TALENT:LCOUNT:상시바이브 + 1)
					EXP:LCOUNT:Ｖ확장경험 += GET_EXP_LV(EXP:LCOUNT:Ｖ확장경험) + TALENT:LCOUNT:상시바이브 - 1
				CALL SETFLAG, "Ｖ受け入れ바이브", TALENT:LCOUNT:상시바이브, LCOUNT, LCOUNT
			ENDIF

			TALENT:LCOUNT:상시Ａ플러그 = COND("身嗜み：Ａ확장", LCOUNT)
			IF TALENT:LCOUNT:상시Ａ플러그
				SIF EXP:LCOUNT:Ａ경험 < EXP_LV(TALENT:LCOUNT:상시Ａ플러그 + 3)
					EXP:LCOUNT:Ａ경험 += EXP_LV(TALENT:LCOUNT:상시Ａ플러그) / 5
				SIF TALENT:LCOUNT:상시Ａ플러그 >= 2 && EXP:LCOUNT:Ａ확장경험 < EXP_LV(TALENT:LCOUNT:상시Ａ플러그 + 1)
					EXP:LCOUNT:Ａ확장경험 += GET_EXP_LV(EXP:LCOUNT:Ａ확장경험) + TALENT:LCOUNT:상시Ａ플러그 - 1
				CALL SETFLAG, "Ａ受け入れ플러그", TALENT:LCOUNT:상시Ａ플러그, LCOUNT, LCOUNT
			ENDIF
			IF COND("身嗜み：요도확장", LCOUNT)
				EXP:LCOUNT:요도경험 += 1 + TALENT:LCOUNT:요도광

				SELECTCASE CALCF("요도径", LCOUNT)
				CASE IS >= CALCF("요도확장の限界", LCOUNT)*6/5

				CASE IS >= CALCF("요도확장の限界", LCOUNT)
					SIF RAND:2
						CALL SETFLAG, "요도확장", MIN_STR(CONDS("요도확장度", LCOUNT) ) + 1 + TALENT:LCOUNT:요도광, LCOUNT
				CASE IS >= CALCF("요도확장の壁", LCOUNT)
					SIF RAND:3
						CALL SETFLAG, "요도확장", MIN_STR(CONDS("요도확장度", LCOUNT) ) + 1 + TALENT:LCOUNT:요도광, LCOUNT
				CASEELSE
					CALL SETFLAG, "요도확장", MIN_STR(CONDS("요도확장度", LCOUNT) ) + 1 + TALENT:LCOUNT:요도광, LCOUNT
				ENDSELECT
			ENDIF
		NEXT
	ENDIF

	;자위
	FOR LCOUNT, 1, CHARANUM
		;자위禁止中だとしない
		SIF COND("身嗜み：자위금지", LCOUNT)
			CONTINUE
		SIF RAND:(ABL:LCOUNT:자위중독 + 1) == 0 || BASE:LCOUNT:체력 < MAX(500, MAXBASE:LCOUNT:기력 / 2 ) || BASE:LCOUNT:정력 < MAXBASE:LCOUNT:정력 / 2
			CONTINUE

		LOCAL = RAND:(ABL:LCOUNT:욕망 + ABL:LCOUNT:자위중독 + TIME*2) / 2
		EXP:LCOUNT:자위경험 += LOCAL

		;욕구불만を少し解消
		SIF LOCAL && RAND:2
			CALL CALC, "욕구불만解消", LCOUNT, (LOCAL + 1)/2
	NEXT
CASE 3
	;MASTERだけは, 요바이の前に少しだけ회복
	CALL CALC, "체력회복상한まで", 200, MASTER
	CALL CALC, "정력회복상한まで", MAXBASE:MASTER:정력/5, MASTER

	;3サイズと신장
	FOR LCOUNT, 1, CHARANUM
		CALL SET_3SIZE, LCOUNT
	NEXT
CASE 4
	;회복は요바이などのあと
	FOR LCOUNT, 1, CHARANUM
		;기력の회복
		BASE:LCOUNT:기력 = MAXBASE:LCOUNT:기력

		SIF BASE:LCOUNT:체력 == MAXBASE:LCOUNT:체력
			CONTINUE

		;체력の회복（午後の조교後は밤の休みが入るので회복大きい）
		IF COND("밤")
			LOCAL = MAX(MAXBASE:LCOUNT:체력/3, 500)
			SIF TALENT:LCOUNT:흡혈귀 || TALENT:LCOUNT:월광욕
				LOCAL += 200
		ELSE
			LOCAL = MAX(MAXBASE:LCOUNT:체력/10, 300) + TALENT:LCOUNT:일광욕*200
		ENDIF

		;휴식플래그（「휴식」を선택, もしくは조교대상でない）
		SIF FLAG:0 || TARGET != LCOUNT
			LOCAL += 400

		;회복빠름, 회복느림, 흡혈귀
		IF TALENT:LCOUNT:회복빠름 || TALENT:LCOUNT:흡혈귀
			LOCAL *= 2
		ELSEIF TALENT:LCOUNT:회복느림
			LOCAL /= 2
		ENDIF

		;치료
		SIF TALENT:MASTER:치료 && TARGET == LCOUNT
			LOCAL += 500
		SIF ASSI && TALENT:ASSI:치료 && TARGET == LCOUNT
			LOCAL += 500
		SIF TALENT:LCOUNT:치료 || TALENT:LCOUNT:조합지식
			LOCAL += 200

		CALL CALC, "체력회복상한まで", LOCAL, LCOUNT
	NEXT

	FOR LCOUNT, 1, CHARANUM
		;정력の회복（午後の조교後は밤の休みが入るので회복大きい）
		IF COND("밤") || FLAG:0
			LOCAL = MAXBASE:LCOUNT:정력
		ELSE
			LOCAL = MAX(MAXBASE:LCOUNT:정력/2, 10)
		ENDIF
		;몽마は회복量がかなり多い
		LOCAL += TALENT:LCOUNT:몽마*10 + TALENT:LCOUNT:상위몽마*20

		SELECTCASE LCOUNT
		CASE MASTER
			;주인は정력が全快なら스톡が１つ회복する
			SIF BASE:LCOUNT:정력 >= MAXBASE:LCOUNT:정력
				BASE:MASTER:스톡 = MIN(BASE:MASTER:스톡 + 1, MAXBASE:MASTER:스톡)
			;휴식を선택しても스톡が１つ회복
			SIF FLAG:0
				BASE:MASTER:스톡 = MIN(BASE:MASTER:스톡 + 1, MAXBASE:MASTER:스톡)
		ENDSELECT
		CALL CALC, "정력회복상한まで", LOCAL, LCOUNT
	NEXT

	;HARD？だと全快
	IF CONFIG("ＨＡＲＤ")
		BASE:MASTER:정력 = MAXBASE:MASTER:정력
		BASE:MASTER:스톡 = MAXBASE:MASTER:스톡
	ENDIF

	;배설물
	FOR LCOUNT, 1, CHARANUM
		IF TALENT:LCOUNT:마도인형
			BASE:LCOUNT:배설물 = 0
			CONTINUE
		ENDIF

		BASE:LCOUNT:배설물 += 1
		;Ａ플러그している場合にはある程度溜める
		SIF TALENT:LCOUNT:상시Ａ플러그 && BASE:LCOUNT:배설물 <= 4
			CONTINUE
		;溜まるほどに배설しやすい（Ａ확장LV > Ａ놀림LVだと溜まりづらい）. 最低でも１残る
		SIF RAND:2 && BASE:LCOUNT:배설물 > MAX(RAND:10 - MAX(ABL:LCOUNT:Ａ확장 - ABL:LCOUNT:Ａ놀림, 0), 1)
			BASE:LCOUNT:배설물 = MAX(BASE:LCOUNT:배설물/2 - 1 - RAND:(ABL:LCOUNT:Ａ확장/3 + 2), 1)
	NEXT

	;노폐물（今のところ、ＶとＰの汚れだけ）
	FOR LCOUNT, 1, CHARANUM
		IF PENIS(LCOUNT)
			SETBIT BASE:LCOUNT:노폐물, 0

			;보통は청소するが…
			SIF COND("身嗜み：Ｐ청소", LCOUNT)
				CLEARBIT BASE:LCOUNT:노폐물, 0
		ENDIF
		IF TALENT:LCOUNT:남자 == 0
			SETBIT BASE:LCOUNT:노폐물, 1

			;보통は청소するが…
			SIF COND("身嗜み：Ｖ掃除", LCOUNT)
				CLEARBIT BASE:LCOUNT:노폐물, 1
		ENDIF
	NEXT

	IF COND("밤")
		FOR LCOUNT, 1, CHARANUM
			;ＶＡ손상の회복
			IF TALENT:LCOUNT:Ｖ손상 + TALENT:LCOUNT:Ａ손상 && BASE:LCOUNT:체력 >= MAXBASE:LCOUNT:체력
				LOCAL = 7

				SIF FLAG:0 || TARGET != LCOUNT
					LOCAL -= 1

				;회복빠름, 회복느림, 흡혈귀
				IF TALENT:LCOUNT:회복빠름 || TALENT:LCOUNT:흡혈귀
					LOCAL -= 1
				ELSEIF TALENT:LCOUNT:회복느림
					LOCAL += 2
				ENDIF

				;치료
				SIF TALENT:MASTER:치료 && TARGET == LCOUNT
					LOCAL -= 1
				IF ASSI
					SIF TALENT:ASSI:치료 && TARGET == LCOUNT
						LOCAL -= 1
				ENDIF
				SIF LOCAL <= 1
					LOCAL = 1

				SIF TALENT:LCOUNT:Ｖ손상 > 0 && RAND:LOCAL == 0
					TALENT:LCOUNT:Ｖ손상 -= 1
				SIF TALENT:LCOUNT:Ａ손상 > 0 && RAND:LOCAL == 0
					TALENT:LCOUNT:Ａ손상 -= 1
			ENDIF
		NEXT
	ENDIF

	;휴식플래그初期化
	FLAG:0 = 0
ENDSELECT


@EVENTTURNEND
#PRI
#DIM LCOUNT

;주인と조수の몽마화체크
IF CONFIG("주인・조수の몽마化") && TARGET && COND("몽마")
	;조수から체크
	IF ASSI && STATE("撃沈", ASSI) && COND("몽마化가능", ASSI)
		PRINTFORML 
		PRINTFORMW %CALLNAME:ASSI%의 상태가 이상하다…
		IF CONFIG("주인・조수の몽마化：캔슬可")
			PRINTL [0] 지켜본다
			PRINTL [1] 말을 건다
			CALL INPUT_SELECT, 2
			IF RESULT == 0
				PRINTFORMW 체력의 한계를 넘어선 %조사처리(CALLNAME:ASSI,"는")%, 몽마가 되었다…
				CALL CHANGE_SUCCUBUS, ASSI, TARGET
				IF CONFIG("주인・조수の몽마化：여체화") && TALENT:ASSI:남자
					PRINTL 
					PRINTW …아직도 상태가 이상하다
					PRINTL [0] 지켜본다
					PRINTL [1] 위험해보이면 멈춘다
					PRINTL [2] 말을 건다
					CALL INPUT_SELECT, 2
					IF RESULT == 0
						PRINTFORMW 몽마가 된 영향으로, %조사처리(CALLNAME:ASSI,"는")% 여자가 되었다…
						CALL CHANGE_SEX, ASSI
					ELSEIF RESULT == 1
						PRINTFORMW 위험한 기색을 느낀 %조사처리(CALLNAME:MASTER,"가")% 말을 걸자, %조사처리(CALLNAME:ASSI,"는")% 상태가 돌아온 것 같다…
						PRINTFORMW 그러나, 아무래도, 남자의 모습으로 있는 것에 위화감을 느끼는 것 같다…
						PRINTFORMW 몽마가 된 영향으로, %조사처리(CALLNAME:ASSI,"는")% 오토코노코가 되고 말았다…! 
						CALL CALC, "작은몸집化", ASSI
						CALL CALC, "男乳化", ASSI
						TALENT:ASSI:오토코노코 = 1
					ELSEIF RESULT == 2
						PRINTFORMW 말을 걸자, %조사처리(CALLNAME:ASSI,"는")% 상태가 돌아온 것 같다
					ENDIF
				ENDIF
			ELSEIF RESULT == 1
				PRINTFORMW 말을 걸자, %조사처리(CALLNAME:ASSI,"는")% 상태가 돌아온 것 같다
			ENDIF
		ELSE
			PRINTFORMW 체력의 한계를 넘어선 %조사처리(CALLNAME:ASSI,"는")% , 몽마가 되었다…
			CALL CHANGE_SUCCUBUS, ASSI, TARGET
			IF CONFIG("주인・조수の몽마化：여체화") && TALENT:ASSI:남자
				PRINTFORMW 몽마가 된 영향으로, %조사처리(CALLNAME:ASSI,"는")% 여자가 되었다…
				CALL CHANGE_SEX, ASSI
			ENDIF
		ENDIF
	ENDIF
	;주인を체크
	IF STATE("撃沈", MASTER) && COND("몽마化가능", MASTER)
		PRINTFORML 
		PRINTFORMW %CALLNAME:MASTER%의 안에서 무언가가 바뀌려 하고있다…
		IF CONFIG("주인・조수の몽마化：캔슬可")
			PRINTL [0] 받아들인다
			PRINTL [1] 저항한다
			CALL INPUT_SELECT, 2
			IF RESULT == 0
				PRINTFORMW 체력의 한계를 넘어선 %조사처리(CALLNAME:MASTER,"는")%, 몽마가 되었다…
				CALL CHANGE_SUCCUBUS, MASTER, TARGET
				IF CONFIG("주인・조수の몽마化：여체화") && TALENT:MASTER:남자
					PRINTL 
					PRINTW …아직도 변화가 계속되는 느낌이 든다…
					PRINTL [0] 받아들인다
					PRINTL [1] 저항한다
					PRINTL [2] 필사적으로 저항한다
					CALL INPUT_SELECT, 3
					IF RESULT == 0
						PRINTFORMW 몽마가 된 영향으로, %조사처리(CALLNAME:MASTER,"는")% 여자가되었다…
						CALL CHANGE_SEX, MASTER
					ELSEIF RESULT == 1
						PRINTFORMW 마음을 강하게 먹자 그것은 서서히 진정되었지만, 아무래도, 남자의 모습으로 있는 것에 위화감을 느껴진다…
						PRINTFORMW 몽마가 된 영향으로, %조사처리(CALLNAME:MASTER,"는")% 오토코노코가 되어버렸다…! 
						CALL CALC, "작은몸집化", MASTER
						CALL CALC, "男乳化", MASTER
						TALENT:MASTER:오토코노코 = 1
					ELSEIF RESULT == 2
						PRINTFORMW 마음을 강하게 먹자, 그것은 서서히 진정되어 갔다…
					ENDIF
				ENDIF
			ELSEIF RESULT == 1
				PRINTFORMW 마음을 강하게 먹자, 그것은 서서히 진정되어 갔다…
			ENDIF
		ELSE
			PRINTFORMW 체력의 한계를 넘은 %조사처리(CALLNAME:MASTER,"는")%, 몽마가 되었다…
			CALL CHANGE_SUCCUBUS, MASTER, TARGET
			IF CONFIG("주인・조수の몽마化：여체화") && TALENT:MASTER:남자
				PRINTFORMW 몽마가 된 영향으로, %조사처리(CALLNAME:MASTER,"는")% 여자가 되었다…
				CALL CHANGE_SEX, MASTER
			ENDIF
		ENDIF
	ENDIF
ENDIF

;플래그変更その1
CALL EVENTTURNEND_FLAG, 1

;アルバイト
SIF NUM("運営店舗")
	CALL CAFE

;外出
SIF COND("밤") && CHARA_NUM("하룻밤개방実行予定")
	CALL NIGHTWALKER

;플래그変更その2
CALL EVENTTURNEND_FLAG, 2


;온천の予定ならば前일準備の模様
IF FLAG:60 && TEXTS("요일") == "금" && TEXTS("시간") == "밤"
	LOCAL = TARGET
	LOCAL:1 = ASSI
	CALL EVENT_TRAVEL_PRE
	TARGET = LOCAL
	ASSI = LOCAL:1
;일常メッセージ
ELSEIF FLAG:205 == 1 || (FLAG:205 == 0 && FLAG:0 == 1)
	LOCAL = TARGET
	CALL DAILY_LIFE
	TARGET = LOCAL
	DRAWLINE
ENDIF

FOR LCOUNT, 1, CHARANUM
	;임신判定&임신이벤트カウント処理
	CALL EVENT_N1, LCOUNT
NEXT

;임신이벤트
IF COND("밤") && CONFIG("임신패치")
	;임신発覚＆出産予定報告処理
	FOR LCOUNT, 1, CHARANUM
		SIF BASE:LCOUNT:태아 + BASE:LCOUNT:육아중
			CALL EVENT_N2, LCOUNT
	NEXT
	;その他の임신이벤트処理
	FOR LCOUNT, 1, CHARANUM
		SIF BASE:LCOUNT:태아 + BASE:LCOUNT:육아중
			CALL EVENT_N3, LCOUNT
	NEXT
ENDIF

;플래그変更その3
CALL EVENTTURNEND_FLAG, 3


;날짜変更時の이벤트呼び出し
SIF COND("밤")
	CALL YOBAI_ASAGAKE

;실금癖などの取得
CALL EVENT_NEXTTIME

;플래그変更その4
CALL EVENTTURNEND_FLAG, 4

;午後なら次の일, 午前なら午後にする
IF COND("밤")
	DAY += 1
	PRINTW 하루가 끝났다・・・

	;호접지몽
	CALL EVENT_KOTYOUNOYUME

	TIME = 0
ELSE
	TIME = 1
ENDIF

;まだ作られてない→やっと試作版だよ! 
CALL BIRTHDAY_GROW

;온천予定のときには無し
SIF FLAG:60 == 0
	CALL EVENT_YAKIMOCHI



;暫定資금獲得処理
;7일おきに資금$1500を加算する
IF DAYEV("日曜", "ゲーム内") && TIME == 0
	DRAWLINE
	CALL ABLUP_MASTER, "주말"
	DRAWLINE
	PRINTW １주일이 지나, 일요일 아침이 왔다.
	IF MONEY:7 > 0
		PRINTFORMW 다방의 수입까지 합해서, 자금 %조사처리(MONEY_C(1500 + MONEY:7),"를")% 획득했다
		MONEY += MONEY:7
	ELSE
		PRINTW 자금 $1500을 획득했다. 
	ENDIF
	MONEY += 1500
	MONEY:7 = 0
ENDIF

;에스트様用タレント変化by>>347.08/10/27
IF TARGET && NO:TARGET == 48
	IF MARK:기분 == 3
		CALL DELETE_CHARA, TARGET
	ELSEIF MARK:기분
		MARK:기분 = 0
	ENDIF
ENDIF

BEGIN SHOP


;-------------------------------------------------
;요바이などの상대を決定して, 이벤트を管理する
;TARGETが이벤트상대, TARGET:1が複数이벤트時の登場キャラとなる
;-------------------------------------------------
@YOBAI_ASAGAKE
#DIM LCOUNT
#DIM MEMO_TARGET
;컨피그で禁止しているなら当然発生しない
SIF CONFIG("요바이아침기습금지")
	RETURN 0
;MASTERしか居ないor身体の入れ替え中は発生しない
SIF CHARANUM <= MASTER + 1
	RETURN 0
;온천に行く일の아침は発生しない
SIF FLAG:60 && TIME == 0
	RETURN 0

;MASTER争奪戦
MEMO_TARGET = TARGET
VARSET TARGET

;昨밤はお楽しみでしたね
IF TIME == 0 && COND("一緒に寝た", MASTER)
	TARGET = COND("一緒に寝た", MASTER)
	;念のためここで初期化
	CVARSET CFLAG, 96, 0
;同衾
ELSEIF COND("밤") && COND("同衾이벤트", MASTER)
	TARGET = FIND_COND("同衾이벤트")
;요바이가능か判定
ELSEIF COND("밤")
	TARGET = FIND_COND("요바이가능")
ELSE
	TARGET = FIND_COND("아침기습가능")
ENDIF

;候補が居る
IF TARGET
	VARSET TCVAR, 0, 0, 30

	SELECTCASE TIME
	CASE 1
		CALL EVENT_YOBAI
	CASE 0
		CALL EVENT_NEWDAY
	ENDSELECT

	FOR LCOUNT, 0, 4
		SIF TARGET:LCOUNT > 0
			CALL SETFLAG, "요바이아침기습종료", TARGET:LCOUNT
	NEXT
ENDIF

VARSET TARGET
TARGET = MEMO_TARGET

@GAMEOVER
DRAWLINE
PRINTL ------------------------------------GAMEOVER-----------------------------------
DRAWLINE

PRINTW 여기서 다시 시작하지 않으면, 게임을 종료합니다
QUIT

