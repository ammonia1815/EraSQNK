@EVENTFIRST
;Verを最新にする
FLAG:29 = VER_ERASQN()

ASSI = 0
DAY = 1

CALL SETFLAG, "아이템플래그調整"

FLAG:6 = 1
;구상の表示플래그
FLAG:7 = 1
;실행치の表示/비표시(0で비표시, 1で表示)
FLAG:8 = 1
;リセット不可な変数フラグ…1bit 主人の외견연령再설정, 2bit 유두の詳細再설정
SETBIT FLAG:11, 1
SETBIT FLAG:11, 2
;表示は슬림
SETBIT FLAG:98, 1

DELCHARA 0
;[0]번은 도플갱어아나타
ADDCHARA 93
ADDCHARA 0
MASTER = 1

;グローバル変数の読み込み
LOADGLOBAL

VARSET LOCAL
$SELECT_MOOD
PRINTL 
;모드선택
PRINTL ★★모드와 설정을 선택해주세요★★
PRINTFORML  [ 0] %"NORMAL", 18, LEFT% (통상모드 / 목표금액 : %TEXT_RJ(MONEY_C(NUM("목표금액：ＮＯＲＭＡＬ") ), 10)%)
PRINTFORML  [ 1] %"EASY", 18, LEFT% (간단모드 / 목표금액 : %TEXT_RJ(MONEY_C(NUM("목표금액：ＥＡＳＹ") ), 10)%)
PRINTFORML  [10] %"컨피그", 18, LEFT%
SIF GLOBAL:99 > 0 && LOCAL == 0
	PRINTFORML  [11] %"컨피그 설정 로드", 18, LEFT% (기록일 : {GLOBAL:99/10000}년 {(GLOBAL:99%10000)/100}월 {GLOBAL:99%100}일)
PRINTFORML [100] %"돌아간다", 18, LEFT%

$INPUT_LOOP
INPUT
IF RESULT == 100
	BEGIN TITLE
ELSEIF RESULT == 11 && GLOBAL:99 > 0 && LOCAL == 0
	CALL LOAD_CONFIGURE
	IF CONFIG("自動購入")
		PRINTFORMW 소모품의 자동구입에 관해서는 재스민을 빼고 전부 해제합니다
		IF GETBIT(FLAG:9, 6)
			FLAG:9 = 1p6
		ELSE
			FLAG:9 = 0
		ENDIF
	ENDIF
	LOCAL = 1
	GOTO SELECT_MOOD
ELSEIF RESULT == 10
	CALL SET_CONFIGURE
	GOTO SELECT_MOOD
ELSEIF RESULT == 1
	FLAG:220 = 1
	PRINTW 능력의 LVUP과 연모의 조건은 물론, 그 밖에도 이것저것 간단해집니다
ELSEIF RESULT == 0
ELSE
	GOTO INPUT_LOOP
ENDIF

CALL START_CHARA_SELECT

CALL SET_NEWNAME, MASTER

PLAYER = MASTER
TARGET = 0

IF NO:MASTER == 0
	;オープニングのはじまりはじまり
	PRINTL 
	PRINTFORMW 「몽마」
	PRINTFORMW 꿈의 마물. 꿈 속에서 나타나 사람과 만난다. 
	PRINTFORMW 그 모습은 언제나 아름다워 결코 몽마의 유혹에서 벗어날 수 없다. 
	PRINTFORMW 몽마에게 매료되어 그 쾌락의 포로가 된다면 그 사람도 또한 영원히 꿈속에서 살게 된다. 
	PRINTFORMW 
	PRINTFORMW 여긴 어디일까. 위도 아래도 분간할 수 없는 몽롱한 장소에 %조사처리(CALLNAME:PLAYER,"가")% 있다. 
	PRINTFORMW 그 어슴푸레한 장소에서, 이목을 끄는 요염한 여자가 분명히 서 있었다. 
	PRINTFORMW 여자는 그 젖은 입술에서, 마치 노래하는 듯이 말을 자아냈다. 
	PRINTFORMW 「당신은 『조교사』, 이제부터 당신은 『몽마』를 『조교』하는거야」
	PRINTFORMW 「더욱 엉망진창으로 범해서…. 어때? 두근두근하지 않아? 」
	PRINTFORMW 「당신의 장소를, 사람을, 역사를, 여기에 만들어…」
	PRINTFORMW 『몽마』? 『조교』? 무슨 말인걸까. 
	PRINTFORMW %조사처리(CALLNAME:PLAYER,"가")% 입을 여는 것보다 빠르게, 그 의식은 멀어져갔다. 
	PRINTFORMW 「그럼, 힘내♪」
	PRINTFORMW ………
	PRINTFORMW ……
	PRINTFORMW …
	PRINTFORMW 눈을 뜨자 낯선 장소에 있다. 아니, 여기는 「%CALLNAME:PLAYER%의 서재」다. 
	PRINTFORMW 지금 여기에서 %CALLNAME:PLAYER%의 조교사로서의 빛나는 나날이 시작되는 거다. 
	PRINTFORMW 목이 타는 듯이 마르다…. 
	PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 눈앞에 놓여진 벌꿀술을 손에 들어 술잔에 찰랑찰랑하게 따르고,
	PRINTFORMW 뇌리에 눌러 붙는 위화감과 함께 목에 흘려보냈다. 
	PRINTFORMW 
	PRINTFORMW 「네에～～엣, eraSQ의 세계에 어서오세요」
	PRINTFORMW 「오늘부터 당신은 몽마라고 불리는 여자아이들을 조교하는 조교사의 생활을 하게 되는거다」
	PRINTFORMW 「어떻게 조교하는 지, 그 후 무엇을 할지, 어떻게 시간을 보낼지는 너의 자유야」
	PRINTFORMW 「왜냐면 여기는…」
	DRAWLINE
ELSEIF NO:MASTER == 34
	PRINTFORMW SQ본편의 어나더 스토리로 읽어주세요. 
	PRINTFORMW 『쳇… 에스트 자식 적당히 헛소리나 지껄이고…』
	PRINTFORMW 아무도 살지 않는 동굴 안에서 스투나는 혼자 있다. 
	PRINTFORMW 마왕의 조각을 가진 용사가 여기를 지나간다고 들어서 들러붙어 있지만 전혀 그런 기색도 없기에 무리도 아니지만…
	PRINTFORMW （사냥감은 지나가니까… 그런 쪽으론 불만은 없지만…）
	PRINTFORMW 세간에서 스투나가 S라고 말하는 것은 사실 그대로 맞는 말이다. 
	PRINTFORMW S라서 꼼짝할 수 없는 상대를 못살게 구는 것이 좋다. 그야말로 S다. 
	PRINTFORMW 그러나 그 주변을 지나가는 "사냥감"은 좋은 말로도 그녀에게 있어서 충분한 "사냥감"이라고는 할 수 없었다. 
	PRINTFORMW 화실히 말해서 학대하는 재미가 없었다. 
	PRINTFORMW （부족하다고…젠자앙…좀 더 생기있는 사냥감은…응? ）
	PRINTFORMW 뭔가 인영이 보였던 것같다… 붉은 머리의 남자다…
	PRINTFORMW （용사 놈은 붉은 머리라고 했었지…빙고! ）
	PRINTFORMW 스투나는 그 인영의 앞에 뛰어나왔다. 지금이라면 방해하는 샐러맨더도 없다. 
	PRINTFORMW 그리고 스투나는, 
	PRINTFORMW 굳어져버렸다…
	PRINTFORMW 
	PRINTFORMW 스투나의 눈앞에는 울고 불고 하는 소년이 하나. 
	PRINTFORMW （붉은 머리긴 한데 이놈은 아니구만…）
	PRINTFORMW 사실은 이 얘가 용사지만. 
	PRINTFORMW （그 바보에게 들켰다간 큰일인데…）
	PRINTFORMW 우선 스투나는 그 소년을 보호하기로 했다…
ELSEIF NO:MASTER == 35
	PRINTFORMW SQ본편의 어나더 스토리로 읽어주세요. 
	PRINTFORMW 『하아～ 심심해～』
	PRINTFORMW 던전 안을 배회하면서 혼잣말로 멜로나가 중얼거렸다. 
	PRINTFORMW 친구 스투나와 함께 이 던전에 틀어박혀 한동안, 몇명의 모험자라던가 마물이라던가 바짝 마르도록 정기를 갈취해온 그녀들이었지만, 
	PRINTFORMW （어떻게 해도 부족하네～）
	PRINTFORMW 몽마로서의 본능을 채워줄 정도의 "사냥감"는 그다지 만나지 못하고 있었다. 
	PRINTFORMW 멜로나가 향하고 있는 것은 동굴의 입구 쪽. 
	PRINTFORMW 그녀들이 사는 동굴의 깊숙한 곳까지 올 수 있는 모험자조차도 그녀들의 상대가 되지 않는것이다. 
	PRINTFORMW 입구 부근에 힘이 다한 모험자따위는 물건의 범주에도 들어가지 않아, 라고 생각하는 것이 보통일 것이다. 
	PRINTFORMW 그러나 그녀들에겐 뼈대가 있는 "사냥감"을 범하고 싶다고 하는 그 외의 곤란한 버릇이 있었다. 
	PRINTFORMW （두리번… 두리번…）
	PRINTFORMW 멜로나가 던전 내부를 둘러본다. 
	PRINTFORMW （킁…킁…）
	PRINTFORMW 마침내 냄새까지 맡기 시작했다…
	PRINTFORMW （! ）
	PRINTFORMW 그때, 멜로나의 시야에 한명의 소년이 들어왔다. 
	PRINTFORMW 천진난만한 얼굴을 한 붉은 머리의 귀여운 남자아이다…
	PRINTFORMW （나우, 겟 어, 찬스! ）
	PRINTFORMW 멜로나는 소년이 자세를 취할 새도 없이 덤벼들어, 
	PRINTFORMW 시원하게 소년을 포획했다. 
ENDIF
IF NO:MASTER == 34 || NO:MASTER == 35
	ADDCHARA 94
	FLAG:1093 = 1
	TALENT:MASTER:불결무시 = 1
	TALENT:MASTER:손대중 = 1
	TARGET = CHARANUM-1
	CALL SET_NEWNAME
ENDIF

MONEY = 10000

IF FLAG:4 == 0
	PRINTFORML （%조사처리(MONEY_C(MONEY),"로")%  상대하고싶은 캐릭터를 소환해주세요）
	PRINTFORML （돈이 남는 경우엔 다른 아이템도 삽시다）
	PRINTFORMW （또, ＳＨＯＰ화면에 어두운 색으로 표시된 커맨드를 선택하면 실행조건의 힌트가 표시됩니다）
ENDIF

;IF GLOBAL:99 == 0
	PRINTL 
	PRINTL 제작자가 특별히 추천하는 컨피그 설정을 사용합니까? 
	PRINTL [0] 예
	PRINTL [1] 아니오
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		PRINTL 
		PRINTL 조교시에, MASTER가 안에 사정하면 임신가능        ON
		FLAG:211 = 1p0 + 1p1
		PRINTL 실행치필터＆탈처녀필터                           ON
		FLAG:209 |= 1p4 + 1p6
		PRINTL 매혹상태일 때는 몽마 마음대로 하게 한다          ON
		FLAG:223 = 1
		PRINTL 능력표시에서, 얻지 않은 경험을 표시하지 않는다    ON
		FLAG:250 = 1
		PRINTL ３사이즈와신장의 표시                            ON
		FLAG:239 = 1p0 + 1p1
		PRINTL 몽마들의 일상이벤트를 시간이 지날때마다 발생          ON
		FLAG:205 = 1
		PRINTL 조교때의 파라미터를 ４줄 표시＆게이지바：컬러Ａ     ON
		FLAG:98 = 1p0 + 1p2 + 1p5

		PRINTL 
		PRINTW 이상의 컨피그를 사용합니다
	ENDIF
;ENDIF

;경험치などの설정
CALL LVUPCHECK_MASTER

BASE:MASTER:스톡 = MAXBASE:MASTER:스톡

BEGIN SHOP

@MASTER_FAMALE_A
IF NO:MASTER
	ADDCHARA 0
	SWAPCHARA CHARANUM-1, MASTER
	DELCHARA CHARANUM-1
ENDIF

CALL RENEWAL_ABL, MASTER

PRINTL 
PRINTL 성별을 선택해주세요
PRINTL [ 0] ♂
PRINTL [ 1] ♀
PRINTL [ 2] 후타나리
PRINTL [ 3] 오토코노코 (남자로는 보이지않는 귀여운 외모에, 여성같은 행동을 즐기는 소년)

CALL INPUT_SELECT, 4

TALENT:MASTER:외견연령 = 2
SELECTCASE RESULT
CASE 1, 2
	TALENT:MASTER:남자 = 0
	IF RESULT == 1
		TALENT:MASTER:여자 = 1
	ELSEIF RESULT == 2
		TALENT:MASTER:후타나리 = 1
	ENDIF
CASE 3
	TALENT:MASTER:오토코노코 = 1
ENDSELECT

;MASTERの感度は全て2LV
ABL:MASTER:Ｃ감각 = 2
ABL:MASTER:Ｖ감각 = 2
ABL:MASTER:Ａ감각 = 2
ABL:MASTER:Ｂ감각 = 2
ABL:MASTER:Ｍ감각 = 2

CALL SET_DETAIL, MASTER

;조교지식선택
CALL SETFLAG, "조교지식선택", MASTER

CALL CREATE_CHARA, MASTER
SIF RESULT == 1
	RESTART

@SET_DETAIL, ARG
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE

MEMO_LINECOUNT = LINECOUNT
REDRAW 0

;色々설정
EXP:ARG:Ｖ경험 = MAX(EXP:ARG:Ｖ경험, EXP:ARG:Ｖ성교경험, EXP:ARG:Ｖ확장경험)
IF EXP:ARG:Ｖ경험
	TALENT:ARG:처녀 = 0
ELSEIF TALENT:ARG:남자 == 0
	TALENT:ARG:처녀 = 1
ENDIF

EXP:ARG:Ａ경험 = MAX(EXP:ARG:Ａ경험, EXP:ARG:Ａ성교경험, EXP:ARG:Ａ확장경험)
IF EXP:ARG:Ａ성교경험
	TALENT:ARG:Ａ처녀 = 0
ELSE
	TALENT:ARG:Ａ처녀 = 1
ENDIF

IF EXP:ARG:Ｖ삽입경험
	TALENT:ARG:동정 = 0
ELSEIF TALENT:ARG:남자
	TALENT:ARG:동정 = 1
ENDIF

IF ABL:ARG:새드끼 >= 5
	TALENT:ARG:새드 = 1
ELSE
	TALENT:ARG:새드 = 0
ENDIF
IF ABL:ARG:마조끼 >= 5
	TALENT:ARG:마조 = 1
ELSE
	TALENT:ARG:마조 = 0
ENDIF
IF ABL:ARG:ＢＬ끼
	EXP:ARG:ＢＬ경험 = EXP_LV(ABL:ARG:ＢＬ끼) + RAND:50
ELSE
	EXP:ARG:ＢＬ경험 = 0
ENDIF
IF ABL:ARG:레즈끼
	EXP:ARG:레즈경험 = EXP_LV(ABL:ARG:레즈끼) + RAND:50
ELSE
	EXP:ARG:레즈경험 = 0
ENDIF

SIF ABL:ARG:기교 || EXP:ARG:Ｖ경험 || EXP:ARG:Ａ경험 || EXP:ARG:Ｖ삽입경험 || EXP:ARG:Ａ삽입경험
	TALENT:ARG:성경험전무 = 0

IF TALENT:ARG:성경험전무
	EXP:ARG:펠라경험 = 0
	EXP:ARG:수음경험 = 0
	EXP:ARG:풋잡경험 = 0
	EXP:ARG:허리사용경험 = 0
	EXP:ARG:파이즈리경험 = 0
	EXP:ARG:Ａ자극경험 = 0
	EXP:ARG:커닐링구스경험 = 0
	EXP:ARG:키스경험 = 0
ELSE
	SIF ABL:ARG:ＢＬ끼 || TALENT:ARG:남자 == 0
		EXP:ARG:펠라경험 = RAND:(EXP_LV(ABL:ARG:기교) + 1) / MAX(ABL:ARG:레즈끼, 1) + ABL:ARG:기교*20 + MAX(RAND:10 - RAND:10, 0)
	EXP:ARG:수음경험 = RAND:(EXP_LV(ABL:ARG:기교) + 1) + ABL:ARG:기교*20
	SIF TALENT:ARG:남자 == 0
		EXP:ARG:풋잡경험 = RAND:(EXP_LV(ABL:ARG:기교) + 1) / MAX(ABL:ARG:레즈끼, 1) + ABL:ARG:기교*20 + MAX(RAND:10 - RAND:10, 0)
	SIF ABL:ARG:레즈끼 || EXP:ARG:Ｖ삽입경험 + EXP:ARG:Ａ삽입경험
		EXP:ARG:허리사용경험 = RAND:(EXP_LV(ABL:ARG:기교) + 1) + EXP:ARG:Ｖ삽입경험 + EXP:ARG:Ａ삽입경험 + ABL:ARG:기교*20 + MAX(RAND:10 - RAND:10, 0)
	SIF TALENT:ARG:남자 == 0
		EXP:ARG:파이즈리경험 = RAND:(EXP_LV(ABL:ARG:기교) + 1) / MAX(ABL:ARG:레즈끼, 1) + ABL:ARG:기교*20 + MAX(RAND:10 - RAND:10, 0)
	SIF EXP:ARG:Ａ삽입경험 || ABL:ARG:새드끼 || ABL:ARG:ＢＬ끼
		EXP:ARG:Ａ자극경험 = RAND:(EXP_LV(ABL:ARG:기교) + 1) + EXP:ARG:Ａ삽입경험 + ABL:ARG:기교*10 + MAX(RAND:10 - RAND:10, 0)
	SIF ABL:ARG:레즈끼 || TALENT:ARG:남자
		EXP:ARG:커닐링구스경험 = RAND:(EXP_LV(ABL:ARG:기교) + 1) / MAX(ABL:ARG:ＢＬ끼, 1) + ABL:ARG:기교*20 + MAX(RAND:10 - RAND:10, 0)
	EXP:ARG:키스경험 = RAND:(EXP_LV(ABL:ARG:기교) + 1) + ABL:ARG:기교*20 + MAX(RAND:10 - RAND:10, 0)
ENDIF

SIF PENIS(ARG) && CONFIG("페니스상세설정") && TALENT:ARG:페니스 == 0
	CALL SETFLAG, "페니스상세설정"
SIF CONFIG("음모") && COND("음모", ARG) == 0
	CALL SETFLAG, "음모と겨털", ARG

CFLAG:ARG:368 = 0

SELECTCASE COND("음모", ARG)
CASE 3
	SETBIT CFLAG:ARG:368, 1
CASE 5
	SETBIT CFLAG:ARG:368, 2
CASE 7
	SETBIT CFLAG:ARG:368, 3
ENDSELECT

SELECTCASE COND("겨털", ARG)
CASE 3
	SETBIT CFLAG:ARG:368, 6
CASE 7
	SETBIT CFLAG:ARG:368, 7
ENDSELECT

VARSET LOCAL
PRINTL 
PRINTL 소질이나 경험 등을 결정해주세요
IF TALENT:ARG:남자 == 0
	PRINTFORM %"Ｖ경험", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	SELECTCASE EXP:ARG:Ｖ경험
	CASE IS >= 300
		PRINTFORM %"풍부", 6, LEFT%
	CASE IS >= 150
		PRINTFORM %"많음", 6, LEFT%
	CASE IS >= 20
		PRINTFORM %"있음", 6, LEFT%
	CASEELSE
		PRINTFORM %"처녀", 6, LEFT%
	ENDSELECT
	RESETCOLOR
	PRINTL  [  0]처녀 [  1]있음 [  2]많음 [  3]풍부
	LOCAL:0 = 1
	IF EXP:ARG:Ｖ경험
		PRINTFORM %"Ｖ성교경험", 12, LEFT% …
		SETCOLOR DEF_COLOR("暗황색")
		SELECTCASE EXP:ARG:Ｖ성교경험
		CASE IS >= 150
			PRINTFORM %"풍부", 6, LEFT%
		CASE IS >= 50
			PRINTFORM %"많음", 6, LEFT%
		CASE IS >= 20
			PRINTFORM %"있음", 6, LEFT%
		CASEELSE
			PRINTFORM %"없음", 6, LEFT%
		ENDSELECT
		RESETCOLOR
		PRINTL  [  5]없음 [  6]있음 [  7]많음 [  8]풍부
		LOCAL:5 = 1
		PRINTFORM %"Ｖ확장경험", 12, LEFT% …
		SETCOLOR DEF_COLOR("暗황색")
		SELECTCASE EXP:ARG:Ｖ확장경험
		CASE IS >= 50
			PRINTFORM %"풍부", 6, LEFT%
		CASE IS >= 20
			PRINTFORM %"많음", 6, LEFT%
		CASE IS >= 3
			PRINTFORM %"있음", 6, LEFT%
		CASEELSE
			PRINTFORM %"없음", 6, LEFT%
		ENDSELECT
		RESETCOLOR
		PRINTL  [ 10]없음 [ 11]있음 [ 12]많음 [ 13]풍부
		LOCAL:10 = 1
	ENDIF
ENDIF
PRINTFORM %"Ａ경험", 12, LEFT% …
SETCOLOR DEF_COLOR("暗황색")
SELECTCASE EXP:ARG:Ａ경험
CASE IS >= 300
	PRINTFORM %"풍부", 6, LEFT%
CASE IS >= 150
	PRINTFORM %"많음", 6, LEFT%
CASE IS >= 20
	PRINTFORM %"있음", 6, LEFT%
CASEELSE
	PRINTFORM %"없음", 6, LEFT%
ENDSELECT
RESETCOLOR
PRINTL  [ 15]없음 [ 16]있음 [ 17]많음 [ 18]풍부
LOCAL:15 = 1
IF EXP:ARG:Ａ경험
	PRINTFORM %"Ａ성교경험", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	SELECTCASE EXP:ARG:Ａ성교경험
	CASE IS >= 150
		PRINTFORM %"풍부", 6, LEFT%
	CASE IS >= 50
		PRINTFORM %"많음", 6, LEFT%
	CASE IS >= 20
		PRINTFORM %"있음", 6, LEFT%
	CASEELSE
		PRINTFORM %"없음", 6, LEFT%
	ENDSELECT
	RESETCOLOR
	PRINTL  [ 20]없음 [ 21]있음 [ 22]많음 [ 23]풍부
	LOCAL:20 = 1
	PRINTFORM %"Ａ확장경험", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	SELECTCASE EXP:ARG:Ａ확장경험
	CASE IS >= 50
		PRINTFORM %"풍부", 6, LEFT%
	CASE IS >= 20
		PRINTFORM %"많음", 6, LEFT%
	CASE IS >= 3
		PRINTFORM %"있음", 6, LEFT%
	CASEELSE
		PRINTFORM %"없음", 6, LEFT%
	ENDSELECT
	RESETCOLOR
	PRINTL  [ 25]없음 [ 26]있음 [ 27]많음 [ 28]풍부
	LOCAL:25 = 1
ENDIF
IF PENIS(ARG)
	PRINTFORM %"Ｖ삽입경험", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	SELECTCASE EXP:ARG:Ｖ삽입경험
	CASE IS >= 150
		PRINTFORM %"풍부", 6, LEFT%
	CASE IS >= 50
		PRINTFORM %"많음", 6, LEFT%
	CASE IS >= 20
		PRINTFORM %"있음", 6, LEFT%
	CASEELSE
		PRINTFORM %"동정", 6, LEFT%
	ENDSELECT
	RESETCOLOR
	PRINTL  [ 30]동정 [ 31]있음 [ 32]많음 [ 33]풍부
	LOCAL:30 = 1
	PRINTFORM %"Ａ삽입경험", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	SELECTCASE EXP:ARG:Ａ삽입경험
	CASE IS >= 150
		PRINTFORM %"풍부", 6, LEFT%
	CASE IS >= 50
		PRINTFORM %"많음", 6, LEFT%
	CASE IS >= 20
		PRINTFORM %"있음", 6, LEFT%
	CASEELSE
		PRINTFORM %"없음", 6, LEFT%
	ENDSELECT
	RESETCOLOR
	PRINTL  [ 35]없음 [ 36]있음 [ 37]많음 [ 38]풍부
	LOCAL:35 = 1
ENDIF

PRINTFORM %"몸집", 12, LEFT% …
SETCOLOR DEF_COLOR("暗황색")
IF TALENT:ARG:장신
	PRINTFORM %"장신", 6, LEFT%
ELSEIF TALENT:ARG:작은몸집
	PRINTFORM %"작음", 6, LEFT%
ELSE
	PRINTFORM %"보통", 6, LEFT%
ENDIF
RESETCOLOR
PRINTFORML  [100]작음 [101]보통 \@ TALENT:ARG:오토코노코 == 0 ? [102]장신 # \@
LOCAL:100 = 1
IF TALENT:ARG:남자 == 0
	PRINTFORM %"가슴 크기", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	IF TALENT:ARG:거유 == 2
		PRINTFORM %"폭유", 6, LEFT%
	ELSEIF TALENT:ARG:거유
		PRINTFORM %"거유", 6, LEFT%
	ELSEIF TALENT:ARG:빈유
		PRINTFORM %"빈유", 6, LEFT%
	ELSE
		PRINTFORM %"보통", 6, LEFT%
	ENDIF
	RESETCOLOR
	PRINTL  [105]빈유 [106]보통 [107]거유 [108]폭유
	LOCAL:105 = 1
ENDIF
IF PENIS(ARG) && CONFIG("페니스상세설정")
	PRINTFORM %"페니스사이즈", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	IF GETBIT(TALENT:ARG:페니스, 3)
		PRINTFORM %"말잦이", 6, LEFT%
	ELSEIF GETBIT(TALENT:ARG:페니스, 2)
		PRINTFORM %"거근", 6, LEFT%
	ELSEIF GETBIT(TALENT:ARG:페니스, 1)
		PRINTFORM %"작음", 6, LEFT%
	ELSEIF GETBIT(TALENT:ARG:페니스, 0)
		PRINTFORM %"보통", 6, LEFT%
	ENDIF
	RESETCOLOR
	PRINTFORML  [110]작음 [111]보통 [112]거근 [113]말잦이
	LOCAL:110 = 1
	PRINTFORM %"포경안함", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	IF GETBIT(TALENT:ARG:페니스, 12)
		PRINTFORM %"진성", 6, LEFT%
	ELSEIF GETBIT(TALENT:ARG:페니스, 11)
		PRINTFORM %"가성", 6, LEFT%
	ELSE
		PRINTFORM %"없음", 6, LEFT%
	ENDIF
	RESETCOLOR
	PRINTFORML  [115]진성 [116]가성 [117]없음
	LOCAL:115 = 1
ENDIF
IF CONFIG("음모")
	PRINTFORM %"음모", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	SELECTCASE COND("음모", ARG)
	CASE 1
		PRINTFORM %"무모", 6, LEFT%
	CASE 2, 3
		PRINTFORM %"제모", 6, LEFT%
	CASE 4, 6
		PRINTFORM %"있음", 6, LEFT%
	CASE 5
		PRINTFORM %"다듬음", 6, LEFT%
	CASE 7
		PRINTFORM %"밀림", 6, LEFT%
	ENDSELECT
	RESETCOLOR
	PRINTL  [120]무모 [121]제모 [122]있음 [123]다듬음 [124]수북
	LOCAL:120 = 1
ENDIF
IF CONFIG("겨털")
	PRINTFORM %TEXTS("겨드랑이")+"털", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	SELECTCASE COND("겨털", ARG)
	CASE 1
		PRINTFORM %"무모", 6, LEFT%
	CASE 2
		PRINTFORM %"탈모", 6, LEFT%
	CASE 3
		PRINTFORM %"제모", 6, LEFT%
	CASE 4
		PRINTFORM %"있음", 6, LEFT%
	CASE 7
		PRINTFORM %"밀림", 6, LEFT%
	ENDSELECT
	RESETCOLOR
	PRINTL  [125]무모 [126]제모 [127]있음 [128]수북
	LOCAL:125 = 1
ENDIF
PRINTFORM %"피부색", 12, LEFT% …
SETCOLOR DEF_COLOR("暗황색")
SELECTCASE TALENT:ARG:피부색
CASE 0
	PRINTFORM %"보통", 6, LEFT%
CASE 1
	PRINTFORM %"갈색", 6, LEFT%
CASE 2
	PRINTFORM %"백색", 6, LEFT%
CASE 3
	PRINTFORM %"푸른색", 6, LEFT%
ENDSELECT
RESETCOLOR
PRINTL  [130]보통 [131]갈색 [132]흰색 [133]푸른색
LOCAL:130 = 1

PRINTFORM %"외견연령", 12, LEFT% …
SETCOLOR DEF_COLOR("暗황색")
SELECTCASE AGE(ARG)
CASE 1
	PRINTFORM %"아이", 6, LEFT%
CASE 2
	PRINTFORM %TALENT:ARG:남자 ? "소년" # "소녀", 6, LEFT%
CASE 3
	PRINTFORM %"청년", 6, LEFT%
CASE 4
	PRINTFORM %"어른", 6, LEFT%
ENDSELECT
RESETCOLOR
IF TALENT:ARG:오토코노코
	PRINTFORML  [135]아이 [136]소년
ELSE
	PRINTFORML  [135]아이 [136]\@ TALENT:ARG:남자 ? 소년 # 소녀 \@ [137]청년 [138]어른
ENDIF
LOCAL:135 = 1

IF TALENT:ARG:남자 == 0 && CONFIG("유두비대")
	PRINTFORM %"유두비대", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	SELECTCASE COND("유두비대", ARG)
	CASE IS >= 3
		PRINTFORM %"LV３", 6, LEFT%
	CASE 2
		PRINTFORM %"LV２", 6, LEFT%
	CASE 1
		PRINTFORM %"LV１", 6, LEFT%
	CASEELSE
		PRINTFORM %"없음", 6, LEFT%
	ENDSELECT
	RESETCOLOR
	PRINTL  [140]없음 [141]LV１ [142]LV２ [143]LV３
	LOCAL:140 = 1
ENDIF
IF TALENT:ARG:남자 == 0 && CONFIG("함몰유두")
	PRINTFORM %"함몰유두", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	SELECTCASE COND("함몰유두", ARG)
	CASE 2
		PRINTFORM %"진성", 6, LEFT%
	CASE 1
		PRINTFORM %"가성", 6, LEFT%
	CASEELSE
		PRINTFORM %"없음", 6, LEFT%
	ENDSELECT
	RESETCOLOR
	PRINTL  [145]없음 [146]가성 [147]진성
	LOCAL:145 = 1
ENDIF
IF TALENT:ARG:남자 == 0 && CONFIG("거유륜")
	PRINTFORM %"거유륜", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	SELECTCASE COND("거유륜", ARG)
	CASE IS >= 3
		PRINTFORM %"LV３", 6, LEFT%
	CASE 2
		PRINTFORM %"LV２", 6, LEFT%
	CASE 1
		PRINTFORM %"LV１", 6, LEFT%
	CASEELSE
		PRINTFORM %"없음", 6, LEFT%
	ENDSELECT
	RESETCOLOR
	PRINTL  [150]없음 [151]LV１ [152]LV２ [153]LV３
	LOCAL:150 = 1
ENDIF


IF TALENT:ARG:남자 == 0
	PRINTFORM %"Ｃ감각", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	IF TALENT:ARG:Ｃ민감
		PRINTFORM %"민감", 6, LEFT%
	ELSEIF TALENT:ARG:Ｃ둔감
		PRINTFORM %"둔감", 6, LEFT%
	ELSE
		PRINTFORM %"보통", 6, LEFT%
	ENDIF
	RESETCOLOR
	PRINTL  [200]둔감 [201]보통 [202]민감
	LOCAL:200 = 1
	PRINTFORM %"Ｖ감각", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	IF TALENT:ARG:Ｖ민감
		PRINTFORM %"민감", 6, LEFT%
	ELSEIF TALENT:ARG:Ｖ둔감
		PRINTFORM %"둔감", 6, LEFT%
	ELSE
		PRINTFORM %"보통", 6, LEFT%
	ENDIF
	RESETCOLOR
	PRINTL  [205]둔감 [206]보통 [207]민감
	LOCAL:205 = 1
ENDIF
PRINTFORM %"Ａ감각", 12, LEFT% …
SETCOLOR DEF_COLOR("暗황색")
IF TALENT:ARG:Ａ민감
	PRINTFORM %"민감", 6, LEFT%
ELSEIF TALENT:ARG:Ａ둔감
	PRINTFORM %"둔감", 6, LEFT%
ELSE
	PRINTFORM %"보통", 6, LEFT%
ENDIF
RESETCOLOR
PRINTL  [210]둔감 [211]보통 [212]민감
LOCAL:210 = 1
IF TALENT:ARG:남자 == 0
	PRINTFORM %"Ｂ감각", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	IF TALENT:ARG:Ｂ민감
		PRINTFORM %"민감", 6, LEFT%
	ELSEIF TALENT:ARG:Ｂ둔감
		PRINTFORM %"둔감", 6, LEFT%
	ELSE
		PRINTFORM %"보통", 6, LEFT%
	ENDIF
	RESETCOLOR
	PRINTL  [215]둔감 [216]보통 [217]민감
	LOCAL:215 = 1
	PRINTFORM %"Ｍ감각", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	IF TALENT:ARG:Ｍ민감
		PRINTFORM %"민감", 6, LEFT%
	ELSEIF TALENT:ARG:Ｍ둔감
		PRINTFORM %"둔감", 6, LEFT%
	ELSE
		PRINTFORM %"보통", 6, LEFT%
	ENDIF
	RESETCOLOR
	PRINTL  [220]둔감 [221]보통 [222]민감
	LOCAL:220 = 1
ENDIF

PRINTFORM %"새드끼", 12, LEFT% …
SETCOLOR DEF_COLOR("暗황색")
IF TALENT:ARG:새드
	PRINTFORM %"새드", 6, LEFT%
ELSEIF ABL:ARG:새드끼 >= 3
	PRINTFORM %"강함", 6, LEFT%
ELSEIF ABL:ARG:새드끼 >= 1
	PRINTFORM %"있음", 6, LEFT%
ELSE
	PRINTFORM %"없음", 6, LEFT%
ENDIF
RESETCOLOR
PRINTL  [225]없음 [226]있음 [227]강함 [228]새드
LOCAL:225 = 1
PRINTFORM %"마조끼", 12, LEFT% …
SETCOLOR DEF_COLOR("暗황색")
IF TALENT:ARG:마조
	PRINTFORM %"마조", 6, LEFT%
ELSEIF ABL:ARG:마조끼 >= 3
	PRINTFORM %"강함", 6, LEFT%
ELSEIF ABL:ARG:마조끼 >= 1
	PRINTFORM %"있음", 6, LEFT%
ELSE
	PRINTFORM %"없음", 6, LEFT%
ENDIF
RESETCOLOR
PRINTL  [230]없음 [231]있음 [232]강함 [233]마조
LOCAL:230 = 1
IF TALENT:ARG:남자
	PRINTFORM %"ＢＬ끼", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	SELECTCASE ABL:ARG:ＢＬ끼
	CASE IS >= 5
		PRINTFORM %"다분함", 6, LEFT%
	CASE IS >= 3
		PRINTFORM %"강함", 6, LEFT%
	CASE IS >= 1
		PRINTFORM %"있음", 6, LEFT%
	CASEELSE
		PRINTFORM %"없음", 6, LEFT%
	ENDSELECT
	RESETCOLOR
	PRINTL  [235]없음 [236]있음 [237]강함 [238]다분함
	LOCAL:235 = 1
ELSE
	PRINTFORM %"레즈끼", 12, LEFT% …
	SETCOLOR DEF_COLOR("暗황색")
	SELECTCASE ABL:ARG:레즈끼
	CASE IS >= 5
		PRINTFORM %"다분함", 6, LEFT%
	CASE IS >= 3
		PRINTFORM %"강함", 6, LEFT%
	CASE IS >= 1
		PRINTFORM %"있음", 6, LEFT%
	CASEELSE
		PRINTFORM %"없음", 6, LEFT%
	ENDSELECT
	RESETCOLOR
	PRINTL  [240]없음 [241]있음 [242]강함 [243]다분함
	LOCAL:240 = 1
ENDIF
PRINTFORM %"성기술", 12, LEFT% …
SETCOLOR DEF_COLOR("暗황색")
SELECTCASE ABL:ARG:기교
CASE IS >= 5
	PRINTFORM %"능란함", 6, LEFT%
CASE IS >= 3
	PRINTFORM %"잘함", 6, LEFT%
CASE IS >= 1
	PRINTFORM %"있음", 6, LEFT%
CASEELSE
	PRINTFORM \@ TALENT:ARG:성경험전무 ? 전무 # 없음 \@
ENDSELECT
RESETCOLOR
;기교で巧みを선택できるのは２週目以降
PRINTFORML  [245]없음 [246]있음 [247]잘함 \@ FLAG:4 ? [248]능란함 # \@
LOCAL:245 = 1
PRINTFORML [999] 결정

$INPUT_LOOP
INPUT

CHOICE = RESULT

IF CHOICE != 999 && LOCAL:(CHOICE/5*5) == 0
	PRINTL 유효하지 않은 값입니다
	GOTO INPUT_LOOP
ENDIF

SELECTCASE CHOICE
CASE 999
	CALL SET_NEWNAME, MASTER
	REDRAW 1
	RETURN 1
CASE 0
	EXP:ARG:Ｖ경험 = 0
	EXP:ARG:Ｖ성교경험 = 0
CASE 1
	EXP:ARG:Ｖ경험 = 20 + RAND:50
CASE 2
	EXP:ARG:Ｖ경험 = 150 + RAND:50
CASE 3
	EXP:ARG:Ｖ경험 = 300 + RAND:300
CASE 5
	EXP:ARG:Ｖ성교경험 = 0
CASE 6
	EXP:ARG:Ｖ성교경험 = 20 + RAND:20
CASE 7
	EXP:ARG:Ｖ성교경험 = 50 + RAND:50
CASE 8
	EXP:ARG:Ｖ성교경험 = 150 + RAND:50
CASE 10
	EXP:ARG:Ｖ확장경험 = 0
CASE 11
	EXP:ARG:Ｖ확장경험 = 3 + RAND:5
CASE 12
	EXP:ARG:Ｖ확장경험 = 20 + RAND:10
CASE 13
	EXP:ARG:Ｖ확장경험 = 50 + RAND:50

CASE 15
	EXP:ARG:Ａ경험 = 0
	EXP:ARG:Ａ성교경험 = 0
CASE 16
	EXP:ARG:Ａ경험 = 20 + RAND:50
CASE 17
	EXP:ARG:Ａ경험 = 150 + RAND:50
CASE 18
	EXP:ARG:Ａ경험 = 300 + RAND:300
CASE 20
	EXP:ARG:Ａ성교경험 = 0
CASE 21
	EXP:ARG:Ａ성교경험 = 20 + RAND:20
CASE 22
	EXP:ARG:Ａ성교경험 = 50 + RAND:50
CASE 23
	EXP:ARG:Ａ성교경험 = 150 + RAND:50
CASE 25
	EXP:ARG:Ａ확장경험 = 0
CASE 26
	EXP:ARG:Ａ확장경험 = 3 + RAND:5
CASE 27
	EXP:ARG:Ａ확장경험 = 20 + RAND:10
CASE 28
	EXP:ARG:Ａ확장경험 = 50 + RAND:50
CASE 30
	EXP:ARG:Ｖ삽입경험 = 0
CASE 31
	EXP:ARG:Ｖ삽입경험 = 20 + RAND:20
CASE 32
	EXP:ARG:Ｖ삽입경험 = 50 + RAND:50
CASE 33
	EXP:ARG:Ｖ삽입경험 = 150 + RAND:50
CASE 35
	EXP:ARG:Ａ삽입경험 = 0
CASE 36
	EXP:ARG:Ａ삽입경험 = 20 + RAND:20
CASE 37
	EXP:ARG:Ａ삽입경험 = 50 + RAND:50
CASE 38
	EXP:ARG:Ａ삽입경험 = 150 + RAND:50

CASE 100
	TALENT:ARG:작은몸집 = 1
	TALENT:ARG:장신 = 0
CASE 101
	TALENT:ARG:작은몸집 = 0
	TALENT:ARG:장신 = 0
CASE 102
	IF TALENT:ARG:오토코노코 == 0
		TALENT:ARG:작은몸집 = 0
		TALENT:ARG:장신 = 1
	ENDIF
CASE 105
	TALENT:ARG:빈유 = 1
	TALENT:ARG:거유 = 0
CASE 106
	TALENT:ARG:빈유 = 0
	TALENT:ARG:거유 = 0
CASE 107
	TALENT:ARG:빈유 = 0
	TALENT:ARG:거유 = 1
CASE 108
	TALENT:ARG:빈유 = 0
	TALENT:ARG:거유 = 2
CASE 110
	TALENT:ARG:페니스 = CLEARBITS(TALENT:ARG:페니스, 0, 3)
	SETBIT TALENT:ARG:페니스, 1
CASE 111
	TALENT:ARG:페니스 = CLEARBITS(TALENT:ARG:페니스, 0, 3)
	SETBIT TALENT:ARG:페니스, 0
CASE 112
	TALENT:ARG:페니스 = CLEARBITS(TALENT:ARG:페니스, 0, 3)
	SETBIT TALENT:ARG:페니스, 2
CASE 113
	TALENT:ARG:페니스 = CLEARBITS(TALENT:ARG:페니스, 0, 3)
	SETBIT TALENT:ARG:페니스, 3
CASE 115
	TALENT:ARG:페니스 = CLEARBITS(TALENT:ARG:페니스, 11, 12)
	SETBIT TALENT:ARG:페니스, 12
CASE 116
	TALENT:ARG:페니스 = CLEARBITS(TALENT:ARG:페니스, 11, 12)
	SETBIT TALENT:ARG:페니스, 11
CASE 117
	TALENT:ARG:페니스 = CLEARBITS(TALENT:ARG:페니스, 11, 12)
CASE 120
	BASE:ARG:음모 = 100
CASE 121
	BASE:ARG:음모 = 300
CASE 122
	BASE:ARG:음모 = 600
CASE 123
	BASE:ARG:음모 = 500
CASE 124
	BASE:ARG:음모 = 700
CASE 125
	BASE:ARG:겨털 = 100
CASE 126
	BASE:ARG:겨털 = 300
CASE 127
	BASE:ARG:겨털 = 400
CASE 128
	BASE:ARG:겨털 = 700
CASE 130 TO 133
	TALENT:ARG:피부색 = CHOICE - 130
;오토코노코はこっちだけ
CASE 135 TO 136
	TALENT:ARG:외견연령 = CHOICE - 134
CASE 137 TO 138
	IF TALENT:ARG:오토코노코
		GOTO INPUT_LOOP
	ELSE
		TALENT:ARG:외견연령 = CHOICE - 134
	ENDIF
CASE 140 TO 143
	TALENT:ARG:유두 = CLEARBITS(TALENT:ARG:유두, 0, 9)
	SETBIT TALENT:ARG:유두, CHOICE - 140
CASE 145 TO 147
	TALENT:ARG:유두 = CLEARBITS(TALENT:ARG:유두, 10, 19)
	SETBIT TALENT:ARG:유두, CHOICE - 135
CASE 150 TO 153
	TALENT:ARG:유두 = CLEARBITS(TALENT:ARG:유두, 20, 29)
	SETBIT TALENT:ARG:유두, CHOICE - 130

CASE 200
	TALENT:ARG:Ｃ민감 = 0
	TALENT:ARG:Ｃ둔감 = 1
CASE 201
	TALENT:ARG:Ｃ민감 = 0
	TALENT:ARG:Ｃ둔감 = 0
CASE 202
	TALENT:ARG:Ｃ민감 = 1
	TALENT:ARG:Ｃ둔감 = 0
CASE 205
	TALENT:ARG:Ｖ민감 = 0
	TALENT:ARG:Ｖ둔감 = 1
CASE 206
	TALENT:ARG:Ｖ민감 = 0
	TALENT:ARG:Ｖ둔감 = 0
CASE 207
	TALENT:ARG:Ｖ민감 = 1
	TALENT:ARG:Ｖ둔감 = 0
CASE 210
	TALENT:ARG:Ａ민감 = 0
	TALENT:ARG:Ａ둔감 = 1
CASE 211
	TALENT:ARG:Ａ민감 = 0
	TALENT:ARG:Ａ둔감 = 0
CASE 212
	TALENT:ARG:Ａ민감 = 1
	TALENT:ARG:Ａ둔감 = 0
CASE 215
	TALENT:ARG:Ｂ민감 = 0
	TALENT:ARG:Ｂ둔감 = 1
CASE 216
	TALENT:ARG:Ｂ민감 = 0
	TALENT:ARG:Ｂ둔감 = 0
CASE 217
	TALENT:ARG:Ｂ민감 = 1
	TALENT:ARG:Ｂ둔감 = 0
CASE 220
	TALENT:ARG:Ｍ민감 = 0
	TALENT:ARG:Ｍ둔감 = 1
CASE 221
	TALENT:ARG:Ｍ민감 = 0
	TALENT:ARG:Ｍ둔감 = 0
CASE 222
	TALENT:ARG:Ｍ민감 = 1
	TALENT:ARG:Ｍ둔감 = 0
CASE 225
	ABL:ARG:새드끼 = 0
CASE 226
	ABL:ARG:새드끼 = 1 + RAND:2
CASE 227
	ABL:ARG:새드끼 = 3 + RAND:2
CASE 228
	ABL:ARG:새드끼 = 5
CASE 230
	ABL:ARG:마조끼 = 0
CASE 231
	ABL:ARG:마조끼 = 1 + RAND:2
CASE 232
	ABL:ARG:마조끼 = 3 + RAND:2
CASE 233
	ABL:ARG:마조끼 = 5
CASE 235
	ABL:ARG:ＢＬ끼 = 0
CASE 236
	ABL:ARG:ＢＬ끼 = 1 + RAND:2
CASE 237
	ABL:ARG:ＢＬ끼 = 3 + RAND:2
CASE 238
	ABL:ARG:ＢＬ끼 = 5
CASE 240
	ABL:ARG:레즈끼 = 0
CASE 241
	ABL:ARG:레즈끼 = 1 + RAND:2
CASE 242
	ABL:ARG:레즈끼 = 3 + RAND:2
CASE 243
	ABL:ARG:레즈끼 = 5
CASE 245
	ABL:ARG:기교 = 0
	PRINTFORML 성 경험이 전혀없는 걸로 합니까？（자동적으로 처녀, 동정이 됩니다）
	PRINTFORML [ 0] 예
	PRINTFORML [ 1] 아니오
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		TALENT:ARG:성경험전무 = 1
		EXP:ARG:Ｖ경험 = 0
		EXP:ARG:Ｖ성교경험 = 0
		EXP:ARG:Ｖ확장경험 = 0
		EXP:ARG:Ａ경험 = 0
		EXP:ARG:Ａ성교경험 = 0
		EXP:ARG:Ａ확장경험 = 0
		EXP:ARG:Ｖ삽입경험 = 0
		EXP:ARG:Ａ삽입경험 = 0
	ENDIF
CASE 246
	ABL:ARG:기교 = 1 + RAND:2
CASE 247
	ABL:ARG:기교 = 3 + RAND:2
CASE 248
	ABL:ARG:기교 = 5
CASEELSE
	GOTO INPUT_LOOP
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@CREATE_CHARA, ARG
#DIM LCOUNT
#DIM MEMO_LINECOUNT

REDRAW 0

MEMO_LINECOUNT = LINECOUNT
PRINTL 

;MASTERの複製作成
ADDCOPYCHARA ARG

CALL SETFLAG, "식별번호", ARG

;보너스소질の獲得など
IF RAND:3 == 0
	IF RAND:2
		TALENT:ARG:아픔에약함 = 1
	ELSE
		TALENT:ARG:아픔에강함 = 1
	ENDIF
ENDIF
;男にも濡れやすさの変化をつける試み
IF RAND:3 == 0
	IF RAND:2
		TALENT:ARG:젖기쉬움 = 1
	ELSE
		TALENT:ARG:젖기어려움 = 1
	ENDIF
ENDIF
;IF RAND:3 == 0
;	IF RAND:2
;		TALENT:ARG:현명함 = 1
;	ELSE
;		TALENT:ARG:미련함 = 1
;	ENDIF
;ENDIF
IF RAND:3 == 0 && TALENT:ARG:남자
	IF RAND:2
		TALENT:ARG:조루 = 1
	ELSE
		TALENT:ARG:지루 = 1
	ENDIF
ENDIF
;SIF RAND:3 == 0 && ABL:ARG:마조끼 + ABL:ARG:새드끼
;	TALENT:ARG:도착적 = 1
;IF RAND:3 == 0 && ABL:ARG:레즈끼 + ABL:ARG:ＢＬ끼
;	IF ABL:ARG:레즈끼 >= 5 && RAND:3 == 0
;		TALENT:ARG:남성혐오 = 1
;	ELSE
;		TALENT:ARG:바이 = 1
;	ENDIF
;ENDIF
SIF RAND:4 == 0
	TALENT:ARG:장사 = 1

IF ABL:ARG:기교
	FOR LCOUNT, 0, 5
		SELECTCASE RAND:10
		CASE 0 TO 5
			LOCAL = 0
		CASE 6 TO 7
			LOCAL = 1
		CASE 8
			LOCAL = 2
		CASE 9
			LOCAL = 3
		ENDSELECT

		SELECTCASE LCOUNT
		CASE 0
			TALENT:ARG:혀놀림 = LOCAL
		CASE 1
			TALENT:ARG:손가락놀림 = LOCAL
		CASE 2
			SIF COND("다리が無い", ARG) == 0
				TALENT:ARG:발놀림 = LOCAL
		CASE 3
			TALENT:ARG:허리놀림 = LOCAL
		CASE 4
			SIF TALENT:ARG:남자 == 0
				TALENT:ARG:마유 = LOCAL
		ENDSELECT
	NEXT
ELSE
	SIF RAND:3 == 0
		EXP:ARG:펠라경험 = 0
	SIF RAND:3 == 0
		EXP:ARG:수음경험 = 0
	SIF RAND:3 == 0
		EXP:ARG:풋잡경험 = 0
	SIF RAND:3 == 0
		EXP:ARG:허리사용경험 = 0
	SIF RAND:3 == 0
		EXP:ARG:파이즈리경험 = 0
	SIF RAND:3 == 0
		EXP:ARG:Ａ자극경험 = 0
	SIF RAND:3 == 0
		EXP:ARG:커닐링구스경험 = 0
	SIF RAND:3 == 0
		EXP:ARG:키스경험 = 0
ENDIF

FOR LCOUNT, 0, 100
	SIF EXP:ARG:LCOUNT
		EXP:ARG:LCOUNT = MAX(MULTIPLY(EXP:ARG:LCOUNT, 50 + (RAND:6 + RAND:6)*10), 1)
NEXT

;ＬＶの初期化
SIF ARG == MASTER && FLAG:255 == 0
	ABL:ARG:ＬＶ = 0

CALL RESET_EXP_GAIN, ARG
CALL SET_3SIZE, ARG
CALL GET_EXABL, ARG
CALL GET_EXTALENT, ARG
CALL SETFLAG, "유두の상세설정", ARG
CALL SETFLAG, "속성", ARG
CALL CALC, "선호체위", ARG

;소질の성경험전무はデバッグスキル扱いなのでここで消去
TALENT:ARG:성경험전무 = 0

CALL NEW_PRINT_STATE, ARG, "キャラメイク"
PRINTL 
PRINTFORML 이대로 좋습니까? 
PRINTFORML [ 0] 예
PRINTFORML [ 1] 재설정
;IF CONFIG("３사이즈") && (COND("오토코노코", ARG) || TALENT:ARG:남자 == 0)
	PRINTFORML [ 2] 리로드
	CALL INPUT_SELECT, 3
;ELSE
;	CALL INPUT_SELECT, 2
;ENDIF
SELECTCASE RESULT
CASE 0, 1
	REDRAW 1
	DELCHARA CHARANUM-1
	RETURN RESULT
CASE 2
	COPYCHARA CHARANUM-1, ARG
	DELCHARA CHARANUM-1

	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDSELECT

@GAME_RENEWAL
#DIM LCOUNT
#DIM MEMO_MASTER

;소지금は100万引いたものか1万かの大きな方とする
MONEY = MAX(MONEY - 1000000, 10000)

;상사상애を失う
TALENT:MASTER:상사상애 = 0
ITEM:서약의반지 = 0

CALL SETFLAG, "아이템플래그調整"
;유니크캐릭터의購入플래그リセット
VARSET FLAG, 0, 1000, 1100
;탐색のCLEAR플래그リセット
VARSET FLAG, 0, 50, 60

FLAG:6 = 1
;リセット不可な変数フラグ…1bit 主人の외견연령再설정, 2bit 유두の詳細再설정
SETBIT FLAG:11, 1
SETBIT FLAG:11, 2
;날짜などリセット…はしない
;DAY = 1
TIME = 0
FLAG:872 = 0
FLAG:873 = 0

;前回보너스の終了
SIF GETBIT(FLAG:33, 0)
	TALENT:MASTER:조합지식 = 0
SIF GETBIT(FLAG:33, 1)
	TALENT:MASTER:치료 = 0
IF GETBIT(FLAG:33, 2)
	TALENT:MASTER:마법사 = 0
	;魔法忘れるのはやめた
	;FOR LCOUNT, 230, 260
	;	TALENT:MASTER:LCOUNT = 0
	;NEXT
ENDIF
SIF GETBIT(FLAG:33, 3)
	TALENT:MASTER:드라군 = 0
SIF GETBIT(FLAG:33, 4)
	TALENT:MASTER:가창기능 = 0
SIF GETBIT(FLAG:33, 5)
	TALENT:MASTER:매혹 = 0
SIF GETBIT(FLAG:33, 6)
	TALENT:MASTER:매력 = 0
SIF GETBIT(FLAG:33, 7)
	TALENT:MASTER:조루 = 0
SIF GETBIT(FLAG:33, 8)
	TALENT:MASTER:지루 = 0
SIF GETBIT(FLAG:33, 9)
	TALENT:MASTER:백발백중 = 0
SIF GETBIT(FLAG:33, 10)
	TALENT:MASTER:바늘다루기 = 0
SIF GETBIT(FLAG:33, 11)
	TALENT:MASTER:소환천재（天才） = 0
SIF GETBIT(FLAG:33, 12)
	TALENT:MASTER:소환천재（天災） = 0
SIF GETBIT(FLAG:33, 13)
	TALENT:MASTER:용의혈통 = 0
SIF GETBIT(FLAG:33, 14)
	TALENT:MASTER:구현 = 0
SIF GETBIT(FLAG:33, 15)
	TALENT:MASTER:현명함 = 0

SIF GETBIT(FLAG:33, 30)
	TALENT:MASTER:혀놀림 = CSVTALENT(NO:MASTER, 180, 0)
SIF GETBIT(FLAG:33, 31)
	TALENT:MASTER:손가락놀림 = CSVTALENT(NO:MASTER, 181, 0)
SIF GETBIT(FLAG:33, 32)
	TALENT:MASTER:발놀림 = CSVTALENT(NO:MASTER, 182, 0)
SIF GETBIT(FLAG:33, 33)
	TALENT:MASTER:허리놀림 = CSVTALENT(NO:MASTER, 183, 0)
SIF GETBIT(FLAG:33, 34)
	TALENT:MASTER:마유 = CSVTALENT(NO:MASTER, 184, 0)

FLAG:33 = 0
;레벨리미터解除
FLAG:247 = 0

;キャラメイク中フラグON
SETBIT FLAG:99, 13

;MASTERが別人になったかを識別する
MEMO_MASTER = BASE:MASTER:식별번호

CALL START_CHARA_SELECT

;別人？
IF MEMO_MASTER != BASE:MASTER:식별번호
	;前の主人限定のフラグの消去
	FOR LCOUNT, 1, CHARANUM
		MARK:LCOUNT:주인굴복각인 = 0
	NEXT
ENDIF

;TFLAGを持ち越す몽마の数の記録に使用
VARSET TFLAG
;보너스
CALL CLEAR_BONUS

;마음에듬の몽마の持ち越し
LOCAL = 0
FOR LCOUNT, 1, CHARANUM
	;繰り越し判定で使用
	CFLAG:LCOUNT:6 = 0
	SIF TALENT:LCOUNT:음란 + TALENT:LCOUNT:연모
		LOCAL += 1
NEXT
SIF LOCAL
	CALL KURIKOSI_MUMA

;선택しなかった몽마をメンバーから外す
FOR LCOUNT, 0, 100
	SIF CHARANUM <= LCOUNT
		BREAK
	SIF LCOUNT <= MASTER || CFLAG:LCOUNT:6
		CONTINUE
	CALL DELETE_CHARA, LCOUNT
	LCOUNT -= 1
NEXT

;攻略人数リセット
FLAG:30 = 0
;다방の収入等リセット
MONEY:7 = 0

FOR LCOUNT, 1, CHARANUM
	;체력とか회복
	BASE:LCOUNT:체력 = MAXBASE:LCOUNT:체력
	BASE:LCOUNT:기력 = MAXBASE:LCOUNT:기력
	BASE:LCOUNT:정력 = MAXBASE:LCOUNT:정력
	BASE:LCOUNT:스톡 = MAXBASE:LCOUNT:스톡

	SIF LCOUNT == MASTER
		CONTINUE

	CFLAG:LCOUNT:97 = 0
	;호감도を2500に. 
	CFLAG:LCOUNT:2 = MIN(CFLAG:LCOUNT:2, 2500)
	;強制的に서약
	TALENT:LCOUNT:서약 = 1
	;상사상애は失う
	TALENT:LCOUNT:상사상애 = 0
NEXT

IF CHARA_NUM("繰り越しキャラ")
	TARGET = MASTER + 1
	PRINTFORMW \@CHARA_NUM("繰り越しキャラ") >= 2 ? %CALLNAME:TARGET%들과 # %조사처리(CALLNAME:TARGET,"과")% \@ 함께 {FLAG:4 + 1}주차를 즐겨주세요. 
ELSE
	TARGET = 0
	PRINTFORMW 그러면 {FLAG:4 + 1}주차를 즐겨주세요. 
ENDIF
;繰越플래그初期化
CVARSET CFLAG, 6, 0

ASSI = 0

@RENEWAL_ABL, ARG
#DIM LCOUNT
FOR LCOUNT, 0, 300
	;수수께끼의매력, 달콤한말, 손대중は残す
	SELECTCASE TALENTNAME:LCOUNT
	CASE "수수께끼의매력", "달콤한말", "손대중"
		CONTINUE
	ENDSELECT

	TALENT:ARG:LCOUNT = 0
NEXT
FOR LCOUNT, 0, 999
	CFLAG:ARG:LCOUNT = 0
NEXT
;레벨以外初期化
FOR LCOUNT, 0, 50
	ABL:ARG:LCOUNT = 0
NEXT
;경험치以外初期化
FOR LCOUNT, 0, 100
	SIF LCOUNT != 5
		EXP:ARG:LCOUNT = 0
NEXT
FOR LCOUNT, 0, 11
	MARK:ARG:LCOUNT = 0
NEXT
FOR LCOUNT, 0, 16
	JUEL:ARG:LCOUNT = 0
NEXT

ABL:ARG:기교 = 2
TALENT:ARG:남자 = 1
;임신이벤트など, ベース플래그のリセット
VARSET BASE:ARG:체력, 0, 20, 60
VARSET TCVAR:ARG:0

;-------------------------------------------------
;PCを선택式にしてみる
;-------------------------------------------------
@START_CHARA_SELECT
PRINTL 플레이어를선택해 주세요
;周回플래그がある場合
IF FLAG:4
	PRINTL 
	PRINTL [ 0] 통상의 주인……이른바 "당신"을 선택하고 캐릭터 설정을 합니다
	PRINTL [ 1] 몽마의 목록에서 선택
	PRINTL [ 2] 이전 주차의 주인을 계승
	CALL INPUT_SELECT, 3
ELSE
	PRINTPLAIN 기본적으로는 [ 0]을 추천합니다
	PRINTL 
	PRINTL [ 0] 통상의 주인……이른바 "당신"을 선택하고 캐릭터 설정을 합니다
	PRINTL [ 1] 몽마의 목록에서 선택
	CALL INPUT_SELECT, 2
ENDIF

SELECTCASE RESULT
CASE 0
	PRINTFORMW 플레이어는 "당신"이 됩니다
	CALL MASTER_FAMALE_A
	RETURN 1
CASE 2
	PRINTFORMW 계속해서, %조사처리(CALLNAME:MASTER,"를")% 플레이어로 합니다
	RETURN 1
ENDSELECT

VARSET LOCAL

CALL START_CHARA_SHOW

$INPUT_LOOP
INPUT

IF RESULT == 0
	PRINTFORMW 플레이어는 "당신"이 됩니다
	CALL MASTER_FAMALE_A
	RETURN 0
;数値は캐릭터의아이템番号の最小値と최대치依存。
ELSEIF RESULT < 0 || RESULT > 98
	GOTO INPUT_LOOP
;売値の설정がないなら再入力
ELSEIF ITEMSALES:(RESULT + 100) == 0
	GOTO INPUT_LOOP
ENDIF

;C = 캐릭터번호, E = 上位種체크(@CHARA_MANUAL)
C = RESULT
;最初のページは基本種
E = 0
;キャラ解説を入れる
CALL CHARA_MANUAL, C, "주인"
;いいえを選ぶと再入力
IF RESULT == 0
	;表示されていたキャラを하양紙に戻す
	DELCHARA CHARANUM-1
	CALL START_CHARA_SHOW
	GOTO INPUT_LOOP
ENDIF

SWAPCHARA CHARANUM-1, MASTER
DELCHARA CHARANUM-1

CALL SET_NEWNAME, MASTER
;마법사持ちは, 마법を持っている가능性アリ
SIF TALENT:MASTER:마법사 == 1
	CALL GET_MAGIC, MASTER

PRINTFORMW 플레이어를 「%CALLNAME:MASTER%」%조사만처리(CALLNAME:MASTER,"로")%  설정합니다
;최대레벨を合わせる
FLAG:255 = ABL:MASTER:ＬＶ

@START_CHARA_SHOW
PRINTL ★★플레이어 캐릭터를 선택해주세요★★
DRAWLINE

CALL SET_CHARA_BUY_SHOW

;"당신"は禁止
ITEMSALES:당신 = 0

DRAWLINE
PRINTFORML [ 0] 변경하지 않음


@KURIKOSI_MUMA
#DIM LCOUNT
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
REDRAW 0
PRINTFORML 
PRINTFORML 계약이 끝난 몽마를 {1 + TFLAG:0}인만 수중에 남길 수 있습니다. 
PRINTFORML （선택된 캐릭터의 호감도는 최대 2500이 \@ TALENT:MASTER:상사상애 ? 되고, 소질：%TALENTNAME:211%등을 잃게 # \@됩니다）
PRINTFORML （또한, 자동으로 서약상태가 되어 다음주차에서 매각할 수 없습니다）
PRINT 다음 중에서 선택해주세요. 선택된 캐릭터는 이름 오른쪽에 
CALL HEARTMARK
PRINTL 가 붙습니다
FOR LCOUNT, 1, CHARANUM
	SIF TALENT:LCOUNT:연모 + TALENT:LCOUNT:음란 == 0
		CONTINUE

	CALL PRINT_NUM_NAME, "繰越キャラ", LCOUNT
NEXT

IF CHARA_NUM("繰り越しキャラ")
	PRINTL  [100] 이것으로 좋다
ELSE
	PRINTL  [100] 아무도 남기지 않는다
ENDIF

$INPUT_LOOP
INPUT
IF RESULT == 100
	REDRAW 1
	RETURN 0
ELSEIF RESULT < MASTER || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ELSEIF TALENT:RESULT:음란 + TALENT:RESULT:연모 == 0
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:6 == 0 && CHARA_NUM("繰り越しキャラ") > TFLAG:0
	GOTO INPUT_LOOP
ENDIF

INVERTBIT CFLAG:RESULT:6, 0

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@CLEAR_BONUS
;FLAG:31（CLEARした캐릭터의種類）で보너스ゲット…開発中
SIF FLAG:4 == 0
	RETURN 0

CALL CLEAR_BONUS_SELECT, FLAG:4 + FLAG:31, FLAG:255

ABL:MASTER:ＬＶ = MIN(ABL:MASTER:ＬＶ, FLAG:255)
;경험치を, LVに見合ったものに変更
EXP:MASTER:경험치 = EXP_NEXTLV(MASTER, ABL:MASTER:ＬＶ - 1)


;ARGに特典ポイント、ARG:1に特典Ｐでどうこうする前の당신のLVが入っている
;TFLAG:0は次週に連れていくキャラ枠の増分
@CLEAR_BONUS_SELECT, ARG, ARG:1
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM IS_SELECT, 100
#DIM CHOICE

MEMO_LINECOUNT = LINECOUNT
REDRAW 0

;선택지[ X]を선택できるならばIS_SELECT:X > 0 となる。値は関連する特典Ｐ
VARSET IS_SELECT

PRINTFORML 
PRINTFORML 클리어 캐릭터의 종류와 주차횟수에 따라, 특전을 얻을 수 있습니다. 
PRINTFORML 아래에서 선택해 주세요：{ARG}포인트 남음
PRINTFORML 
IF TALENT:MASTER:조합지식 == 0
	PRINTFORML  [ 0] \@ GETBIT(FLAG:33, 0) ? ○ # －\@ 조합지식 （특수 아이템, 미약강화 등） ３Ｐ
	IS_SELECT:0 = 3
ENDIF
IF TALENT:MASTER:치료 == 0
	PRINTFORML  [ 1] \@ GETBIT(FLAG:33, 1) ? ○ # －\@ 치료 （시간에 따른 체력회복ＵＰ） ３Ｐ
	IS_SELECT:1 = 3
ENDIF
IF TALENT:MASTER:마법사 == 0
	PRINTFORML  [ 2] \@ GETBIT(FLAG:33, 2) ? ○ # －\@ 마법사   ６Ｐ
	IS_SELECT:2 = 6
ENDIF
IF TALENT:MASTER:드라군 == 0
	PRINTFORML  [ 3] \@ GETBIT(FLAG:33, 3) ? ○ # －\@ 용과 나 （용간사용가능） ５Ｐ
	IS_SELECT:3 = 5
ENDIF
IF TALENT:MASTER:가창기능 == 0
	PRINTFORML  [ 4] \@ GETBIT(FLAG:33, 4) ? ○ # －\@ 가창기능   ４Ｐ
	IS_SELECT:4 = 4
ENDIF
;매력のほぼ上位
IF TALENT:MASTER:매혹 == 0
	PRINTFORML  [ 5] \@ GETBIT(FLAG:33, 5) ? ○ # －\@ 매혹 （호감도ＵＰ,반감감소등） ４Ｐ
	IS_SELECT:5 = 4
ENDIF
IF TALENT:MASTER:매력 == 0
	PRINTFORML  [ 6] \@ GETBIT(FLAG:33, 6) ? ○ # －\@ 매력 （호감도ＵＰ） ３Ｐ
	IS_SELECT:6 = 3
ENDIF
IF TALENT:MASTER:조루 == 0 && PENIS(MASTER)
	PRINTFORML  [ 7] \@ GETBIT(FLAG:33, 7) ? ○ # －\@ 조루 ２Ｐ
	IS_SELECT:7 = 2
ENDIF
IF TALENT:MASTER:지루 == 0 && PENIS(MASTER)
	PRINTFORML  [ 8] \@ GETBIT(FLAG:33, 8) ? ○ # －\@ 지루 ２Ｐ
	IS_SELECT:8 = 2
ENDIF
IF TALENT:MASTER:백발백중 == 0 && PENIS(MASTER) && CONFIG("임신패치")
	PRINTFORML  [ 9] \@ GETBIT(FLAG:33, 9) ? ○ # －\@ 백발백중 （확실히임신시킵니다） ３Ｐ
	IS_SELECT:9 = 3
ENDIF
IF TALENT:MASTER:바늘다루기 == 0
	PRINTFORML  [10] \@ GETBIT(FLAG:33, 10) ? ○ # －\@ 바늘다루기 （바늘의 효과ＵＰ） ２Ｐ
	IS_SELECT:10 = 2
ENDIF
IF TALENT:MASTER:소환천재（天才） == 0
	PRINTFORML  [11] \@ GETBIT(FLAG:33, 11) ? ○ # －\@ 소환천재（천재）（소환사고 조작가능）   10Ｐ
	IS_SELECT:11 = 10
ENDIF
IF TALENT:MASTER:소환천재（天災） == 0
	PRINTFORML  [12] \@ GETBIT(FLAG:33, 12) ? ○ # －\@ 소환천재（천재지변）（소환사고율100％! ）    ５Ｐ
	IS_SELECT:12 = 5
ENDIF
IF TALENT:MASTER:용의혈통 == 0
	PRINTFORML  [13] \@ GETBIT(FLAG:33, 13) ? ○ # －\@ 용의혈통 （정력ＵＰ등）   ６Ｐ
	IS_SELECT:13 = 6
ENDIF
IF TALENT:MASTER:구현 == 0
	PRINTFORML  [14] \@ GETBIT(FLAG:33, 14) ? ○ # －\@ 구현 （페니스의 구현화）   ６Ｐ
	IS_SELECT:14 = 6
ENDIF
IF TALENT:MASTER:현명함 == 0
	PRINTFORML  [15] \@ GETBIT(FLAG:33, 15) ? ○ # －\@ 현명함 ３Ｐ
	IS_SELECT:15 = 3
ENDIF

IF TALENT:MASTER:혀놀림 < 3
	PRINTFORML  [30] \@ GETBIT(FLAG:33, 30) ? ○ # －\@ 혀놀림 （컨닐링구스등의 효과ＵＰ） ４Ｐ
	IS_SELECT:30 = 4
ENDIF
IF TALENT:MASTER:손가락놀림 < 3
	PRINTFORML  [31] \@ GETBIT(FLAG:33, 31) ? ○ # －\@ 손가락놀림 （애무등의 효과ＵＰ） ４Ｐ
	IS_SELECT:31 = 4
ENDIF
IF TALENT:MASTER:발놀림 < 3
	PRINTFORML  [32] \@ GETBIT(FLAG:33, 32) ? ○ # －\@ 발놀림 （풋잡의 효과ＵＰ） ４Ｐ
	IS_SELECT:32 = 4
ENDIF
IF TALENT:MASTER:허리놀림 < 3
	PRINTFORML  [33] \@ GETBIT(FLAG:33, 33) ? ○ # －\@ 허리놀림 （섹스계의 효과ＵＰ） ４Ｐ
	IS_SELECT:33 = 4
ENDIF
IF TALENT:MASTER:마유 < 3 && TALENT:MASTER:남자 == 0
	PRINTFORML  [34] \@ GETBIT(FLAG:33, 34) ? ○ # －\@ 가슴놀림（=마유,파이즈리의 효과ＵＰ） ４Ｐ
	IS_SELECT:34 = 4
ENDIF

IF TALENT:MASTER:새드
	PRINTFORML  [40] \@ GETBIT(FLAG:33, 40) ? ○ # －\@ 새드를 잃는다１Ｐ
	IS_SELECT:40 = 1
ENDIF
IF TALENT:MASTER:마조
	PRINTFORML  [41] \@ GETBIT(FLAG:33, 41) ? ○ # －\@ 마조를 잃는다１Ｐ
	IS_SELECT:41 = 1
ENDIF
IF TALENT:MASTER:몽마 || TALENT:MASTER:상위몽마
	PRINTFORML  [42] \@ GETBIT(FLAG:33, 42) ? ○ # －\@ 몽마를 잃는다１Ｐ
	IS_SELECT:42 = 1
ENDIF

IF CHARA_NUM("연모음란") >= 2
	PRINTL 
	PRINTFORML  [50] \@ TFLAG:0 ? +{TFLAG:0} # －\@ 다음주차에 인계할 캐릭터를 늘린다（복수 가능）　　５Ｐ
	IS_SELECT:50 = 5
ENDIF

PRINTL 
PRINTFORML %NAME:MASTER%의LV:{ARG:1} \@ FLAG:255 != ARG:1 ? → {FLAG:255} # \@

PRINTFORML  [\@ FLAG:255 > 2 || GETBIT(FLAG:33, 50) ? 90 # × \@] \@ GETBIT(FLAG:33, 50) ? ○ # －\@ MASTER가 ２LV를 잃는 대신에 １Ｐ를 얻는다 +１Ｐ
SIF FLAG:255 > 2 || GETBIT(FLAG:33, 50)
	IS_SELECT:90 = 1

PRINTFORML  [\@ FLAG:255 > 4 || GETBIT(FLAG:33, 51) ? 91 # × \@] \@ GETBIT(FLAG:33, 51) ? ○ # －\@ MASTER가 ４LV를 잃는 대신에 ２Ｐ를 얻는다 +２Ｐ
SIF FLAG:255 > 4 || GETBIT(FLAG:33, 51)
	IS_SELECT:91 = 2

PRINTFORML  [\@ FLAG:255 > 8 || GETBIT(FLAG:33, 52) ? 92 # × \@] \@ GETBIT(FLAG:33, 52) ? ○ # －\@ MASTER가 ８LV를 잃는 대신에 ４Ｐ를 얻는다 +４Ｐ
SIF FLAG:255 > 8 || GETBIT(FLAG:33, 52)
	IS_SELECT:92 = 4

PRINTFORML  [\@ FLAG:255 > 16 || GETBIT(FLAG:33, 53) ? 93 # × \@] \@ GETBIT(FLAG:33, 53) ? ○ # －\@ MASTER가 16LV를 잃는 대신에 ８Ｐ를 얻는다 +８Ｐ
SIF FLAG:255 > 16 || GETBIT(FLAG:33, 53)
	IS_SELECT:93 = 8

PRINTFORML  [\@ FLAG:255 > 32 || GETBIT(FLAG:33, 54) ? 94 # × \@] \@ GETBIT(FLAG:33, 54) ? ○ # －\@ MASTER가 32LV를 잃는 대신에 16Ｐ를 얻는다 +16Ｐ
SIF FLAG:255 > 32 || GETBIT(FLAG:33, 54)
	IS_SELECT:94 = 16

PRINTL 
PRINTFORML [100] 포인트 배분 종료\@ ARG ? （아직 {ARG}포인트가 남아있습니다） # \@

$INPUT_LOOP
INPUT

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	;実際の処理
	SIF GETBIT(FLAG:33, 0)
		TALENT:MASTER:조합지식 = 1
	SIF GETBIT(FLAG:33, 1)
		TALENT:MASTER:치료 = 1
	IF GETBIT(FLAG:33, 2)
		TALENT:MASTER:마법사 = 1
		TALENT:MASTER:미니멈 = 1
		TALENT:MASTER:센시브 = 1
		TALENT:MASTER:우르즈군 = 1
		TALENT:MASTER:이사군 = 1
	ENDIF
	SIF GETBIT(FLAG:33, 3)
		TALENT:MASTER:드라군 = 1
	SIF GETBIT(FLAG:33, 4)
		TALENT:MASTER:가창기능 = 1
	SIF GETBIT(FLAG:33, 5)
		TALENT:MASTER:매혹 = 1
	SIF GETBIT(FLAG:33, 6)
		TALENT:MASTER:매력 = 1
	IF GETBIT(FLAG:33, 7)
		TALENT:MASTER:조루 = 1
		TALENT:MASTER:지루 = 0
	ENDIF
	IF GETBIT(FLAG:33, 8)
		TALENT:MASTER:조루 = 0
		TALENT:MASTER:지루 = 1
	ENDIF
	SIF GETBIT(FLAG:33, 9)
		TALENT:MASTER:백발백중 = 1
	SIF GETBIT(FLAG:33, 10)
		TALENT:MASTER:바늘다루기 = 1
	SIF GETBIT(FLAG:33, 11)
		TALENT:MASTER:소환천재（天才） = 1
	SIF GETBIT(FLAG:33, 12)
		TALENT:MASTER:소환천재（天災） = 1
	SIF GETBIT(FLAG:33, 13)
		TALENT:MASTER:용의혈통 = 2
	SIF GETBIT(FLAG:33, 14)
		TALENT:MASTER:구현 = 1
	IF GETBIT(FLAG:33, 15)
		TALENT:MASTER:현명함 = 1
		TALENT:MASTER:미련함 = 0
	ENDIF

	SIF GETBIT(FLAG:33, 30)
		TALENT:MASTER:혀놀림 = 3
	SIF GETBIT(FLAG:33, 31)
		TALENT:MASTER:손가락놀림 = 3
	SIF GETBIT(FLAG:33, 32)
		TALENT:MASTER:발놀림 = 3
	SIF GETBIT(FLAG:33, 33)
		TALENT:MASTER:허리놀림 = 3
	SIF GETBIT(FLAG:33, 34)
		TALENT:MASTER:마유 = 3

	SIF GETBIT(FLAG:33, 40)
		TALENT:MASTER:새드 = 0
	SIF GETBIT(FLAG:33, 41)
		TALENT:MASTER:마조 = 0
	IF GETBIT(FLAG:33, 42)
		TALENT:MASTER:몽마 = 0
		TALENT:MASTER:상위몽마 = 0
	ENDIF

	REDRAW 1
	RETURN 0
CASE 0 TO 49
	IF IS_SELECT:CHOICE
		;ボーナスを取り消す
		IF GETBIT(FLAG:33, CHOICE)
			CLEARBIT FLAG:33, CHOICE
			ARG += IS_SELECT:CHOICE
		;ボーナス獲得処理
		ELSEIF ARG >= IS_SELECT:CHOICE
			SETBIT FLAG:33, CHOICE
			ARG -= IS_SELECT:CHOICE

			;相反する소질を獲得した場合の処理
			SELECTCASE CHOICE
			;조루と지루
			CASE 7
				IF GETBIT(FLAG:33, 8)
					CLEARBIT FLAG:33, 8
					ARG += IS_SELECT:8
				ENDIF
			CASE 8
				IF GETBIT(FLAG:33, 7)
					CLEARBIT FLAG:33, 7
					ARG += IS_SELECT:7
				ENDIF
			CASE 11
				IF GETBIT(FLAG:33, 12)
					CLEARBIT FLAG:33, 12
					ARG += IS_SELECT:12
				ENDIF
			;소환천재（天才）と天災
			CASE 12
				IF GETBIT(FLAG:33, 11)
					CLEARBIT FLAG:33, 11
					ARG += IS_SELECT:11
				ENDIF
			ENDSELECT
		ENDIF
	ENDIF

CASE 50
	IF IS_SELECT:CHOICE
		IF ARG < 5 && TFLAG:0 == 0
			PRINTFORMW 포인트가 부족합니다
		ELSEIF CHARA_NUM("연모음란") == 2 
			IF TFLAG:0 == 0 && ARG >= 5
				PRINTW 제한을 하나 늘렸습니다
				ARG -= 5
				TFLAG:0 = 1
			ELSE
				ARG += 5
				TFLAG:0 = 0
			ENDIF
		ELSE
			ARG += TFLAG:0 * 5
			TFLAG:0 = 0
			PRINTFORML ～계승할 수 있는 캐릭터 목록～
			FOR LCOUNT, 1, CHARANUM
				SIF TALENT:LCOUNT:연모 + TALENT:LCOUNT:음란 == 0
					CONTINUE
				CALL PRINT_NUM_NAME, "체력", LCOUNT
			NEXT
			PRINTFORML 다음 주차에 계승할 캐릭터 1명 당 5Ｐ필요합니다.
			PRINTFORML 여기서 포인트를 지불하지 않아도 1명은 데리고 갈 수 있습니다. 몇 명 추가로 데리고 갑니까？
			PRINTL 
			PRINTFORML [ 0] 추가하지 않는다
			FOR LCOUNT, 1, MIN(ARG/5, CHARA_NUM("연모음란") - 1) + 1
				PRINTFORML [{LCOUNT, 2}] {LCOUNT}명 추가
			NEXT
			SIF MIN(ARG/5, CHARA_NUM("연모음란") - 1) >= 6
				PRINTFORML [6～{MIN(ARG/5, CHARA_NUM("연모음란") - 1)}] 입력한 인원만큼 추가

			CALL INPUT_SELECT, MIN(ARG/5, CHARA_NUM("연모음란") - 1) + 1

			IF RESULT
				ARG -= 5*RESULT
				TFLAG:0 = RESULT
			ENDIF
		ENDIF
	ENDIF
CASE 90 TO 99
	IF IS_SELECT:CHOICE
		;ポイントに変えたLVを戻す
		IF GETBIT(FLAG:33, CHOICE - 40)
			CLEARBIT FLAG:33, CHOICE - 40
			ARG -= IS_SELECT:CHOICE
			FLAG:255 += IS_SELECT:CHOICE * 2
		;LVをポイントに変換
		ELSE
			SETBIT FLAG:33, CHOICE - 40
			ARG += IS_SELECT:CHOICE
			FLAG:255 -= IS_SELECT:CHOICE * 2
		ENDIF
	ENDIF

ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

