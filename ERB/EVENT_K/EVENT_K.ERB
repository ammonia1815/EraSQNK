;조교時キャラ別구상を一括管理
;-----------------------------------------------------------------------------------
;구상用システムファイルの存在判定
;EVENT_K.ERB(つまりこのファイル)が存在しているかを判定
;구상が使用가능で구상機能を能動的にOFFにしていない場合には, 自動的にスイッチが入る
;各구상ファイル内でキャラ플래그を制御するので, 
;EVENT_Kxx.ERB(キャラ別구상ファイル)を途中で削除してもエラーはでない
;ただし, 구상をONにした状態でこのファイルを削除するとエラーが発生する
;あっても邪魔にはならないので削除しない方が良いだろう
;-----------------------------------------------------------------------------------

;--------------------------------------------------
;@KOJO_MESSAGE_COM関係
;커맨드実行時に出力されます
;--------------------------------------------------
@KOJO_MESSAGE_COM
#DIM MEMO_LINECOUNT
SIF FLAG:7 == 0
	RETURN 0
PRINTL 

;구상があるかどうかを行数で체크
MEMO_LINECOUNT = LINECOUNT

;재갈着用時には通常の구상をスキップする（OFFだと입を塞いでるのに喋りまくる）
IF TEQUIP:재갈 && SELECTCOM != 135
	CALL KOJO_MESSAGE_GAG
	TRYCALL TRAIN_MESSAGE_C
ELSE
	TRYCALLFORM KOJO_MESSAGE_COM_{NO:TARGET}_{CFLAG:209}

	;범용구상など
	IF LINECOUNT == MEMO_LINECOUNT
		;식스나인
		IF SELECTCOM == 99
			TRYCALLFORM COM_ABLE{TEQUIP:식스나인}
			SIF RESULT
				SELECTCOM = TEQUIP:식스나인
			IF PREVCOM == TEQUIP:식스나인
				TRYCALLFORM COM_ABLE{TEQUIP:PLAYER:식스나인}
				SIF RESULT
					SELECTCOM = TEQUIP:PLAYER:식스나인
			ENDIF
			TRYCALLFORM KOJO_MESSAGE_COM_{NO:TARGET}_{CFLAG:209}
			SELECTCOM = 99
		ELSEIF SELECTCOM == 97
			SELECTCOM = 90
			TRYCALLFORM KOJO_MESSAGE_COM_{NO:TARGET}_{CFLAG:209}
			SELECTCOM = 97
		ELSE
			CALL TRAINVOICE
		ENDIF
	ENDIF
ENDIF

;何も喋っていない場合には１行消す
SIF LINECOUNT == MEMO_LINECOUNT
	CLEARLINE 1

;사정게이지の체크
CALL CHECK_BASEPLAYER2

;効果を及ぼすのはメ임프レイヤー一人のみ
CALL CALC, "에너지드레인"

;--------------------------------------------------
;@KOJO_MESSAGE_BEFORE_COM関係
;커맨드実行時に조교텍스트の前に出力されます
;--------------------------------------------------
@KOJO_MESSAGE_BEFORE_COM
#DIM MEMO_LINECOUNT

SIF FLAG:7 == 0
	RETURN 0

;플레이ヤーの절정度など試算(COND("절정", PLAYER)を使うため)
CALL SAMEN_CHECK

PRINTL 
MEMO_LINECOUNT = LINECOUNT

;재갈着用時には구상をスキップする（OFFだと입を塞いでるのに喋りまくる）
IF TEQUIP:재갈 && SELECTCOM != 135
	CALL KOJO_MESSAGE_BEFORE_GAG
ELSE
	TRYCALLFORM KOJO_MESSAGE_BEFORE_COM_{NO:TARGET}_{CFLAG:209}
ENDIF

SIF LINECOUNT == MEMO_LINECOUNT
	CLEARLINE 1

;플레이ヤーの절정度などを元の値に戻す
CALL SAMEN_CHECK, -1

;사정게이지の체크
CALL CHECK_BASEPLAYER2

;--------------------------------------------------
;@KOJO_MESSAGE_AFTER_COM関係
;커맨드実行時にターン終了時に出力されます
;--------------------------------------------------
@KOJO_MESSAGE_AFTER_COM
SIF FLAG:7 == 0
	RETURN 0

;재갈着用時には구상をスキップする（OFFだと입を塞いでるのに喋りまくる）
SIF TEQUIP:재갈 && SELECTCOM != 135
	RETURN 0

TRYCALLFORM KOJO_MESSAGE_AFTER_COM_{NO:TARGET}_{CFLAG:209}

;-------------------------------------------------
;@KOJO_MESSAGE_PALAMCNG関係
;파라미터変動をトリガーにした구상
;파라미터変動後に구상を発動します
;-------------------------------------------------
@KOJO_MESSAGE_PALAMCNG
SIF FLAG:7 == 0
	RETURN 0

VARSET FLAG, 0, 100, 200

;재갈着用時には구상をスキップする（OFFだと입を塞いでるのに喋りまくる）
SIF TEQUIP:재갈
	RETURN 0

LOCALS = %CHECK_MESSAGE_PALAMCNG()%
SIF LOCALS == ""
	RETURN 0

PRINTL 

TRYCALLFORM KOJO_MESSAGE_PALAMCNG_{NO:TARGET}_{CFLAG:209}, LOCALS

;연모の時に섹스で破瓜
SIF GETBIT(NOWEX:처녀상실, 1)
	PRINTFORML %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:MASTER%에게 처녀를 바칠 수 있어서  행복을 느끼고 있는 것같다…

;상대が상위몽마の場合には効果を発揮する공략각인（굴복각인取得に요정する파라미터量を減らす）を得る
IF FIRST_EV(LOCALS) == 0
	SELECTCASE LOCALS
	CASE "Ｃ절정", "요도절정", "Ｖ절정", "Ａ절정", "Ｂ절정", "Ｍ절정", "절정방뇨"
		IF TALENT:상위몽마 && MARK:굴복각인 < 3
			DRAWLINE
			PRINTFORMW ☆★☆이벤트 달성·굴복각인 습득의 패널티가 경감되었습니다☆★☆
			DRAWLINE
		ENDIF
		MARK:공략각인 += 1
	CASE "Ｃ절정20回이상", "요도절정20回이상", "Ｖ절정20回이상", "Ａ절정20回이상", "Ｂ절정20回이상", "Ｍ절정20回이상", 
		IF TALENT:상위몽마 && MARK:굴복각인 < 3
			DRAWLINE
			PRINTFORMW ☆★☆이벤트 달성·굴복각인습득의 패널티가 대폭 경감되었습니다☆★☆
			DRAWLINE
		ENDIF
		MARK:공략각인 += 2
	ENDSELECT
ENDIF

;喋った구상の플래그を記録
CALL SET_EV, TARGET, LOCALS
;사정게이지の체크
CALL CHECK_BASEPLAYER2

;-------------------------------------------------
;@KOJO_MESSAGE_PALAMCNG で何について喋るかの判定
;優先度の高い順に, 처녀상실, 初절정, 절정방뇨, 절정20回, 焦らし, 사정분유, 通常절정, PALAM３ＬＶ돌파
;-------------------------------------------------
@CHECK_MESSAGE_PALAMCNG()
#FUNCTIONS
LOCALS = 
;-------------------------------------------------
;처녀상실
;-------------------------------------------------
IF NOWEX:처녀상실 > 0
	LOCALS = 처녀상실
	RETURNF LOCALS
ENDIF

;-------------------------------------------------
;初めての절정（二回目以降の절정は優先度が低いので下の方にある）
;-------------------------------------------------
LOCAL = 1
IF NOWEX:Ｃ절정 > 0 && PENIS(TARGET) == 0 && FIRST_EV("Ｃ절정") == 0
	SIF LOCALS == "" || RAND:LOCAL == 0
		LOCALS = Ｃ절정
	LOCAL += 1
ENDIF
IF NOWEX:요도절정 > 0 && FIRST_EV("요도절정") == 0
	SIF LOCALS == "" || RAND:LOCAL == 0
		LOCALS = 요도절정
	LOCAL += 1
ENDIF
IF NOWEX:Ｖ절정 > 0 && FIRST_EV("Ｖ절정") == 0
	SIF LOCALS == "" || RAND:LOCAL == 0
		LOCALS = Ｖ절정
	LOCAL += 1
ENDIF
IF NOWEX:Ａ절정 > 0 && FIRST_EV("Ａ절정") == 0
	SIF LOCALS == "" || RAND:LOCAL == 0
		LOCALS = Ａ절정
	LOCAL += 1
ENDIF
IF NOWEX:Ｂ절정 > 0 && FIRST_EV("Ｂ절정") == 0
	SIF LOCALS == "" || RAND:LOCAL == 0
		LOCALS = Ｂ절정
	LOCAL += 1
ENDIF
IF NOWEX:Ｍ절정 > 0 && FIRST_EV("Ｍ절정") == 0
	SIF LOCALS == "" || RAND:LOCAL == 0
		LOCALS = Ｍ절정
	LOCAL += 1
ENDIF

SIF LOCALS != ""
	RETURNF LOCALS
;-------------------------------------------------
;절정방뇨した
;-------------------------------------------------
IF NOWEX:절정실금 > 0
	LOCALS = 절정방뇨
	RETURNF LOCALS
ENDIF

;-------------------------------------------------
;절정しまくり （３回に一回程度しゃべる. 多重절정だと확률ＵＰ）
;-------------------------------------------------
LOCAL = 3
IF EX:Ｃ절정 >= 20 && PENIS(TARGET) == 0 && NOWEX:Ｃ절정 >= MAX(NOWEX:Ａ절정, NOWEX:Ｂ절정, NOWEX:Ｍ절정, NOWEX:요도절정, 1)
	SIF RAND:LOCAL == 0
		LOCALS = Ｃ절정20회이상
	LOCAL += 1
ENDIF
IF EX:요도절정 >= 20 && NOWEX:요도절정 >= MAX(NOWEX:Ｃ절정, NOWEX:Ｖ절정, NOWEX:Ａ절정, NOWEX:Ｂ절정, NOWEX:Ｍ절정, 1)
	SIF RAND:LOCAL == 0
		LOCALS = 요도절정20回이상
	LOCAL += 1
ENDIF
IF EX:Ｖ절정 >= 20 && NOWEX:Ｖ절정 >= MAX(NOWEX:Ｃ절정, NOWEX:Ａ절정, NOWEX:Ｂ절정, NOWEX:Ｍ절정, NOWEX:요도절정, 1)
	SIF RAND:LOCAL == 0
		LOCALS = Ｖ절정20회이상
	LOCAL += 1
ENDIF
IF EX:Ａ절정 >= 20 && NOWEX:Ａ절정 >= MAX(NOWEX:Ｃ절정, NOWEX:Ａ절정, NOWEX:Ｂ절정, NOWEX:Ｍ절정, NOWEX:요도절정, 1)
	SIF RAND:LOCAL == 0
		LOCALS = Ａ절정20회이상
	LOCAL += 1
ENDIF
IF EX:Ｂ절정 >= 20 && NOWEX:Ｂ절정 >= MAX(NOWEX:Ｃ절정, NOWEX:Ａ절정, NOWEX:Ｂ절정, NOWEX:Ｍ절정, NOWEX:요도절정, 1)
	SIF RAND:LOCAL == 0
		LOCALS = Ｂ절정20회이상
	LOCAL += 1
ENDIF
IF EX:Ｍ절정 >= 20 && NOWEX:Ｍ절정 >= MAX(NOWEX:Ｃ절정, NOWEX:Ａ절정, NOWEX:Ｂ절정, NOWEX:Ｍ절정, NOWEX:요도절정, 1)
	SIF RAND:LOCAL == 0
		LOCALS = Ｍ절정20회이상
	LOCAL += 1
ENDIF

SIF LOCALS != ""
	RETURNF LOCALS

;-------------------------------------------------
;焦らし （３回に一回程度しゃべる. 多重절정だと확률ＵＰ）
;-------------------------------------------------
LOCAL = 3
IF NOWEX:Ｃ절정직전멈춤 > 0 && CUP:쾌Ｃ && TALENT:요도성감 && COND("요도성감플래그") && CHECK("요도플레이")
	SIF RAND:LOCAL == 0
		LOCALS = 焦らし
	LOCAL += 1
ELSEIF NOWEX:Ｃ절정직전멈춤 > 0 && CUP:쾌Ｃ && PENIS(TARGET) == 0
	SIF RAND:LOCAL == 0
		LOCALS = Ｃ안달남
	LOCAL += 1
ENDIF
IF NOWEX:Ｖ절정직전멈춤 > 0 && CUP:쾌Ｖ
	SIF RAND:LOCAL == 0
		LOCALS = Ｖ안달남
	LOCAL += 1
ENDIF
IF NOWEX:Ａ절정직전멈춤 > 0 && CUP:쾌Ａ
	SIF RAND:LOCAL == 0
		LOCALS = Ａ안달남
	LOCAL += 1
ENDIF
IF NOWEX:Ｂ절정직전멈춤 > 0 && CUP:쾌Ｂ
	SIF RAND:LOCAL == 0
		LOCALS = Ｂ안달남
	LOCAL += 1
ENDIF
IF NOWEX:Ｍ절정직전멈춤 > 0 && CUP:쾌Ｍ
	SIF RAND:LOCAL == 0
		LOCALS = Ｍ안달남
	LOCAL += 1
ENDIF
IF NOWEX:사정직전멈춤 > 0
	SIF RAND:LOCAL == 0
		LOCALS = 사정안달남
	LOCAL += 1
ENDIF

SIF LOCALS != ""
	RETURNF LOCALS

;-------------------------------------------------
;각종이벤트（처녀상실と절정방뇨は優先度が高いので上の方にある）
;-------------------------------------------------
LOCAL = 1
IF NOWEX:사정 > 0
	SIF LOCALS == "" || RAND:LOCAL == 0
		LOCALS = 사정했다
	LOCAL += 1
ENDIF
IF NOWEX:분유 > 0
	SIF LOCALS == "" || RAND:LOCAL == 0
		LOCALS = 분유했다
	LOCAL += 1
ENDIF

SIF LOCALS != ""
	RETURNF LOCALS

;-------------------------------------------------
;通常の절정（初回절정は優先度が高いので上の方にある）
;-------------------------------------------------
LOCAL = 4
IF NOWEX:Ｃ절정 > 0 && PENIS(TARGET) == 0 && FIRST_EV("Ｃ절정")
	SIF RAND:LOCAL == 0
		LOCALS = Ｃ절정
	LOCAL += 1
ENDIF
IF NOWEX:요도절정 > 0 && FIRST_EV("요도절정")
	SIF RAND:LOCAL == 0
		LOCALS = 요도절정
	LOCAL += 1
ENDIF
IF NOWEX:Ｖ절정 > 0 && FIRST_EV("Ｖ절정")
	SIF RAND:LOCAL == 0
		LOCALS = Ｖ절정
	LOCAL += 1
ENDIF
IF NOWEX:Ａ절정 > 0 && FIRST_EV("Ａ절정")
	SIF RAND:LOCAL == 0
		LOCALS = Ａ절정
	LOCAL += 1
ENDIF
IF NOWEX:Ｂ절정 > 0 && FIRST_EV("Ｂ절정")
	SIF RAND:LOCAL == 0
		LOCALS = Ｂ절정
	LOCAL += 1
ENDIF
IF NOWEX:Ｍ절정 > 0 && FIRST_EV("Ｍ절정")
	SIF RAND:LOCAL == 0
		LOCALS = Ｍ절정
	LOCAL += 1
ENDIF

SIF LOCALS != ""
	RETURNF LOCALS

;-------------------------------------------------
;初めての各PALAM上昇 3LV
;-------------------------------------------------
LOCAL = 1
IF PALAM:Ｖ윤 + CUP:Ｖ윤 >= 3000 && FIRST_EV("Ｖ윤LV3돌파") == 0
	SIF LOCALS == "" || RAND:LOCAL == 0
		LOCALS = Ｖ윤LV3돌파
	LOCAL += 1
ENDIF
IF PALAM:욕정 + CUP:욕정 >= 3000 && FIRST_EV("욕정LV3돌파") == 0
	SIF LOCALS == "" || RAND:LOCAL == 0
		LOCALS = 욕정LV3돌파
	LOCAL += 1
ENDIF
IF PALAM:치정 + CUP:치정 >= 3000 && FIRST_EV("치정LV3돌파") == 0
	SIF LOCALS == "" || RAND:LOCAL == 0
		LOCALS = 치정LV3돌파
	LOCAL += 1
ENDIF
IF TEQUIP:넘어뜨리기 == 0 && PALAM:공포 + CUP:공포 >= 3000 && FIRST_EV("공포LV3돌파") == 0
	SIF LOCALS == "" || RAND:LOCAL == 0
		LOCALS = 공포LV3돌파
	LOCAL += 1
ENDIF

RETURNF LOCALS

;-------------------------------------------------
;@KOJO_MESSAGE_SAMEN_AND_EXTACY関係
;PLAYER, 드래곤, 촉수の사정＆절정をトリガーにした구상
;-------------------------------------------------
@KOJO_MESSAGE_SAMEN_AND_EXTACY
SIF FLAG:7 == 0
	RETURN 0

;재갈着用時には구상をスキップする（OFFだと입を塞いでるのに喋りまくる）
SIF TEQUIP:재갈
	RETURN 0

TRYCALLFORM KOJO_MESSAGE_SAMEN_AND_EXTACY_{NO:TARGET}_{CFLAG:209}

;NTR이벤트
CALL EVENT_DRAGON_NTR, "사정"

;喋った구상の플래그を記録
CALL SETFLAG, "사정＆절정이벤트플래그"

;사정게이지の체크
CALL CHECK_BASEPLAYER2

;-------------------------------------------------
;@KOJO_MESSAGE_SHOOT関係
;PLAYERの사정箇所선택直前の구상
;-------------------------------------------------
@KOJO_MESSAGE_SHOOT
SIF FLAG:7 == 0
	RETURN 0

;재갈着用時には구상をスキップする（OFFだと입を塞いでるのに喋りまくる）
SIF TEQUIP:재갈
	RETURN 0

TRYCALLFORM KOJO_MESSAGE_SHOOT_{NO:TARGET}_{CFLAG:209}

;사정게이지の체크
CALL CHECK_BASEPLAYER2

;-------------------------------------------------
;@COUNTER_KOJO関係
;反撃をトリガーにした구상・処理
;反撃플래그ON時に구상を発動します
;ARGSは이벤트種別
;-------------------------------------------------
@COUNTER_KOJO, ARGS
SIF FLAG:7 == 0
	RETURN 0

;재갈着用時には구상をスキップする（OFFだと입を塞いでるのに喋りまくる）
SIF TEQUIP:재갈
	RETURN 0

TRYCALLFORM COUNTER_KOJO_{NO:TARGET}_{CFLAG:209}, ARGS

;사정게이지の체크
CALL CHECK_BASEPLAYER2

SELECTCASE TFLAG:90
CASE 1 TO 9
	;効果を及ぼすのはメ임프レイヤー一人のみ
	CALL CALC, "에너지드레인"
ENDSELECT

;-------------------------------------------------
;@KOJO_MESSAGE_MARKCNG関係
;刻印変動をトリガーにした구상
;刻印変動後に구상を発動します
;-------------------------------------------------
@KOJO_MESSAGE_MARKCNG, ARGS
SIF FLAG:7 == 0
	RETURN 0

VARSET FLAG, 0, 100, 200

;재갈着用時には구상をスキップする（OFFだと입を塞いでるのに喋りまくる）
SIF TEQUIP:재갈
	RETURN 0

TRYCALLFORM KOJO_MESSAGE_MARKCNG_{NO:TARGET}_{CFLAG:209}, ARGS
LOCAL = RESULT

;상대が상위몽마の場合には공략각인（굴복각인取得に요정する파라미터量を減らす）を得る
IF FIRST_EV(ARGS) == 0
	SELECTCASE ARGS
	CASE "고통각인Lv3취득", "쾌락각인Lv3취득"
		IF TALENT:상위몽마 && MARK:굴복각인 < 3
			DRAWLINE
			PRINTFORMW ☆★☆이벤트 달성·굴복각인 습득의 패널티가 경감되었습니다☆★☆
			DRAWLINE
		ENDIF
		MARK:공략각인 += 1
	ENDSELECT
ENDIF

;喋った구상の플래그を記録
SIF LOCAL
	CALL SET_EV, TARGET, ARGS

;사정게이지の체크
CALL CHECK_BASEPLAYER2

;-------------------------------------------------
;@SELF_KOJO関係
;이벤트時の구상, 이벤트開始時に出力します
;-------------------------------------------------
@SELF_KOJO, ARGS, ARG
#DIM MEMO_LINECOUNT
SIF FLAG:7 == 0
	RETURN 0

SIF ARG <= 0 && TARGET
	ARG = TARGET

SWAP TARGET, ARG

VARSET FLAG, 0, 100, 200

;재갈着用時には구상をスキップする（OFFだと입を塞いでるのに喋りまくる）
SIF TEQUIP:재갈
	RETURN 0

;구상があるかどうかを行数で체크
MEMO_LINECOUNT = LINECOUNT

TRYCALLFORM SELF_KOJO_K{NO:TARGET}_{CFLAG:209}, ARGS

;喋った구상の플래그を記録
IF LINECOUNT > MEMO_LINECOUNT
	CALL SET_EV, TARGET, ARGS
;범용구상など
ELSE
	CALL TRAINVOICE_SELF, ARGS
ENDIF

;사정게이지の체크
CALL CHECK_BASEPLAYER2

SWAP TARGET, ARG

;-------------------------------------------------
;セリフの前後に改行を付けたSELF_KOJO
;地の文などが無く、基本一行程度のセリフを出す場合に向く
;-------------------------------------------------
@SELF_KOJO_L, ARGS, ARG
#DIM MEMO_LINECOUNT

PRINTL 
MEMO_LINECOUNT = LINECOUNT

CALL SELF_KOJO, ARGS, ARG

IF LINECOUNT == MEMO_LINECOUNT
	CLEARLINE 1
ELSE
	PRINTL 
ENDIF

;--------------------------------------------------
;@DAILY_LIFE_MESSAGE関係
;휴식実行時に出力されます
;--------------------------------------------------
@DAILY_LIFE_MESSAGE
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
;날짜이벤트のみ
TRYCALLFORM DAILY_LIFE_DAYEV_{NO:TARGET}_{CFLAG:209}
;이벤트発生したならここで終了
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1

TRYCALLFORM DAILY_LIFE_MESSAGE_{NO:TARGET}_{CFLAG:209}

;-------------------------------------------------
;@SINGLE_ENDING関係
;単体エンドの条件を満たした時に出力します
;なお, 엔딩の텍스트が用意されている場合には最後をRETURN 1 , 無いならRETURN 0とするようにして区別しています. 
;-------------------------------------------------
@SINGLE_ENDING
TRYCALLFORM SINGLE_ENDING_K{NO:TARGET}_{CFLAG:209}

;-------------------------------------------------
;이하は구상セレクタのポータルファイルです
;구상ファイルを呼び出すための初回선택지と振り分けを行っています
;-------------------------------------------------

;--------------------------------------------------
;@EVENTTRAIN関係　初조교時のみ선택
;엑스트라모드・及び구상선택모드構文　CFLAG:209を使用
;-------------------------------------------------
@EVENTTRAIN
#PRI
#DIM LCOUNT
#DIM MEMO_ABL, 100
#DIM MEMO_EXP, 100
#DIM MEMO_TALENT, 1000
#DIM MEMO_LINECOUNT

SIF CFLAG:209 > 0
	RETURN 1

;ABLとEXPとTALENTの変化を記録して後に違いを表示する試み
FOR LCOUNT, 0, 100
	MEMO_ABL:LCOUNT = ABL:LCOUNT
NEXT

;EXPはたいした差がないのでとりあえずは割愛
;FOR LCOUNT, 0, 100
;	MEMO_EXP:LCOUNT = EXP:LCOUNT
;NEXT

FOR LCOUNT, 0, 1000
	MEMO_TALENT:LCOUNT = TALENT:LCOUNT
NEXT

;ここの導入文は自由に作者さんが書き換えてください
SELECTCASE NO:TARGET
CASE 1
	PRINTFORML 서큐버스그렇다고 하면？
	PRINTFORML [ 0] 역시남자의 사람을 좋아하구나♪
	PRINTFORML [ 1] 아라, 여자 아이도 정말 좋아해요. 물론후타나리씨도 말야♪아하♪
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		PRINTFORMW 이번에는 서큐버스 대사_1을 사용합니다. 
		CFLAG:209 = 1
	ELSEIF RESULT == 1
		PRINTFORMW 이번에는 서큐버스 대사_2를 사용합니다. 
		CFLAG:209 = 2
	ENDIF
CASE 3
	PRINTFORML 당신(이)가 바란다드라이어드씨는？
	PRINTFORML [ 0] 지적인 누나를 흐트러지게 하고 싶다！
	PRINTFORML [ 1] 적극적인 누나에게 짜내지고 싶다！
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		PRINTFORMW 이번에는 드라이어드 대사_1을 사용합니다. 
		CFLAG:209 = 1
	ELSEIF RESULT == 1
		PRINTFORMW 이번에는 드라이어드 대사_2를 사용합니다. 
		CFLAG:209 = 2
	ENDIF
CASE 4
	PRINTFORML 픽시(와)과 어떤 관계가 되고 싶다？
	PRINTFORML [ 0] 몸도 마음도 포로가 되는 것 같은 달콤한 관계
	PRINTFORML [ 1] 체격차이를 아랑곳하지 않는 격렬한 관계
	IF CFLAG:8 == 3
		PRINTFORML [ 2] 요정의 공주님과 신분을 넘은 관계
		CALL INPUT_SELECT, 3
	ELSE
		CALL INPUT_SELECT, 2
	ENDIF
	IF RESULT == 0
		PRINTFORMW 이번에는 픽시 대사_1을 사용합니다. 
		CFLAG:209 = 1
	ELSEIF RESULT == 1
		PRINTFORMW 이번에는 픽시 대사_2를 사용합니다. 
		CFLAG:209 = 2
	ELSEIF RESULT == 2
		PRINTFORMW 이번에는 픽시 대사_3…요정 공주 티타니아를 사용합니다. 
		CFLAG:209 = 3
	ENDIF
CASE 7
	PRINTFORML 이번은 어느워울프를 선택합니까？
	PRINTFORML [ 0] 언제나 대로의사족보행！
	PRINTFORML [ 1] 부부판의 2발보행！사람의 말도 말한다！
	PRINTFORML [ 2] 강아지적인워울프！망상의 산물입니다！
	CALL INPUT_SELECT, 3
	IF RESULT == 0
		PRINTFORMW 이번에는 워울프 대사_1을 사용합니다. 
		CFLAG:209 = 1
	ELSEIF RESULT == 1
		PRINTFORMW 이번에는 워울프 대사_2를 사용합니다. 
		CFLAG:209 = 2
		TALENT:꿋꿋함 = 1
		TALENT:솔직 = 1
		TALENT:양기 = 1
		TALENT:정조관결여 = 0
		TALENT:미련함 = 0
		TALENT:월광욕 = 1
		TALENT:사족보행 = 0
		CALL CALC, "장신化", TARGET
		CALL SETFLAG, "속성", TARGET
	ELSEIF RESULT == 2
		PRINTFORMW 이번에는 워울프 대사_3을 사용합니다. 
		CFLAG:209 = 3
	ENDIF
CASE 8
	;밴시
	IF CFLAG:8 == 2
		CFLAG:209 = 2
	ELSE
		CFLAG:209 = 1
	ENDIF
CASE 9
	;교만？
	IF TALENT:교만
		CFLAG:209 = 2
		RETURN 0
	ENDIF
	PRINTFORML 처녀노빌리져그렇다고 하면？
	PRINTFORML [ 0] 물론 소박한 마을 아가씨
	CALL PRINT_STR, "[ 1] 我侭に育てられたお嬢様タイプも良いよね（_하트핑크_個別ＥＮＤ有_）_L_"
	;PRINTFORML [ 2] それとも……人間と몽마の狭間で悩んでいるのも良いかも？
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		PRINTFORMW 이번에는 버진·빌리져 대사_1을 사용합니다. 
		CFLAG:209 = 1
	ELSEIF RESULT == 1
		PRINTFORMW 이번에는 버진·빌리져 대사_2를 사용합니다. 
		CFLAG:209 = 2
	ELSEIF RESULT == 2
		PRINTFORMW 이번에는 버진·빌리져 대사_을 사용합니다. 
		CFLAG:209 = 3
	ENDIF
CASE 10
	;멜루진
	IF CFLAG:8 == 2
		CFLAG:209 = 2
	ELSE
		CFLAG:209 = 1
	ENDIF
CASE 11
	;세이렌
	IF CFLAG:8 == 2
		CFLAG:209 = 2
	ELSE
		CFLAG:209 = 1
	ENDIF
CASE 12
	PRINTFORML 리저드맨그렇다고 하면？
	PRINTFORML [ 0] 승기로솔직(이)가 될 수 없는 튼 모습을ﾆﾖﾆﾖ바라보고 싶다！
	PRINTFORML   ※43씨＋140씨의구상됩니다
	PRINTFORML [ 1] 데레시로 해 다워지는 갭이야말로 생명！
	PRINTFORML   ※829씨의구상됩니다
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		PRINTFORMW 이번에는 리저드맨 대사_43+140을 사용합니다. 
		CFLAG:209 = 1
	ELSEIF RESULT == 1
		PRINTFORMW 이번에는 리저드맨 대사_829을 사용합니다. 
		CFLAG:209 = 2
	ENDIF
CASE 15
	PRINTFORML 이번스킬라는 어느 쪽？
	PRINTFORML [ 0] 장편판
	PRINTFORML 마녀의 저주에 의해 하반신이 괴물이 되어 버렸다소녀
	PRINTFORML 괴물이 된 뒤는 카리브디스와 함께 바다에서 인간을 덮치고 있었다
	PRINTFORML 그 때문인지강기태도를 취하기 쉽지만, 원래 경험이 적다소녀에서 만났기 때문에 의외로솔직이다
	PRINTFORML 덮친 인간은 먹고 있었기 때문에 성교 경험은 거의 없다.
	PRINTFORML 반대로자위하촉수조차 사용해 가고 있었기 때문에 베테랑（？）의 영역에 이르고 있다.
	PRINTFORML 반격 시에는 마음껏 그 테크닉을 사용해 오기 (위해)때문에,촉수쾌감에 빠지지 않게 충분히 주의.
	PRINTFORML 
	CALL PRINT_STR, "[ 1] 短編版（_하트핑크_個別ＥＮＤ有_）_L_"
	PRINTFORML 신화의 시대, 마녀의질투에 의해 하반신을 괴물로 되었다소녀(와)과 전해지는 몬스터
	PRINTFORML 몽마에 범해지고몽마화하고 나서는 태고의소녀시대의 성격이 소생했는지헌신적이어 노력파
	PRINTFORML 몬스터가 된 원인인 「질투」라고 하는 감정을 싫어해 있어 욕구를자위에 향하기 쉽다
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		PRINTFORMW 이번엔 장편판으로 합니다
		CFLAG:209 = 1
		TALENT:아픔에약함 = 1
		TALENT:구지배자의자취 = 1
	ELSEIF RESULT == 1
		PRINTFORMW 이번엔 단편판으로 합니다
		CFLAG:209 = 2
		EXP:레즈경험 += 20
		ABL:레즈끼 += 1
		TALENT:솔직 = 1
		TALENT:바이 = 1
		TALENT:구지배자의자취 = 1
		;소녀化
		TALENT:외견연령 = 2
	ENDIF
CASE 17
	;엠푸사
	IF CFLAG:8 == 1
		CFLAG:209 = 2
	ELSE
		CFLAG:209 = 1
	ENDIF
CASE 22
	PRINTFORML 쿠노이치에 뭐라고 불리고 싶다？
	PRINTFORML [ 0] 주！
	PRINTFORML   ※729씨＋985씨의구상됩니다
	PRINTFORML [ 1] 너！
	PRINTFORML   ※829씨의구상됩니다
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		PRINTFORMW 이번에는 쿠노이치 대사_729+985를 사용합니다. 
		CFLAG:209 = 1
	ELSEIF RESULT == 1
		PRINTFORMW 이번에는 쿠노이치 대사_829를 사용합니다. 
		CFLAG:209 = 2
	ENDIF
CASE 24
	PRINTFORML 살로메씨라고 하면？
	PRINTFORML [ 0] 눈에 빛을 되찾아 가는 과정을 보고 싶어！
	PRINTFORML  ※221씨+857씨의구상됩니다
	PRINTFORML [ 1] 사랑에 굶고 있다소녀(이)야！
	PRINTFORML  ※371씨+963씨의구상됩니다
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		PRINTFORMW 이번엔 살로메 대사_221+857를 사용합니다. 
	ELSEIF RESULT == 1
		PRINTFORMW 이번에는 살로메 대사_371+963을 사용합니다. 
		PRINTFORMW 소질이 일부 변경됩니다. 
		TALENT:망신 = 0
	ENDIF
	CFLAG:209 = RESULT + 1
CASE 25
	PRINTFORML 뱀파이어그렇다고 하면？
	PRINTFORML [ 0] 츤데레계 아가씨이지만 얼빠진 아이속성
	CALL PRINT_STR, "[ 1] デレるまではあらあらうふふ、デレてからは普通の乙女（_하트핑크_個別ＥＮＤ有_）_L_"
	PRINTFORML [ 2] 교만인 체하며 있지만 실은 멍청이인소녀뭐라고 하는 것은 어떨까？
	CALL INPUT_SELECT, 3
	IF RESULT == 0
		PRINTFORMW 이번에는 뱀파이어 대사_1을 사용합니다. 
		CFLAG:209 = 1
	ELSEIF RESULT == 1
		PRINTFORMW 이번에는 뱀파이어 대사_2를 사용합니다. 
		CFLAG:209 = 2
	ELSEIF RESULT == 2
		PRINTFORMW 이번은뱀파이어구상_3를 채용합니다（소질하지만 변화해, 경험이 반감합니다…）. 
		CFLAG:209 = 3
		FOR LCOUNT, 0, 100
			;経験が２이상のものは全て半減
			SIF EXP:LCOUNT != 1
				EXP:LCOUNT /= 2
		NEXT

		ABL:기교 = 1
		ABL:정액중독 = 0
		SIF ABL:새드끼 < 5
			ABL:새드끼 += 1
		TALENT:심술쟁이 = 1
		TALENT:겁쟁이 = 1
		TALENT:기품 = 0
		TALENT:현명함 = 0
		TALENT:장신 = 0
		TALENT:아픔에강함 = 0
		TALENT:아픔에약함 = 1
		TALENT:쾌감에솔직 = 0
		TALENT:악취둔감 = 0

		;소녀化
		TALENT:외견연령 = 2

		;まな板にしようぜ! 
		CALL CALC, "빈유化", TARGET
		SIF TALENT:작은몸집 == 0
			CALL CALC, "보통体型化", TARGET
		CALL SETFLAG, "속성", TARGET
	ENDIF
CASE 30
	PRINTFORML 그렇게 말하면,소환했다유니콘의 확인을 제대로 갔을 것인가？
	CALL PRINT_STR, "[ 0] もちろんした（_하트핑크_個別ＥＮＤ有_）_L_"
	;バイコーン
	PRINTFORML [ 1] 저것, 그렇게 말하면, 어쩐지 위화감을 기억한 것 같은……？
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		CFLAG:209 = 1
	ELSEIF RESULT == 1
		CFLAG:209 = 2
		CFLAG:8 = 1
		ABL:Ａ감각 = ABL:Ｖ감각
		ABL:Ｖ감각 = 0
		TALENT:Ｖ민감 = 0
		TALENT:Ａ민감 = 1
		TALENT:Ａ둔감 = 0
		TALENT:호색 = 1
		TALENT:치료 = 0
		TALENT:고무 = 1
		TALENT:로긴 = 0
		TALENT:우르즈군 = 1
		;名前が유니콘ならば, バイコーンに改名
		SIF NAME:TARGET == "유니콘"
			NAME:TARGET = バイコーン
		SIF CALLNAME:TARGET == "유니콘"
			CALLNAME:TARGET = バイコーン
	ENDIF
CASE 31
	PRINTFORML 고블린그렇다고 하면？
	PRINTFORML [ 0] 당신를 밀어 넘어뜨리는 것이 너무 좋아！
	PRINTFORML [ 1] 과거에 상처를 가진다소녀(와)과 우정과 같은 사랑을 기르겠어！
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		PRINTFORMW 이번에는 고블린 대사_1을 사용합니다. 
		CFLAG:209 = 1
	ELSEIF RESULT == 1
		PRINTFORMW 이번에는 고블린대사_2를 사용합니다. 
		CFLAG:209 = 2
	ENDIF
CASE 42
	PRINTFORML 번더스내치라고 어떤몽마？
	PRINTFORML [ 0] 붙잡을 곳이 없는 느낌
	CALL PRINT_STR, "[ 1] 大人しそうな感じ（_하트핑크_個別ＥＮＤ有_）_L_"
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		PRINTFORMW 이번은번더스내치구상_1를 채용합니다. 
		CFLAG:209 = 1
	ELSEIF RESULT == 1
		PRINTFORMW 이번은번더스내치구상_2를 채용합니다. 
		CFLAG:209 = 2
		EXP:Ａ경험 += 10
		ABL:Ｖ감각 = 1
		ABL:Ａ감각 = 2
		TALENT:솔직 = 1
		TALENT:강기 = 0
		TALENT:냉정 = 1
		TALENT:양기 = 0
		TALENT:현명함 = 1
		TALENT:Ｃ민감 = 0
		TALENT:Ｖ민감 = 0
		TALENT:Ａ민감 = 1
		TALENT:Ａ둔감 = 0
		;素の번더스내치は長編っぽい感じで고양이귀でしたのでちょっとなくしてみます
		TALENT:동물귀 = 0

		;衣装の설정や가슴 크기など
		CALL CALC, "평유化", TARGET
		CALL COORDINATE_MUMA, TARGET
		CALL MEMO_FAVORITE_DRESS, TARGET
		CALL SETFLAG, "속성", TARGET
	ENDIF
CASE 44
	PRINTFORML 미란다에 오기 전의린하…
	PRINTFORML [ 0] 밝은 느낌. 스승으로 불리고 싶습니다…
	PRINTFORML   ※331씨의구상됩니다
	PRINTFORML [ 1] 차분히 마을 아가씨.빌리져에 가까운 이미지.
	PRINTFORML   ※그 외의구상됩니다
	CALL PRINT_STR, "[ 2] 明るいけどちょっと影のある感じ(>>331氏の설정を踏襲)（_하트핑크_個別ＥＮＤ有_）_L_"
	PRINTFORML   ※eraSQ제작 스레드>>644씨의구상됩니다
	CALL INPUT_SELECT, 3
	IF RESULT == 0
		PRINTFORMW 이번에는 린 대사_331을 사용합니다. 
		CFLAG:209 = 1
	ELSEIF RESULT == 1
		PRINTFORMW 이번에는 그 외의 린 대사를 사용합니다. 
		CFLAG:209 = 2
	ELSEIF RESULT == 2
		PRINTFORMW 이번엔 644씨의 린 대사를 사용합니다. 
		CFLAG:209 = 3
	ENDIF
CASE 48
	PRINTFORML 에스트모양이라고 하면…
	PRINTFORML [ 0] 미숙한조교스승에도 용서 없음！
	PRINTFORML [ 1] 미숙한조교스승에는손가감(상태)해 준다！
	PRINTFORML   ※체형이 바뀌면구상도가슴의 크기도 바뀝니다.
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		PRINTFORMW 이번에는 에스트님１을 사용합니다. 
		CFLAG:209 = 1
	ELSEIF RESULT == 1
		PRINTFORMW 이번에는 에스트님２를 사용합니다. 
		CFLAG:209 = 2
	ENDIF
CASE 60
	PRINTFORML 허니비라고 어떤몽마？
	PRINTFORML [ 0] 일에 불타는 기능벌씨
	CALL PRINT_STR, "[ 1] 강기だけど、世話焼きなおねーさん（_하트핑크_個別ＥＮＤ有_）_L_"
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		PRINTFORMW 이번엔 허니비 대사_１을 사용합니다. 
		CFLAG:209 = 1
	ELSEIF RESULT == 1
		PRINTFORMW 이번엔 허니비 대사_２를 사용합니다. 
		CFLAG:209 = 2
		TALENT:헌신 = 1
		TALENT:고무 = 1
	ENDIF
CASE 94
	IF NO:MASTER == 34
		;스투나
		CFLAG:209 = 1
	ELSEIF NO:MASTER == 35
		;멜로나
		CFLAG:209 = 2
	ENDIF
CASE 97
	IF BASE:종족 == 33
		CFLAG:209 = 33
	ELSEIF BASE:종족 == 19
		CFLAG:209 = 19
	ELSEIF BASE:종족 == 22
		CFLAG:209 = 22
	ELSE
		CFLAG:209 = 857
	ENDIF
CASE 98
	PRINTFORML 다음의 어느 쪽을 선택합니까？
	PRINTFORML [ 0] 종래씨
	PRINTFORML   ※127씨의구상됩니다
	PRINTFORML [ 1] 카피하는 자질이 많아서 무입씨
	PRINTFORML   ※1026씨의구상됩니다
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		CFLAG:209 = 1
	ELSEIF RESULT == 1
		CFLAG:209 = 2
	ENDIF
ENDSELECT

CFLAG:209 = MAX(CFLAG:209, 1)

PRINTL 

MEMO_LINECOUNT = LINECOUNT

;ABLやEXPやTALENTに変化あったか？
FOR LCOUNT, 0, 100
	;変化あり
	SIF ABL:LCOUNT && MEMO_ABL:LCOUNT != ABL:LCOUNT
		PRINTFORML %TEXT_RJ(ABLNAME:LCOUNT, 8)%：LV{MEMO_ABL:LCOUNT} → LV{ABL:LCOUNT}
NEXT

VARSET LOCALS
FOR LCOUNT, 0, 1000
	;失った
	IF MEMO_TALENT:LCOUNT > 0 && TALENT:LCOUNT == 0
		LOCALS = %LOCALS%[%TALENT_NAME(LCOUNT, MEMO_TALENT:LCOUNT)%]%BL(1)%
	;得た(変化含む)
	ELSEIF TALENT:LCOUNT > 0 && MEMO_TALENT:LCOUNT != TALENT:LCOUNT
		LOCALS:1 = %LOCALS:1%[%TALENT_NAME(LCOUNT, TALENT:LCOUNT)%]%BL(1)%
	ENDIF
NEXT

SIF LOCALS != ""
	PRINTFORML ×：%LOCALS%
SIF LOCALS:1 != ""
	PRINTFORML ○：%LOCALS:1%

SIF LINECOUNT > MEMO_LINECOUNT
	WAIT

