;重요정!! 　탐색커맨드はあくまでバイト감각! 
;탐색者はPLAYER, Iは入手아이템, Mは獲得金額
;Rは一時的な変数記憶(탐색커맨드구상で使用)
;FLAG:50の各bitで各種フラグを記録する(基本的にはボス遭遇と보스격파)
; 0- 4	에르툼삼림
; 5- 9	류두흐동굴
;10-14	안쇼스 해
;15-19	베르나도트의대미궁
;20-24	고즈번 사막
;25-29	키아즈마의유곽
;60-63	범용フラグ（毎回탐색開始の時にリセットされる）

@EXPLORER, P, M
#DIM MEMO_TARGET
#DIM MEMO_PLAYER
#DIM MEMO_LINECOUNT

REDRAW 0

MEMO_LINECOUNT = LINECOUNT

CALL SHOW_CHARA_LIST_PRE, "탐색"
CALL SHOW_CHARA_LIST, "탐색", P, M
SIF CHARA_NUM("조수가능") && CONFIG("조교상대변경불가") == 0
	PRINTFORML  [109]%TEXTS("조수の명칭")%변경
PRINT  [200]탐험의 방침：
SELECTCASE FLAG:246
CASE 0
	PRINTFORML 지시 없음
CASE 1
	PRINTFORML 팍팍 가자구!
CASE 2
	PRINTFORML 목숨을 소중히
CASE 3
	PRINTFORML 플래그를 세운다
ENDSELECT

;악세사리の탐색効果
IF TALENT:MASTER:은밀 > 0 || TALENT:MASTER:보물상자출현율 > 0 || TALENT:MASTER:문발견율 > 0
	PRINTFORM 악세사리의 효과：
	FOR LOCAL, 301, 306
		SELECTCASE FLAG:LOCAL / 1000
		CASE 50 TO 59
			PRINTFORM %ACCESSORY_EFFECT(FLAG:LOCAL)% 
		ENDSELECT
	NEXT
	PRINTL 
ENDIF

$INPUT_LOOP
INPUTS

SELECTCASE RESULTS
;탐색での능력
CASE "遭遇回避"
	CALL PRINT_STR, "몽마와의 조우율이 _황색_５％ 감소_합니다_W"
CASE "遭遇증가"
	CALL PRINT_STR, "몽마와의 조우율이 _파랑_５％ 상승_합니다_W"
CASE "罠解除"
	CALL PRINT_STR, "보물상자를 열 때 트랩발동률이 _황색_４분의 1_로 줄어듭니다_W"
CASE "罠誘発"
	CALL PRINT_STR, "보물상자를 열 때 트랩발동률이 _파랑_2.5배_로 늘어납니다_W"
CASE "財宝発見"
	CALL PRINT_STR, "보물상자를 발견할 확률이 _황색_５％ 상승_합니다_W"
CASE "財宝見逃し"
	CALL PRINT_STR, "보물상자를 발견할 확률이 _파랑_５％ 감소_합니다_W"
CASE "階段発見"
	CALL PRINT_STR, "다음 층으로 가는 문을 발견할 확률이 _황색_５％ 상승_합니다_W"
CASE "회복"
	CALL PRINT_STR, "각 행동마다 정력이 _황색_１Ｐ회복_하고, 전투 종료 시엔_황색_２Ｐ 더 회복_합니다_W"
CASE "戦闘補助"
	CALL PRINT_STR, "수치만큼 탐색자의 전투력이 증가합니다_W"
CASE "상태異常無効"
	CALL PRINT_STR, "보물상자의 함정에 의한 상태 이상을 무효화합니다_W"
;それ以外は数値入力へ変換
CASEELSE
	RESULT = TOINT(RESULTS)
	IF RESULT == 100
		REDRAW 1
		RETURN 0
	ELSEIF RESULT == 109 && CHARA_NUM("조수가능") && CONFIG("조교상대변경불가") == 0
		FLAG:45 = 1p1 + 1p2
		CALL SELECT_ASSI, 0, 0
		FLAG:45 = 0
		RESTART
	ELSEIF RESULT == 200
		PRINTFORML 탐색 방침을 결정합니다. 어느 것으로 합니까？
		PRINTL 
		PRINTFORML [0] %"지시하지 않는다", 20, LEFT%…수치 변동 없음
		PRINTFORML [1] %"팍팍 가자구！", 20, LEFT%…조우율＆문발견율ＵＰ, 보물 발견율DOWN. 전투에 자신있을 때
		PRINTFORML [2] %"목숨을 소중히", 20, LEFT%…보물 발견율DOWN, 전투 유리. 전투에 자신없을 때
		PRINTFORML [3] %"플래그를 세운다", 20, LEFT%…가게 한 몽마가 따라 올지도 모른다. 동료마를 늘리고 싶을 때

		CALL INPUT_SELECT, 4

		FLAG:246 = RESULT
		PRINTW 탐색 방침을 변경했습니다.
		PRINTL 

		RESTART
	ELSEIF RESULT == 2000 && CONFIG("세대別표시")
		M = 0
		P = 0
		RESTART
	ELSEIF RESULT == 2001 && CONFIG("세대別표시")
		M += 1
		P = 0
		RESTART
	ELSEIF RESULT == 2002 && CONFIG("세대別표시")
		M -= 1
		P = 0
		RESTART
	ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("표시人数") && Q >= 2 && P != RESULT - 1001
		P = RESULT - 1001
		RESTART
	ELSEIF RESULT < MASTER || RESULT >= CHARANUM
		GOTO INPUT_LOOP
	ELSEIF RESULT > MASTER && COND("조수가능", RESULT) == 0
		GOTO INPUT_LOOP
	ENDIF

	MEMO_TARGET = TARGET
	MEMO_PLAYER = PLAYER
	;탐색者はPLAYERとして扱う
	PLAYER = RESULT

	CALL SET_EXPLORER

	TARGET = MEMO_TARGET
	PLAYER = MEMO_PLAYER
	;戻る を선택
	SIF RESULT == 100
		RESTART

	VARSET TFLAG
	BEGIN TURNEND
	RETURN 1
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@SET_EXPLORER
#DIM LCOUNT
;探検する際の조수…ASSIを連れていない場合にはPLAYERがこれになって寂しい
#DIM ASSI_EXPLORER
#DIM MEMO_LINECOUNT
#DIMS DUNGEON, 10
#DIMS FIELD_EXPLORER

REDRAW 1

ASSI_EXPLORER = PLAYER
IF PLAYER == MASTER
	IF ASSI
		PRINTFORM %CALLNAME:ASSI%에게 
		ASSI_EXPLORER = ASSI
	ENDIF
ELSE
	PRINTFORM %조사처리(CALLNAME:PLAYER,"가")% 
ENDIF

;お茶
SELECTCASE NO:ASSI_EXPLORER
CASE 16, 22
	PRINTFORM 키아즈마의 차와 
CASE 20
	PRINTFORM 에란드 브랜드와 
CASE 25
	PRINTFORM 로즈 티와 
CASE 19, 46, 47
	PRINTFORM 리즈베그 티와 
CASEELSE
	PRINTFORM 민트 티와 
ENDSELECT

;お弁当
SELECTCASE NAMES("食事", FLAG:40)
CASE "시프리샌드"
	LASTWORD = 시프리샌드 도시락
	CALL SET_PALAM_EXPLORER, "정력회복보정", 2
CASE "마운틴스테이크"
	LASTWORD = 마운틴스테이크를 끼운 샌드위치
	CALL SET_PALAM_EXPLORER, "정력회복보정", 4
CASE "과일샐러드"
	LASTWORD = 열매의 샐러드 샌드
	CALL SET_PALAM_EXPLORER, "정력회복보정", 2
CASE "훈제연어"
	LASTWORD = 스모크드 새먼을 끼운 샌드위치
	CALL SET_PALAM_EXPLORER, "정력회복보정", 3
CASE "허니비스킷"
	LASTWORD = 간식으로 허니비스킷
	CALL SET_PALAM_EXPLORER, "정력회복보정", 1
CASE "베이크드애플"
	LASTWORD = 간식으로 베이크드 애플
	CALL SET_PALAM_EXPLORER, "정력회복보정", 1
;もう舐めおわりました
;CASE "문드롭스"
;	LASTWORD = 大好きな문드롭스
;	CALL SET_PALAM_EXPLORER, "정력회복보정", 5
CASEELSE
	IF DAY%2
		LASTWORD = 도시락
	ELSE
		IF TIME == 0
			PRINT 점심에 먹다 남은 것들을 끼워 넣은
		ELSE
			PRINT 저녁에 먹다 남은 것들을 끼워 넣은
		ENDIF
		LASTWORD = 샌드위치
	ENDIF
ENDSELECT

IF PLAYER != ASSI_EXPLORER
	PRINTFORM %LASTWORD를()% 준비시켜서 
ELSE
	PRINTFORM %LASTWORD를()% 가지고 
ENDIF
PRINTFORML 탐색에 나섭니다. 
PRINTFORM 탐색능력：
;一人の탐색
IF PLAYER == ASSI_EXPLORER
	CALL TEXT, "탐색능력", PLAYER
;主人と조수のペア
ELSE
	CALL TEXT, "탐색능력페어"
ENDIF

VARSET DUNGEON
;行先일람
DUNGEON:0 = 초원
DUNGEON:1 = 에르툼삼림
DUNGEON:2 = 류두흐동굴
DUNGEON:3 = 안쇼스 해
DUNGEON:4 = 베르나도트의대미궁
DUNGEON:5 = 고즈번 사막
DUNGEON:6 = 키아즈마의유곽

PRINTFORML 어디로 갑니까? 
PRINTFORML 

FOR LCOUNT, 0, 10
	SIF DUNGEON:LCOUNT == ""
		CONTINUE
	;CALL SET_DATA_FIELD_EXPLORER, DUNGEON:LCOUNT
	PRINTFORM  [{LCOUNT, 2}] %TEXT_LJ(DUNGEON:LCOUNT, 22)%
	LOCALS = %DUNGEON:LCOUNT%：보스격파
	SIF PALAM_EXPLORER(LOCALS)
		CALL PRINT_COLORTEXT, "※답파완료※", DEF_COLOR("노랑")
	;PRINTFORM 遭遇率{PALAM_EXPLORER("遭遇率")}％　
	;PRINTFORM 難易度{PALAM_EXPLORER("敵の強さ")}　
	;PRINTFORM トレジャーLV{PALAM_EXPLORER("아이템레벨")}　
	;PRINTFORM 宝箱発見率{PALAM_EXPLORER("아이템発見率")}％
	PRINTL 
NEXT

PRINTFORML [100] 돌아간다

$INPUT_LOOP
INPUT

SELECTCASE RESULT
CASE 100
	REDRAW 0
	RETURN 100
CASE 0 TO 10
	SIF DUNGEON:RESULT == ""
		GOTO INPUT_LOOP
	FIELD_EXPLORER = %DUNGEON:RESULT%
CASEELSE
	GOTO INPUT_LOOP
ENDSELECT

CALL SET_DATA_FIELD_EXPLORER, FIELD_EXPLORER

CALL TITLE_DATA_FIELD_EXPLORER, FIELD_EXPLORER

SELECTCASE FIELD_EXPLORER
CASE "키아즈마의유곽"
	PRINTFORML %FIELD_EXPLORER%의 탐색에는 %조사처리(TEXTS("조수の명칭"), "가")% 동행하지 못하고, $1,000～$10,000의 대금이 듭니다
	IF MONEY < 1000
		PRINTFORML …안 됐지만 돈이 없는 것 같네요
		PRINTFORML 다음 방문을 기다리겠습니다
		RESTART
	ENDIF
ENDSELECT

PRINTL 여기의 탐색을 개시합니까? 
PRINTL [0] 예
PRINTL [1] 아니오
CALL INPUT_SELECT, 2
SIF RESULT == 1
	RESTART

SAVESTR:15 = %FIELD_EXPLORER%
;60-63	범용フラグ（毎回탐색開始の時にリセットされる）
FLAG:50 = CLEARBITS(FLAG:50, 60, 63)

PRINTL 

MEMO_LINECOUNT = LINECOUNT

SIF PLAYER == MASTER && ASSI
	TRYCALLFORM GUEST_KOJO_K{NO:ASSI}_{CFLAG:ASSI:209}, "탐색：탐색개시", ASSI

SIF LINECOUNT != MEMO_LINECOUNT
	PRINTL 
PRINTFORMW %TEXTS("탐색先")%의 탐색을 개시합니다.

CALL START_EXPLORER


@TITLE_DATA_FIELD_EXPLORER, ARGS
DRAWLINE
SELECTCASE ARGS
CASE "초원"
	PRINTFORML %"～초원～", 25, LEFT%
CASE "에르툼삼림"
	PRINTFORML %"～에르툼삼림～", 25, LEFT% %"BOSS：스코그슬로", 25, LEFT% 유니크：티타니아
CASE "류두흐동굴"
	PRINTFORML %"～류두흐동굴～", 25, LEFT% %"BOSS：스투나＆멜로나", 25, LEFT% 유니크：뷔이브
CASE "안쇼스 해"
	PRINTFORML %"～안쇼스 해～", 25, LEFT% %"BOSS：레비아탄", 25, LEFT% 유니크：카리브디스
CASE "베르나도트의대미궁"
	PRINTFORML %"～베르나도트의 대미궁～", 25, LEFT% %"BOSS：베르나도트", 25, LEFT%
CASE "고즈번 사막"
	PRINTFORML %"～베르나도트의 대미궁～", 25, LEFT% %"BOSS：하피 세 자매", 25, LEFT% 유니크：스핑크스
CASE "키아즈마의유곽"
	PRINTFORML %"～키아즈마의 유곽～", 25, LEFT% %"BOSS：린", 25, LEFT% 유니크：하트메, 타마모
ENDSELECT
PRINTFORM 조우율：{PALAM_EXPLORER("遭遇率")}％     
PRINTFORM 난이도：{PALAM_EXPLORER("敵の強さ")}     
PRINTFORM 트레져：LV{PALAM_EXPLORER("아이템레벨")}     
PRINTFORML 보물상자 발견율：{PALAM_EXPLORER("아이템発見率")}％
DRAWLINE


@SET_DATA_FIELD_EXPLORER, ARGS
VARSET TFLAG
SELECTCASE ARGS
CASE "초원"
	CALL SET_PALAM_EXPLORER, "遭遇率", 20
	CALL SET_PALAM_EXPLORER, "아이템発見率", 20
	CALL SET_PALAM_EXPLORER, "아이템레벨", 1
	CALL SET_PALAM_EXPLORER, "계단발견率", 0
	CALL SET_PALAM_EXPLORER, "플로어", 1
	CALL SET_PALAM_EXPLORER, "最深플로어", 1
	CALL SET_PALAM_EXPLORER, "敵の強さ", 1
	;出現するのは서큐버스＆임프＆실프
	CALL SET_PALAM_EXPLORER, "몽마", 1, 1
	CALL SET_PALAM_EXPLORER, "몽마", 2, 1
	CALL SET_PALAM_EXPLORER, "몽마", 5, 1
	CALL SET_PALAM_EXPLORER, "基本の敵キャラ", 3

CASE "에르툼삼림"
	CALL SET_PALAM_EXPLORER, "遭遇率", 20
	CALL SET_PALAM_EXPLORER, "아이템発見率", 30
	CALL SET_PALAM_EXPLORER, "아이템레벨", 3
	CALL SET_PALAM_EXPLORER, "계단발견率", 20
	CALL SET_PALAM_EXPLORER, "플로어", 1
	CALL SET_PALAM_EXPLORER, "最深플로어", 8
	CALL SET_PALAM_EXPLORER, "敵の強さ", 2
	;드라이어드, 픽시, 실프, 네코마타, 워울프, 빌리져, 쿠즈노하, 고블린
	CALL SET_PALAM_EXPLORER, "몽마", 3, 2
	CALL SET_PALAM_EXPLORER, "몽마", 4, 2
	CALL SET_PALAM_EXPLORER, "몽마", 5, 2
	CALL SET_PALAM_EXPLORER, "몽마", 6, 1
	CALL SET_PALAM_EXPLORER, "몽마", 7, 1
	CALL SET_PALAM_EXPLORER, "몽마", 8, 2
	CALL SET_PALAM_EXPLORER, "몽마", 16, 2
	CALL SET_PALAM_EXPLORER, "몽마", 31, 2
	CALL SET_PALAM_EXPLORER, "基本の敵キャラ", 14

CASE "류두흐동굴"
	CALL SET_PALAM_EXPLORER, "遭遇率", 20
	CALL SET_PALAM_EXPLORER, "아이템発見率", 30
	CALL SET_PALAM_EXPLORER, "아이템레벨", 5
	CALL SET_PALAM_EXPLORER, "계단발견率", 20
	CALL SET_PALAM_EXPLORER, "플로어", 1
	CALL SET_PALAM_EXPLORER, "最深플로어", 8
	CALL SET_PALAM_EXPLORER, "敵の強さ", 3
	;라미아, 리저드맨, 하피. 뷔이브, ストメロは後で追加. 
	CALL SET_PALAM_EXPLORER, "몽마", 10, 2
	CALL SET_PALAM_EXPLORER, "몽마", 11, 2
	CALL SET_PALAM_EXPLORER, "몽마", 12, 2
	CALL SET_PALAM_EXPLORER, "基本の敵キャラ", 6

CASE "안쇼스 해"
	CALL SET_PALAM_EXPLORER, "遭遇率", 30
	CALL SET_PALAM_EXPLORER, "아이템発見率", 35
	CALL SET_PALAM_EXPLORER, "아이템레벨", 3
	CALL SET_PALAM_EXPLORER, "계단발견率", 20
	CALL SET_PALAM_EXPLORER, "플로어", 1
	CALL SET_PALAM_EXPLORER, "最深플로어", 10
	CALL SET_PALAM_EXPLORER, "敵の強さ", 4
	;머메이드、압사라、스킬라。레비아탄はあとで追加。
	CALL SET_PALAM_EXPLORER, "몽마", 105, 2
	CALL SET_PALAM_EXPLORER, "몽마", 13, 1
	CALL SET_PALAM_EXPLORER, "몽마", 213, 1
	CALL SET_PALAM_EXPLORER, "몽마", 14, 2
	CALL SET_PALAM_EXPLORER, "몽마", 214, 1
	CALL SET_PALAM_EXPLORER, "몽마", 15, 2
	CALL SET_PALAM_EXPLORER, "몽마", 215, 1
	CALL SET_PALAM_EXPLORER, "基本の敵キャラ", 10

CASE "베르나도트의대미궁"
	CALL SET_PALAM_EXPLORER, "遭遇率", 20
	CALL SET_PALAM_EXPLORER, "아이템発見率", 30
	CALL SET_PALAM_EXPLORER, "아이템레벨", 6
	CALL SET_PALAM_EXPLORER, "계단발견率", 3
	CALL SET_PALAM_EXPLORER, "플로어", 1
	CALL SET_PALAM_EXPLORER, "最深플로어", 3
	CALL SET_PALAM_EXPLORER, "敵の強さ", 5
	;캣시, 리난시, 다키니, 알라우네, ハイＬＶ실프, 윗치
	CALL SET_PALAM_EXPLORER, "몽마", 203, 2
	CALL SET_PALAM_EXPLORER, "몽마", 204, 2
	CALL SET_PALAM_EXPLORER, "몽마", 305, 2
	CALL SET_PALAM_EXPLORER, "몽마", 206, 2
	CALL SET_PALAM_EXPLORER, "몽마", 314, 2
	CALL SET_PALAM_EXPLORER, "몽마", 206, 2
	CALL SET_PALAM_EXPLORER, "몽마", 19, 2
	CALL SET_PALAM_EXPLORER, "基本の敵キャラ", 14

CASE "고즈번 사막"
	CALL SET_PALAM_EXPLORER, "遭遇率", 10
	CALL SET_PALAM_EXPLORER, "아이템発見率", 10
	CALL SET_PALAM_EXPLORER, "아이템레벨", 7
	CALL SET_PALAM_EXPLORER, "계단발견率", 10
	CALL SET_PALAM_EXPLORER, "플로어", 1
	CALL SET_PALAM_EXPLORER, "最深플로어", 6
	CALL SET_PALAM_EXPLORER, "敵の強さ", 6
	;アルプ、メリジューヌ、하피、아라크네、スフィンクス
	CALL SET_PALAM_EXPLORER, "몽마", 202, 2
	CALL SET_PALAM_EXPLORER, "몽마", 210, 1
	CALL SET_PALAM_EXPLORER, "몽마", 11, 3
	CALL SET_PALAM_EXPLORER, "몽마", 18, 1
	CALL SET_PALAM_EXPLORER, "몽마", 406, 1
	CALL SET_PALAM_EXPLORER, "基本の敵キャラ", 8

CASE "키아즈마의유곽"
	CALL SET_PALAM_EXPLORER, "遭遇率", 40
	CALL SET_PALAM_EXPLORER, "아이템発見率", 10
	CALL SET_PALAM_EXPLORER, "아이템레벨", 2
	CALL SET_PALAM_EXPLORER, "계단발견率", 0
	CALL SET_PALAM_EXPLORER, "플로어", 1
	CALL SET_PALAM_EXPLORER, "最深플로어", 1
	CALL SET_PALAM_EXPLORER, "敵の強さ", 7
	;네코마타, 쿠즈노하, 쿠노이치
	CALL SET_PALAM_EXPLORER, "몽마", 6, 1
	CALL SET_PALAM_EXPLORER, "몽마", 16, 1
	CALL SET_PALAM_EXPLORER, "몽마", 22, 1
	CALL SET_PALAM_EXPLORER, "基本の敵キャラ", 3

ENDSELECT


@START_EXPLORER
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM MEMO_ASSI
#DIM IS_ITEMGET
#DIM MEMO_EXP
#DIM IS_EXPGET
#DIMS GETITEMS

MEMO_ASSI = ASSI

SELECTCASE TEXTS("탐색先")
CASE "초원"

CASE "에르툼삼림"
	SIF PALAM_EXPLORER("에르툼삼림：보스격파")
		CALL SET_PALAM_EXPLORER, "계단발견率", 20
CASE "류두흐동굴"
	SIF PALAM_EXPLORER("류두흐동굴：보스격파")
		CALL SET_PALAM_EXPLORER, "계단발견率", 20
CASE "안쇼스 해"
	SIF PALAM_EXPLORER("안쇼스 해：보스격파")
		CALL SET_PALAM_EXPLORER, "계단발견率", 20
CASE "베르나도트의대미궁"
	SIF PALAM_EXPLORER("베르나도트의대미궁：보스격파")
		CALL SET_PALAM_EXPLORER, "계단발견率", 20
CASE "고즈번 사막"
	SIF PALAM_EXPLORER("고즈번 사막：보스격파")
		CALL SET_PALAM_EXPLORER, "계단발견率", 20
CASE "키아즈마의유곽"
	SIF PALAM_EXPLORER("키아즈마의유곽：보스격파")
		CALL SET_PALAM_EXPLORER, "계단발견率", 20
	ASSI = 0
	PRINTFORML 어느 코스로 합니까？ 소지금:%MONEY_C(MONEY)%
	PRINTFORML [ 0] 매화  $1,000 
	PRINTFORML [ 1] 대나무 $5,000 하츠메와 타마모가 등장합니다
	IF PALAM_EXPLORER("키아즈마의유곽：보스격파") == 0 || CONFIG("탐색ボス")
		PRINTFORML [ 2] 소나무 $10,000 린이 등장합니다
		CALL INPUT_SELECT, 3
	ELSE
		CALL INPUT_SELECT, 2
	ENDIF
	SELECTCASE RESULT
	CASE 0
		MONEY -= 1000
	CASE 1
		MONEY -= 5000
		CALL SET_PALAM_EXPLORER, "몽마", 316, 3
		CALL SET_PALAM_EXPLORER, "몽마", 222, 3
	CASE 2
		MONEY -= 10000
		CALL SET_PALAM_EXPLORER, "몽마", 316, 3
		CALL SET_PALAM_EXPLORER, "몽마", 222, 3
		CALL SET_PALAM_EXPLORER, "몽마", 444, 10
	ENDSELECT
CASEELSE
	THROW %GAMEBASE_YEAR% Ver.{VER_ERASQN()} BUG : "%TEXTS("탐색先")%"를 찾을 수 없습니다.
ENDSELECT

;탐색者と조수の効果
FOR LCOUNT, 1, CHARANUM
	SELECTCASE LCOUNT
	CASE PLAYER
	CASE ASSI
		SIF PLAYER != MASTER
			CONTINUE
	CASEELSE
		CONTINUE
	ENDSELECT

	SIF COND("탐색：遭遇回避", LCOUNT)
		CALL SET_PALAM_EXPLORER, "遭遇率", -5
	SIF COND("탐색：遭遇증가", LCOUNT)
		CALL SET_PALAM_EXPLORER, "遭遇率", 5
	SIF COND("탐색：財宝発見", LCOUNT)
		CALL SET_PALAM_EXPLORER, "아이템発見率", 5
	SIF COND("탐색：財宝見逃し", LCOUNT)
		CALL SET_PALAM_EXPLORER, "아이템発見率", -5
	SIF COND("탐색：階段発見", LCOUNT)
		CALL SET_PALAM_EXPLORER, "계단발견率", 5
	SIF COND("탐색：회복", LCOUNT)
		CALL SET_PALAM_EXPLORER, "정력회복보정", 2
NEXT
;악세사리効果（은밀, 宝箱, ドア）
;은밀
IF TALENT:PLAYER:은밀 > 0
	LOCAL = MULTIPLY(PALAM_EXPLORER("遭遇率"), TALENT:PLAYER:은밀)
	CALL SET_PALAM_EXPLORER, "遭遇率", -LOCAL
ENDIF
;宝箱
IF TALENT:PLAYER:보물상자출현율 > 0
	LOCAL = MULTIPLY(PALAM_EXPLORER("아이템発見率"), TALENT:PLAYER:보물상자출현율)
	CALL SET_PALAM_EXPLORER, "아이템発見率", LOCAL
ENDIF
;ドア
IF TALENT:PLAYER:문발견율 > 0
	LOCAL = MULTIPLY(PALAM_EXPLORER("계단발견率"), TALENT:PLAYER:문발견율)
	CALL SET_PALAM_EXPLORER, "계단발견率", LOCAL
ENDIF
;作戦方바늘보정
;ガンガンいこうぜ!  문발견율小ＵＰ, 宝発見率小DOWN
IF FLAG:246 == 1
	CALL SET_PALAM_EXPLORER, "遭遇率", 10
	CALL SET_PALAM_EXPLORER, "아이템発見率", -10
	CALL SET_PALAM_EXPLORER, "계단발견率", 15
;いのちをだいじに 宝発見率小DOWN, 戦闘小有利
ELSEIF FLAG:246 == 2
	CALL SET_PALAM_EXPLORER, "아이템発見率", -5
	CALL SET_PALAM_EXPLORER, "敵の強さ", -2
ENDIF

;獲得아이템と＄等の初期化
VARSET I
MONEY:5 = 0
MONEY:6 = 0
;경험치の記録
MEMO_EXP = EXP:PLAYER:경험치

;ひとまず30ターン탐색
FOR COUNT:99, 0, 30
	;시간経過で정력회복
	BASE:PLAYER:정력 = MIN(BASE:PLAYER:정력 + PALAM_EXPLORER("정력회복보정")/2 + 1, MAXBASE:PLAYER:정력)

	;섹스バトル発生
	IF RAND:100 < PALAM_EXPLORER("遭遇率")
		CALL SEXBATTLE_EXPLORER
	;아이템発見
	ELSEIF RAND:100 < PALAM_EXPLORER("아이템発見率")
		CALL GET_TREASURE_EXPLORER
	ELSE
		CALL SEARCH_FOR_EXPLORER
	ENDIF

	;敗北した？
	SIF BASE:PLAYER:정력 <= 0
		BREAK

	IF PALAM_EXPLORER("ハーレム")
		CALL SET_PALAM_EXPLORER, "ハーレム", -1
		CALL SET_PALAM_EXPLORER, "遭遇率", -10
		SIF PALAM_EXPLORER("ハーレム") == 0
			PRINTFORML 하렘의 함정효과가 끝났다. 
	ENDIF
	IF PALAM_EXPLORER("幸運")
		CALL SET_PALAM_EXPLORER, "幸運", -1
		IF PALAM_EXPLORER("幸運") == 0
			CALL SET_PALAM_EXPLORER, "아이템発見率", -50
			CALL SET_PALAM_EXPLORER, "아이템레벨", -3
			PRINTFORML 리난시의 함정? 의 효과가 끝났다. 
		ENDIF
	ENDIF
	IF PALAM_EXPLORER("クラムジ")
		CALL SET_PALAM_EXPLORER, "クラムジ", -1
		SIF PALAM_EXPLORER("クラムジ") == 0
			PRINTFORML 아르카나의 함정 효과가 끝났다. 
	ENDIF
	IF PALAM_EXPLORER("센시브")
		CALL SET_PALAM_EXPLORER, "센시브", -1
		SIF PALAM_EXPLORER("센시브") == 0
			PRINTFORML 미약 함정의 효과가 끝났다. 
	ENDIF

	;시간調整
	SIF COUNT:99 < 0
		COUNT:99 = 0
	SIF COUNT:99 > 29
		BREAK
NEXT

;獲得경험치
IS_EXPGET = EXP:PLAYER:경험치 - MEMO_EXP

IF BASE:PLAYER:정력 <= 0
	PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 체력이 한계다…. 탐색을 종료합니다. 
	BASE:PLAYER:체력 = 500
ELSE
	PRINTFORMW 귀환했습니다. 
	BASE:PLAYER:체력 *= BASE:PLAYER:정력
	BASE:PLAYER:체력 /= MAXBASE:PLAYER:정력
	SIF BASE:PLAYER:체력 < 500
		BASE:PLAYER:체력 = 500
ENDIF
PRINTL 

MEMO_LINECOUNT = LINECOUNT

IF PLAYER == MASTER && ASSI
	TRYCALLFORM GUEST_KOJO_K{NO:ASSI}_{CFLAG:ASSI:209}, "탐색：帰還", ASSI
	;二人きりでの冒険で、욕구불만も少し解消
	CALL CALC, "욕구불만解消", ASSI, 15
ENDIF
SIF LINECOUNT != MEMO_LINECOUNT
	PRINTL 

BASE:PLAYER:정력 = MAX(BASE:PLAYER:정력, 1)
PRINTFORML ～이번의 성과～
IF PALAM_EXPLORER("最深플로어") > 1
	PRINTFORM 도달계층：
	IF PALAM_EXPLORER("플로어") == PALAM_EXPLORER("最深플로어")
		PRINTFORML 최심부
	ELSE
		PRINTFORML {PALAM_EXPLORER("플로어")}Ｆ
	ENDIF
ENDIF
SIF MONEY:6
	PRINTFORML 획득금액：%MONEY_C(MONEY:6)%
MONEY += MONEY:6

SIF IS_EXPGET
	PRINTFORML  경험치：{IS_EXPGET}Ｐ

IS_ITEMGET = 0
GETITEMS = 
FOR LCOUNT, 0, 330
	SIF I:LCOUNT == 0
		CONTINUE
	GETITEMS = %GETITEMS%%ITEMNAME:LCOUNT%×{I:LCOUNT}%BL(1)%
	ITEM:LCOUNT += I:LCOUNT

	SELECTCASE LCOUNT
	;通常아이템と詩集など
	CASE 0 TO 99, 310 TO 319
		SETBIT IS_ITEMGET, 0
	;収集専用아이템（解説付）
	CASE 300 TO 309
		SETBIT IS_ITEMGET, 1
	;収集専用아이템（악세사리）
	CASE 320 TO 329
		SETBIT IS_ITEMGET, 2
	ENDSELECT
NEXT

IF IS_ITEMGET
	PRINTFORML 입수아이템：%GETITEMS%
	SIF GETBIT(IS_ITEMGET, 1)
		PRINTFORML 스푼이나 찻잔은 식사때는 물론, 탐험 중의 휴식에서도 우아한 한때를 연출하겠지요…
	;악세사리の鑑定
	SIF GETBIT(IS_ITEMGET, 2)
		PRINTFORML 감정되지않은 악세사리는 『악세서리상점:신기루당』에 가져가시는 것을 추천합니다
ELSEIF IS_ITEMGET + MONEY:6 == 0
	PRINTL 아이템 입수 등：없음
ENDIF

SIF I:999 && PLAYER == MASTER
	PRINTFORML %조사처리(NAME:TARGET,"를")% 동료 몽마로 만들었으므로 경험치에 보너스가 발생합니다
;LVUP
SIF PLAYER == MASTER
	CALL ABLUP_MASTER, "通常"

ASSI = MEMO_ASSI
FORCEWAIT

@SEARCH_FOR_EXPLORER
IF RAND:100 < PALAM_EXPLORER("계단발견率") && PALAM_EXPLORER("플로어") < PALAM_EXPLORER("最深플로어")
	CALL SET_PALAM_EXPLORER, "플로어", 1
	PRINTFORML 문을 발견. 다음 층으로 이동합니다. 
	;시간延長
	COUNT:99 = MAX(COUNT:99 - 3 - RAND:5, 0)
	SETCOLOR DEF_COLOR("황색")
	PRINTFORM {PALAM_EXPLORER("플로어")}Ｆ탐색개시……
	RESETCOLOR
	PRINTL 
	;もぐると敵の強さ等ＵＰ
	SIF RAND:2
		CALL SET_PALAM_EXPLORER, "敵の強さ", 1
	SIF PALAM_EXPLORER("아이템発見率") > RAND:100
		CALL SET_PALAM_EXPLORER, "아이템発見率", RAND:3 + 1
	SIF RAND:4 == 0
		CALL SET_PALAM_EXPLORER, "아이템레벨", 1

	;新たな敵出現
	SELECTCASE TEXTS("탐색先")
	CASE "에르툼삼림"
		SELECTCASE PALAM_EXPLORER("플로어")
		;티타니아出現
		CASE 3
			CALL SET_PALAM_EXPLORER, "몽마", 304, 1
		;스코그슬로出現
		CASE 8
			CALL SET_PALAM_EXPLORER, "몽마", 303, 5
		ENDSELECT
	CASE "류두흐동굴"
		SELECTCASE PALAM_EXPLORER("플로어")
		;뷔이브出現
		CASE 3
			CALL SET_PALAM_EXPLORER, "몽마", 412, 1
		;멜로나出現
		CASE 5
			SIF CONFIG("탐색ボス") || GETCHARA(35) < 0
				CALL SET_PALAM_EXPLORER, "몽마", 35, 1
		;스투나出現
		CASE 8
			SIF CONFIG("탐색ボス") || GETCHARA(34) < 0
				CALL SET_PALAM_EXPLORER, "몽마", 34, 5
		ENDSELECT
	CASE "안쇼스 해"
		SELECTCASE PALAM_EXPLORER("플로어")
		;카리브디스増量
		CASE 3
			CALL SET_PALAM_EXPLORER, "몽마", 215, 1
		;레비아탄出現
		CASE 10
			CALL SET_PALAM_EXPLORER, "몽마", 313, 5
		ENDSELECT
	CASE "베르나도트의대미궁"
		SELECTCASE PALAM_EXPLORER("플로어")
		;미믹出現
		;CASE 2
		;	CALL SET_PALAM_EXPLORER, "몽마", 33, 2
		;베르나도트出現
		CASE 3
			SIF CONFIG("탐색ボス") || GETCHARA(47) < 0
				CALL SET_PALAM_EXPLORER, "몽마", 47, 5
		ENDSELECT
	CASE "고즈번 사막"
		SELECTCASE PALAM_EXPLORER("플로어")
		;에스프라나出現
		CASE 6
			SIF CONFIG("탐색ボス") || GETCHARA(38) < 0
				CALL SET_PALAM_EXPLORER, "몽마", 38, 5
		ENDSELECT
	CASE "키아즈마의유곽"
	ENDSELECT
ELSE
	PRINT 탐색 중…
ENDIF


;탐색の各種数値はTFLAGで管理する
; 1	정력회복보정
; 3～14	ダンジョン파라미터ー
;15～29	戦闘で使用する
;30～68	敵캐릭터의遭遇表
;69	敵캐릭터의遭遇表の母数
;70～	トラップなど
@SET_PALAM_EXPLORER, ARGS, ARG, ARG:1
#DIM LCOUNT
SELECTCASE ARGS
CASE "정력회복보정"
	TFLAG:1 += ARG
CASE "遭遇率"
	TFLAG:3 = LIMIT(TFLAG:3 + ARG, 0, 90)
CASE "아이템発見率"
	TFLAG:4 = LIMIT(TFLAG:4 + ARG, 0, 90)
CASE "아이템레벨"
	TFLAG:5 = MAX(TFLAG:5 + ARG, 1)
CASE "財宝期待値"
	TFLAG:6 = ARG
CASE "財宝"
	TFLAG:7 = ARG

CASE "계단발견率"
	TFLAG:8 = LIMIT(TFLAG:8 + ARG, 1, 90)
CASE "플로어"
	TFLAG:9 += ARG
CASE "最深플로어"
	TFLAG:10 = ARG
CASE "敵の強さ"
	TFLAG:11 = MAX(TFLAG:11 + ARG, 1)

CASE "전투플래그리셋"
	TFLAG:22 = 0
	TFLAG:23 = 0
	TFLAG:24 = 0
CASE "行動"
	TFLAG:17 = ARG

CASE "텐더키스"
	TFLAG:17 = 1
CASE "키스"
	TFLAG:17 = 2
CASE "파이즈리"
	TFLAG:17 = 3
CASE "애널자극"
	TFLAG:17 = 4
CASE "안면기승"
	TFLAG:17 = 5
CASE "손훑기"
	TFLAG:17 = 6
CASE "펠라치오"
	TFLAG:17 = 7
CASE "풋잡"
	TFLAG:17 = 8
CASE "식스나인"
	TFLAG:17 = 9
CASE "달콤한말"
	TFLAG:17 = 10
CASE "바스트"
	TFLAG:17 = 11
CASE "ヴァッジ"
	TFLAG:17 = 12
CASE "애널"
	TFLAG:17 = 13

CASE "敵キャラ数"
	TFLAG:20 += ARG
CASE "무드"
	TFLAG:21 = MAX(TFLAG:21 + ARG, 0)
CASE "탈의"
	TFLAG:22 = ARG
CASE "체위"
	TFLAG:23 = ARG
CASE "Ｖ인서트"
	TFLAG:23 = 1
	TFLAG:24 |= 1
CASE "Ａ인서트"
	TFLAG:23 = 2
	TFLAG:24 |= 2
CASE "에이스오브스페이드"
	TFLAG:23 = 3
CASE "몽마"
	TFLAG:30 += ARG:1
	FOR LCOUNT, 31, 60
		SIF TFLAG:LCOUNT
			CONTINUE
		FOR LOCAL, 0, ARG:1
			TFLAG:(LCOUNT + LOCAL) = ARG
		NEXT
		BREAK
	NEXT
CASE "基本の敵キャラ"
	TFLAG:69 = ARG

CASE "ハーレム"
	TFLAG:70 = MAX(TFLAG:70 + ARG, 0)
CASE "幸運"
	TFLAG:71 = MAX(TFLAG:71 + ARG, 0)
CASE "クラムジ"
	TFLAG:72 = MAX(TFLAG:72 + ARG, 0)
CASE "센시브"
	TFLAG:73 = MAX(TFLAG:73 + ARG, 0)

;FLAG:50の各bitで各種フラグを記録する(基本的にはボス遭遇と보스격파)
; 0- 4	에르툼삼림
CASE "에르툼삼림：보스격파"
	SETBIT FLAG:50, 0
; 5- 9	류두흐동굴
CASE "류두흐동굴：中보스격파"
	SETBIT FLAG:50, 5
CASE "류두흐동굴：보스격파"
	SETBIT FLAG:50, 6
;10-14	안쇼스 해
CASE "안쇼스 해：보스격파"
	SETBIT FLAG:50, 10
;15-19	베르나도트의대미궁
CASE "베르나도트의대미궁：보스격파"
	SETBIT FLAG:50, 15
;20-24	고즈번 사막
CASE "고즈번 사막：보스격파"
	SETBIT FLAG:50, 20
;25-29	키아즈마의유곽
CASE "키아즈마의유곽：보스격파"
	SETBIT FLAG:50, 21
;60-63	범용フラグ（毎回탐색開始の時にリセットされる）
CASE "中보스격파"
	SETBIT FLAG:50, 60
CASE "보스격파"
	SETBIT FLAG:50, 61
CASEELSE
	THROW %GAMEBASE_YEAR% Ver.{VER_ERASQN()} BUG : "%ARGS%" 를 찾을 수 없습니다.
ENDSELECT

@PALAM_EXPLORER(ARGS, ARG)
#FUNCTION
SELECTCASE ARGS
CASE "정력회복보정"
	;食器で회복量上昇
	IF ITEM:미스릴찻잔
		RETURNF TFLAG:1 + 8
	ELSEIF ITEM:미스릴스푼
		RETURNF TFLAG:1 + 7
	ELSEIF ITEM:은찻잔
		RETURNF TFLAG:1 + 5
	ELSEIF ITEM:은스푼
		RETURNF TFLAG:1 + 4
	ELSEIF ITEM:도자기찻잔
		RETURNF TFLAG:1 + 2
	ELSEIF ITEM:도자기스푼
		RETURNF TFLAG:1 + 1
	ENDIF
	RETURNF TFLAG:1
CASE "遭遇率"
	RETURNF TFLAG:3
CASE "아이템発見率"
	RETURNF TFLAG:4
CASE "아이템레벨"
	RETURNF TFLAG:5
CASE "財宝期待値"
	RETURNF TFLAG:6
CASE "財宝"
	RETURNF TFLAG:7
CASE "계단발견率"
	RETURNF TFLAG:8
CASE "플로어"
	RETURNF TFLAG:9
CASE "最深플로어"
	RETURNF TFLAG:10
CASE "敵の強さ"
	RETURNF TFLAG:11
CASE "戦闘턴수"
	RETURNF TFLAG:12
CASE "エンカウント回数"
	RETURNF TFLAG:13
CASE "랜덤エンカウント"
	RETURNF TFLAG:(31 + RAND:(TFLAG:30) )
CASE "제한エンカウント"
	RETURNF TFLAG:(31 + RAND:(TFLAG:69) )
CASE "敵キャラ数"
	RETURNF TFLAG:20
CASE "一人目"
	RETURNF CHARANUM - TFLAG:20
CASE "二人目"
	RETURNF CHARANUM - (TFLAG:20 - 1)
CASE "三人目"
	RETURNF CHARANUM - (TFLAG:20 - 2)
CASE "무드"
	RETURNF TFLAG:21
CASE "탈의"
	RETURNF TFLAG:22

CASE "공격력"
	LOCAL = ABL:기교 + 1
	SIF ABL:ＬＶ > ABL:PLAYER:ＬＶ
		LOCAL += 1 + (ABL:ＬＶ - ABL:PLAYER:ＬＶ) / 3
	RETURNF LOCAL
CASE "탐색者공격력"
	;악세사리보정
	LOCAL = MULTIPLY(ABL:ARG:기교, 100 + (TALENT:ARG:기교강화 + TALENT:ARG:매력강화)/2 )
	SIF TALENT:ARG:용의혈통
		LOCAL += 1
	SIF TALENT:ARG:장사
		LOCAL += 1
	SIF TALENT:ARG:마왕의조각
		LOCAL += 3
	;SIF COND("유니크", ARG)
	;	LOCAL += 1
	LOCAL += TALENT:ARG:마법사 + TALENT:ARG:상위몽마
	RETURNF LOCAL
;마법사については아르카나補助部分に分割される
CASE "조수공격력"
	LOCAL = (ABL:ARG:기교 + 1)/3 + TALENT:ARG:상위몽마
	SIF TALENT:ARG:용의혈통
		LOCAL += 1
	SIF TALENT:ARG:장사
		LOCAL += 1
	SIF TALENT:ARG:마왕의조각
		LOCAL += 2
	;SIF COND("유니크", ARG)
	;	LOCAL += 1
	RETURNF LOCAL
CASE "아르카나補助"
	LOCAL = 0
	SIF TALENT:ARG:센시브 || TALENT:ARG:우르즈군 || TALENT:ARG:시길군 || TALENT:ARG:에올군 || TALENT:ARG:이사군 || TALENT:ARG:마법사 >= 2
		LOCAL += 1
	SIF TALENT:ARG:투와이스 || TALENT:ARG:바인드 || TALENT:ARG:마법사 >= 2
		LOCAL += 1
	SIF TALENT:ARG:아르스군 || TALENT:ARG:마법사 >= 2
		LOCAL += 1
	RETURNF LOCAL
CASE "텐더키스"
	SIF TFLAG:17 == 1
		RETURNF 1
CASE "키스"
	SIF TFLAG:17 == 2
		RETURNF 1
CASE "파이즈리"
	SIF TFLAG:17 == 3
		RETURNF 1
CASE "애널자극"
	SIF TFLAG:17 == 4
		RETURNF 1
CASE "안면기승"
	SIF TFLAG:17 == 5
		RETURNF 1
CASE "손훑기"
	SIF TFLAG:17 == 6
		RETURNF 1
CASE "펠라치오"
	SIF TFLAG:17 == 7
		RETURNF 1
CASE "풋잡"
	SIF TFLAG:17 == 8
		RETURNF 1
CASE "식스나인"
	SIF TFLAG:17 == 9
		RETURNF 1
CASE "달콤한말"
	SIF TFLAG:17 == 10
		RETURNF 1
CASE "바스트"
	SIF TFLAG:17 == 11
		RETURNF 1
CASE "ヴァッジ"
	SIF TFLAG:17 == 12
		RETURNF 1
CASE "애널"
	SIF TFLAG:17 == 13
		RETURNF 1

CASE "체위"
	RETURNF TFLAG:23
CASE "Ｖ인서트"
	SIF TFLAG:23 == 1
		RETURNF 1
CASE "Ａ인서트"
	SIF TFLAG:23 == 2
		RETURNF 1
CASE "에이스오브스페이드"
	SIF TFLAG:23 == 3
		RETURNF 1
CASE "両穴征옷"
	SIF GETBIT(TFLAG:24, 0) && GETBIT(TFLAG:24, 1)
		RETURNF 1

CASE "ハーレム"
	RETURNF TFLAG:70
CASE "幸運"
	RETURNF TFLAG:71
CASE "クラムジ"
	RETURNF TFLAG:72
CASE "센시브"
	RETURNF TFLAG:73

;FLAG:50の各bitで各種フラグを記録する(基本的にはボス遭遇と보스격파)
CASE "초원：보스격파"
; 0- 4	에르툼삼림
CASE "에르툼삼림：보스격파"
	RETURNF GETBIT(FLAG:50, 0)
; 5- 9	류두흐동굴
CASE "류두흐동굴：中보스격파"
	RETURNF GETBIT(FLAG:50, 5)
CASE "류두흐동굴：보스격파"
	RETURNF GETBIT(FLAG:50, 6)
;10-14	안쇼스 해
CASE "안쇼스 해：보스격파"
	RETURNF GETBIT(FLAG:50, 10)
;15-19	베르나도트의대미궁
CASE "베르나도트의대미궁：보스격파"
	RETURNF GETBIT(FLAG:50, 15)
;20-24	고즈번 사막
CASE "고즈번 사막：보스격파"
	RETURNF GETBIT(FLAG:50, 20)
;25-29	키아즈마의유곽
CASE "키아즈마의유곽：보스격파"
	RETURNF GETBIT(FLAG:50, 21)
;60-63	범용フラグ（毎回탐색開始の時にリセットされる）
CASE "中보스격파"
	RETURNF GETBIT(FLAG:50, 60)
CASE "보스격파"
	RETURNF GETBIT(FLAG:50, 61)
CASEELSE
	THROW %GAMEBASE_YEAR% Ver.{VER_ERASQN()} BUG : "%ARGS%" 를 찾을 수 없습니다.
ENDSELECT
RETURNF 0
