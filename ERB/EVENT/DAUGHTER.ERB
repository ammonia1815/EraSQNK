@DAUGHTER_CHECK, ARGS
#DIM MEMO_TARGET
#DIM MOTHER
#DIM FATHER

MEMO_TARGET = TARGET

MOTHER = TARGET
FATHER = COND("태아의부친")

;딸は조교가능になるかなどを判定します. 
SIF CHARANUM < 100
	CALL DAUGHTER_BIRTH, ARGS, MOTHER, FATHER

TARGET = MEMO_TARGET

@DAUGHTER_BIRTH, ARGS, MOTHER, FATHER
#DIM LCOUNT
#DIM MOTHER
#DIM FATHER
#DIM TBONUS
#DIM HP_BONUS

;아들かどうか
IF COND("육아중：아들", MOTHER)
	ADDCHARA 95
ELSE
	ADDCHARA 97
ENDIF
TARGET = CHARANUM-1

;기력체력の処理

MAXBASE:체력 = MAX(MAX(CSVBASE(BASE:MOTHER:종족, 0, 0), MAXBASE:MOTHER:체력)/120*100 + TALENT:남자*200, 1000)
MAXBASE:기력 = MAX(MAX(CSVBASE(BASE:MOTHER:종족, 1, 0), MAXBASE:MOTHER:기력)/60*50, 800)

BASE:체력 = MAXBASE:체력
BASE:기력 = MAXBASE:기력

;순종の処理, 産みの親の반발각인が少ないほど最初から순종
ABL:순종 = MAX(2 - MARK:MOTHER:반발각인, 0)

;遺伝の処理
;부친が子離れの時にいると, 母がMASTERか20％の확률で소질は父のものを参照
;また딸は付く소질が制限される
IF FATHER > 0 && (MOTHER == MASTER || RAND:5 == 0)
	CALL D_TALENT_INHERITANCE, FATHER, MOTHER
ELSE
	CALL D_TALENT_INHERITANCE, MOTHER, FATHER
ENDIF
;가슴 크기は모친のものしか影響しない
CALL D_BUST_SET, MOTHER
;딸固有の소질を付加, 反する소질の制限. 
TALENT:호기심 = 1
TALENT:아픔에약함 = 1
TALENT:아픔에강함 = 0
;小柄
TALENT:장신 = 0
TALENT:작은몸집 = 1
;유치
TALENT:유치 = 1

;親の소질LV5による보너스の計算
LOCAL = 0
;부친が優秀だった時にも보너스
SELECTCASE FATHER
CASE MASTER
	;당신の時にはＬＶ依存
	LOCAL += FLAG:255/5
CASE IS > 0
	FOR LCOUNT, 0, 20
		SIF ABL:FATHER:LCOUNT >= 5
			LOCAL += 1
		;최대LVが5이상の能力は, 최대LVに達していることでさらに+1
		SIF ABL:FATHER:LCOUNT >= 6 && ABL:FATHER:LCOUNT == NUM("최대레벨", LCOUNT)
			LOCAL += 1
	NEXT
ENDSELECT

FOR LCOUNT, 0, 20
	SIF ABL:MOTHER:LCOUNT >= 5
		LOCAL += 1
	;최대LVが5이상の能力は, 최대LVに達していることでさらに+1
	SIF ABL:MOTHER:LCOUNT >= 6 && ABL:MOTHER:LCOUNT == NUM("최대레벨", LCOUNT)
		LOCAL += 1
NEXT
;高LVが5個につき1つ, 보너스がもらえます
TBONUS = LOCAL / 5

;소질보너스は最高5個
TBONUS = MIN(TBONUS, 5)

;보너스が0の場合はそのまま次の処理へ
IF TBONUS > 0
	FOR LCOUNT, 0, TBONUS
		CALL DAUGHTER_BONUS, MOTHER, FATHER
	NEXT
ENDIF

;플래그立て
CFLAG:2 = LIMIT(CFLAG:MOTHER:2/10, 0, 500 + TALENT:MOTHER:마음에듬*100 + TALENT:MOTHER:상사상애*200)
BASE:생일 = COND("출산일", MOTHER)
CFLAG:MOTHER:485 = 0

;ランクの遺伝
CFLAG:8 = CFLAG:MOTHER:8
BASE:종족 = BASE:MOTHER:종족
SIF BASE:종족 == 0 && FATHER > 0
	BASE:종족 = BASE:FATHER:종족
BASE:종족사고 = BASE:종족
BASE:몇번째 = TALENT:MOTHER:출산경험

;-3なら드래곤の딸
IF FATHER > 0
	BASE:부친 = BASE:FATHER:식별번호
ELSE
	BASE:부친 = FATHER
ENDIF
IF MOTHER > 0
	BASE:모친 = BASE:MOTHER:식별번호
ELSE
	BASE:모친 = MOTHER
ENDIF
BASE:세대 = BASE:MOTHER:세대 + 1

;드래곤の아이？용의혈통(TALENT:용의혈통)の濃さをここで操作. 
IF BASE:부친 == -3
	TALENT:용의혈통 += RAND:2 + 1
ELSEIF TALENT:용의혈통 > 0
	TALENT:용의혈통 -= 1
ENDIF

;촉수の아이？
SIF BASE:부친 == -5
	TALENT:구지배자의자취 = 1

;상성決定
FOR LCOUNT, 1, 99
	SIF RELATION:MOTHER:LCOUNT != 0 && RELATION:MOTHER:LCOUNT != 100
		RELATION:LCOUNT = (RELATION:MOTHER:LCOUNT)/2+50
	SIF RELATION:LCOUNT > 190
		RELATION:LCOUNT = 190
NEXT
;親との상성よし
SIF FATHER > 0
	RELATION:(NO:FATHER) = 200
RELATION:(NO:MOTHER) = 200

;아들なら
IF NO:TARGET == 95
	TALENT:처녀 = 0
	;男にも濡れやすさの変化をつける試み
	;TALENT:젖기쉬움 = 0
	;TALENT:젖기어려움 = 0
	TALENT:Ｖ손상 = 0
	TALENT:Ａ손상 = 0
	TALENT:음호 = 0
	TALENT:음유 = 0
	;바이は…どうなんだろう
	TALENT:바이 = 0
	TALENT:오토코노코 = RAND:2
	TALENT:Ｖ둔감 = 0
	TALENT:Ｖ민감 = 0
	TALENT:빈유 = 0
	TALENT:거유 = 0
	TALENT:여자 = 0
	TALENT:후타나리 = 0
	TALENT:남자 = 1
	TALENT:구현 = 0
	TALENT:동정 = 1
	IF RAND:10 == 0
		TALENT:지루 = RAND:3
	ELSEIF RAND:9 == 0
		TALENT:조루 = 1
	ENDIF
	ABL:Ｖ감각 = 0
	ABL:ＢＬ끼 = ABL:레즈끼
	ABL:레즈끼 = 0
	ABL:Ｂ감각 = MAX(ABL:Ｂ감각 - 1, 0)
ENDIF

;注いだ量等で, 몽마は체력기력に보너스
HP_BONUS = MIN(BASE:MOTHER:태아의영양, 100)
IF TALENT:상위몽마
	HP_BONUS *= 4
ELSEIF TALENT:몽마
	HP_BONUS *= 2
ELSE
	HP_BONUS = 0
ENDIF

;보너스표기
IF TBONUS || HP_BONUS || TALENT:용의혈통
	SETCOLOR DEF_COLOR("황색")
	PRINTL
	SIF TBONUS
		PRINTFORML 부모의 소질이 우수하기때문에, {TBONUS}개의 소질에 보너스가 발생합니다
	SIF HP_BONUS
		PRINTFORML 임신 중의 모체에 주입된 정액의 영향으로 체력, 기력에 {HP_BONUS}의 보너스가 발생합니다
	SIF TALENT:용의혈통
		PRINTFORML 몸에 흐르는 용의 혈통이 영향으로 체력, 기력에 \@ TALENT:용의혈통 <= 2 ? 400 # 800 \@의 보너스가 발생합니다
	RESETCOLOR
	WAIT
ENDIF

;용의혈통が体を流れているか？
IF TALENT:용의혈통 > 0
	;血が濃いほど, 체력기력に보너스
	IF TALENT:용의혈통 <= 2
		HP_BONUS += 400
	ELSE
		HP_BONUS += 800
	ENDIF
ENDIF

MAXBASE:체력 += HP_BONUS
MAXBASE:기력 += HP_BONUS
BASE:체력 = MAXBASE:체력
BASE:기력 = MAXBASE:기력
BASE:MOTHER:태아의영양 = 0

PRINTL 
SELECTCASE ARGS
CASE "슬라임"
	;分裂展開に限り、100％슬라임
	TALENT:슬라임 = 1

	PRINTFORMW 분열된 슬라임은 %CALLNAME:MOTHER%의 아이로 받아들여졌습니다.
	PRINTFORML %CALLNAME:MOTHER%의 아이는 당신의 일에 흥미를 갖고 있는 듯합니다
	PRINTFORMW %CALLNAME:MOTHER%의 아이가 조교 가능하게 되었습니다
CASEELSE
	PRINTFORML %조사처리(CALLNAME:MOTHER,"가")% 기르던 아이는 당신의 일에 흥미를 갖고 있는 듯합니다
	PRINTFORMW %CALLNAME:MOTHER%의 아이가 조교 가능하게 되었습니다. 
ENDSELECT

IF TALENT:남자
	PRINTL 
	PRINTFORML 그런데, 이 남자아이는 어떻게 \@ ARGS == "슬라임" ? 보입니 # 자라났습니 \@까? 
	PRINTFORML [ 0] 평범한 남자아이
	PRINTFORML [ 1] 겉보기엔 여자로밖엔 안보이지만, 이른바 오토코노코
	CALL INPUT_SELECT, 2
	SIF RESULT == 1
		SETBIT TALENT:오토코노코, 0
	PRINTL 
ENDIF

CALL SETFLAG, "식별번호", TARGET
;허리놀림LVで힙と웨스트に影響が出るのを防ぐ
CALL SET_3SIZE, TARGET
CALL RESET_EXP_GAIN, TARGET
CALL GET_EXABL, TARGET
CALL GET_EXTALENT, TARGET
CALL SETFLAG, "음모と겨털", TARGET
CALL SETFLAG, "유두の상세설정", TARGET
CALL SETFLAG, "페니스상세설정"
CALL SETFLAG, "속성", TARGET

ABL:ＬＶ = MAX(ABL:MOTHER:ＬＶ/3, 1)

CALL SET_DAUGHTER_NAME, MOTHER

;조교しないならココでおしまい
SIF RESULT == 4
	RETURN 1

CALLNAME:TARGET = %NAME:TARGET%

$STATUS_LOOP
PRINTFORML %조사처리(NAME:TARGET,"를")% 조교합니까? 
PRINTFORML [ 0] 한다
PRINTFORML [ 1] 스테이터스를 본다
PRINTFORML [ 2] 조교하는 것을 그만둔다
CALL INPUT_SELECT, 3
IF RESULT == 0

ELSEIF RESULT == 1
	CALL NEW_PRINT_STATE, TARGET, "通常"
	GOTO STATUS_LOOP
ELSEIF RESULT == 2
	DELCHARA TARGET
ENDIF

RETURN 1

@SET_DAUGHTER_NAME, ARG
IF ARG > 0 && CSTR:ARG:90 != ""
	NAME:TARGET = %CSTR:ARG:90%
	CSTR:ARG:90 = 
	RETURN 0
ENDIF

PRINTL 이름을 붙입니까? 
PRINTFORML [0] 예
PRINTFORML [1] 아니오
IF ARG == MASTER
	PRINTFORML [2] 이름을 떠올려 본다（이름 후보중에서 선택）
ELSE
	PRINTFORML [2] %CALLNAME:ARG%에게 어떤 이름이 좋을 지 물어본다
ENDIF
PRINTFORML [3] 스테이터스를 본다
PRINTFORML [4] 조교하는 것을 그만둔다

CALL INPUT_SELECT, 5

IF RESULT == 0
	PRINTL 이름을 입력해주세요. 아무것도 입력하지 않으면 선택지로 돌아갑니다
	CALL INPUTS_SELECT, 16
	SIF RESULTS == ""
		RESTART
	NAME:TARGET = %RESULTS%
ELSEIF RESULT == 1
ELSEIF RESULT == 2
	CALL GIVE_NAME
ELSEIF RESULT == 3
	CALL NEW_PRINT_STATE, TARGET, "通常"
	RESTART
ELSEIF RESULT == 4
	DELCHARA TARGET
	RETURN 4
ENDIF

@BEFOREHAND_DAUGHTER_NAME
PRINTL 이름을 어떻게 합니까? 
PRINTL [0] 생각이 나지않으므로 나중에 생각한다
PRINTL [1] 지금 붙인다
IF TARGET == MASTER
	PRINTFORML [2] 이름을 떠올려 본다（이름 후보중에서 선택）
ELSE
	PRINTFORML [2] %CALLNAME:TARGET%에게 어떤 이름이 좋을 지 물어본다
ENDIF
CALL INPUT_SELECT, 3
IF RESULT == 0
	RETURN 0
ELSEIF RESULT == 1
	PRINTL 이름을 입력해주세요. 아무것도 입력하지 않으면 선택지로 돌아갑니다
	CALL INPUTS_SELECT, 16
	SIF RESULTS == ""
		RESTART
	CSTR:90 = %RESULTS%
ELSEIF RESULT == 2
	CALL BEFOREHAND_GIVE_NAME
ENDIF

@GIVE_NAME
#DIM MOTHER
#DIM IDEA
#DIM PREVIDEA

VARSET LOCAL
MOTHER = 0
IF COND("모친") > MASTER
	MOTHER = COND("모친")
	PRINTFORMW %CALLNAME:MOTHER%에게 이름을 생각해보도록 부탁했습니다
ENDIF
PREVIDEA = 0
$START

;名前はジャンルで分ける
IF COND("모친") > 0
	;母の종족が키아즈마かどうか
	SELECTCASE BASE:MOTHER:종족
	CASE 16, 22, 44
		IDEA = RAND:60
	CASEELSE
		IDEA = 100 + RAND:192
	ENDSELECT
ELSEIF RAND:2
	IDEA = RAND:60
ELSE
	IDEA = 100 + RAND:192
ENDIF

;連続で同じ名前は提案しない
SIF IDEA == PREVIDEA || NAMES("아이の名前", IDEA) == "딸"
	GOTO START

NAME:TARGET = %NAMES("아이の名前", IDEA)%

CALL PRINT_GIVENAME, COND("모친"), NAME:TARGET
PRINTFORML 이 이름으로 좋습니까? 
PRINTL [0] 예
PRINTL [1] 다시 한번 생각한다
CALL INPUT_SELECT, 2
IF RESULT == 0
	PRINTFORMW 아이의 이름은 %조사처리(NAME:TARGET,"로")%  정해졌습니다
	RETURN 0
ELSEIF RESULT == 1
	PREVIDEA = IDEA
	GOTO START
ENDIF

@BEFOREHAND_GIVE_NAME
#DIM IDEA
#DIM PREVIDEA

SIF TARGET > MASTER
	PRINTFORMW %CALLNAME:TARGET%에게 이름을 생각해보도록 부탁했습니다
PREVIDEA = 0

$START

;名前はジャンルで分ける
IF TARGET
	;名付けする人の종족が키아즈마かどうか
	SELECTCASE BASE:종족사고
	CASE 16, 22, 44
		IDEA = RAND:60
	CASEELSE
		IDEA = 100 + RAND:192
	ENDSELECT
ELSEIF RAND:2
	IDEA = RAND:60
ELSE
	IDEA = 100 + RAND:192
ENDIF

;連続で同じ名前は提案しない
SIF IDEA == PREVIDEA || NAMES("아이の名前", IDEA) == "딸"
	GOTO START

CSTR:90 = %NAMES("아이の名前", IDEA)%

CALL PRINT_GIVENAME, TARGET, CSTR:90
PRINTFORML 이 이름으로 좋습니까? 
PRINTL [0] 예
PRINTL [1] 다시 한번 생각한다
PRINTL [2] 역시 나중에 생각한다
CALL INPUT_SELECT, 3
IF RESULT == 0
	PRINTFORMW 아이의 이름은 %조사처리(CSTR:90,"로")% 정해졌습니다. 
ELSEIF RESULT == 1
	PREVIDEA = IDEA
	GOTO START
ELSEIF RESULT == 2
	CSTR:90 = 
ENDIF

@PRINT_GIVENAME, ARG, ARGS
IF ARG >= 1
	SELECTCASE NO:ARG
	CASE 0
		PRINTFORMW %CALLNAME:MASTER%의 머리에 떠오른 이름은……%ARGS%
	CASE 6
		PRINTFORMW 「뉴우……%ARGS%, 같은 건 어떻냥? 」
	CASE 7
		PRINTFORMW 「그릉……%ARGS%」
	CASE 8, 9, 23, 24, 32, 43, 44
		PRINTFORMW 「이 아이의 이름……%ARGS%, 같은 건 어떨까나? 」
	CASE 12, 13, 15, 19, 26, 28, 39
		PRINTFORMW 「잠깐 기다려……%ARGS%, 같은 건 어때?」
	CASE 16
		PRINTFORMW 「으으음……%ARGS%, 같은 건 어떤가? 」
	CASE 20
		PRINTFORMW 「그렇네요……%ARGS%, 같은 건 어떻습니까? 」
	CASE 21
		PRINTFORMW 「그렇군……%ARGS%, 같은 건 어때? 」
	CASE 22, 30, 61
		PRINTFORMW 「그렇네요……%ARGS%, 같은 건 어때요?」
	CASE 24
		PRINTFORMW 「응……. %ARGS%, 같은 건, 어때…? 」
	CASE 27
		PRINTFORM 「예언하겠어요. 이 아이의 이름은…, %ARGS%
		IF RAND:4 == 0
			PRINTFORMW . 분명 3턴 후에는 가버릴 것 같이 되겠지요…」
		ELSEIF RAND:3 == 0
			PRINTFORMW . 분명 캐비어를 좋아하는, 건강한 아이가 되겠지요…? 」
		ELSEIF RAND:2 == 0
			PRINTFORMW . 분명 동쪽나라를 다스리겠지요…」
		ELSE
			PRINTFORMW 」
		ENDIF
	CASE 29
		PRINTFORMW 「이 아이의 이름은, 푸루푸루～웃♪…이 아니라, %ARGS%, 같은 건 어떨까? 」
	CASE 2, 4, 31, 33, 40, 50, 55
		PRINTFORMW 「에에-……%ARGS%, 귀엽지? 」
	CASE 1, 3, 5, 10, 11, 14, 17, 18, 25, 37, 38, 48, 49
		PRINTFORMW 「그래……%ARGS%, 같은 건 어떨까? 」
	CASEELSE
		PRINTFORMW 상의한 결과, 아이의 이름은 %조사처리(ARGS,"가")% 된 것 같습니다. 
	ENDSELECT
ELSE
	PRINTFORMW 그렇군…%ARGS%, 같은 건 어떨까? 
ENDIF

;ARG, ARG:1は両親. ARGの소질を優先的に得る
@D_TALENT_INHERITANCE, ARG, ARG:1
#DIM LCOUNT
#DIM PATTERN
#DIM GENE, 3

;親が成長済みでない場合, 一時的に成長
VARSET LOCAL
IF !COND("성장끝", ARG)
	FOR LCOUNT, 1, 300
		LOCAL:LCOUNT = TALENT:ARG:LCOUNT
	NEXT
	CALL CALC, "아이の성장", ARG, CFLAG:ARG:484
	LOCAL = 1
ENDIF

;遺伝の処理(성격に関係のないもの)
FOR LCOUNT, 1, 300
	SIF CALCF("遺伝가능な소질", LCOUNT) == 0 || CALCF("성격소질", LCOUNT)
		CONTINUE
	IF RAND:4 == 0 && ARG:1 > 0
		TALENT:LCOUNT = TALENT:(ARG:1):LCOUNT
	ELSE
		TALENT:LCOUNT = TALENT:ARG:LCOUNT
	ENDIF
NEXT
;大마법사は無し
SIF TALENT:마법사 >= 2
	TALENT:마법사 = 1

;親の能力によって○놀림に보너스
FOR LCOUNT, 30, 36
	IF RAND:4 == 0 && ARG:1 > 0
		SIF ABL:(ARG:1):LCOUNT - TALENT:(ARG:1):(150+LCOUNT) >= 5
			TALENT:(150+LCOUNT) += 1
	ELSE
		SIF ABL:ARG:LCOUNT - TALENT:ARG:(150+LCOUNT) >= 5
			TALENT:(150+LCOUNT) += 1
	ENDIF
NEXT

;親から遺伝する속성を1つ선택
GENE:0 = 0
PATTERN = 1
FOR LCOUNT, 0, NUM("속성")
	SIF GETBIT(BASE:ARG:속성, LCOUNT) == 0
		CONTINUE
	SELECTCASE LCOUNT
	CASE 1 TO 7, 10 TO 12, 14
		SIF RAND:PATTERN == 0
			GENE:0 = LCOUNT
		PATTERN += 1
	;ここでは[에로]は出やすい
	CASE 17
		SIF RAND:PATTERN <= 1
			GENE:0 = LCOUNT
		PATTERN += 1
	ENDSELECT
NEXT

;親から遺伝できる속성がない場合, 適当に설정
;20%ほどで랜덤
$CHOICE_ELEMENT1
IF GENE:0 == 0 || RAND:5 == 0
	GENE:0 = CHOICE_NEWELEMENT()
	GOTO CHOICE_ELEMENT1
ENDIF

;25%の확률で성격が반전する
IF RAND:4 == 0
	SELECTCASE GENE:0
	CASE 1
		IF RAND:2
			GENE:0 = 6
		ELSE
			GENE:0 = 7
		ENDIF
	CASE 2
		GENE:0 = 17
	CASE 3
		IF RAND:2
			GENE:0 = 5
		ELSE
			GENE:0 = 11
		ENDIF
	CASE 4
		GENE:0 = 10
	CASE 5
		GENE:0 = 3
	CASE 6
		GENE:0 = 1
	CASE 7
		GENE:0 = 1
	CASE 10
		IF RAND:3
			GENE:0 = 4
		ELSE
			GENE:0 = 14
		ENDIF
	CASE 11
		GENE:0 = 3
	CASE 12
		GENE:0 = 17
	CASE 14
		GENE:0 = 10
	CASE 17
		IF RAND:3
			GENE:0 = 2
		ELSE
			GENE:0 = 12
		ENDIF
	ENDSELECT
ENDIF

$CHOICE_ELEMENT2
GENE:1 = CHOICE_NEWELEMENT(GENE:0)
GENE:2 = CHOICE_NEWELEMENT(GENE:0, GENE:1)
SIF GENE:1 == 0 && GENE:2 == 0
	GOTO CHOICE_ELEMENT2

;決定した속성をもとに성격소질を설정
FOR LCOUNT, 0, 3
	SIF GENE:LCOUNT == 0
		CONTINUE
	IF GETBIT(BASE:ARG:속성, GENE:LCOUNT)
		SELECTCASE GENE:LCOUNT
		CASE 1
			TALENT:강기 = 1
			TALENT:반항적 = TALENT:ARG:반항적
			TALENT:꿋꿋함 = TALENT:ARG:꿋꿋함
			TALENT:건방짐 = TALENT:ARG:건방짐
			TALENT:눈에띄고싶어함 = TALENT:ARG:눈에띄고싶어함
		CASE 2
			TALENT:청초 = 1
		CASE 3
			TALENT:양기 = 1
			TALENT:눈에띄고싶어함 = TALENT:ARG:눈에띄고싶어함
		CASE 4
			TALENT:솔직 = 1
			TALENT:호기심 = TALENT:ARG:호기심
		CASE 5
			TALENT:음기 = 1
			TALENT:심술쟁이 = TALENT:ARG:심술쟁이
			TALENT:보수적 = TALENT:ARG:보수적
		CASE 6
			TALENT:내성적 = 1
			TALENT:프라이드낮음 = TALENT:ARG:프라이드낮음
			TALENT:보수적 = TALENT:ARG:보수적
		CASE 7
			TALENT:겁쟁이 = 1
			TALENT:프라이드낮음 = TALENT:ARG:프라이드낮음
			TALENT:보수적 = TALENT:ARG:보수적
		CASE 10
			TALENT:교만 = 1
			TALENT:반항적 = TALENT:ARG:반항적
			TALENT:건방짐 = TALENT:ARG:건방짐
		CASE 11
			TALENT:냉정 = TALENT:ARG:냉정
			TALENT:감정결핍 = TALENT:ARG:감정결핍
			IF TALENT:냉정 == 0 && TALENT:감정결핍 == 0
				IF RAND:2
					TALENT:냉정 = 1
				ELSE
					TALENT:감정결핍 = 1
				ENDIF
			ENDIF
			TALENT:무관심 = TALENT:ARG:무관심
		CASE 12
			TALENT:부끄럼쟁이 = 1
		CASE 14
			TALENT:헌신 = 1
		CASE 17
			TALENT:호색 = 1
			TALENT:부끄럼없음 = TALENT:ARG:부끄럼없음
			TALENT:반하기쉬움 = TALENT:ARG:반하기쉬움
			TALENT:소악마 = TALENT:ARG:소악마
		ENDSELECT
	ELSE
		SELECTCASE GENE:LCOUNT
		CASE 1
			TALENT:강기 = 1
			SIF RAND:3 == 0 || TALENT:ARG:반항적
				TALENT:반항적 = 1
			SIF RAND:3 == 0 || TALENT:ARG:꿋꿋함
				TALENT:꿋꿋함 = 1
			SIF RAND:3 == 0 || TALENT:ARG:건방짐
				TALENT:건방짐 = 1
		CASE 2
			TALENT:청초 = 1
			TALENT:정조관결여 = 0
			TALENT:조야 = 0
			SIF RAND:2
				TALENT:정조관념 = 1
		CASE 3
			TALENT:양기 = 1
		CASE 4
			TALENT:솔직 = 1
			SIF RAND:2 || TALENT:ARG:호기심
				TALENT:호기심 = 1
		CASE 5
			TALENT:음기 = 1
			SIF RAND:2 || TALENT:ARG:심술쟁이
				TALENT:심술쟁이 = 1
			SIF RAND:4 == 0 || TALENT:ARG:보수적
				TALENT:보수적 = 1
		CASE 6
			TALENT:내성적 = 1
			SIF RAND:3 == 0 || TALENT:ARG:프라이드낮음
				TALENT:프라이드낮음 = 1
			SIF RAND:4 == 0 || TALENT:ARG:보수적
				TALENT:보수적 = 1
		CASE 7
			TALENT:겁쟁이 = 1
			SIF RAND:3 == 0 || TALENT:ARG:프라이드낮음
				TALENT:프라이드낮음 = 1
			SIF RAND:4 == 0 || TALENT:ARG:보수적
				TALENT:보수적 = 1
		CASE 10
			TALENT:교만 = 1
			SIF RAND:3 == 0 || TALENT:ARG:반항적
				TALENT:반항적 = 1
			SIF RAND:3 == 0 || TALENT:ARG:건방짐
				TALENT:건방짐 = 1
		CASE 11
			IF RAND:2
				TALENT:냉정 = 1
			ELSE
				TALENT:감정결핍 = 1
			ENDIF
		CASE 12
			TALENT:부끄럼쟁이 = 1
		CASE 14
			TALENT:헌신 = 1
		CASE 17
			TALENT:호색 = 1
			TALENT:정조관념 = 0
			SIF RAND:3 == 0
				TALENT:정조관결여 = 1
			SIF RAND:3 == 0 || TALENT:ARG:부끄럼없음
				TALENT:부끄럼없음 = 1
			SIF RAND:3 == 0 || TALENT:ARG:소악마
				TALENT:소악마 = 1
		ENDSELECT
	ENDIF
NEXT

;딸は必ず처녀
TALENT:처녀 = 1

;딸固有の소질や, 関連する소질を持つ場合, CFLAG:484へ記憶する
CFLAG:484 = 0
FOR LCOUNT, 2, 10
	SIF TALENT:CALCF("성장後に変化する소질", LCOUNT)
		SETBIT CFLAG:484, LCOUNT
NEXT

;一時的に成長させた親を元に戻す
IF LOCAL
	FOR LCOUNT, 1, 300
		TALENT:ARG:LCOUNT = LOCAL:LCOUNT
	NEXT
	CLEARBIT CFLAG:ARG:484, 1
ENDIF


@DAUGHTER_GROW
PRINTFORML 경험치를 1000사용하여, %조사처리(CALLNAME:TARGET,"를")% 한사람 몫을 하는 \@(TALENT:몽마 || TALENT:상위몽마) ? 몽마 # 인물\@로 성장시킵니다
PRINTFORML %조사처리(CALLNAME:TARGET,"를")% 성장시킵니까? 
PRINTFORML ※성장시키는 경우, 원래대로 되돌리는 것은 기본적으로 불가능합니다!
PRINTL [ 0] 예
PRINTL [ 1] 아니오
CALL INPUT_SELECT,2
IF RESULT == 0
	CALL CALC, "아이の성장", TARGET, CFLAG:484
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	IF AGE(TARGET) <= 2
		PRINT 귀엽게 
	ELSEIF TALENT:남자 && !TALENT:오토코노코
		PRINT 멋지게 
	ELSE
		PRINT 아름답게 
	ENDIF
	PRINTFORML 성장\@(RESULTS:0 != "" || RESULTS:1 != "") ? 하여,  # 한것으로 보입니다…?\@
	SIF RESULTS:1 != ""
		PRINTFORML %조사처리(RESULTS:1,"를")% 잃\@(RESULTS:0 != "") ? 고,  # 었다! \@
	SIF RESULTS:0 != ""
		PRINTFORML %조사처리(RESULTS:0,"를")% 획득했다! 
	PRINTW 
	EXP:경험치 -= 1000
ENDIF

@CHOICE_NEWELEMENT(ARG:0 = 0, ARG:1 = 0)
#FUNCTION
;新たな속성を一つ選ぶ
;ここでは[에로]は出にくい
LOCAL = RAND:23 / 2 + 1
SELECTCASE LOCAL
CASE 8 TO 10
	LOCAL += 2
CASE 11
	LOCAL = 14
CASE 12
	LOCAL = 17
ENDSELECT
SIF D_GOOD_COMBINATION(LOCAL, ARG:0) && D_GOOD_COMBINATION(LOCAL, ARG:1)
	RETURNF LOCAL
RETURNF 0

@D_GOOD_COMBINATION(ARG, ARG:1)
#FUNCTION
;対になっていたりする속성同士の場合, 0を返す. そうでない場合1を返す. 
IF ARG > ARG:1
	SWAP ARG, ARG:1
ELSEIF ARG == ARG:1
	RETURNF 0
ENDIF
SELECTCASE ARG
CASE 1
	SELECTCASE ARG:1
	CASE 5 TO 7
		RETURNF 0
	ENDSELECT
CASE 2
	SELECTCASE ARG:1
	CASE 10, 17
		RETURNF 0
	ENDSELECT
CASE 3
	SELECTCASE ARG:1
	CASE 5 TO 7, 11
		RETURNF 0
	ENDSELECT
CASE 4
	SELECTCASE ARG:1
	CASE 6, 10
		RETURNF 0
	ENDSELECT
CASE 6
	SELECTCASE ARG:1
	CASE 10
		RETURNF 0
	ENDSELECT
CASE 7
	SELECTCASE ARG:1
	CASE 10
		RETURNF 0
	ENDSELECT
CASE 10
	SELECTCASE ARG:1
	CASE 14
		RETURNF 0
	ENDSELECT
CASE 12
	SELECTCASE ARG:1
	CASE 17
		RETURNF 0
	ENDSELECT
CASE 14
	SELECTCASE ARG:1
	CASE 17
		RETURNF 0
	ENDSELECT
ENDSELECT
RETURNF 1


@D_BUST_SET, MOTHER
#DIM LCOUNT
#DIM MOTHER
;가슴 크기の処理
TALENT:빈유 = 0
TALENT:거유 = 0

;현재の가슴＆장래성の決定. 実際の増加이벤트は@CALCの"바스트UP"で行う
IF TALENT:MOTHER:거유
	LOCAL = 10
	TALENT:장래성 = RAND:3
ELSEIF TALENT:MOTHER:빈유
	LOCAL = 4
	SIF RAND:5 == 0
		TALENT:장래성 = 1
ELSE
	LOCAL = 7
	SIF RAND:3 == 0
		TALENT:장래성 = 1
ENDIF
SELECTCASE RAND:LOCAL
CASE IS >= 6
	TALENT:거유 = 1
CASE 3 TO 5
CASE 0 TO 2
	TALENT:빈유 = 1
ENDSELECT


@DAUGHTER_BONUS, MOTHER, FATHER
#DIM MOTHER
#DIM FATHER

;条件					入手する소질	打ち消す소질
;순종5かつ봉사정신5			헌신		반항적
;욕망5					반하기쉬움	일선을넘지않음
;기교5					현명함		미련함
;Ｃ감각5				Ｃ민감		Ｃ둔감
;Ｖ감각5				Ｖ민감		Ｖ둔감
;Ａ감각5				Ａ민감		Ａ둔감
;노출벽5かつ[부끄럼쟁이]なし		눈에띄고싶어함
;마조끼/새드끼のいずれかが5		도착적
;BL/레즈끼のいずれかが5		바이		남성혐오
;자위중독/정액중독のどちらかが5		중독되기쉬움
;Ｂ감각5				Ｂ민감		Ｂ둔감
;조수LV5				고무
;Ｍ감각5				Ｍ민감		Ｍ둔감
;부친が수수께끼의매력				수수께끼의매력

CALL CALC, "랜덤선택"
SIF ABL:MOTHER:순종 == 5 && ABL:MOTHER:봉사정신 == 5 && TALENT:헌신 == 0
	CALL CALC, "랜덤선택", 1
SIF ABL:MOTHER:욕망 == 5 && TALENT:반하기쉬움 == 0
	CALL CALC, "랜덤선택", 2
SIF ABL:MOTHER:기교 >= 5 && TALENT:현명함 == 0
	CALL CALC, "랜덤선택", 3
SIF ABL:MOTHER:Ｃ감각 >= 5 && TALENT:Ｃ민감 == 0
	CALL CALC, "랜덤선택", 4
SIF ABL:MOTHER:Ｖ감각 >= 5 && TALENT:Ｖ민감 == 0
	CALL CALC, "랜덤선택", 5
SIF ABL:MOTHER:Ａ감각 >= 5 && TALENT:Ａ민감 == 0
	CALL CALC, "랜덤선택", 6
SIF ABL:MOTHER:노출벽 == 5 && TALENT:눈에띄고싶어함 == 0 && TALENT:부끄럼쟁이 == 0
	CALL CALC, "랜덤선택", 7
SIF (ABL:MOTHER:마조끼 == 5 || ABL:MOTHER:새드끼 == 5) && TALENT:도착적 == 0
	CALL CALC, "랜덤선택", 8
SIF (ABL:MOTHER:레즈끼 == 5 || ABL:MOTHER:ＢＬ끼 == 5) && TALENT:바이 == 0
	CALL CALC, "랜덤선택", 9
SIF (ABL:MOTHER:자위중독 == 5 || ABL:MOTHER:정액중독 == 5) && TALENT:중독되기쉬움 == 0
	CALL CALC, "랜덤선택", 10
SIF ABL:MOTHER:Ｂ감각 >= 5 && TALENT:Ｂ민감 == 0
	CALL CALC, "랜덤선택", 11
SIF ABL:MOTHER:조수 == 5 && TALENT:고무 == 0
	CALL CALC, "랜덤선택", 12
SIF ABL:MOTHER:Ｍ감각 >= 5 && TALENT:Ｍ민감 == 0
	CALL CALC, "랜덤선택", 13
SIF FATHER > 0 && TALENT:FATHER:수수께끼의매력 && TALENT:수수께끼의매력 == 0
	CALL CALC, "랜덤선택", 14

LOCAL = 0
SELECTCASE RESULT
CASE 1
	LOCAL = 11
CASE 2
	LOCAL = 27
CASE 3
	LOCAL = 51
CASE 4
	LOCAL = 101
CASE 5
	LOCAL = 103
CASE 6
	LOCAL = 105
CASE 7
	TALENT:눈에띄고싶어함 = 1
CASE 8
	TALENT:도착적 = 1
CASE 9
	LOCAL = 82
CASE 10
	TALENT:중독되기쉬움 = 1
CASE 11
	LOCAL = 107
CASE 12
	TALENT:고무 = 1
CASE 13
	LOCAL = 96
CASE 14
	TALENT:수수께끼의매력 = 1
ENDSELECT

SIF LOCAL == 0
	RETURN 0

IF TALENT:LOCAL
	TALENT:LOCAL = 0
ELSE
	TALENT:CALCF("逆の소질", LOCAL) = 1
ENDIF

@BIRTHDAY_GROW
#DIM LCOUNT
;생일に딸が成長するぞ
FOR LCOUNT, 1, CHARANUM
	;보통の人間は2倍かかる
	SIF !(TALENT:LCOUNT:몽마 + TALENT:LCOUNT:상위몽마 + TALENT:LCOUNT:마왕의조각) && (DAY - BASE:LCOUNT:생일) < 240
		CONTINUE
	SIF !COND("성장성", LCOUNT)
		CONTINUE
	SIF DAY - BASE:LCOUNT:생일 < 120
		CONTINUE

	PRINTFORML %조사처리(CALLNAME:LCOUNT,"가")% 독립한 후부터 상당한 시간이 지나, 여러가지를 배운 것 같다
	PRINTFORMW 제 몫을 할 정도로 충분한 경험을 쌓으면, 새로운 성장 가능성이 보이겠지……
	SETBIT CFLAG:LCOUNT:484, 0
NEXT

