;------------------------------------------------------------------------
;호접지몽
;------------------------------------------------------------------------
@KOTYOUNOYUME, ARG
#DIM LCOUNT

PRINTFORML 한권을 %CALLNAME:ARG%\@ ARG == MASTER ? 에게 건네주기 # 가 갖기 \@로합니다. 한사람 더 선택해주세요

VARSET LOCAL
FOR LCOUNT, 1, CHARANUM
	SELECTCASE LCOUNT
	CASE ARG
		CONTINUE
	CASE MASTER
	CASEELSE
		SIF COND("임신이벤트中", LCOUNT) || MARK:LCOUNT:반발각인 || ABL:LCOUNT:순종 < 4
			CONTINUE
	ENDSELECT

	CALL PRINT_NUM_NAME, "체력", LCOUNT
	LOCAL:LCOUNT = 1
NEXT
PRINTL [100] 돌아간다

$INPUT_LOOP
CALL INPUT_SELECT, CHARANUM, 100

SELECTCASE RESULT
CASE 100
	PRINTFORMW 호접지몽의 사용을 취소했습니다
	RETURN 0
CASEELSE
	SIF LOCAL:RESULT == 0
		GOTO INPUT_LOOP
ENDSELECT

LOCAL = RESULT

;ARGの方を캐릭터번호が若い方とする
SIF ARG > LOCAL
	SWAP ARG, LOCAL

IF ARG == MASTER
	PRINTFORML %조사처리(CALLNAME:ARG,"가")% 한권을 갖고, 나머지 한권을 [{LOCAL, 2}]%CALLNAME:LOCAL%에게 줍니다. 
ELSE
	PRINTFORML [{ARG, 2}]%조사처리(CALLNAME:ARG,"와")% [{LOCAL, 2}]%CALLNAME:LOCAL%에게 책을 줍니다. 
ENDIF
PRINTL 
PRINTL 책을 통한 신체 교환기간을 어느정도로 합니까?
PRINTL  [ 0] １일
PRINTL  [ 1] ２일
PRINTL  [ 2] ３일
IF ARG == MASTER
	PRINTL  [ 3] 꿈을 끝내고 싶어질 때까지
ELSE
	PRINTL  [ 3] 꿈을 끝낼 때까지
ENDIF
PRINTL [100] 역시 그만둔다
CALL INPUT_SELECT, 4, 100


SELECTCASE RESULT
CASE 100
	PRINTFORMW 호접지몽의 사용을 취소했습니다
	RETURN 0
CASE 3
	ITEM:호접지몽 = 100
	PRINTFORML 선택된 교환 기간：꿈으로부터 깰 때까지…
CASEELSE
	ITEM:호접지몽 = RESULT + 1
	PRINTFORML 선택된 교환 기간：{ITEM:호접지몽}일
ENDSELECT

PRINTL 
PRINTFORMW 두 사람은 갑작스레 졸음에 빠졌다…

CALL BODYSWAP, ARG, LOCAL


;------------------------------------------------------------------------
;一일の終わりに호접지몽の플래그체크
;ARGSに"夢の終わり"と入れることで突然終わりにもできる
;------------------------------------------------------------------------
@EVENT_KOTYOUNOYUME, ARGS
#DIM LCOUNT

SELECTCASE ITEM:호접지몽
CASE 0
	RETURN 0
CASE 100
	SIF ARGS != "夢の終わり"
		RETURN 0
;날짜変更時に1づつ減らす
CASEELSE
	ITEM:호접지몽 -= 1
ENDSELECT

SIF ARGS == "夢の終わり"
	ITEM:호접지몽 = 0

;効果終了
IF ITEM:호접지몽 == 0
	VARSET LOCAL
	FOR LCOUNT, 1, CHARANUM
		SIF COND("호접지몽", LCOUNT) <= 0
			CONTINUE

		IF LOCAL == 0
			LOCAL = LCOUNT
		ELSEIF LOCAL:1 == 0
			LOCAL:1 = LCOUNT
			BREAK
		ENDIF
	NEXT
	CALL BODYSWAP, LOCAL, LOCAL:1
ENDIF


;------------------------------------------------------------------------
;ARGとARG:1の신체를 入れ替える
;------------------------------------------------------------------------
@BODYSWAP, ARG, ARG:1
#DIMS NAME_ARG, 2

;소질を入れ換える
FOR LOCAL, 0, 600
	;精神系・技能系소질は除外（見づらいので分割）
	SELECTCASE LOCAL
	CASE 10 TO 41, 50 TO 53, 55, 58 TO 64, 70 TO 72, 75, 76, 80 TO 88, 91 TO 92
		CONTINUE
	CASE 117 TO 118, 123, 125, 126, 131, 132, 151, 157, 180 TO 194, 210, 211
		CONTINUE
	ENDSELECT
	SWAP TALENT:ARG:LOCAL, TALENT:(ARG:1):LOCAL
NEXT

;経験を入れ換える（71まで見る）（保留. 놀림たい人は90～97行頭のセミコロンを削除してください）
FOR LOCAL, 0, 99
	;精神系・技能系소질は除外（見づらいので分割）
	SELECTCASE LOCAL
	CASE 2 TO 7, 10, 14, 17 TO 51, 54 TO 56, 58 TO 61, 70 TO 80
		CONTINUE
	ENDSELECT
	SWAP EXP:ARG:LOCAL, EXP:(ARG:1):LOCAL
NEXT

;정액각인を入れ替える
SWAP MARK:ARG:정액각인, MARK:(ARG:1):정액각인

;能力を入れ換える
SWAP ABL:ARG:Ｃ감각, ABL:(ARG:1):Ｃ감각
SWAP ABL:ARG:Ｖ감각, ABL:(ARG:1):Ｖ감각
SWAP ABL:ARG:Ａ감각, ABL:(ARG:1):Ａ감각
SWAP ABL:ARG:Ｂ감각, ABL:(ARG:1):Ｂ감각
SWAP ABL:ARG:Ｍ감각, ABL:(ARG:1):Ｍ감각

;CFLAGを入れ換える
;성전환時の소질플래그
SWAP CFLAG:ARG:4, CFLAG:(ARG:1):4
;종족に於けるランク識別플래그
SWAP CFLAG:ARG:8, CFLAG:(ARG:1):8
;실금癖치료플래그
SWAP CFLAG:ARG:34, CFLAG:(ARG:1):34

;딸の成長플래그関連
;SWAP CFLAG:ARG:484, CFLAG:(ARG:1):484
;SWAP CFLAG:ARG:485, CFLAG:(ARG:1):485

;갈아입히기の현재の옷の状態管理など
FOR LOCAL, 40, 60
	SWAP EQUIP:ARG:LOCAL, EQUIP:(ARG:1):LOCAL
	SWAP CSTR:ARG:LOCAL, CSTR:(ARG:1):LOCAL
NEXT

;체력や身体のサイズを交換
SWAP BASE:ARG:체력, BASE:(ARG:1):체력
FOR LOCAL, 20, 50
	SWAP BASE:ARG:LOCAL, BASE:(ARG:1):LOCAL
NEXT

;得ている経験の플래그を消す
CALL RESET_EXP_GAIN, ARG
CALL RESET_EXP_GAIN, ARG:1

;ややこしい呼び名の설정
NAME_ARG = %CALLNAME:ARG%
NAME_ARG:1 = %CALLNAME:(ARG:1)%
IF NAME:ARG == NAME:(ARG:1)
	IF NAME:ARG == "당신"
		NAME_ARG = 당신
		NAME_ARG:1 = 이세계의당신
	ELSE
		NAME_ARG = [{ARG, 2}]%CALLNAME:ARG%
		NAME_ARG:1 = [ARG:1, 2]%CALLNAME:(ARG:1)%
	ENDIF
ENDIF

;호접지몽発動
IF ITEM:호접지몽
	IF ARG == MASTER
		PRINTFORML …
		PRINTFORML …
		PRINTFORMW …
		PRINTFORMW 꿈 속에서 %조사처리(NAME_ARG,"는")% %조사처리(NAME_ARG:1,"가")% 되어있었다. 
		PRINTFORMW %조사처리(NAME_ARG:1,"가")% 된 %조사처리(NAME_ARG,"는")%, %조사처리(NAME_ARG,"과")% 함께 있었다. 
		PRINTFORML 과연, %조사처리(NAME_ARG,"가")% %NAME_ARG:1%의 꿈을 꾸고있는 것인지, 
		PRINTFORMW 자신은 사실 %조사처리(NAME_ARG:1,"이고")%, 이것이 현실인지…
		PRINTFORMW 그런 것을 생각하고 있는 사이에, 의식이 멀어져간다…
		PRINTFORML …
		PRINTFORML …
		PRINTFORMW …
		PRINTFORMW %조사처리(NAME_ARG,"는")% 눈을 떴습니다. 
		PRINTFORMW …？ 뭔가 이상하다…
		PRINTFORMW 갑자기, 문이 열렸다. 거기에는… "%NAME_ARG%"%조사만처리(NAME_ARG,"가")% 서있었다! 
		PRINTFORMW 그 "%NAME_ARG%"%조사만처리(NAME_ARG,"는")%, 「%조사처리(CNAMET(ARG:1, "一人称"),"는")% %조사처리(NAME_ARG:1,"이다")%」라고 말하고있다. 
		PRINTFORMW 자신의 모습을 확인하자, 놀랍게도 %NAME_ARG:1%의 모습이 되어있었다! 
	ELSE
		PRINTFORML …
		PRINTFORML …
		PRINTFORMW …
		PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% 눈을 떴습니다. 
		PRINTFORMW …？ 뭔가 떠들썩하다…
		PRINTFORMW 상황을 보러가자, %조사처리(NAME_ARG,"과")% %조사처리(NAME_ARG:1,"가")% 무언가 이야기를 나누고있다. 
		PRINTFORML 무슨 일이 일어난건지 물어보자, %조사처리(NAME_ARG,"는")% 자신이 %조사처리(NAME_ARG:1,"이라")%고 말한다, 
		PRINTFORMW 한쪽의 %조사처리(NAME_ARG:1,"는")%, 자신이 %조사처리(NAME_ARG,"이라")%고 말하고있다. 
		PRINTFORMW 아무래도, 어제의 책이 이 소동을 일으킨 모양이다…
	ENDIF
	CALL SETFLAG, "호접지몽플래그", ARG
	CALL SETFLAG, "호접지몽플래그", ARG:1
	BASE:ARG:호접지몽 = BASE:(ARG:1):식별번호
	BASE:(ARG:1):호접지몽 = BASE:ARG:식별번호
ELSE
	PRINTFORML …
	PRINTFORML …
	PRINTFORMW …
	PRINTFORMW %NAME_ARG%는 눈을 떴다.
	PRINTFORMW 당황해 자신의 모습을 확인하면… 익숙해진 분명한 원래의 모습이다.
	PRINTFORMW 하지만, 이것이 정말로 자신의 모습이 맞을까？
	PRINTFORMW 잠에 취한 눈을 비비는 %NAME_ARG%의 뇌리에 떠오르는 모습은…%NAME_ARG:1%의 것이었다.

	BASE:ARG:호접지몽 = 0
	BASE:(ARG:1):호접지몽 = 0
ENDIF

PRINTL 
SETCOLOR DEF_COLOR("노랑")
PRINTFORMW [{ARG, 2}]%조사처리(CALLNAME:ARG,"과")% [{ARG:1, 2}]%CALLNAME:(ARG:1)%의 몸이 \@ ITEM:호접지몽 ? 뒤바뀌었다!  # 원래대로 돌아왔다\@
RESETCOLOR

CALL EXP50_CHECK, ARG
CALL EXP50_CHECK, ARG:1

