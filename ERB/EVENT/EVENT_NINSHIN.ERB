;---------------------------------------------------------
;임신이벤트カウント処理
;---------------------------------------------------------
@EVENT_N1, ARG
LOCAL = TARGET
TARGET = ARG

CALL CHECK_NINSHIN, TARGET, "システム処理"

;임신이벤트カウント処理
IF COND("밤") && CONFIG("임신패치")
	;お腹の中の태아の成長
	IF COND("임신中")
		;すくすく育ってます
		BASE:태아 += 1
		;人でないなら進行率がＵＰ(내사정によって速度アップ)
		SIF TALENT:몽마 + TALENT:상위몽마 + TALENT:마왕의조각 && RAND:(3 + BASE:태아의영양/10)
			BASE:태아 += 1

		;分娩室待ち
		IF COND("임신中：分娩直前")
			CALL SETFLAG, "임신中：分娩", TARGET
		;臨月～出産일(人なら三일ほど)
		ELSEIF COND("임신中：臨月")
			;出産間近이벤트(子育て中には発生しない)
			IF COND("임신中：各이벤트進行度") >= 3
				IF BASE:육아중
					BASE:태아 -= 1 + RAND:2
				ELSE
					CALL SETFLAG, "임신中：分娩直前", TARGET
				ENDIF
			ENDIF
		;임신発覚終了～臨月まで(人なら10일ほど)
		ELSEIF COND("임신中：임신発覚済み")
			;臨月이벤트
			SIF COND("임신中：各이벤트進行度") >= 11
				CALL SETFLAG, "임신中：臨月", TARGET
		;임신発覚이벤트待ち
		ELSEIF COND("임신中：임신発覚直前")
		;임신潜伏期間～임신発覚이벤트(人なら三일ほど)
		ELSEIF COND("임신中：各이벤트進行度") >= 4
			CALL SETFLAG, "임신中：임신発覚直前", TARGET
		ENDIF
	ENDIF
	;子育て. 人で２週間, 몽마たちなら１週間程度. 
	IF BASE:육아중
		BASE:육아중 += 1
		;모친が소질【몽마, 상위몽마, エルダー서큐버스, 魔王の欠片】持ち
		SIF TALENT:몽마 + TALENT:상위몽마 + TALENT:마왕의조각
			BASE:육아중 += 1
		;子育て終了
		SIF COND("육아중：아이が大きくなった")
			CALL SETFLAG, "육아중：育児종료", TARGET
	ENDIF
ENDIF

TARGET = LOCAL

;---------------------------------------------------------
;임신判定処理
;ARGSには"システム処理"が入る。これ以外の場合は시간経過によるシステム処理以外での임신判定となる
;CHECK_NINSHIN_ALLACTORは全部のPLAYER:0～1とTARGET:0～3について行う
;---------------------------------------------------------
@CHECK_NINSHIN_ALLACTOR, ARGS
#DIM LCOUNT

FOR LCOUNT, 1, CHARANUM
	SELECTCASE LCOUNT
	CASE PLAYER, PLAYER:1, TARGET, TARGET:1, TARGET:2, TARGET:3
		CALL CHECK_NINSHIN, LCOUNT, ARGS
	ENDSELECT
NEXT

@CHECK_NINSHIN, ARG, ARGS
#DIM LCOUNT
#DIM MEMO_LINECOUNT

SIF ARG < 0
	RETURN 0
SIF ARG == 0 && TARGET
	ARG = TARGET

;임신判定があって男なら, ここで消去. 
IF TALENT:ARG:남자
	BASE:ARG:태내정액량 = 0
	BASE:ARG:자궁정자량 = 0
ENDIF

SELECTCASE ARGS
CASE "システム処理"
	;PRINTFORM [{ARG, 2}]%TEXT_LJ(CALLNAME:ARG, 16)% {DAY - CFLAG:ARG:499, 3}日前, 임신率：{COND("受精확률"), 3}％, {COND("태内の정액량", ARG), 6}/{BASE:ARG:자궁정자량, 4}→

	;정액각인持ちは체크をここ以外の場所でやっているので、精子量の減少を체크前に行う
	SIF MARK:ARG:정액각인
		CALL CHECK_NINSHIN_SPERM, ARG

	;피임약使用
	IF TALENT:ARG:피임약복용 > 0 && TALENT:ARG:임신 == 0
		IF COND("밤")
			TALENT:ARG:피임약복용 -= 1
			SIF TALENT:ARG:피임약복용 == 0
				PRINTFORMW %CALLNAME:ARG%하지만피임약를 다 사용했습니다.
		ENDIF
	;임신判定(出産後すぐに임신できるようにしてみる)
	ELSEIF BASE:ARG:자궁정자량 && BASE:ARG:태아 == 0 && COND("임신가능", ARG)
		IF COND("배란中", ARG) || RAND:10 == 0 || (TALENT:ARG:슬라임 && RAND:3 == 0)
			IF RAND:100 < COND("受精확률", ARG)
				BASE:ARG:태아 = 1
				;20%で아들（産まれる時に選べるようにもしてあるので実はなくても良い）
				SIF RAND:5 == 0
					CALL SETFLAG, "임신中：아들", ARG
			ENDIF
		ENDIF
	ENDIF
	SIF MARK:ARG:정액각인 == 0
		CALL CHECK_NINSHIN_SPERM, ARG
CASEELSE
	;受精확률が変動しているかを체크
	SIF COND("受精확률", ARG) == TCVAR:ARG:110
		RETURN 0

	;受精확률の変動を表示
	IF CONFIG("임신패치：受精확률표시")
	    PRINTFORM %CALLNAME:ARG%의 태내정액량：{BASE:ARG:태내정액량}pt    
	    PRINTFORML 자궁정자량：{BASE:ARG:자궁정자량}pt
		LOCALS = %CALLNAME:ARG%の受精확률：_

		;最終확률
		SELECTCASE COND("受精확률", ARG)
		CASE IS >= 100
			LOCALS = %LOCALS%하트핑크_임신確実！_
		CASE IS > 50
			LOCALS = %LOCALS%하트핑크_{COND("受精확률", ARG)}％_
		CASE IS > 20
			LOCALS = %LOCALS%황색_{COND("受精확률", ARG)}％_
		CASEELSE
			LOCALS = %LOCALS%{COND("受精확률", ARG)}％_
		ENDSELECT

		;伸び幅
		SELECTCASE COND("受精확률", ARG) - TCVAR:ARG:110
		CASE IS >= 15
			LOCALS = %LOCALS%핑크_（↑{COND("受精확률", ARG) - TCVAR:ARG:110}％）_
		CASE IS >= 10
			LOCALS = %LOCALS%하트핑크_（↑{COND("受精확률", ARG) - TCVAR:ARG:110}％）_
		CASE IS >= 5
			LOCALS = %LOCALS%황색_（↑{COND("受精확률", ARG) - TCVAR:ARG:110}％）_
		CASE IS > 0
			LOCALS = %LOCALS%（↑{COND("受精확률", ARG) - TCVAR:ARG:110}％）_
		CASE IS < 0
			LOCALS = %LOCALS%파랑_（↓{TCVAR:ARG:110 - COND("受精확률", ARG)}％）_
		ENDSELECT

		CALL PRINT_STR, LOCALS

		IF COND("受精확률", ARG) >= 100
			FORCEWAIT
		ELSE
			WAIT
		ENDIF
		PRINTL 
	ENDIF

	;정액각인持ちはここで임신判定も行ってしまう
	IF MARK:ARG:정액각인 && (COND("受精확률", ARG) >= 100 || RAND:100 < COND("受精확률", ARG) - TCVAR:ARG:110)
		;おめでた
		BASE:ARG:태아 = 1
		;20%で아들（産まれる時に選べるようにもしてあるので実はなくても良い）
		SIF RAND:5 == 0
			CALL SETFLAG, "임신中：아들", ARG

		;피임약は返してもらう
		MONEY += TALENT:ARG:피임약복용*150
		TALENT:ARG:피임약복용 = 0

		CALL SETFLAG, "受精확인플래그", ARG

		PRINTFORMW %CALLNAME:ARG%노정액각인의 모습이 이상하다……어지럽게 색과 형태가 변해간다！
		SETCOLOR DEF_COLOR("하트핑크")
		CALL HEARTMARK
		PRINTFORM %CALLNAME:ARG%하%CONDS("정액의우세", ARG)%의 정자로 수정했습니다
		CALL HEARTMARK
		RESETCOLOR

		FORCEWAIT
		PRINTL 

		;セリフがあるかの判定
		MEMO_LINECOUNT = LINECOUNT

		TRYCALLFORM GUEST_KOJO_K{NO:ARG}_{CFLAG:ARG:209}, "受精확인", ARG

		SIF LINECOUNT > MEMO_LINECOUNT
			PRINTL 
	ENDIF

	TCVAR:ARG:110 = COND("受精확률", ARG)

ENDSELECT

;---------------------------------------------------------
;胎内の정액량＆精子量を減少させる. だいたい３일程で死滅. 
;ちなみにCFLAG:499は最新の내사정일
;---------------------------------------------------------
@CHECK_NINSHIN_SPERM, ARG

VARSET LOCAL

SELECTCASE DAY - CFLAG:ARG:499
;出した直後
CASE 0
	LOCAL = 150 + TALENT:ARG:흡정체질*50 + MARK:ARG:정액각인*20 + 10*RAND:6
	LOCAL:1 = 120 + TALENT:ARG:흡정체질*10 + MARK:ARG:정액각인*5 + 10*RAND:6
CASE 1
	LOCAL = 200 + TALENT:ARG:흡정체질*50 + MARK:ARG:정액각인*20 + 10*RAND:11
	LOCAL:1 = 150 + TALENT:ARG:흡정체질*10 + MARK:ARG:정액각인*50 + 10*RAND:11
CASE 2
	IF COND("낮")
		LOCAL = 300 + TALENT:ARG:흡정체질*50 + MARK:ARG:정액각인*20 + 10*RAND:11
		LOCAL:1 = 200 + TALENT:ARG:흡정체질*10 + MARK:ARG:정액각인*5 + 10*RAND:11
	ENDIF
ENDSELECT
IF LOCAL || LOCAL:1
	BASE:ARG:자궁정자량 = DIVIDE(BASE:ARG:자궁정자량, LOCAL)
	BASE:ARG:태내정액량 = DIVIDE(BASE:ARG:태내정액량, LOCAL:1)
ELSE
	BASE:ARG:자궁정자량 = 0
	BASE:ARG:태내정액량 = 0
ENDIF

;PRINTFORML %CALLNAME:ARG, 8, LEFT% : {COND("태内の정액량", ARG), 6}/{BASE:ARG:자궁정자량, 4}, %CALLNAME:COND("정액의우세", ARG)%\@ BASE:ARG:태아 ? , 受精!  # \@

;精子が死滅したら부친플래그は消滅
SIF BASE:ARG:자궁정자량 == 0 && BASE:ARG:태아 == 0
	BASE:ARG:정액의우세 = 0

;---------------------------------------------------------
;임신発覚＆出産予定報告＆子の親離れ処理
;---------------------------------------------------------
@EVENT_N2, ARG
#DIM MEMO_TARGET
MEMO_TARGET = TARGET

TARGET = ARG

;子育て中
IF BASE:육아중
	;子の親離れ
	IF COND("육아중：育児종료")
		DRAWLINE
		;몽마以外の場合も考えないとなぁ…
		PRINTFORM %조사처리(CALLNAME:TARGET,"가")% 기르고 있던 아이는 
		IF TALENT:몽마 || TALENT:상위몽마 || TALENT:마왕의조각
			PRINTFORM 인간으로서는 있을 수 없는 속도로 
		ELSE
			PRINTFORM 저택에 충만한 힘탓인지, 상당히 빨리 
		ENDIF
		PRINTFORMW 자라났습니다. 
		PRINTFORMW 이젠 돌봐주지않아도 괜찮을 것같습니다. 

		CALL SELF_KOJO, "子離れ"

		;아이대상化
		CALL DAUGHTER_CHECK, "通常"

		BASE:육아중 = 0
	ENDIF
ENDIF

;出産予定報告(子育て中には産まれない)
IF COND("임신中：分娩直前") && TALENT:임신 == 2 && BASE:육아중 == 0
	DRAWLINE
	PRINTFORMW %CALLNAME:TARGET%의 뱃속에는 태아가 활발하게 움직이고 있는 것같습니다. 
	PRINTFORMW 내일이라도 태어날 것같습니다…
	TALENT:임신 = 3
ELSEIF COND("임신中：臨月") && TALENT:임신 == 1
	DRAWLINE
	PRINTFORMW %CALLNAME:TARGET%의 배는 눈에 띄게 커지고있습니다. 
	PRINTFORMW 앞으로 며칠있다가 태어날 것같습니다…
	TALENT:임신 = 2
;子育て中以外の임신発覚
ELSEIF COND("임신中：임신発覚直前") && TALENT:임신 == 0
	TALENT:임신 = 1
	;임신確定플래그に変更
	CALL SETFLAG, "임신中：임신発覚済み", TARGET

	CALL EVENT_N2_TEXT

	;슬라임が分裂した場合
	IF TALENT:슬라임 && CONFIG("임신패치：슬라임分裂")
		;이하は通常の임신時の動作の一部の抜出し
		;임신＆태아플래그終了
		TALENT:임신 = 0
		BASE:태아 = 0
		CFLAG:469 += 1
		;CALL SELF_KOJO, "출산"
		;아이の数+1
		TALENT:출산경험 += 1
		;부친플래그の確定
		BASE:태아의부친 = BASE:정액의우세
		BASE:정액의우세 = 0
		;CALL SELF_KOJO, "子離れ"
		;아이대상化
		CALL DAUGHTER_CHECK, "슬라임"
	ELSEIF COND("임신中") && CFLAG:468 == 0
		PRINTL 
		PRINTFORMW 임신에 따라서, %CALLNAME:TARGET%의 가슴이 커지고있습니다. 
		;소질変更など
		IF TALENT:모유체질 == 0
			TALENT:모유체질 = 2
			CFLAG:468 |= 1p2
		ENDIF
		;가슴 크기
		IF TALENT:빈유
			CFLAG:468 |= 1p0
			;２階級特進
			IF RAND:3 == 0
				PRINTFORMW 이런 일도 있는 건지 불가사의하지만, \@ TALENT:작은몸집 ? %CALLNAME:MASTER%의 예상을 넘어서 # \@ 출렁출렁하게 될 것같다…
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% \@ GETBIT(CFLAG:468, 2) ? [모유체질]과 # \@[거유]를 얻었습니다. 
				CALL CALC, "거유化", TARGET
			ELSE
				PRINTFORMW 겉보기에, 보통크기정도는 된 것같다…
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% \@ GETBIT(CFLAG:468, 2) ? [모유체질]을 얻고,  # \@[빈유]를 잃었습니다. 
				CALL CALC, "평유化", TARGET
			ENDIF
			PRINTFORMW 
		ELSEIF TALENT:빈유 == 0 && TALENT:거유 == 0
			CFLAG:468 |= 1p1
			PRINTFORMW 아무래도, 적당한 크기가 된 것같다…
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% \@ GETBIT(CFLAG:468, 2) ? [모유체질]과 # \@[거유]를 얻었습니다. 
			CALL CALC, "거유化", TARGET
		ELSEIF GETBIT(CFLAG:468, 2)
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% [모유체질]을 얻었습니다. 
		ENDIF
	ENDIF
ENDIF

;임신に付随する모유체질や거유の消去
IF CFLAG:468 && COND("임신中") == 0 && BASE:육아중 == 0
	PRINTL 
	PRINTFORMW （역할을 끝낸 %CALLNAME:TARGET%의 가슴은 원래 크기로 돌아왔습니다）
	SIF GETBIT(CFLAG:468, 0)
		CALL CALC, "빈유化", TARGET
	SIF GETBIT(CFLAG:468, 1)
		CALL CALC, "평유化", TARGET
	SIF GETBIT(CFLAG:468, 2)
		TALENT:모유체질 = 0

	CFLAG:468 = 0
ENDIF

TARGET = MEMO_TARGET

;---------------------------------------------------------
;임신発覚이벤트
;CALLNAME:(COND("정액의우세") ) とかなると見苦しいので, FATHER = COND("정액의우세")とする
;---------------------------------------------------------
@EVENT_N2_TEXT
#DIM FATHER

FATHER = COND("정액의우세")

DRAWLINE
;슬라임分裂! 
IF TALENT:슬라임 && CONFIG("임신패치：슬라임分裂")
	IF TARGET == MASTER
		PRINTFORMW 갑자기 %조사처리(CALLNAME:MASTER,"는")% 온몸에 고통을 느끼고, 신체의 형태를 유지할 수 없게되었다
		PRINTFORMW 점액상태로 돌아가도 %조사처리(CALLNAME:MASTER,"는")% 한동안 계속 고통에 몸부림쳤다…
		PRINTFORMW ………
		PRINTFORMW ……
		PRINTFORMW …
		PRINTFORMW 
		PRINTFORM 당분간 지나 근처를 바라보면%CALLNAME:MASTER%
		PRINTFORM \@ !TALENT:MASTER:작은몸집 ? の소녀時代 # \@에 잘 닮은, 그러나
	ELSE
		PRINTFORMW 돌연%조사처리(CALLNAME:TARGET,"가")% 움찔 떨자, 신체가점액으로 돌아가버렸다
		PRINTFORMW %조사처리(CALLNAME:TARGET,"였")%던 점액은 부들부들거리면서 부피를 늘려갔다…
		PRINTFORMW ………
		PRINTFORMW ……
		PRINTFORMW …
		PRINTFORMW 
		PRINTFORMW 부들부들 거리던 것이 진정되자 점액은 두명의 소녀의 모습이 되었다. 
		PRINTFORML 평소같은 분열일까 생각했지만, 한쪽의 소녀는 다른 한쪽의 %CALLNAME:TARGET%보다 어리고, 
	ENDIF

	SELECTCASE FATHER
	;부친は캐릭터의誰か
	CASE IS > 0
		PRINTFORM 얼굴인화성에%CALLNAME:FATHER%의 모습이 있다
	;부친は드래곤
	CASE -3
		PRINTFORM 몸 안에 감춰진 강력함이 느껴지는 
	;부친は촉수
	CASE -5
		PRINTFORM 몸 안에서 불길한 마력이 느껴지는 
	;부친は行きずりの男
	CASEELSE
		PRINTFORM 생김새가 %조사처리(CALLNAME:MASTER,"과")% 조금 차이가 있는 
	ENDSELECT
	IF TARGET == MASTER
		PRINTL 
		PRINTFORMW 슬라임 소녀가 %조사처리(CALLNAME:MASTER,"를")% 지긋이 쳐다보고있다…
	ELSE
		PRINTFORMW 같은 생각이 든다…
	ENDIF

	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% ……
	SELECTCASE FATHER
	;부친は캐릭터의誰か
	CASE IS > 0
		PRINTFORM %CALLNAME:FATHER%
	;부친は드래곤
	CASE -3
		PRINT 놀랍게도 드래곤
	;부친は촉수
	CASE -5
		PRINT 놀랍게도 촉수
	;부친は行きずりの男
	CASEELSE
		PRINT 누군가
	ENDSELECT
	PRINTFORML 노정액를자궁에 받아 분열해 버린 것 같다！

	FORCEWAIT

	CALL SELF_KOJO, "임신発覚"
ELSE
	IF TARGET == MASTER
		PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% 어쩐지 몸상태가 좋지 않아서, 조금 주의깊게 검사를 해보았습니다…
		PRINTFORMW ………
		PRINTFORMW ……
		PRINTFORMW …
		PRINTFORMW 검사해본 결과, \@ BASE:육아중 ? 육아중임에도 불구하고 # 아무래도 \@ 
		SELECTCASE FATHER
		CASE IS > 0
			PRINTFORM %CALLNAME:FATHER%의 
		CASE -3
			PRINTFORM 놀랍게도 드래곤의 
		CASE -5
			PRINTFORM 놀랍게도 촉수의 
		ENDSELECT
		PRINTFORML 아이를임신해 버린 것 같습니다.
	;子育て中
	ELSEIF BASE:육아중
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 몸상태의 변화를 호소하고있다…
		PRINTFORML 조사한 결과,%타겟은()% 육아의 도중에 다시임신한 것 같다.
	;연모
	ELSEIF TALENT:연모
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 임신했다고 호소해왔다…
		PRINTFORML 조사한 결과, 확실히%타겟은()% 임신한 것 같다.
	;その他
	ELSE
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 몸상태의 변화를 호소하고있다…
		PRINTFORML 조사한 결과,%타겟은()% 임신한 것 같다.
	ENDIF

	FORCEWAIT

	;피임약は回収
	IF TALENT:피임약복용 > 0
		PRINTFORML …피임약를 마시는 것이 늦었던 것 같다. 
		PRINTFORML 어쩔 수 없기 때문에, 남은 약은 환금합니다. 
		PRINTFORMW 소지금＋%MONEY_C(TALENT:피임약복용*150)%
		MONEY += TALENT:피임약복용*150
		TALENT:피임약복용 = 0
	ENDIF

	CALL SELF_KOJO, "임신発覚"

	;MASTER以外はリアクション
	IF TARGET == MASTER
	;부친は드래곤
	ELSEIF FATHER == -3
		PRINTFORML 뱃속의 아이의 부친이 드래곤이라는 것을 알자, 
		PRINTFORMW 예상외의 사건에 %조사처리(CALLNAME:TARGET,"는")% 놀라움을 넘어 망연해지고말았다…
	;부친は촉수! 
	ELSEIF FATHER == -5
		PRINTFORML 뱃속의 아이의 부친이 이형의 촉수이라는 것을 알자, 
		IF TALENT:연모
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %조사처리(CALLNAME:MASTER,"이")% 아닌 사람의 아이를 임신한 것에 유감스러운 얼굴을 했다…
		ELSEIF TALENT:구지배자의자취
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 뭐라고 말할 수 없는 얼굴하고있다…
		;겁쟁이or大人しいor悲観的
		ELSEIF TALENT:겁쟁이 || TALENT:내성적 || TALENT:음기
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 치솟아 오르는 것을 억누르지 못하고 격렬하게 구토를 했다…
		;억압or저항or一線or보수적or꿋꿋함
		ELSEIF TALENT:억압 || TALENT:저항 || TALENT:일선을넘지않음 || TALENT:보수적 || TALENT:꿋꿋함
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 망연해하면서 부푼 배를 쳐다보았다…
		ELSE
			PRINTFORMW 지독한 사실에 %조사처리(CALLNAME:TARGET,"는")% 털썩 무릎을 꿇고, 망연해하고말았다…
		ENDIF
	;연모
	ELSEIF TALENT:연모
		;부친はMASTER
		IF FATHER == MASTER
			;반항적orプライド高いor강기
			IF TALENT:반항적 || TALENT:교만 || TALENT:강기
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 부푼 배를 보이고, 쓰다듬어달라고 부탁해왔다…
			;솔직or楽観的
			ELSEIF TALENT:솔직 || TALENT:양기
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:MASTER%에게 미소지으면서, 부푼 배를 쓰다듬고있다…
			ELSE
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 부푼 배를 쓰다듬으면서, 기뻐하고있다…
			ENDIF
		ELSE
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %조사처리(CALLNAME:MASTER,"가")% 아닌 사람의 아이를 가진 것이 유감인 모양입니다…
		ENDIF
	;子育て中
	ELSEIF BASE:육아중
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 뭐라고 말할 수 없는 얼굴을 하고있다…
	;반발각인Lv3or반항적
	ELSEIF MARK:반발각인 == 3 || TALENT:반항적
		;출산경험あり
		IF TALENT:출산경험
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 뭔가를 포기한 미소를 지으면서, 부푼 배를 쓰다듬고있다…
		;출산경험なし
		;반항적orプライド高いor강기
		ELSEIF TALENT:반항적 || TALENT:교만 || TALENT:강기
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 부푼 배를 쓰다듬고있지만, %조사처리(CALLNAME:MASTER,"가")% 가까이 다가가자 마구 노려보았다…
		;겁쟁이or大人しいor悲観的
		ELSEIF TALENT:겁쟁이 || TALENT:내성적 || TALENT:음기
			PRINTFORML 그렇게 전해들은 %조사처리(CALLNAME:TARGET,"는")% 새파래져서, 입을 억눌렀지만 곧바로 구토를 시작해, 
			PRINTFORMW 토사물을 바닥에 쏟았다…
		;억압or저항or一線or보수적or꿋꿋함
		ELSEIF TALENT:억압 || TALENT:저항 || TALENT:일선을넘지않음 || TALENT:보수적 || TALENT:꿋꿋함
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 부푼 배를 텅빈 눈으로 쳐다보고있다…
		ELSE
			PRINTFORMW 그렇게 전해들은 %조사처리(CALLNAME:TARGET,"는")% 오열하기 시작했다…
		ENDIF
	;굴복각인Lv2or순종4LV
	ELSEIF MARK:굴복각인 >= 2 || ABL:순종 >= 4
		;반항적orプライド高いor강기
		IF TALENT:반항적 || TALENT:교만 || TALENT:강기
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 복잡한 얼굴을 하면서, 부푼 배를 쓰다듬고있따…
		;겁쟁이or大人しいor悲観的
		ELSEIF TALENT:겁쟁이 || TALENT:내성적 || TALENT:음기
			PRINTFORMW 그렇게 전해들은 %조사처리(CALLNAME:TARGET,"는")% 불안한 듯이, %CALLNAME:MASTER%의 손을 살며시 붙잡았다…
		ELSE
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 애정이 담긴 손놀림으로 부푼 배를 어루만디고있다…
		ENDIF
	;その他
	ELSEIF FATHER > 0
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %조사처리(CALLNAME:FATHER,"과")%의 사이에서 아이가 생긴 것에 놀라워하고있다…
	ELSE
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 아이가 생긴 것에 놀라워하고있다…
	ENDIF
ENDIF

;望まぬ임신の場合は이상경험Get
CALL EXP50_CHECK

;---------------------------------------------------------
;その他の임신이벤트処理
;CALLNAME:(COND("정액의우세") ) とかなると見苦しいので, FATHER に代入する
;---------------------------------------------------------
@EVENT_N3, ARG
#DIM FATHER

LOCAL = TARGET
TARGET = ARG

;お産(子育て中には出産しない)
IF COND("임신中：分娩") && TALENT:임신 && BASE:육아중 == 0
	FATHER = COND("정액의우세")
	DRAWLINE
	IF TARGET == MASTER
		PRINTFORM %조사처리(CALLNAME:MASTER,"는")% 
		SELECTCASE FATHER
		CASE IS > 0
			SIF TALENT:FATHER:연모
				PRINTFORM %CALLNAME:FATHER%
			SIF TALENT:출산경험
				LASTWORD = \@ TALENT:FATHER:연모 ? %조사만처리(CALLNAME:FATHER,"와")%  # \@아이
		ENDSELECT
		SIF CFLAG:469/100 + CFLAG:469 % 100 >= 2
			LASTWORD = %LASTWORD%들
		SIF FATHER > 0 || TALENT:출산경험
			PRINT %LASTWORD를()% 보살피던 중에
		PRINT , 
	ELSE
		PRINTFORMW %조사처리(CALLNAME:TARGET,"가")% 산기가 도는 것같습니다. 
		PRINTFORMW ………
		PRINTFORMW ……
		PRINTFORMW …
		PRINTFORMW 
		PRINTFORM %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:MASTER%
		SIF TALENT:출산경험
			PRINTFORM %조사만처리(CALLNAME:MASTER,"과")% 아이
		SIF CFLAG:469/100 + CFLAG:469 % 100 >= 2
			PRINT 들
		PRINTFORM 에게 격려받으면서, 
	ENDIF
	PRINTFORMW 무사하게 출산했습니다.

	;試しに子供の性別選ばせてみるぜ
	PRINTL 
	PRINTFORML 태어난 아이는……
	PRINTFORML [ 0] 여자 아이
	PRINTFORML [ 1] 오토코노코
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		CLEARBIT BASE:태아, 20
	ELSE
		CALL SETFLAG, "임신中：아들", TARGET
	ENDIF

	IF COND("임신中：아들")
		PRINTFORMW \@ FATHER > 0 ? 부친似の # \@건강한 사내 아이입니다.
		CFLAG:469 += 100
		;아들플래그
		CALL SETFLAG, "육아중：아들", TARGET
	ELSE
		PRINTFORMW \@ TARGET == MASTER ? %CALLNAME:MASTER% # 모친 \@似の可愛い女の子です。
		CFLAG:469 += 1
	ENDIF

	;연모
	IF TALENT:연모
		PRINTFORMW 갓난아이를 안고있는 %조사처리(CALLNAME:TARGET,"는")%, %CALLNAME:MASTER%에게 자신의 아이를 안아달라고 졸랐습니다. 
	;반발각인Lv2
	ELSEIF MARK:반발각인 == 2
		PRINTFORMW 갓난아이를 안고있는 %조사처리(CALLNAME:TARGET,"는")%, 자신의 아이를 향해서 미소짓고있습니다. 
	;반발각인Lv3
	ELSEIF MARK:반발각인 == 3
		IF TALENT:반항적 || TALENT:교만 || TALENT:강기
			PRINTFORMW 갓난아이를 안고있는 %조사처리(CALLNAME:TARGET,"는")%, %조사처리(CALLNAME:MASTER,"를")% 위협하듯이 매섭게 쏘아보았습니다. 
		ELSE
			PRINTFORMW 갓난아이를 안고있는 %조사처리(CALLNAME:TARGET,"는")%, 자신의 아이를 %CALLNAME:MASTER%에게서 보호하듯이 꼭 껴안았습니다.
		ENDIF
	ELSEIF TARGET != MASTER
		PRINTFORMW 갓난아이를 안고있는 %조사처리(CALLNAME:TARGET,"는")%, 자신의 아이와 %조사처리(CALLNAME:MASTER,"를")% 향해서 미소짓고있습니
	ENDIF

	CALL SELF_KOJO, "출산"

	CALL BEFOREHAND_DAUGHTER_NAME
	CFLAG:485 = DAY
	TARGET = ARG

	SIF TALENT:몽마 || TALENT:상위몽마
		PRINTFORMW 몽마의 아이는 성장이 빠르기때문에, 곧 뛰어노는 모습을 볼 수 있을 것이다…
	SIF TALENT:마왕의조각
		PRINTFORMW 마왕의 마력의 잔재가 순식간에 아이를 성장시켜, 곧 뛰어노는 모습을 볼 수 있을 것이다…

	;임신＆태아플래그終了
	BASE:태아 = 0
	TALENT:임신 = 0

	;子育て開始
	BASE:육아중 += 1
	;아이の数+1
	TALENT:출산경험 += 1
	;부친플래그の確定
	BASE:태아의부친 = BASE:정액의우세
	BASE:정액의우세 = 0

	;一時的な소질の消去(まだ消えません)
	IF CFLAG:468
		PRINTL 
		PRINTFORMW （출산을 마쳐, %CALLNAME:TARGET%의 가슴은 원래대로 돌아갈 것입니다…）
	ENDIF
	SETBIT CFLAG:MASTER:95, 0
;子育て中
ELSEIF BASE:육아중 && RAND:4 && COND("손一杯", MASTER) + COND("同衾이벤트", MASTER) == 0
	FATHER = COND("태아의부친")
	DRAWLINE
	;임신しているのはMASTER
	IF TARGET == MASTER
		PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% 아이 돌보기에 쫓기고있습니다…
			SETBIT CFLAG:MASTER:95, 0
	;부친はMASTERor既にいないor연모
	ELSEIF FATHER == MASTER || FATHER < 0 || TALENT:연모
		PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% %조사처리(CALLNAME:TARGET,"과")% 아이의 상태를 보러갔습니다. 
		;반발각인Lv3
		IF MARK:반발각인 == 3
			IF RAND:5 == 0 && (TALENT:반항적 || TALENT:교만 || TALENT:강기)
				PRINTFORMW 방에 들어갈 수 없었습니다…
			ELSEIF RAND:4 == 0
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %조사처리(CALLNAME:MASTER,"를")% 노려보고있습니다…
			ELSEIF RAND:3 == 0
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:MASTER%의 얼굴을 보자 아이를 지키듯이 감쌌습니다…
			ELSEIF RAND:2 == 0
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 즐거운듯이 아이와 놀고있습니다. 
				PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% 그대로 자기방으로 돌아갔습니다…
			ELSE
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 환성을 지르며 아이와 놀고있었습니다만, 
				PRINTFORMW %조사처리(CALLNAME:MASTER,"가")% 방아넹 들어오자 그만둬버렸습니다…
			ENDIF
		ELSEIF RAND:6 == 0
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 아이에게 젖을 주던 중이었습니다. 
			PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% %CALLNAME:TARGET%의 어머니다운 모습을보고, 왠지 훈훈해졌습니다…
		ELSEIF RAND:5 == 0
			PRINTFORMW 두사람이 졸린듯이 마중 나갔기때문에, 오늘 밤은 함께 자주기로 했습니다…
			SETBIT CFLAG:MASTER:95, 0
			CFLAG:MASTER:96 = TARGET
			CFLAG:96 = MASTER
		ELSEIF RAND:4 == 0
			PRINTFORMW %조사처리(CALLNAME:TARGET,"가")% 잠들지 않는 아이때문에 곤란해하고있어서, 함께 밤을샐 각오로 달래기로 했습니다…
			SETBIT CFLAG:MASTER:95, 0
			CFLAG:MASTER:96 = TARGET
			CFLAG:96 = MASTER
		ELSEIF RAND:3 == 0
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 아이와 찰싹찰싹 만지기놀이를 하고있는 %조사처리(CALLNAME:MASTER,"를")%, 행복하게 바라보고있습니다…
		ELSEIF RAND:2 == 0
			PRINTFORMW 아이가 멍해보여서 셋이서 함께 간단한 놀이를 하면서 분주하게 보냈습니다…
		ELSE
			PRINTFORM %조사처리(CALLNAME:TARGET,"가")% 어딘지 육아에 불안해보여서, 
			IF RAND:3 == 0
				PRINT 머리카락을 빗어주면서
			ELSEIF RAND:2 == 0
				PRINT 상냥하게 쓰다듬으면서 
			ELSE
				IF  TALENT:작은몸집
					PRINT 포옹하면서 
				ELSE
					PRINT 스킨쉽하면서 
				ENDIF
			ENDIF
			PRINTFORMW 고민을 들어주었습니다…
		ENDIF
	;부친はMASTER以外（現状保留）
	;ELSEIF FATHER > MASTER
	ENDIF
;임신中
ELSEIF TALENT:임신 && COND("손一杯", MASTER) + COND("同衾이벤트", MASTER) == 0 && RAND:4
	FATHER = COND("정액의우세")
	DRAWLINE
	;임신しているのはMASTER
	IF TARGET == MASTER
		PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% 돌아다니는 것이 귀찮아서, 자기방에서 쉬기로 했습니다. 
			SETBIT CFLAG:MASTER:95, 0
	;부친はMASTERor既にいないor연모
	ELSEIF FATHER == MASTER || FATHER < 0 || TALENT:연모
		PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% %CALLNAME:TARGET%의 상태를 보러갔습니다. 
		;반발각인Lv3
		IF MARK:반발각인 == 3
			IF RAND:4 == 0
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 공허한 눈으로 부푼 배를 쳐다보고있었습니다…
			ELSEIF RAND:3 == 0
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 배에 귀를 대거나하는 %조사처리(CALLNAME:MASTER,"를")%, 무감정하게 내려다보고있습니다…
			ELSEIF RAND:2 == 0
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 공허하게 웃으면서 뱃속이 아이에게 말을 걸고있습니다…
			ELSE
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 즐겁게 뱃속의 아이에게 말을 걸고있었습니다만, 
				PRINTFORMW %조사처리(CALLNAME:MASTER,"가")% 보고있자 갑자기 눈물을 흘리기시작했습니다…
			ENDIF
		ELSEIF FATHER == MASTER && RAND:4
			IF RAND:4 == 0
				PRINTFORMW %조사처리(CALLNAME:TARGET,"가")% 졸린 듯이 배웅을 하기에, 오늘 밤에는 같이 자주기로했습니다…
				SETBIT CFLAG:MASTER:95, 0
				CFLAG:MASTER:96 = TARGET
				CFLAG:96 = MASTER
			ELSEIF RAND:3 == 0
				PRINTFORMW …만, %조사처리(CALLNAME:TARGET,"가")% 이미 자고있는 듯해서, 키스만 해주고 돌아갔습니다…
			ELSEIF RAND:2
				PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 배에 귀를 대고 %조사처리(CALLNAME:MASTER,"를")%, 눈을 가늘게 뜨고 기쁜듯이 바라보고있습니다…
			ELSE
				PRINTFORMW %조사처리(CALLNAME:TARGET,"가")% 태어날 아이의 이야기를 하거나, 즐거운 시간을 보냈습니다…
			ENDIF
		ELSE
			PRINTFORM %조사처리(CALLNAME:TARGET,"가")% 어딘지 불안해보여서, 
			IF RAND:3 == 0
				PRINT 머리카락을 빗어주면서 
			ELSEIF RAND:2 == 0
				PRINT 상냥하게 쓰다듬으면서 
			ELSE
				IF  TALENT:작은몸집
					PRINT 포옹하면서 
				ELSE
					PRINT 스킨쉽하면서 
				ENDIF
			ENDIF
			PRINTFORMW 고민을 들어주었습니다…
		ENDIF
	;부친はMASTER以外（現状保留）
	;ELSEIF FATHER > MASTER
	ENDIF
ENDIF

TARGET = LOCAL

;---------------------------------------------------------
;주인と몽마たちによる임신判定체크
;シチュエーションARGSでARGが임신するかどうかを判定する. ARG:1は정액량. 
;ARGSは"通常", "조교外", "드래곤", "일반인상대", "촉수"
;SPERMで자궁に入る精子量, IS_FATHERで誰が부친になるかの番号管理を行う
;---------------------------------------------------------
@CHECK_PREGNANT, ARG, ARG:1, ARGS
#DIM SPERM
#DIM SPERM_DENS
#DIM IS_FATHER

;정액량がない場合
SIF ARG:1 <= 0
	RETURN 0

;조교中かどうかとPLAYERが일반인かどうかを체크
SELECTCASE ARGS
;손가락定されている場合にはARGSの変化なし
CASE "드래곤", "촉수", "일반인상대", "조교外", "通常"
CASEELSE
	IF TALENT:PLAYER:일반인
		ARGS = 일반인상대
	ELSEIF COND("조교中")
		ARGS = 通常
	ELSE
		ARGS = 조교外
	ENDIF
ENDSELECT

;SPERMで자궁に入る精子量, IS_FATHERで誰が부친になるかの番号管理を行う
SPERM = ARG:1 * (10 + RAND:21)
SPERM_DENS = 90 + RAND:21

SELECTCASE ARGS
CASE "드래곤"
	IS_FATHER = -3
CASE "일반인상대"
	;처녀は判定しない
	SIF TALENT:ARG:처녀
		RETURN 0
	IS_FATHER = -4
	;정액량は館のメンバーに比べると少ない
	SPERM = MAX(SPERM/2, 1)
CASE "촉수"
	IS_FATHER = -5

	;촉수は당신？
	SIF FLAG:87 == 1
		IS_FATHER = MASTER
CASEELSE
	SELECTCASE ARG
	;조교자の임신체크
	CASE PLAYER
		IS_FATHER = TARGET

	;TARGETやASSI等の임신체크
	CASEELSE
		SELECTCASE BASE:PLAYER:식별번호
		CASE IS > 0
			IS_FATHER = PLAYER
		CASEELSE
			IS_FATHER = -4
		ENDSELECT

		;절정を迎える事で확률上昇
		SIF NOWEX:ARG:절정수합계
			SPERM += MIN(NOWEX:ARG:절정수합계, 2)/2

		IF COND("자궁삽입플래그", ARG)
			SPERM *= 10
		;삽입자궁구자극の時には임신率上昇
		ELSEIF SELECTCOM == 67
			TIMES SPERM, 1.20
		ENDIF
	ENDSELECT
	;호접지몽
	SIF ITEM:호접지몽 && IS_FATHER > 0 && COND("호접지몽", IS_FATHER) > 0
		IS_FATHER = COND("호접지몽", IS_FATHER)
ENDSELECT

;임신判定플래그
BASE:ARG:태내정액량 += SPERM * SPERM_DENS

;피임中は精子は受け入れない
IF COND("피임中", ARG)
	SPERM = 0
;임신する가능性ある人は주인？
ELSEIF CONFIG("임신패치：주인임신가능") == 0 && ARG == MASTER
	SPERM = 0
;주인以外が부친でも임신가능？
ELSEIF CONFIG("임신패치：주인の精子のみ") && IS_FATHER != MASTER
	SPERM = 0
ENDIF

;조교以外でも임신가능？
SELECTCASE ARGS
CASE "드래곤", "촉수"
	;有効精子量が少ない
	SPERM = MAX(SPERM/3, 1)
CASE "일반인상대"
	SIF CONFIG("임신패치：조교以外で임신가능") == 0
		SPERM = 0
	;몽마の人はパンピー상대にはかなり強い
	SIF COND("몽마", ARG)
		SPERM = MAX(SPERM/(TALENT:ARG:몽마 + TALENT:ARG:상위몽마*2 + 1), 1)
CASE "조교外"
	SIF CONFIG("임신패치：조교以外で임신가능") == 0
		SPERM = 0
ENDSELECT
;흡정체질の人は精子を頂いちゃう
SIF TALENT:ARG:흡정체질
	SPERM = MAX(MIN(1 + EX:ARG:Ｖ내사정*2, SPERM), SPERM/POWER(2, TALENT:ARG:흡정체질) )

BASE:ARG:자궁정자량 += SPERM

;부친一時플래그
IF SPERM && BASE:ARG:태아 == 0 && COND("임신確実", ARG) == 0 && COND("피임中", ARG) == 0
	;백발백중
	IF IS_FATHER > 0 && TALENT:IS_FATHER:백발백중
		BASE:ARG:정액의우세 = BASE:IS_FATHER:식별번호
		SETBIT BASE:ARG:바이오리듬, 14
 	ELSEIF BASE:ARG:자궁정자량 == 0 || COND("정액의우세", ARG) == 0 || RAND:3 == 0
 		SELECTCASE IS_FATHER
		CASE IS > 0
			BASE:ARG:정액의우세 = BASE:IS_FATHER:식별번호
		CASEELSE
			BASE:ARG:정액의우세 = IS_FATHER
		ENDSELECT
	ENDIF
ENDIF
;배란유발제
SIF TEQUIP:ARG:배란유발제
	SETBIT BASE:ARG:바이오리듬, 13
;내사정された最新の날짜を上書き
CFLAG:ARG:499 = DAY

