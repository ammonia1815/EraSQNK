;-------------------------------------------------
;욕구불만持ちの몽마を당신から能動的に解消する
;システム以外は뱀파이어２구상作者のIRC>>あねふぁさんが大体作りました
;ARGSには"요바이난교"とかシチュ指定が入り限定的な地の文を出す際の分岐に使える
;-------------------------------------------------
@EVENT_RANKOU, ARGS
#DIM LCOUNT
#DIM MEMO_LINECOUNT
;난교参加者の人数
#DIM CHARANUM_RANKOU
;난교プレイ参加者の名前の羅列。%CALLNAME:TARGET%と%CALLNAME:(TARGET:1)% など
#DIMS CNAMES_RANKOU
;プレイの記録
#DIM MAINCOM
#DIM SUBCOM
;스톡を記録
#DIM MEMO_BASE_STOCK

;난교参加者の人数
CHARANUM_RANKOU = CHARA_NUM("난교참가中")
;난교プレイ参加者の名前の羅列。%CALLNAME:TARGET%と%CALLNAME:(TARGET:1)%など
CNAMES_RANKOU = %타겟과()% %CALLNAME:(TARGET:1)%\@ TARGET:2 > 0 ? %조사만처리(CALLNAME:(TARGET:1),"과")% %CALLNAME:(TARGET:2)% # \@
;副次的なプレイは基本的には無し
SUBCOM = -1
;스톡を記録
MEMO_BASE_STOCK = BASE:MASTER:스톡

;参加者のPALAM、EXP、EX、JUEL、BASEを記録(後で増加分を算出する)
CALL SIMULATE_TRAIN_FIRST, CHARANUM_RANKOU

SELECTCASE ARGS
CASE "요바이난교"
	PRINTFORMW %마스터는()% 지만 잠자리에 들려고 했을 때,%조사처리(CNAMES_RANKOU,"가")% 방을 방문해 왔다
	;PRINTFORMW 相当焦れてるなと直感した%CALLNAME:MASTER%は、%TEXTS("漢数字概数", CHARANUM_RANKOU)%人を部屋に招いて淫靡な宴をはじめることにした…
ENDSELECT

PRINTL 
;セリフがあるかの判定
MEMO_LINECOUNT = LINECOUNT

;開始セリフ
FOR LCOUNT, 0, CHARANUM_RANKOU
	;主なプレイ内容(CSTR:난교)で分岐させることができる
	TRYCALLFORM GUEST_KOJO_K{NO:(TARGET:LCOUNT)}_{CFLAG:(TARGET:LCOUNT):209}, "난교개시", TARGET:LCOUNT
NEXT

;セリフがあった
IF LINECOUNT > MEMO_LINECOUNT
	WAIT
	PRINTL 
ENDIF

;プレイ内容で分岐
SELECTCASE CSTR:난교
CASE "重ねて交互突き"
	PRINTFORMW %TEXTS("漢数字概数", CHARANUM_RANKOU)%사람은 침대에 가로놓여,%TEXTR("보지를 펼쳐", "포즈를 취해", "옷을 벌리며")% 조르는 경쟁을 시작했다
	PRINTFORMW %CALLNAME:TARGET%들은 %CALLNAME:MASTER%에 범해지면서, 서로의 몸을 애무하고 즐기고 있다
	MAINCOM = 60
	SUBCOM = 13
CASE "次々に犯す"
	PRINTFORMW %TEXTS("漢数字概数", CHARANUM_RANKOU)%사람은 %CALLNAME:MASTER%에 향하여 엉덩이를 내밀어, 속옷을 내려 재촉을 해 왔다
	PRINTFORMW %마스터는()% 눈 앞에 있는 엉덩이의 유혹에 저항하지 못하고, 흐물흐물 빨려들어 차례차례 범했다
	MAINCOM = 61
	SUBCOM = 13
CASE "기승위輪姦"
	IF RAND:2
		PRINTFORMW %TEXTS("漢数字概数", CHARANUM_RANKOU)%사람은 협력해 %마스터를()% 넘어뜨리고, 얼굴과 허리에 걸쳐 쾌락을 탐내기 시작했다
	ELSE
		PRINTFORMW %마스터는()% %TEXTS("漢数字概数", CHARANUM_RANKOU)%사람에게 밀어 넘어뜨려져 기승위로 번갈아 범해졌다
	ENDIF
	IF RAND:6 == 0
		PRINTFORMW %CALLNAME:TARGET%들은 %CALLNAME:MASTER%의 얼굴을 히죽히죽 응시하면서, 그 표정을 즐기듯이 허리를 그라인드 시키고 있다！
	ELSEIF RAND:5 == 0
		PRINTFORMW %CALLNAME:TARGET%들은 스스로 움직이려고 하지 않고, %CALLNAME:MASTER%에 허리를 흔드라고 명령해 온다
	ELSEIF RAND:4 == 0
		PRINTFORMW %CALLNAME:TARGET%들은 %CALLNAME:MASTER%의 몸을 단단히 억눌러, 오로지 격렬하게 허리를 계속 흔들어 온다！
	ELSEIF RAND:3 == 0
		PRINTFORMW %CALLNAME:TARGET%들은 %마스터와()% 몸을 밀착시켜, 점막의 움직임만으로 짜내려고 해 온다！
	ELSEIF RAND:2
		PRINTFORMW 즐거운 듯이 허리를 흔드는 %CALLNAME:TARGET%에 지지 않으려고, 차례를 기다리는%조사처리(CALLNAME:(TARGET:1),"가")% %CALLNAME:MASTER%의 입술을 빼앗는다
	ELSE
		PRINTFORMW 깔아 눌려져 허리를 흔드는 %CALLNAME:MASTER%의 옆에서는, 차례를 기다리는 몽마가 추잡한 말을 속삭여 온다
	ENDIF

	IF CANTRAINS("넘어뜨리기기승위") == CHARANUM_RANKOU && (RAND:2 || CANTRAINS("넘어뜨리기애널기승위") < CHARANUM_RANKOU)
		MAINCOM = 65
	ELSE
		MAINCOM = 75
	ENDIF
	IF CANTRAINS("넘어뜨리기컨닐링구스") == CHARANUM_RANKOU && (RAND:2 || CANTRAINS("넘어뜨리기애널핥기") < CHARANUM_RANKOU)
		SUBCOM = 1
	ELSE
		SUBCOM = 9
	ENDIF
	;넘어뜨리기ＯＮ
	CVARSET TEQUIP, 30, 1
CASE "식스나인"
	PRINTFORMW %TEXTS("漢数字概数", CHARANUM_RANKOU)%사람은 얽히듯이 해 서로의 성기에 혀를 기게 하면서, %CALLNAME:MASTER%에 향하여 과격히 졸라 왔다
	IF RAND:2
		PRINTFORMW %CALLNAME:TARGET%들은 입과 사타구니를 교대로 범해지는 것을 스파이스로 해, 땀투성이로 ６９를 즐기고 있다
	ELSE
		PRINTFORMW %CALLNAME:TARGET%들은 범해지면서, 서로의 부끄러운 부분을 열중해 맛보고 있다
	ENDIF
	MAINCOM = 69
	SUBCOM = 61
CASE "Ｗ스마타"
	PRINTFORMW %TEXTS("漢数字概数", CHARANUM_RANKOU)%사람은 %마스터를()%넘어뜨리고 둘러싸,\@ TARGET:2 ? 교대로 # \@Ｗ스마타를 해 왔다
	PRINTFORMW 그곳의 부드러운 감촉으로 페니스를 괴롭힐 수 있어 절정하는 모습을,%CALLNAME:TARGET%들은 재미있게 바라보고 있다
	MAINCOM = 93
CASE "풋잡"
	;넘어뜨리기ＯＮ
	CVARSET TEQUIP, 30, 1
	MAINCOM = 95

	PRINTFORMW %TEXTS("漢数字概数", CHARANUM_RANKOU)%사람은 %마스터를()% 넘어뜨리고, \@ TARGET:2 ? 교대로 # \@풋잡을 해 왔다
	IF RAND:3 == 0 && CANTRAINS("넘어뜨리기컨닐링구스") == CHARANUM_RANKOU
		PRINTFORMW 반사적으로 피하려고 한%CALLNAME:MASTER%였지만, 용서가 없는 안면기승에 의해 반격할수 없게되어 버린다……
		SUBCOM = 1
	ELSEIF RAND:2 && CANTRAINS("조개벌리기") == CHARANUM_RANKOU
		PRINTFORMW 그 뿐만 아니라 겁없는 시선으로 내려다 봐지면서 전원에게 그곳을 보게 되어 %마스터는()% 눈을 떼어 놓을 수 없게 되어 버렸다……
		SUBCOM = 13
	ELSE
		PRINTFORMW 둘러싸지면서의 무자비한풋잡에,%마스터는()% 페니스를 격분하게 할 수 밖에 없다！
	ENDIF
CASE "오나홀플레이"
	PRINTFORMW %TEXTS("漢数字概数", CHARANUM_RANKOU)%사람은 %마스터를()% 넘어뜨리고,  오나홀을 이용해 교대로 페니스를 다뤄 왔다
	PRINTFORMW 몰아세워지는 %CALLNAME:MASTER%의 안색이, 오나홀을 움직이는 %CALLNAME:TARGET%들에게는 재미있어서 어쩔 수 없는 같다
	;넘어뜨리기ＯＮ
	CVARSET TEQUIP, 30, 1
	MAINCOM = 105
;雛形（ここへは来ない）
CASEELSE
	PRINTFORMW %CALLNAME:TARGET%들은 %CSTR:난교%를 해 왔다
	;MAINCOM = 
	;SUBCOM = 
ENDSELECT

CALL EVENT_RANKOU_PLAY, MAINCOM, CHARANUM_RANKOU, SUBCOM

;당신のイかされた回数と스톡の減少表示
CALL PRINT_EVENT_RANKOU_STOCK, MEMO_BASE_STOCK

;プレイ終了時の地の文
SELECTCASE CSTR:난교
CASE "次々に犯す"
	PRINTFORMW 질안에 충분히 정액을 채워 %CALLNAME:TARGET%들은 행복한 것 같다……
CASE "기승위輪姦"
	IF RAND:2
		PRINTFORMW 해방된 %CALLNAME:MASTER%의 옆에서,%CALLNAME:TARGET%들은 짜낸 정액을 서로 과시하고 있다……
	ELSE
		PRINTFORMW %CALLNAME:TARGET%들은\@ MAINCOM == 65 ? 보지를 # 애널을 펴\@넓혀, 짜낸 정액을 %CALLNAME:MASTER%에 과시하고 있다……
	ENDIF
CASE "오나홀플레이"
	PRINTFORMW 오나홀의 안에 짜낸 정액을,%CALLNAME:TARGET%들은 맛있을 것 같게 돌려 마셔 하고 있다……
CASEELSE
	IF RAND:2
		PRINTFORMW %CALLNAME:TARGET%들은 정액의 들러붙은 얼굴을 서로 서로 빨고 있다……
	ELSE
		PRINTFORMW %CALLNAME:TARGET%들은, 짜낸 정액을 입으로 서로 나누고 있다……
	ENDIF
ENDSELECT
PRINTL 

;経験や구슬などの獲得量計算
CALL SIMULATE_TRAIN_CALC
;結果発表
CALL SIMULATE_TRAIN_SHOW
PRINTL 

;ピロートーク
SELECTCASE ARGS
CASE "요바이난교"
	;主人ダウン
	IF BASE:MASTER:정력 <= 1 && BASE:MASTER:스톡 == 0
		IF RAND:2 && PENIS(MASTER)
			PRINTFORMW %CALLNAME:TARGET%들은 노력한 %CALLNAME:MASTER%의 페니스를 만지작거리면서, 이것저것 격려해 왔다
			PRINTFORMW 그리고%TEXTS("漢数字概数", CHARANUM_RANKOU + 1)%사람은 서로 몸을 의지해 얼싸안고, 깊은 잠에 떨어졌다…
		ELSE
			PRINTFORMW %CALLNAME:TARGET%들은 움직일 수 없는 %마스터를()% 어루만지면서, 와글와글 이야기꽃을 피우고 있다
			PRINTFORMW 그 후 %TEXTS("漢数字概数", CHARANUM_RANKOU + 1)%사람은 서로 몸을 의지해 얼싸안고, 깊은 잠에 떨어졌다…
		ENDIF
	ELSE
		;とりあえず今のところ一番上のみ。他は蛇발な気がする
		IF 1
			PRINTFORMW %CALLNAME:TARGET%들을 몇번이나 절정시킨 후,%TEXTS("漢数字概数", CHARANUM_RANKOU + 1)%사람은 서로 몸을 의지해 얼싸안고, 깊은 잠에 떨어졌다…
		ELSEIF RAND:2
			PRINTFORMW 만족한 %CALLNAME:TARGET%들은,%CALLNAME:MASTER%에 몸을 밀착시켜 끈적끈적 응석부려 왔다
			PRINTFORMW %마스터는()% {CHARANUM_RANKOU}인분의 따뜻함을 느끼면서, 온화한 수다의 시간를 보냈다……
		ELSE
			IF RAND:2
				PRINTFORMW %CALLNAME:TARGET%들은 %CALLNAME:MASTER%의 활동에 만족하고, 감사의 말을 말하면서 어루만져 왔다
			ELSE
				PRINTFORMW 만족한 %CALLNAME:TARGET%들은 %마스터와()% 수다 하면서, 문질문질 여러 곳을 손대어 왔다
			ENDIF
			PRINTFORMW 간지러운 것 같은 %마스터를()% 봐,{CHARANUM_RANKOU}사람의 장난은 에스컬레이트할 뿐이다……
		ENDIF
	ENDIF
	;MASTERが一緒に寝た相手フラグ(TARGET優遇)
	IF RAND:3
		CFLAG:MASTER:96 = TARGET
		CFLAG:96 = MASTER
	ELSEIF RAND:2 && TARGET:2 > 0
		CFLAG:MASTER:96 = TARGET:2
		CFLAG:(TARGET:2):96 = MASTER
	ELSE
		CFLAG:MASTER:96 = TARGET:1
		CFLAG:(TARGET:1):96 = MASTER
	ENDIF
ENDSELECT

CVARSET TEQUIP, 30, 0

;욕구불만解消
FOR LCOUNT, 0, CHARANUM_RANKOU
	CALL CALC, "욕구불만解消", TARGET:LCOUNT, 20
NEXT

;仲良くなる
CALL CHANGE_RELATION, 5, TARGET, TARGET:1, TARGET:2

;-------------------------------------------------
;CSTR:난교 を決める前段階の決定をする
;ARGSには"요바이난교"とかシチュ指定が入る
;-------------------------------------------------
@EVENT_RANKOU_SET, ARGS
#DIM LCOUNT
;仲良しフラグ
#DIM IS_FRIEND
;난교参加者の人数
#DIM CHARANUM_RANKOU
;ランダム선택に用いる
#DIM NUM_FIND

;参加者の人数フラグ初期化
CHARANUM_RANKOU = 1
;난교参加済みフラグON
SETBIT CFLAG:95, 5
;仲良しフラグ。最初の一人呼んだ時点で仲良しだとグループ全体が仲良しになる
IS_FRIEND = 0

;二人まで仲間を呼ぶ…욕구불만の人が多い場合を除いて、相性のいい人優先。ここでTARGET:1, TARGET:2を決定
FOR LCOUNT, 1, 3
	IF CHARA_NUM("욕구불만で난교가능") >= 3 + RANDF(7)
		TARGET:LCOUNT = FIND_COND("난교가능", "욕구불만")
	ELSEIF FIND_COND("상성☆", "난교가능")
		TARGET:LCOUNT = FIND_COND("상성☆", "난교가능")
		;확률６７％
		SIF LCOUNT == 1 && RAND:3
			IS_FRIEND = 1
	ELSEIF FIND_COND("상성◎", "난교가능")
		TARGET:LCOUNT = FIND_COND("상성◎", "난교가능")
		;확률３３％
		SIF LCOUNT == 1 && RAND:3 == 0
			IS_FRIEND = 1
	;仲良しフラグ発動
	ELSEIF IS_FRIEND && LCOUNT >= 2
		BREAK
	ELSEIF FIND_COND("상성○", "난교가능")
		TARGET:LCOUNT = FIND_COND("상성○", "난교가능")
	ELSEIF FIND_COND("난교가능", "욕구불만")
		TARGET:LCOUNT = FIND_COND("난교가능", "욕구불만")
	ELSEIF FIND_COND("난교가능", "ご無沙汰")
		TARGET:LCOUNT = FIND_COND("난교가능", "ご無沙汰")
	ELSEIF FIND_COND("난교가능")
		TARGET:LCOUNT = FIND_COND("난교가능")
	ELSE
		BREAK
	ENDIF
	;参加者増
	CHARANUM_RANKOU += 1
	;난교参加済みフラグON
	SETBIT CFLAG:(TARGET:LCOUNT):95, 5
NEXT
;メンバーが決まったら各人の실행가능명령の確認
FOR LCOUNT, 0, CHARANUM_RANKOU
	CALL CHECK_COM_ABLE, TARGET:LCOUNT
NEXT

;プレイの種類の記録…基本プレイが何かをSELECTCOMで判定して、種類を分けることにする
;CANTRAINS("커맨드名") == CHARANUM_RANKOU となることが基本の実行条件
NUM_FIND = 0

SELECTCASE ARGS
CASE "요바이난교"
	FOR LCOUNT, 0, 10
		SELECTCASE LCOUNT
		CASE 0
			SIF CANTRAINS("정상위") < CHARANUM_RANKOU
				CONTINUE
			SIF CANTRAINS("조개벌리기") < CHARANUM_RANKOU
				CONTINUE
			LOCALS = 重ねて交互突き
		CASE 1
			SIF CANTRAINS("후배위") < CHARANUM_RANKOU
				CONTINUE
			SIF CANTRAINS("조개벌리기") < CHARANUM_RANKOU
				CONTINUE
			LOCALS = 次々に犯す
		CASE 2
			SIF CANTRAINS("넘어뜨리기기승위") < CHARANUM_RANKOU && CANTRAINS("넘어뜨리기애널기승위") < CHARANUM_RANKOU
				CONTINUE
			SIF CANTRAINS("넘어뜨리기컨닐링구스") < CHARANUM_RANKOU && CANTRAINS("넘어뜨리기애널핥기") < CHARANUM_RANKOU
				CONTINUE
			LOCALS = 기승위輪姦
		CASE 3
			SIF CANTRAINS("스마타") < CHARANUM_RANKOU
				CONTINUE
			LOCALS = Ｗ스마타
		CASE 4
			SIF CANTRAINS("넘어뜨리기풋잡") < CHARANUM_RANKOU
				CONTINUE
			LOCALS = 풋잡
		CASE 5
			SIF CANTRAINS("넘어뜨리기오나홀플레이") < CHARANUM_RANKOU
				CONTINUE
			LOCALS = オナホプレイ
		CASE 6
			SIF CANTRAINS("후배위") < CHARANUM_RANKOU
				CONTINUE
			SIF CANTRAINS("식스나인") < CHARANUM_RANKOU
				CONTINUE
			LOCALS = 식스나인
		CASEELSE
			BREAK
		ENDSELECT

		NUM_FIND += 1

		SIF RAND:NUM_FIND
			CONTINUE

		CSTR:요바이 = %LOCALS%
	NEXT
	;난교プレイが実行가능ならば決定
	SIF CSTR:요바이 != ""
		RETURN 1
ENDSELECT

;난교が起きないならフラグリセット
FOR LCOUNT, 0, CHARANUM_RANKOU
	CLEARBIT CFLAG:(TARGET:LCOUNT):95, 5
NEXT
TARGET:1 = 0
TARGET:2 = 0


;-------------------------------------------------
;具体的な커맨드선택
;ARGにはプレイ内容(SELECTCOM)が、ARG:1には参加人数が入る
;ARG:2は基本-1だが、0이상の場合はその番号の커맨드を実行する
;-------------------------------------------------
@EVENT_RANKOU_PLAY, ARG, ARG:1, ARG:2
#DIM LCOUNT

FOR LCOUNT, 0, ARG:1
	SWAP TARGET, TARGET:LCOUNT

	DO
		;まずは키스や애무
		SIF CANTRAIN("키스한다") && RAND:(ARG:1) == 0 && (RAND:3 == 0 || TALENT:연모 || TALENT:음탕한입술 || COND("키스에약함", MASTER) )
			CALL SIMULATE_TRAIN_COM, 6, 1
		SIF CANTRAIN("엉덩이애무") && RAND:(ARG:1) == 0 && (TALENT:거유 >= RAND:4 || TALENT:음유 || COND("가슴에약함", MASTER) )
			CALL SIMULATE_TRAIN_COM, 5, 1
		SIF CANTRAIN("컨닐링구스") && RAND:(ARG:1) == 0 && (RAND:5 == 0 || TALENT:음핵 || COND("♀에약함", MASTER) )
			CALL SIMULATE_TRAIN_COM, 1, 1
		SIF CANTRAIN("애널핥기") && RAND:(ARG:1) == 0 && (TALENT:애널광 || COND("엉덩이에약함", MASTER) )
			CALL SIMULATE_TRAIN_COM, 9, 1
		SIF CANTRAIN("펠라한다") && RAND:(ARG:1) == 0 && COND("♂에약함", MASTER)
			CALL SIMULATE_TRAIN_COM, 4, 1
		SIF CANTRAIN("수음한다") && RAND:(ARG:1) == 0 && (RAND:3 == 0 || COND("♂에약함", MASTER) )
			CALL SIMULATE_TRAIN_COM, 10, 1

		;そして基本プレイ
		CALL SIMULATE_TRAIN_COM, ARG, BASE:욕구불만/10 + (ABL:욕망 + 1)/3 + TALENT:음란 + COND("ご無沙汰") + 1
		SIF ARG:2 >= 0
			CALL SIMULATE_TRAIN_COM, ARG:2, 1

		;後のプレイはどうするかな
		SIF CANTRAIN("손훑기") && RAND:3 == 0 && (ABL:손가락놀림 >= RAND:8 || COND("손에약함", MASTER) )
			CALL SIMULATE_TRAIN_COM, 90, 1
		SIF CANTRAIN("펠라치오") && RAND:3 == 0 && (ABL:혀놀림 >= RAND:8 || TALENT:헌신 || COND("입에약함", MASTER) )
			CALL SIMULATE_TRAIN_COM, 91, 1
		SIF CANTRAIN("컨닐링구스시킨다") && RAND:3 == 0 && (ABL:혀놀림 >= RAND:8 || TALENT:헌신 || COND("입에약함", MASTER) )
			CALL SIMULATE_TRAIN_COM, 98, 1
		SIF CANTRAIN("파이즈리") && TALENT:거유 >= RAND:4 && (ABL:마유 >= RAND:8 || COND("가슴에약함", MASTER) )
			CALL SIMULATE_TRAIN_COM, 92, 1
		SIF CANTRAIN("파후파후") && TALENT:거유 >= RAND:4 && (ABL:마유 >= RAND:8 || COND("가슴에약함", MASTER) )
			CALL SIMULATE_TRAIN_COM, 104, 1
		SIF CANTRAIN("풋잡") && (ABL:새드끼 + ABL:MASTER:마조끼 + 1)/2 >= RAND:10 && (ABL:발놀림 >= RAND:8 || COND("다리에약함", MASTER) )
			CALL SIMULATE_TRAIN_COM, 95, 1

	LOOP (BASE:욕구불만 >= (EXP:2 - TCVAR:302)*4 + (EXP:6 - TCVAR:306)*3)

	SWAP TARGET, TARGET:LCOUNT
NEXT

;-------------------------------------------------
;ARGには난교前の主人の스톡数が入っている
;-------------------------------------------------
@PRINT_EVENT_RANKOU_STOCK, ARG
#DIM LCOUNT
#DIM MEMO_EXP6_ALL

MEMO_EXP6_ALL = 0
FOR LCOUNT, 0, 3
	SIF TARGET:LCOUNT == 0
		BREAK
	MEMO_EXP6_ALL += EXP:(TARGET:LCOUNT):6 - TCVAR:(TARGET:LCOUNT):306
NEXT

SIF BASE:MASTER:스톡 >= ARG && MEMO_EXP6_ALL == 0
	RETURN 0

SETCOLOR DEF_COLOR("하트핑크")

;SIF EXP:MASTER:2 > TCVAR:MASTER:302
;	PRINTFORML %CALLNAME:MASTER%は{EXP:MASTER:2 - TCVAR:MASTER:302}回イかされてしまった！
;SIF EXP:MASTER:3 > TCVAR:MASTER:303
;	PRINTFORML %CALLNAME:MASTER%は{EXP:MASTER:3 - TCVAR:MASTER:303}回사정してしまった！
IF MEMO_EXP6_ALL > 10
	PRINTFORML %마스터는()% {MEMO_EXP6_ALL}회나 가게 되어 버렸다！
ELSE
	PRINTFORML %마스터는()% {MEMO_EXP6_ALL}회 가게 되어 버렸다！
ENDIF

SETCOLOR DEF_COLOR("물색")

IF ARG > BASE:MASTER:스톡
	IF MEMO_EXP6_ALL
		PRINTFORM 그리고,
	ELSE
		PRINTFORM %마스터는()% 
	ENDIF
	IF ARG - BASE:MASTER:스톡 >= 2
		PRINTFORML {ARG - BASE:MASTER:스톡}회나 다운해 버렸다！
	ELSE
		PRINTFORML {ARG - BASE:MASTER:스톡}회 다운해 버렸다！
	ENDIF
ENDIF

RESETCOLOR

WAIT

PRINTL 


