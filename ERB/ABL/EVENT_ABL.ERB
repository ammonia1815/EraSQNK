;主に, 특수な技能などの설정関数を置く. 
;-------------------------------------------------
@GET_EXABL, ARG
#DIM LCOUNT
SIF ARG <= 0
	RETURN 0

;具体的なＬＶの算出
FOR LCOUNT, 30, 35
	LOCALS = %ABLNAME:LCOUNT%
	SELECTCASE LOCALS
	CASE "혀놀림"
		LOCAL = (EXP:ARG:펠라경험 + EXP:ARG:커닐링구스경험 + EXP:ARG:키스경험)/3
	CASE "손가락놀림"
		LOCAL = EXP:ARG:수음경험
	CASE "발놀림"
		LOCAL = EXP:ARG:풋잡경험
	CASE "허리놀림"
		LOCAL = EXP:ARG:허리사용경험
	CASE "마유"
		LOCAL = EXP:ARG:파이즈리경험
	ENDSELECT
	IF ARG == MASTER
		TIMES LOCAL , 0.50
	ELSEIF TALENT:ARG:현명함
		TIMES LOCAL , 1.50
	ELSEIF TALENT:ARG:미련함
		TIMES LOCAL , 0.70
	ENDIF
	ABL:ARG:LOCALS = TALENT:ARG:LOCALS + MAX(GET_EXP_LV(LOCAL) - 1, 0)
NEXT

CALL GET_EXABL2, ARG


@GET_EXABL2, ARG
SIF ARG <= 0
	RETURN 0
;ＶＡ명기도보정(성전환に備えて男でもＶ명기도をここで설정)
IF MIN(TALENT:ARG:Ｖ명기, TALENT:ARG:Ａ명기, TALENT:ARG:목명기, TALENT:ARG:요도명기) == 0
	SELECTCASE RAND:100 + TALENT:ARG:상위몽마*10
	CASE IS <= 49
		TALENT:ARG:Ｖ명기 = 1
	CASE IS <= 89
		TALENT:ARG:Ｖ명기 = 1 + RAND:2
	CASEELSE
		TALENT:ARG:Ｖ명기 = 1 + RAND:3
	ENDSELECT

	SELECTCASE RAND:100 + TALENT:ARG:상위몽마*10
	CASE IS <= 49
		TALENT:ARG:Ａ명기 = 1
	CASE IS <= 89
		TALENT:ARG:Ａ명기 = 1 + RAND:2
	CASEELSE
		TALENT:ARG:Ａ명기 = 1 + RAND:3
	ENDSELECT

	SELECTCASE RAND:100 + TALENT:ARG:상위몽마*10
	CASE IS <= 49
		TALENT:ARG:목명기 = 1
	CASE IS <= 89
		TALENT:ARG:목명기 = 1 + RAND:2
	CASEELSE
		TALENT:ARG:목명기 = 1 + RAND:3
	ENDSELECT

	SELECTCASE RAND:100 + TALENT:ARG:상위몽마*10
	CASE IS <= 49
		TALENT:ARG:요도명기 = 1
	CASE IS <= 89
		TALENT:ARG:요도명기 = 1 + RAND:2
	CASEELSE
		TALENT:ARG:요도명기 = 1 + RAND:3
	ENDSELECT
ENDIF

;Ｖ명기LV（20未満だと緩く, 70이상だと명기）
CALL GET_EXABL_V, ARG
ABL:ARG:Ｖ명기도 = ABL_V(ARG)

;Ａ명기LV（20未満だと緩く, 70이상だと명기）
CALL GET_EXABL_A, ARG
ABL:ARG:Ａ명기도 = ABL_A(ARG)

;喉LV(딥펠라や이라마치오に関係)
CALL GET_EXABL_T, ARG
ABL:ARG:목명기도 = ABL_T(ARG)

;の拡張状況（には명기도は無い…のは過去の話である）
CALL GET_EXABL_U, ARG
ABL:ARG:요도명기도 = ABL_U(ARG)


@GET_EXABL_V, ARG
SIF ARG <= 0
	RETURN 0
;질のLV取得
;-------------------------------------------------
;질などの判定ベース
;基本的に. 入れるものの大きさ基準値(例：바이브や페니스なら1, 太い바이브なら2)とペアで使う
;SEXの時の사정게이지増加などを見るときは, 　基本値(Ｖ活用LV)*보정(Ｖ확장LVの過부족)　で算出する. 
;Ｖ확장조교の時には, 入れるものの大きさ基準値とＶ확장LVを比較して아픔などを決める. 
;基準値+1の値の時がちょうど良い. 
;その際, Ｖ活用LVが大きさ基準値이상なら, +1できる. 
;슬라임は活用が巧いが, 확장LVも+1される
;-------------------------------------------------
IF TALENT:ARG:남자 || TALENT:ARG:처녀 == 1
	ABL:ARG:Ｖ놀림 = 0
	ABL:ARG:Ｖ확장 = 0
	RETURN 0
ENDIF

;Ｖ活用LV
;Ｖ경험, Ｖ성교경험, Ｖ破壊経験, Ｖ감각, Ｖ민감, 음호で判定
;Ｖ확장LVの方も同時に用いることが多い. 
ABL:ARG:Ｖ놀림 = 1
VARSET LOCAL
LOCAL:0 = EXP:ARG:Ｖ경험 - EXP_GAIN(0, ARG)
LOCAL:12 = EXP:ARG:Ｖ성교경험 - EXP_GAIN(12, ARG)
LOCAL:53 = EXP:ARG:Ｖ확장경험 - EXP_GAIN(53, ARG)

SIF ABL:ARG:Ｖ감각 + TALENT:ARG:Ｖ민감*2 >= 4
	ABL:ARG:Ｖ놀림 += 1

IF LOCAL:0 >= 150
	ABL:ARG:Ｖ놀림 += 2
ELSEIF LOCAL:0 >= 50
	ABL:ARG:Ｖ놀림 += 1
ENDIF
SIF LOCAL:0 < LOCAL:53 * 10 && LOCAL:0 >= 50
	ABL:ARG:Ｖ놀림 -= 1

IF LOCAL:12 >= 150 || LOCAL:0 >= 1000
	ABL:ARG:Ｖ놀림 += 2
ELSEIF LOCAL:12 >= 50 || LOCAL:0 >= 600
	ABL:ARG:Ｖ놀림 += 1
ENDIF

IF LOCAL:12 < LOCAL:53 * 5 && (LOCAL:12 >= 50 || LOCAL:0 >= 600)
	ABL:ARG:Ｖ놀림 -= 1
ELSEIF ABL:ARG:Ｖ놀림 < 2 && COND("몽마", ARG)
	ABL:ARG:Ｖ놀림 += 1
ENDIF
;확장적정ボーナス
SIF ABL:ARG:Ｖ놀림 < 3 && TALENT:ARG:확장적정
	ABL:ARG:Ｖ놀림 += 1

IF TALENT:ARG:음호
	ABL:ARG:Ｖ놀림 += 1
	SIF LOCAL:12 >= 300 || LOCAL:0 >= 2000
		ABL:ARG:Ｖ놀림 += 1
ENDIF
;슬라임
SIF TALENT:ARG:슬라임
	ABL:ARG:Ｖ놀림 += 2
;장사
SIF TALENT:ARG:장사
	ABL:ARG:Ｖ놀림 += 1
;호색
SIF TALENT:ARG:호색 && ABL:ARG:Ｖ놀림 < 3
	ABL:ARG:Ｖ놀림 += 1
;損傷したら、即座に아픔で操れなくなる(확장적정があるなら怪我が治れば元通り)
SIF TALENT:ARG:Ｖ손상 || (EXP:ARG:Ｖ손상경험 && TALENT:ARG:확장적정 == 0)
	ABL:ARG:Ｖ놀림 = MULTIPLY(ABL:ARG:Ｖ놀림, 100 - MIN(EXP:ARG:Ｖ손상경험 + TALENT:ARG:Ｖ손상, 3)*30)

;보지に근육이완제
IF GETBIT(TEQUIP:ARG:근육이완제, 2)
	IF TALENT:ARG:약독내성
		ABL:ARG:Ｖ놀림 /= 2
	ELSE
		ABL:ARG:Ｖ놀림 = 0
	ENDIF
ENDIF

;처녀で無い場合の확장度については, ここで最低値だけ管理して個別にABLUP画面で올린다ものとする
IF TALENT:ARG:작은몸집
	LOCAL = 0
ELSE
	LOCAL = 1
ENDIF
;슬라임
SIF TALENT:ARG:슬라임
	LOCAL += 1

ABL:ARG:Ｖ확장 = MAX(ABL:ARG:Ｖ확장, LOCAL)

;-------------------------------------------------
;Ｖ명기도
;Ｖ경험, Ｖ성교경험, Ｖ확장경험のバランスで명기도決定. 음호の보너스もここで処理
;확장적정があると손상が発生しているとき以外のペナルティは無視
;ARGSに"절대치"を入れると疲れや経験を無視した最高値が出る
;-------------------------------------------------
@ABL_V(ARG, ARGS)
#FUNCTION
SIF ARG <= 0
	RETURNF 0
SIF TALENT:ARG:남자
	RETURNF 0

VARSET LOCAL

;快感の基準はＶ活用LV
LOCAL = 15 + ABL:ARG:Ｖ놀림*5 + ABL:ARG:Ｖ확장 + TALENT:ARG:Ｖ명기*5

SELECTCASE ARGS
CASE "절대치"
	LOCAL:1 = EXP:ARG:Ｖ경험
	LOCAL:2 = EXP:ARG:Ｖ성교경험
	LOCAL:3 = EXP:ARG:Ｖ확장경험
CASEELSE
	LOCAL:1 = EXP:ARG:Ｖ경험 - EXP_GAIN(0, ARG)
	LOCAL:2 = EXP:ARG:Ｖ성교경험 - EXP_GAIN(12, ARG)
	LOCAL:3 = EXP:ARG:Ｖ확장경험 - EXP_GAIN(53, ARG)
ENDSELECT

;Ｖ확장경험がＶ경험の４％を超えているとペナルティ発生(Ｖ확장LVがＶ놀림LVを上回っていると, かなり厳しい)
;확장적정がある場合にはペナルティは無い
IF TALENT:ARG:확장적정
	LOCAL:4 = 100
ELSE
	;Ｖ확장ＬＶがＶ놀림ＬＶを上回っていて, かつ, Ｖ경험100이상orＶ확장ＬＶが４이상
	IF ABL:ARG:Ｖ확장 > ABL:ARG:Ｖ놀림 && (LOCAL:1 >= 100 || ABL:ARG:Ｖ확장 >= 4)
		;Ｖ명기도を, （Ｖ확장ＬＶとＶ놀림ＬＶの差）×（Ｖ경험の豊富さに応じた値）だけ減らす. ただし最低でも５は残す
		LOCAL = MAX(LOCAL - (ABL:ARG:Ｖ확장 - ABL:ARG:Ｖ놀림) * GET_EXP_LV(EXP:ARG:Ｖ경험) * 2, 5)

		;LOCAL:4はＶ명기도に最終的にかかる보정の値. 100を越えると명기도が上がり, 100未満だと下がる
		;ここでの式は, 100 * (100 + Ｖ경험*2) / (100 + Ｖ경험 + Ｖ확장경험*25) なので, Ｖ확장경험が無い場合には100이상になる. （상한100なのでペナルティは無いということ）
		;そして, Ｖ확장경험*25がＶ경험を越えていると, 加速度的に명기도が下がる事となる
		LOCAL:4 = LIMIT(100 * (100 + LOCAL:1*2) / (100 + LOCAL:1 + LOCAL:3*25), 20, 100)
	ELSE
		;Ｖ확장경험*10がＶ경험を越えていると, 加速度的に명기도が下がる事となる
		LOCAL:4 = LIMIT(100 * (100 + LOCAL:1*2) / (100 + LOCAL:1 + LOCAL:3*10), 20, 100)
	ENDIF
ENDIF

;Ｖ성교경험がＶ경험の25％を超えていると보너스発生
;ここでの式は, LOCAL:4 * (100 + Ｖ경험 + Ｖ성교경험*4 + 쾌Ｖ의 구슬/5000) / (100 + Ｖ경험*2) なので, 
;（Ｖ성교경험*4+쾌Ｖ의 구슬/5000）がＶ경험を越えていると, 명기도が上がる事となる（최대で２倍まで）
LOCAL:4 = LIMIT(LOCAL:4 * (100 + LOCAL:1 + LOCAL:2*4 + JUEL:ARG:쾌Ｖ/5000) / (100 + LOCAL:1*2), 20, 200)

LOCAL = MULTIPLY(LOCAL, LOCAL:4 + TALENT:ARG:음호*50 + TALENT:ARG:장사*10)
SIF TALENT:ARG:Ｖ손상
	LOCAL /= TALENT:ARG:Ｖ손상 + 1

RETURNF LOCAL


@GET_EXABL_A, ARG
;애널のLV取得
SIF ARG <= 0
	RETURN 0

VARSET LOCAL

LOCAL:1 = EXP:ARG:Ａ경험 - EXP_GAIN(1, ARG)
LOCAL:13 = EXP:ARG:Ａ성교경험 - EXP_GAIN(13, ARG)
LOCAL:57 = EXP:ARG:Ａ확장경험 - EXP_GAIN(57, ARG)

ABL:ARG:Ａ놀림 = 0
SIF ABL:ARG:Ａ감각 + TALENT:ARG:Ａ민감*2 >= 4
	ABL:ARG:Ａ놀림 += 1

IF LOCAL:1 >= 150
	ABL:ARG:Ａ놀림 += 3
ELSEIF LOCAL:1 >= 50
	ABL:ARG:Ａ놀림 += 2
ELSEIF LOCAL:1 >= 20
	ABL:ARG:Ａ놀림 += 1
ENDIF
SIF LOCAL:1 < LOCAL:57 * 10 && LOCAL:1 >= 20
	ABL:ARG:Ａ놀림 -= 1

IF LOCAL:13 >= 150 || LOCAL:1 >= 1000
	ABL:ARG:Ａ놀림 += 2
ELSEIF LOCAL:13 >= 50 || LOCAL:1 >= 600
	ABL:ARG:Ａ놀림 += 1
ENDIF
SIF LOCAL:13 < LOCAL:57 * 5 && (LOCAL:13 >= 50 || LOCAL:1 >= 600)
	ABL:ARG:Ａ놀림 -= 1
;확장적정ボーナス
SIF ABL:ARG:Ａ놀림 < 3 && TALENT:ARG:확장적정
	ABL:ARG:Ａ놀림 += 1
;애널광
IF TALENT:ARG:애널광
	ABL:ARG:Ａ놀림 += 1
	SIF LOCAL:13 >= 300 || LOCAL:1 >= 2000
		ABL:ARG:Ａ놀림 += 1
ENDIF
;슬라임
SIF TALENT:ARG:슬라임
	ABL:ARG:Ａ놀림 += 2
;장사
SIF TALENT:ARG:장사
	ABL:ARG:Ａ놀림 += 1
;호색
SIF TALENT:ARG:호색 && ABL:ARG:Ａ놀림 < 3
	ABL:ARG:Ａ놀림 += 1

;損傷したら、即座に아픔で操れなくなる(확장적정があるなら怪我が治れば元通り)
SIF TALENT:ARG:Ａ손상 || (EXP:ARG:Ａ손상경험 && TALENT:ARG:확장적정 == 0)
	ABL:ARG:Ａ놀림 = MULTIPLY(ABL:ARG:Ａ놀림, 100 - MIN(EXP:ARG:Ａ손상경험 + TALENT:ARG:Ａ손상, 3)*30)

;애널に근육이완제
IF GETBIT(TEQUIP:ARG:근육이완제, 0)
	IF TALENT:ARG:약독내성
		ABL:ARG:Ａ놀림 /= 2
	ELSE
		ABL:ARG:Ａ놀림 = 0
	ENDIF
ENDIF

;확장度については, ここで最低値だけ管理して個別にABLUP画面で올린다ものとする
IF TALENT:ARG:작은몸집
	LOCAL = 0
ELSE
	LOCAL = 1
ENDIF
;슬라임
SIF TALENT:ARG:슬라임
	LOCAL += 1

ABL:ARG:Ａ확장 = MAX(ABL:ARG:Ａ확장, LOCAL)

;-------------------------------------------------
;Ａ명기도
;Ａ경험, Ａ성교경험, Ａ확장경험のバランスで명기도決定. 애널광の보너스もここ
;확장적정があると손상が発生しているとき以外のペナルティは無視
;ARGSに"절대치"を入れると疲れや経験を無視した最高値が出る
;-------------------------------------------------
@ABL_A(ARG, ARGS)
#FUNCTION
SIF ARG <= 0
	RETURNF 0

VARSET LOCAL

;快感の基準はＡ活用LV
LOCAL = 25 + ABL:ARG:Ａ놀림*5 + ABL:ARG:Ａ확장 + TALENT:ARG:Ａ명기*5

SELECTCASE ARGS
CASE "절대치"
	LOCAL:1 = EXP:ARG:Ａ경험
	LOCAL:2 = EXP:ARG:Ａ성교경험
	LOCAL:3 = EXP:ARG:Ａ확장경험
CASEELSE
	LOCAL:1 = EXP:ARG:Ａ경험 - EXP_GAIN(1, ARG)
	LOCAL:2 = EXP:ARG:Ａ성교경험 - EXP_GAIN(13, ARG)
	LOCAL:3 = EXP:ARG:Ａ확장경험 - EXP_GAIN(57, ARG)

ENDSELECT

;Ａ확장경험がＡ경험の４％を超えているとペナルティ発生
IF TALENT:ARG:확장적정
	LOCAL:4 = 100
ELSE
	IF ABL:ARG:Ａ확장 > ABL:ARG:Ａ놀림 && (LOCAL:1 >= 100 || ABL:ARG:Ａ확장 >= 4)
		LOCAL = MAX(LOCAL - (ABL:ARG:Ａ확장 - ABL:ARG:Ａ놀림) * GET_EXP_LV(EXP:ARG:Ａ경험) * 2, 5)
		LOCAL:4 = LIMIT(100 * (100 + LOCAL:1*2) / (100 + LOCAL:1 + LOCAL:3*25), 20, 100)
	ELSE
		LOCAL:4 = LIMIT(100 * (100 + LOCAL:1*2) / (100 + LOCAL:1 + LOCAL:3*10), 20, 100)
	ENDIF
ENDIF

;Ａ성교경험がＡ경험の20％を超えていると보너스発生
LOCAL:4 = LIMIT(LOCAL:4 * (100 + LOCAL:1 + LOCAL:2*5 + JUEL:ARG:쾌Ａ/5000) / (100 + LOCAL:1*2), 20, 200)

LOCAL = MULTIPLY(LOCAL, LOCAL:4 + TALENT:ARG:애널광*50 + TALENT:ARG:장사*10)
SIF TALENT:ARG:Ａ손상
	LOCAL /= TALENT:ARG:Ａ손상 + 1

RETURNF LOCAL


@GET_EXABL_T, ARG
;-------------------------------------------------
;喉を使う時の判定ベース
;咥える物のサイズと比較して判定
;페니스の太さがきつい時は奥まで飲み込めないし, 余裕も無い
;슬라임は보너스
;基準値+1の値の時がちょうど良い. 
;-------------------------------------------------
SIF ARG <= 0
	RETURN 0
;喉の확장LV取得
;펠라경험, 기교, 혀놀림, 몽마
VARSET LOCAL
LOCAL = EXP:ARG:펠라경험 - EXP_GAIN(22, ARG)
LOCAL += 5 * (EXP:ARG:목성교경험 - EXP_GAIN(76, ARG) )

;(펠라경험など, 목확장LV)の対応は, (150, 1), (300, 2), (600, 3), (1000, 4)
ABL:ARG:목확장 = LIMIT(GET_EXP_LV(LOCAL) - 2, 0, 4)

IF ABL:ARG:목확장 <= 1
	IF TALENT:ARG:상위몽마
		ABL:ARG:목확장 += 1
	ELSEIF LOCAL >= 50 && ABL:ARG:기교 >= 2 && TALENT:ARG:몽마
		ABL:ARG:목확장 += 1
	ENDIF
ENDIF
SIF TALENT:ARG:작은몸집 && ABL:ARG:목확장 >= 1
	ABL:ARG:목확장 -= 1
SIF TALENT:ARG:마도인형 || TALENT:ARG:슬라임 || TALENT:ARG:라미아
	ABL:ARG:목확장 = MAX(ABL:ARG:목확장 + 1, 3)
SIF ABL:ARG:목확장 <= TALENT:ARG:혀놀림
	ABL:ARG:목확장 += 1

;요정は半分
SIF TALENT:ARG:요정
	ABL:ARG:목확장 /= 2

@ABL_T(ARG)
#FUNCTION
SIF ARG <= 0
	RETURNF 0
;快感の基準は기교や봉사
LOCAL = 10 + ABL:ARG:기교 + ABL:ARG:봉사정신*2 + TALENT:ARG:목명기*5
SIF TALENT:ARG:슬라임
	LOCAL += 5
SIF TALENT:ARG:구토반사내성
	LOCAL += 5

SELECTCASE ABL:ARG:목확장
CASE 1
	LOCAL += 5
CASE 2
	LOCAL = MULTIPLY(LOCAL, 120) + 10
CASE 3
	LOCAL = MULTIPLY(LOCAL, 150) + 20
CASE IS >= 4
	LOCAL = MULTIPLY(LOCAL, 200) + 30
ENDSELECT

RETURNF LOCAL

@GET_EXABL_U, ARG
;-------------------------------------------------
;おもに요도の太さ ABL:요도확장 の判定をする（これは페니스サイズなどに即した値）
;CSTR:3がＶ요도확장度で, CSTR:4がＰ요도확장度である. 二つに相関関係は一切ない
;それらが설정されていないなら, まずはここで初期설정する
;-------------------------------------------------
SIF ARG <= 0
	RETURN 0

;Ｖ요도확장度の初期설정
SIF CSTR:ARG:3 == ""
	CSTR:ARG:3 = %CONDS("Ｖ요도の初期확장度", ARG)%
;Ｐ요도확장度の初期설정
SIF CSTR:ARG:4 == ""
	CSTR:ARG:4 = %CONDS("Ｐ요도の初期확장度", ARG)%

;확장度
SELECTCASE CALCF("요도径", ARG)
;ここまでは손가락삽입も무리
CASE IS < 15
	ABL:ARG:요도확장 = -2
;ここまでは페니스삽입は무리
CASE IS < 26
	ABL:ARG:요도확장 = -1
;페니스삽입가능になります
CASE IS < 33
	ABL:ARG:요도확장 = 0
;이하7mm刻み
CASE IS < 40
	ABL:ARG:요도확장 = 1
CASE IS < 47
	ABL:ARG:요도확장 = 2
;現状は３が최대とする
CASEELSE
	ABL:ARG:요도확장 = 3
ENDSELECT


@ABL_U(ARG)
#FUNCTION
SIF ARG <= 0
	RETURNF 0

;快感の基準は요도경험
LOCAL = 20 + GET_EXP_LV(EXP:ARG:요도경험)*2 + GET_EXP_LV(EXP:ARG:요도성교경험)*5 + TALENT:ARG:요도명기*5
SIF TALENT:ARG:슬라임
	LOCAL += 5
SIF TALENT:ARG:요도성감
	LOCAL += 5
SIF TALENT:ARG:요도광
	LOCAL += 20

SELECTCASE ABL:ARG:요도확장
CASE 0
	LOCAL += 5
CASE 1
	LOCAL = MULTIPLY(LOCAL, 120) + 10
CASE 2
	LOCAL = MULTIPLY(LOCAL, 150) + 20
CASE IS >= 3
	LOCAL = MULTIPLY(LOCAL, 200) + 30
ENDSELECT

RETURNF LOCAL

;-------------------------------------------------
;～놀림など, 特技は調整の簡便さ等の観点から, csvには記述せずに, ここで全て処理することにする. 
;ARGSに소질名, ARGにキャラを代入する(ARG==0でTARGETが居るならARG = TARGET)
;-------------------------------------------------
@SET_EXTALENT(ARGS, ARG)
#FUNCTION

SIF ARG == 0 && TARGET
	ARG = TARGET

VARSET LOCAL

SELECTCASE ARGS
CASE "혀놀림"
	SELECTCASE BASE:ARG:종족
	CASE 6, 17, 35, 46 TO 48, 57
		LOCAL = 3
	CASE 10, 21, 29, 34, 41, 42
		LOCAL = 2
	CASE 1, 13, 22, 37, 43
		LOCAL = 1
	ENDSELECT
CASE "손가락놀림"
	SELECTCASE BASE:ARG:종족
	CASE 18, 48
		LOCAL = 3
	CASE 3, 17, 21
		LOCAL = 2
	CASE 1, 22, 46, 54, 41, 61
		LOCAL = 1
	ENDSELECT
CASE "발놀림"
	SELECTCASE BASE:ARG:종족
	CASE 38, 48
		LOCAL = 3
	CASE 11, 12, 39, 41, 42
		LOCAL = 2
	CASE 1, 14, 17, 21, 31, 40
		LOCAL = 1
	ENDSELECT
CASE "허리놀림"
	SELECTCASE BASE:ARG:종족
	CASE 48
		LOCAL = 3
	CASE 7, 14, 18, 21, 22, 23, 26, 33, 34, 61
		LOCAL = 2
	CASE 1, 6, 29, 30, 31, 35, 42, 43, 46, 47, 50
		LOCAL = 1
	ENDSELECT
CASE "마유"
	SIF TALENT:ARG:남자
		RETURNF 0

	SELECTCASE BASE:ARG:종족
	CASE 29, 34, 48
		LOCAL = 3
	CASE 10, 14
		LOCAL = 2
	CASE 1, 18, 21, 35, 43, 61
		LOCAL = 1
	ENDSELECT
CASE "외견연령"
	;설정済みなら戻る
	SIF TALENT:ARG:외견연령
		RETURNF TALENT:ARG:외견연령

	;유치とか小柄とかそういうのを抜きにした외견연령
	SELECTCASE BASE:ARG:종족
	;例外のある종족…ただしここでのCFLAG:ARG:209による分岐は특수なキャラメイクの時のみ有効
	;通常の소환をするときには無意味なので、初回조교前の구상선택画面での설정も必須となる
	;스킬라
	CASE 15
		IF CFLAG:ARG:209 == 2
			LOCAL = 2
		ELSE
			LOCAL = 3
		ENDIF
	;뱀파이어
	CASE 25
		IF CFLAG:ARG:209 == 3
			LOCAL = 2
		ELSE
			LOCAL = 3
		ENDIF
	;おとな
	CASE 10, 14, 18, 27, 47, 49
		LOCAL = 4
	;아이
	CASE 2, 4, 31, 40
		LOCAL = 1
	;소녀
	CASE 8, 9, 20, 21, 32, 33, 39, 44, 50, 52, 55, 57, 59, 60
		LOCAL = 2
	CASE 98
		LOCAL = TALENT:MASTER:외견연령
	;언니
	CASEELSE
		LOCAL = 3
	ENDSELECT

CASE "흡정체질"
	SIF (TALENT:ARG:몽마 && ABL:ARG:정액중독 >= 5) || (TALENT:ARG:상위몽마 && ABL:ARG:정액중독 >= 3)
		LOCAL += 1
	;악마
	SIF TALENT:ARG:꼬리 == 6 || TALENT:ARG:뿔 == 6 || TALENT:ARG:날개 == 6
		LOCAL += 1
	SIF TALENT:ARG:흡혈귀
		LOCAL += 1
	SIF TALENT:ARG:상위몽마 >= 2
		LOCAL += 1
	SIF TALENT:ARG:마법사 >= 2 && TALENT:ARG:조합지식 && TALENT:ARG:금단의지식
		LOCAL += 1

	;소질関係なく, もって居てしかるべきキャラを判定. 重複しないように注意! 
	SELECTCASE NO:ARG
	CASE 3
		;알라우네
		SIF CFLAG:ARG:8 == 2
			LOCAL += 1
	CASE 8, 9
		;밴시, 알바스타
		SIF CFLAG:ARG:8 == 2 || CFLAG:ARG:8 == 4
			LOCAL += 1
	;아라크네, 루시펠
	CASE 18, 21
		LOCAL += 1
	ENDSELECT

	;정액각인3LV이상
	SIF MARK:ARG:정액각인 >= 3
		LOCAL += 1

CASE "구토반사내성"
	SIF TALENT:ARG:슬라임 || TALENT:ARG:마도인형 || TALENT:ARG:라미아
		RETURNF 1
CASE "확장적정"
	SIF TALENT:ARG:슬라임
		RETURNF 1
ENDSELECT

RETURNF LOCAL


;-------------------------------------------------
;さまざまな소질の取得や変動の総合管理の場
;-------------------------------------------------
@GET_EXTALENT, ARG
#DIM LCOUNT
SIF ARG <= 0
	RETURN 0

IF EXP:ARG:조교횟수 == 0 && COND("子孫", ARG) == 0
	SIF TALENT:ARG:혀놀림 == 0
		TALENT:ARG:혀놀림 = SET_EXTALENT("혀놀림", ARG)
	SIF TALENT:ARG:손가락놀림 == 0
		TALENT:ARG:손가락놀림 = SET_EXTALENT("손가락놀림", ARG)
	SIF TALENT:ARG:발놀림 == 0
		TALENT:ARG:발놀림 = SET_EXTALENT("발놀림", ARG)
	SIF TALENT:ARG:허리놀림 == 0
		TALENT:ARG:허리놀림 = SET_EXTALENT("허리놀림", ARG)
	SIF TALENT:ARG:마유 == 0
		TALENT:ARG:마유 = SET_EXTALENT("마유", ARG)
ENDIF
SIF TALENT:ARG:남자
	TALENT:ARG:마유 = 0

;-------------------------------------------------
;外見, 雰囲気由来の年齢層判定. MASTER(당신)は언니orお兄さん枠. 
;=0 아이　=1 소녀(-16)　=2 언니(17-20前半)　=3おとな(20後半-)
;-------------------------------------------------
TALENT:ARG:외견연령 = SET_EXTALENT("외견연령", ARG)

IF EXP:ARG:Ａ경험 == 0
	TALENT:ARG:Ａ처녀 = 1
ELSEIF EXP:ARG:Ａ성교경험
	TALENT:ARG:Ａ처녀 = 0
ENDIF

IF PENIS(ARG) && EXP:ARG:Ｖ삽입경험 == 0
	TALENT:ARG:동정 = 1
ELSEIF PENIS(ARG) == 0
	TALENT:ARG:동정 = 0
ENDIF

SIF TALENT:ARG:젖기쉬움
	TALENT:ARG:젖기어려움 = 0

;흡정체질
TALENT:ARG:흡정체질 = SET_EXTALENT("흡정체질", ARG)

;구토반사내성
SIF SET_EXTALENT("구토반사내성", ARG)
	TALENT:ARG:구토반사내성 = 1
;확장적정
SIF SET_EXTALENT("확장적정", ARG)
	TALENT:ARG:확장적정 = 1

;大마법사
IF TALENT:ARG:마법사 == 2
	FOR LCOUNT, 230, 260
		TALENT:ARG:LCOUNT = 0
	NEXT
ENDIF

;분신・分裂
IF TALENT:ARG:비젼 == 0
	;ハツメは最初から覚えている
	IF COND("Ｃハツメ", ARG)
		TALENT:ARG:비젼 = 2
	;後で시간が出来たら調整する予定
	ELSEIF TALENT:ARG:슬라임
	;	TALENT:ARG:비젼 = 3
	ENDIF
ENDIF

;가창기능
IF TALENT:ARG:가창기능
	SELECTCASE EXP:ARG:가창경험
	CASE IS >= 500
		TALENT:ARG:가창기능 = 5
	CASE IS >= 300
		TALENT:ARG:가창기능 = 4
	CASE IS >= 150
		TALENT:ARG:가창기능 = 3
	CASE IS >= 50
		TALENT:ARG:가창기능 = 2
	CASEELSE
		TALENT:ARG:가창기능 = 1
	ENDSELECT
ENDIF

;各種知識などなど
IF TALENT:ARG:조교지식 == 0 && COND("子孫", ARG) == 0
	SELECTCASE BASE:ARG:종족
	CASE 1
		IF TALENT:ARG:도착적 && RAND:2
			TALENT:ARG:조교지식 = 1p2
		ELSEIF RAND:2
			TALENT:ARG:조교지식 = 1p0
		ELSE
			TALENT:ARG:조교지식 = 1p1
		ENDIF
	CASE 3
		IF CFLAG:ARG:8 == 2 && RAND:3 == 0
			TALENT:ARG:조교지식 = 1p6
		ELSEIF RAND:2
			TALENT:ARG:조교지식 = 1p3
		ELSE
			TALENT:ARG:조교지식 = 1p1
		ENDIF
	CASE 5
		IF RAND:5
			TALENT:ARG:조교지식 = 1p0
		ELSE
			TALENT:ARG:조교지식 = 1p6
		ENDIF
	CASE 6
		SIF CFLAG:ARG:8 == 2
			TALENT:ARG:조교지식 = 1p1
	CASE 10
		IF RAND:5 == 0
			TALENT:ARG:조교지식 = 1p0
		ELSEIF RAND:4 == 0
			TALENT:ARG:조교지식 = 1p1
		ELSEIF RAND:3 == 0
			TALENT:ARG:조교지식 = 1p2
		ELSEIF RAND:2
			TALENT:ARG:조교지식 = 1p3
		ELSE
			TALENT:ARG:조교지식 = 1p5
		ENDIF
	CASE 14
		IF RAND:5 == 0
			TALENT:ARG:조교지식 = 1p0
		ELSEIF RAND:4 == 0
			TALENT:ARG:조교지식 = 1p1
		ELSEIF RAND:3 == 0
			TALENT:ARG:조교지식 = 1p3
		ELSEIF RAND:2
			TALENT:ARG:조교지식 = 1p5
		ELSE
			TALENT:ARG:조교지식 = 1p6
		ENDIF
	CASE 15
		IF RAND:2
			TALENT:ARG:조교지식 = 1p0
		ELSE
			TALENT:ARG:조교지식 = 1p5
		ENDIF
	CASE 16
		IF CFLAG:ARG:8 == 2 && RAND:2
			TALENT:ARG:조교지식 = 1p2
		ELSEIF RAND:2
			TALENT:ARG:조교지식 = 1p0
		ELSE
			TALENT:ARG:조교지식 = 1p1
		ENDIF
	CASE 17
		TALENT:ARG:조교지식 = 1p2
	CASE 18
		SIF CFLAG:ARG:8 == 3
			TALENT:ARG:조교지식 = 1p2
	CASE 19
		TALENT:ARG:조교지식 = 1p5
	CASE 21
		TALENT:ARG:조교지식 = 1p3
	CASE 22
		TALENT:ARG:조교지식 = 1p0
	CASE 25
		TALENT:ARG:조교지식 = 1p1
	CASE 33
		TALENT:ARG:조교지식 = 1p5
	CASE 34
		TALENT:ARG:조교지식 = 1p1
	CASE 35
		TALENT:ARG:조교지식 = 1p0
	CASE 38
		TALENT:ARG:조교지식 = 1p1
	CASE 46
		TALENT:ARG:조교지식 = 1p0
	CASE 48
		TALENT:ARG:조교지식 = 1p0
	CASE 50
		TALENT:ARG:조교지식 = 1p5
	CASE 58
		IF RAND:6 == 0
			TALENT:ARG:조교지식 = 1p5
		ELSEIF RAND:5 == 0
			TALENT:ARG:조교지식 = 1p4
		ELSEIF RAND:4 == 0
			TALENT:ARG:조교지식 = 1p3
		ELSEIF RAND:3 == 0
			TALENT:ARG:조교지식 = 1p2
		ELSEIF RAND:2
			TALENT:ARG:조교지식 = 1p1
		ELSE
			TALENT:ARG:조교지식 = 1p0
		ENDIF
	ENDSELECT
ENDIF

;初期のLV설정(短編ＬＶが目安)
CALL SET_LV_MUMA, ARG


@SET_LV_MUMA, ARG
SIF ARG <= 0
	RETURN 0
SIF ABL:ARG:ＬＶ
	RETURN 0

VARSET LOCAL
SELECTCASE NO:ARG
CASE 0
	IF CONFIG("ＥＡＳＹ")
		LOCAL = 1 + ABL:ARG:기교*3
	ELSEIF CONFIG("ＮＯＲＭＡＬ")
		LOCAL = 1 + ABL:ARG:기교*2
	ELSEIF CONFIG("ＨＡＲＤ")
		LOCAL = 1 + ABL:ARG:기교
	ENDIF
CASE 1
	;릴리스
	IF CFLAG:ARG:8 == 4
		LOCAL = 25
	;아스모데
	ELSEIF CFLAG:ARG:8 == 3
		LOCAL = 19
	;나이트메어
	ELSEIF CFLAG:ARG:8 == 2
		LOCAL = 14
	;서큐버스
	ELSE
		LOCAL = 5 + CFLAG:ARG:8*5
	ENDIF
CASE 2
	;릴림
	IF CFLAG:ARG:8 == 4
		LOCAL = 24
	;알프
	ELSEIF CFLAG:ARG:8 == 2
		LOCAL = 12
	;임프
	ELSE
		LOCAL = 3 + CFLAG:ARG:8*5
	ENDIF
CASE 3
	;스코그슬로
	IF CFLAG:ARG:8 == 3
		LOCAL = 18
	;알라우네
	ELSEIF CFLAG:ARG:8 == 2
		LOCAL = 13
	;드라이어드
	ELSE
		LOCAL = 5 + CFLAG:ARG:8*4
	ENDIF
CASE 4
	;티타니아
	IF CFLAG:ARG:8 == 3
		LOCAL = 18
	;리난시
	ELSEIF CFLAG:ARG:8 == 2
		LOCAL = 11
	;픽시
	ELSE
		LOCAL = 3 + CFLAG:ARG:8*5
	ENDIF
CASE 5
	;운디네
	IF CFLAG:ARG:8 == 1
		LOCAL = 5
	;에리얼
	ELSEIF CFLAG:ARG:8 == 3
		LOCAL = 11
	;실프
	ELSE
		LOCAL = 5 + CFLAG:ARG:8*3
	ENDIF
CASE 6
	;스핑크스
	IF CFLAG:ARG:8 == 4
		LOCAL = 19
	;검정고양이
	ELSEIF CFLAG:ARG:8 == 3
		LOCAL = 6
	;캣시
	ELSEIF CFLAG:ARG:8 == 2
		LOCAL = 12
	;네코마타
	ELSE
		LOCAL = 6 + CFLAG:ARG:8*3
	ENDIF
CASE 7
	;케르베로스
	IF CFLAG:ARG:8 == 3
		LOCAL = 17
	;워울프
	ELSE
		LOCAL = 6 + CFLAG:ARG:8*3
	ENDIF
CASE 8 || NO:ARG == 9
	;알바스타
	IF CFLAG:ARG:8 == 4
		LOCAL = 20
	;밴시
	ELSEIF CFLAG:ARG:8 == 2
		LOCAL = 12
	;빌리져
	ELSE
		LOCAL = 3 + CFLAG:ARG:8*4
	ENDIF
CASE 10
	;에키드나
	IF CFLAG:ARG:8 == 4
		LOCAL = 20
	;멜루진
	ELSEIF CFLAG:ARG:8 == 2
		LOCAL = 14
	;라미아
	ELSE
		LOCAL = 4 + CFLAG:ARG:8*4
	ENDIF
CASE 11
	;모리안
	IF CFLAG:ARG:8 == 3
		LOCAL = 17
	;세이렌
	ELSEIF CFLAG:ARG:8 == 2
		LOCAL = 19
	;하피
	ELSE
		LOCAL = 10 + CFLAG:ARG:8*4
	ENDIF
CASE 12
	;뷔이브
	IF CFLAG:ARG:8 == 4
		LOCAL = 18
	;샐러맨더
	ELSEIF CFLAG:ARG:8 == 2
		LOCAL = 12
	;리저드맨
	ELSE
		LOCAL = 4 + CFLAG:ARG:8*4
	ENDIF
CASE 13
	;레비아탄
	IF CFLAG:ARG:8 == 3
		LOCAL = 18
	;머메이드
	ELSE
		LOCAL = 10 + CFLAG:ARG:8*2
	ENDIF
CASE 14
	;다키니
	IF CFLAG:ARG:8 == 3
		LOCAL = 14
	;압사라
	ELSE
		LOCAL = 5 + CFLAG:ARG:8*3
	ENDIF
CASE 15
	;카리브디스
	IF CFLAG:ARG:8 == 2
		LOCAL = 17
	;스킬라
	ELSE
		LOCAL = 14 + CFLAG:ARG:8*2
	ENDIF
CASE 16
	;타마모
	IF CFLAG:ARG:8 == 3
		LOCAL = 19
	;닷키
	ELSEIF CFLAG:ARG:8 == 2
		LOCAL = 11
	;쿠즈노하
	ELSE
		LOCAL = 7 + CFLAG:ARG:8*3
	ENDIF
CASE 17
	;엠푸사
	IF CFLAG:ARG:8 == 1
		LOCAL = 8
	;마이에스트레
	ELSEIF CFLAG:ARG:8 == 3
		LOCAL = 18
	;퀸메이브
	ELSE
		LOCAL = 12 + CFLAG:ARG:8*2
	ENDIF
CASE 18
	;실로브
	IF CFLAG:ARG:8 == 3
		LOCAL = 14
	;아라크네
	ELSE
		LOCAL = 9 + CFLAG:ARG:8*2
	ENDIF
CASE 19
	;윗치
	LOCAL = 10 + CFLAG:ARG:8*2
CASE 20
	;가브리엘
	IF CFLAG:ARG:8 == 2
		LOCAL = 12
	;엔젤
	ELSE
		LOCAL = 7 + CFLAG:ARG:8*3
	ENDIF
CASE 21
	;루시펠
	LOCAL = 20 + CFLAG:ARG:8*1
CASE 22
	;하츠메
	IF CFLAG:ARG:8 == 2
		LOCAL = 19
	;쿠노이치
	ELSE
		LOCAL = 11 + CFLAG:ARG:8*2
	ENDIF
CASE 23
	;화이트래빗
	LOCAL = 15 + CFLAG:ARG:8*1
CASE 24
	;살로메
	LOCAL = 16 + CFLAG:ARG:8*1
CASE 25
	;카밀라
	IF CFLAG:ARG:8 == 2
		LOCAL = 20
	;뱀파이어
	ELSE
		LOCAL = 14 + CFLAG:ARG:8*2
	ENDIF
CASE 26
	;드라군
	LOCAL = 19 + CFLAG:ARG:8*1
CASE 27
	;프로페테스
	LOCAL = 13 + CFLAG:ARG:8*1
CASE 28
	;솔저
	LOCAL = 7 + CFLAG:ARG:8*2
CASE 29
	;슬라임
	LOCAL = 18 + CFLAG:ARG:8*1
CASE 30
	;유니콘
	LOCAL = 16 + CFLAG:ARG:8*1
CASE 31
	;고블린
	LOCAL = 4 + CFLAG:ARG:8*3
CASE 32
	;마샤
	LOCAL = 4
CASE 33
	;마몬
	IF CFLAG:ARG:8 == 4
		LOCAL = 18
	;미믹
	ELSE
		LOCAL = 1
	ENDIF
CASE 34
	;스투나
	LOCAL = 20
CASE 35
	;멜로나
	LOCAL = 17
CASE 36

CASE 37
	;프레데리카
	LOCAL = 12
CASE 38
	;에스프라나
	LOCAL = 15
CASE 39
	;리즈・라즈
	LOCAL = 10
CASE 40
	;롯코・바롯코
	LOCAL = 5
CASE 41
	;자바워크
	LOCAL = 20
CASE 42
	;번더스내치
	LOCAL = 21
CASE 43
	;쟈브쟈브
	LOCAL = 19
CASE 44
	;린(見習い？)
	IF CFLAG:ARG:8 == 4
		LOCAL = 25
	ELSE
		LOCAL = 4
	ENDIF
CASE 45

CASE 46
	;클라라
	LOCAL = 14
CASE 47
	;베르나도트
	LOCAL = 16
CASE 48
	;에스트
	LOCAL = 25
CASE 49
	;핌블베트
	LOCAL = 18
CASE 50
	;스텔라
	LOCAL = 20
CASE 52
	;루빗사
	LOCAL = 10
CASE 54
	;메르야
	LOCAL = 14
CASE 55
	;소녀
	LOCAL = 1
CASE 57
	;흑사피
	LOCAL = 20
CASE 58
	;마치헤어
	LOCAL = 19
CASE 59
	;돌로레스
	LOCAL = 1 + CFLAG:ARG:8*3
CASE 60
	;허니비
	LOCAL = 12
CASE 61
	;발키리
	LOCAL = 18
CASE 94
	;랄스소년
	LOCAL = 1
CASE 95
	;아들
	LOCAL = 1
CASE 97
	;딸
	LOCAL = 1
CASEELSE
	LOCAL = 1 + CFLAG:ARG:8*4
ENDSELECT

ABL:ARG:ＬＶ = MIN(LOCAL, NUM("최대레벨", 90) )

;주인の場合には경험치も得る
SIF ARG == MASTER
	EXP:ARG:경험치 = EXP_NEXTLV(ARG, ABL:ARG:ＬＶ - 1)


@GET_MAGIC, ARG
SIF ARG <= 0
	RETURN 0

SIF TALENT:ARG:마법사 == 2
	RETURN 0

SIF ABL:ARG:기교 >= RAND:5 + 1
	TALENT:ARG:미니멈 = 1
SIF ABL:ARG:기교 >= RAND:5
	TALENT:ARG:센시브 = 1
SIF ABL:ARG:기교 >= RAND:5
	TALENT:ARG:우르즈군 = 1
SIF ABL:ARG:기교 >= RAND:5 + 1
	TALENT:ARG:시길군 = 1
SIF ABL:ARG:기교 >= RAND:5 + 1
	TALENT:ARG:에올군 = 1

SIF ABL:ARG:기교 >= RAND:5
	TALENT:ARG:로긴 = 1
SIF ABL:ARG:기교 >= RAND:5 + 3
	TALENT:ARG:투와이스 = 1
SIF ABL:ARG:기교 >= RAND:5 + 1
	TALENT:ARG:이사군 = 1
SIF ABL:ARG:기교 >= RAND:5 + 2
	TALENT:ARG:아르스이사 = 1
SIF ABL:ARG:기교 >= RAND:5 + 2
	TALENT:ARG:바인드 = 1
SIF ABL:ARG:기교 >= RAND:5 + 1
	TALENT:ARG:인라지 = 1
SIF ABL:ARG:기교 >= RAND:5 + 3
	TALENT:ARG:비젼 = 1

;最低でもこのどれかは覚えている
IF TALENT:ARG:미니멈 + TALENT:ARG:센시브 + TALENT:ARG:우르즈군 == 0
	IF RAND:3 == 0
		TALENT:ARG:미니멈 = 1
	ELSEIF RAND:2
		TALENT:ARG:센시브 = 1
	ELSE
		TALENT:ARG:우르즈군 = 1
	ENDIF
ENDIF

@SET_3SIZE, ARG
VARSET LOCAL

;体型の変更
IF BASE:ARG:신장
	;신장の変化による소질の変化
	IF TALENT:ARG:요정
		SIF BASE:ARG:신장 > NUM("요정の상한")
			LOCAL = 1
	ELSE
		SELECTCASE BASE:ARG:신장
		;요정では無くなった時
		CASE IS <= NUM("요정の상한")
			LOCAL = 1
		CASE IS <= NUM("小柄の상한")
			SIF TALENT:ARG:장신 || TALENT:ARG:작은몸집 == 0
				LOCAL = 1
		CASE IS <= NUM("보통の상한")
			SIF TALENT:ARG:장신 || TALENT:ARG:작은몸집
				LOCAL = 1
		CASEELSE
			SIF TALENT:ARG:장신 == 0 || TALENT:ARG:작은몸집
				LOCAL = 1
		ENDSELECT
	ENDIF
	IF LOCAL
		SIF CONFIG("３사이즈：표시")
			PRINTFORML （%CALLNAME:ARG%의 신장과 허리둘레 등을 재측정합니다）

		IF TALENT:ARG:요정
			SIF BASE:ARG:신장 > NUM("요정の상한")
				CALL CALC, "요정化", ARG
		ELSE
			SELECTCASE BASE:ARG:신장
			;요정では無くなった時
			CASE IS <= NUM("요정の상한")
				IF TALENT:ARG:장신
					CALL CALC, "장신化", ARG
				ELSEIF TALENT:ARG:작은몸집
					CALL CALC, "小柄化", ARG
				ELSE
					CALL CALC, "보통体型化", ARG
				ENDIF
			CASE IS <= NUM("小柄の상한")
				TALENT:ARG:장신 = 0
				TALENT:ARG:작은몸집 = 1
				CALL CALC, "ＢＷＨ計測", ARG
			CASE IS <= NUM("보통の상한")
				TALENT:ARG:장신 = 0
				TALENT:ARG:작은몸집 = 0
				CALL CALC, "ＢＷＨ計測", ARG
			CASEELSE
				TALENT:ARG:장신 = 1
				TALENT:ARG:작은몸집 = 0
				CALL CALC, "ＢＷＨ計測", ARG
			ENDSELECT
		ENDIF
	ENDIF

	LOCAL = 0
	;라미아の꼬리
	IF TALENT:ARG:라미아 && BASE:ARG:신장의추가요소 == 0
		SIF CONFIG("３사이즈：표시")
			PRINTFORML （%CALLNAME:ARG%의 꼬리 길이를 재겠습니다）
		BASE:ARG:신장의추가요소 = 350 + RAND:100
	ENDIF

	LOCAL = 0

	;바스트컵の変化による소질の変化
	BASE:ARG:컵 = CALCF("바스트컵", CALCF("바스트컵計算値", ARG) )

	IF TALENT:ARG:남자
		SIF BASE:ARG:컵
			CALL CALC, "男乳化", ARG
	ELSEIF TALENT:ARG:거유 >= 3
		SIF BASE:ARG:컵 <= NUM("폭유の상한")
			LOCAL = 1
	ELSEIF TALENT:ARG:거유 == 2
		SIF BASE:ARG:컵 <= NUM("거유の상한")
			LOCAL = 1
	ELSEIF TALENT:ARG:거유 == 1
		SIF BASE:ARG:컵 <= NUM("평유の상한") || BASE:ARG:컵 > NUM("거유の상한")
			LOCAL = 1
	ELSEIF TALENT:ARG:빈유
		SIF BASE:ARG:컵 > NUM("빈유の상한")
			LOCAL = 1
	ELSEIF TALENT:ARG:거유 + TALENT:ARG:빈유 == 0
		SIF BASE:ARG:컵 <= NUM("빈유の상한") || BASE:ARG:컵 > NUM("평유の상한")
			LOCAL = 1
	ENDIF
	IF LOCAL
		SIF CONFIG("３사이즈：표시")
			PRINTFORML （%CALLNAME:ARG%의 바스트 사이즈를 재측정합니다）

		CALL SETFLAG, "바스트설정", ARG

		SIF TALENT:ARG:임신 && CONFIG("３사이즈：표시")
			PRINTFORML （웨이스트 사이즈를 재측정하는 것은, 역시 그만두겠습니다…）
	ENDIF

	;엉덩이のサイズ計測（웨스트と힙から判定するだけなので問題ない）
	CALL SETFLAG, "힙설정", ARG
	RETURN 0
ENDIF

;ゴールデンカノン (바스트 = 신장*0.54, 웨스트 = 신장*0.38, 힙 = 신장*0.54)を参考. 誤差は上下10%程が妥当か. 
;허리놀림LVが高いと, 웨스트が低く, 힙が大きくなりやすい
;BASE:신장=신장, BASE:바스트=바스트, BASE:컵=바스트컵, BASE:웨스트=웨스트, BASE:힙=힙
;요정
IF TALENT:ARG:요정
	CALL CALC, "요정化", ARG
;장신 164～180cm
ELSEIF TALENT:ARG:장신
	CALL CALC, "장신化", ARG
;작은몸집 115～144cm
ELSEIF TALENT:ARG:작은몸집
	CALL CALC, "小柄化", ARG
;보통 145～163cm
ELSE
	CALL CALC, "보통体型化", ARG
ENDIF

;바스트컵計測. 바스트サイズ(BASE:바스트)と컵(BASE:컵)が決められる
CALL SETFLAG, "바스트설정", ARG

;라미아の꼬리
SIF TALENT:ARG:라미아 && BASE:ARG:신장의추가요소 == 0
	BASE:ARG:신장의추가요소 = 350 + RAND:100

