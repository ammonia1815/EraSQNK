@ABL_UP, ARGS, ARGS:1, ARGS:2
#DIM LCOUNT
;(A:X, B:X)～(E:X, F:X)が(필요な구슬の数, 구슬の種類)
;(G:X, H:X)～(K:X, L:X)が(필요な経験, 経験の種類)
;(M, N)が全体を通して필요な経験
;(O, P), (Q, R)が全体を通して필요な能力
;(S, T)が全体を通して필요な刻印
;(U, V)が全体を通して필요な절정
;Z:Xが上を踏まえた가능かどうかの判定. ただし, 가능な場合が０である

VARSET LOCAL

DRAWLINE
SELECTCASE ARGS
CASE "ＬＶ"
	IF ABL:ARGS >= NUM("최대레벨", 90) && ARGS:1 == ""
		PRINTW 이미 MAX입니다.
		RETURN 0
	ENDIF

	CALL ISABLUP, ARGS
	SIF TALENT:몽마
		PRINTFORML 몽마가 15LV이상이되려면, 소질[상위몽마]（필요10LV 이상）가 필요
	IF A:0 > 0
		PRINTFORM  [ 0] - 레벨을 {ABL:ARGS + 1} 로 올린다：경험치
		SIF EXP:경험치 < A:0
			SETCOLOR DEF_COLOR("회색")
		PRINTFORM {EXP:경험치}
		RESETCOLOR
		PRINTFORML /{A:0} ……%PRINT_ABLUP_OK(Z:0)%
		LOCAL:0 = !(Z:0)
	ENDIF
	;LVUP가능なら, 限界まであげる
	IF LOCAL:0
		PRINTFORML  [ 1] - 레벨을 올릴 수 있는 만큼 올린다
		LOCAL:1 = 1
	ENDIF
	IF TALENT:몽마 && TALENT:상위몽마 == 0
		PRINTFORM  [ 2] - 상위몽마가 된다：경험치
		SIF EXP:경험치 < A:2
			SETCOLOR DEF_COLOR("회색")
		PRINTFORM {EXP:경험치}
		RESETCOLOR
		PRINTFORML /{A:2} ……%PRINT_ABLUP_OK(Z:2)%
		LOCAL:2 = !(Z:2)
	ENDIF

	PRINTFORM  [\@ EXP:경험치 >= NUM("경험치：소질획득")? {3, 2} # × \@] - 여러가지 소질을 획득한다：경험치
	SIF EXP:경험치 < NUM("경험치：소질획득")
		SETCOLOR DEF_COLOR("회색")
	PRINTFORM {EXP:경험치}
	RESETCOLOR
	PRINTFORML /{NUM("경험치：소질획득")}
	SIF COND("소질획득가능")
		LOCAL:3 = 1

	IF COND("성장기")
		PRINTFORM  [\@ EXP:경험치 >= 1000? {4,2} # × \@] - %타겟을()% 성장시킨다：경험치
		SIF EXP:경험치 < 1000
			SETCOLOR DEF_COLOR("회색")
		PRINTFORM {EXP:경험치}
		RESETCOLOR
		PRINTFORML /1000
		SIF EXP:경험치 >= 1000
			LOCAL:4 = 1
	ENDIF
	PRINTL [100] - 그만둔다

	CALL INPUT_SELECT, 10, 100

	IF RESULT == 100
		RETURN 0
	ELSEIF LOCAL:RESULT == 0
		RESTART
	ELSEIF RESULT == 0
		ABL:ARGS += 1
		EXP:경험치 -= A:0
		PRINTFORMW %NAME:TARGET%의 레벨이 {ABL:ＬＶ}\@ABL:ＬＶ == NUM("최대레벨", 90) ? (MAX!) # \@가 되었습니다.
	ELSEIF RESULT == 1
		FOR LOCAL, 0, NUM("최대레벨", 90)
			ABL:ARGS += 1
			EXP:경험치 -= A:0

			CALL ISABLUP, ARGS
			SIF Z:0 || ABL:ARGS == NUM("최대레벨", 90)
				BREAK
		NEXT
		PRINTFORMW %NAME:TARGET%의 레벨이 {ABL:ARGS}\@ABL:ARGS == NUM("최대레벨", 90) ? (MAX!) # \@가 되었습니다.
	ELSEIF RESULT == 2
		EXP:경험치 -= A:2
		PRINTFORMW %타겟은()% 상위몽마가 되었습니다
		TALENT:몽마 = 0
		TALENT:상위몽마 = 1
	ELSEIF RESULT == 3
		CALL GET_TALENT
	ELSEIF RESULT == 4
		CALL DAUGHTER_GROW
	ENDIF

	CALL ISABLUP, ARGS
	SIF RESULT
		RESTART
	RETURN 0
CASE "Ｖ확장", "Ａ확장"
	SIF CONFIG("성기상세표시") == 0
		RETURN 0
	;확장LVが高すぎると緩いという事態に…
	;0小さい　1보통　2経験豊富. 太い바이브　3緩め. 극태바이브or피스트(女)
	;4ゆるゆる. 피스트(男)　5ガバガバ. 드래곤

	SELECTCASE ARGS
	CASE "Ｖ확장"
		SIF TALENT:남자
			RETURN 0
		IF ABL:ARGS >= NUM("최대레벨", 23)
			PRINTW 이미 MAX입니다.
			RETURN 0
		ENDIF

		PRINTFORML %CALLNAME:TARGET%의 현재 %ARGS% 레벨은{ABL:ARGS}이고 Ｖ명기도는{ABL_V(TARGET, "절대치")}입니다
		PRINTFORML %ARGS%레벨을 상승 시키겠습니까？（주의：상대적 명기도는 줄어들게 됩니다）
		PRINTFORML 
		;ＬＶＵＰ가능
		IF CALCF("Ｖ확장の상한", TARGET) > ABL:Ｖ확장
			ABL:ARGS += 1
			PRINTFORML  [ 0] - %ARGS%레벨을 {ABL:ARGS}에 올린다（레벨을 올린 이후 Ｖ명기도는 {ABL_V(TARGET, "절대치")}입니다.）
			ABL:ARGS -= 1
			LOCAL:0 = 1
		ELSE
			SETCOLOR DEF_COLOR("회색")
			PRINTFORML  [×] - %ARGS%레벨을 {ABL:ARGS + 1}로 올린다 ……Ｖ확장 경험과 소질이 부족한 것 같습니다
			RESETCOLOR
		ENDIF
	CASE "Ａ확장"
		IF ABL:ARGS >= NUM("최대레벨", 26)
			PRINTW 이미 MAX입니다.
			RETURN 0
		ENDIF

		PRINTFORML %CALLNAME:TARGET%의 현재 %ARGS% 레벨을{ABL:ARGS}이고 Ａ명기도는{ABL_A(TARGET, "절대치")}입니다
		PRINTFORML %ARGS%레벨을 상승 시키겠습니까?？（주의：상대적 명기도는 줄어들게 됩니다）
		PRINTFORML 
		;ＬＶＵＰ가능
		IF CALCF("Ａ확장の상한", TARGET) > ABL:Ａ확장
			ABL:ARGS += 1
			PRINTFORML  [ 0] - %ARGS%레벨을 {ABL:ARGS}에 올린다（레벨을 올린 이후 Ａ명기도는{ABL_A(TARGET, "절대치")}입니다.）
			ABL:ARGS -= 1
			LOCAL:0 = 1
		ELSE
			SETCOLOR DEF_COLOR("회색")
			PRINTFORML  [×] - %ARGS%레벨을 {ABL:ARGS + 1}로 올린다 ……Ａ확장경험과 소질이 부족한것 같습니다
			RESETCOLOR
		ENDIF
	ENDSELECT

	PRINTFORML [100] - 그만둔다

	CALL INPUT_SELECT, 1, 100

	IF RESULT == 100
		RETURN 0
	ELSEIF LOCAL:RESULT == 0
		RESTART
	ELSEIF RESULT == 0
		ABL:ARGS += 1
		CALL GET_EXABL2, TARGET

		SELECTCASE ARGS
		CASE "Ｖ확장"
			PRINTFORMW %NAME:TARGET%의 %ARGS%레벨은 {ABL:ARGS}, 명기도는{ABL_V(TARGET, "절대치")}이 되었습니다. 
		CASE "Ａ확장"
			PRINTFORMW %NAME:TARGET%의 %ARGS%레벨은 {ABL:ARGS}, 명기도는{ABL_A(TARGET, "절대치")}이 되었습니다. 
		ENDSELECT
	ENDIF

	CALL ISABLUP, ARGS
	SIF RESULT
		RESTART
	RETURN 0
CASE "아르카나"
	CALL LEARNING_MAGIC, TARGET, "通常"
	RETURN 0
CASE "연모", "음란"
	CALL SETFLAG, ARGS
	RETURN 0
CASE "음핵", "음호", "애널광", "음유", "음탕한입술", "새드", "마조", "도착적", "요도광"
	CALL ISABLUP, ARGS
	JUEL:B -= A
	TALENT:ARGS = 1
	PRINTFORMW %CALLNAME:TARGET%의 이쪽을 보는 시선이 이상하다…
	SELECTCASE ARGS
	CASE "음핵"
		IF TALENT:연모 || TALENT:음란
			PRINTFORM %타겟은()% 행위가 끝났음에도 불구하고, %CALLNAME:MASTER%에게 
			PRINTFORMW \@ PENIS(TARGET) ? 페니스 # 클리토리스 \@의 저림을 진정시켜달라고 졸라왔다…
		ELSE
			PRINTFORM %타겟은()% %CALLNAME:MASTER% 쪽을 가만히 쳐다보면서, 손으로 자신의 
			PRINTFORMW \@ PENIS(TARGET) ? 음경 # 콩알 \@을 주물렀다…
		ENDIF
	CASE "요도광"
		IF TALENT:연모 || TALENT:음란
			PRINTFORMW %타겟은()% 조교하지만 끝난 뒤에도 불구하고,%CALLNAME:MASTER%에 오줌 구멍을 괴롭혔으면 좋겠다고 간원 해 왔다…
		ELSE
			PRINTFORMW %타겟은()% 스스로를 위로하면서 %CALLNAME:MASTER%에 갖고 싶어하는 듯한 시선을 보내고 있다…
		ENDIF
	CASE "음호"
		IF TALENT:연모 || PENIS(MASTER) == 0
			PRINTFORMW %타겟은()% 행위가 끝났음에도 불구하고, %CALLNAME:MASTER%에게 자궁의 갈증을 풀어달라고 매달려왔다…
		ELSE
			PRINTFORMW %CALLNAME:TARGET%의 꿀단지는 개발이 끝나, 완전히 %CALLNAME:MASTER%의 물건에 적응해버린 것같다…
		ENDIF
	CASE "애널광"
		IF TALENT:연모 || TALENT:음란
			PRINTFORMW %타겟은()% 조교가 끝났음에도 불구하고, %CALLNAME:MASTER%에게 엉덩이를 괴롭혀달라고 애원했다…
		ELSE
			PRINTFORMW %타겟은()% 자신의 엉덩이를 위로하면서 %CALLNAME:MASTER%에게 원하는 듯한 시선을 보내왔다…
		ENDIF
	CASE "음유"
		IF CONFIG("니플퍽") && TALENT:거유 && TALENT:슬라임 == 0
			PRINTFORMW %타겟은()% 완전히 음란하게 개화한 유두에 손가락을 푹푹 찌르며, 가벼운 자위행위를 보여주고있습니다…
			PRINTFORMW …이렇다면 가슴을 이용해 더욱 하드한 플레이도 가능할 것입니다! 
			CALL PRINT_STR, "하트핑크_※니플퍽을 할 수 있게 되었습니다※_W"
		ELSEIF TALENT:연모 || TALENT:음란
			PRINTFORMW %타겟은()% 행위가 끝났음에도 불구하고, %CALLNAME:MASTER%에게 가슴을 주물러 달라고 졸라왔다…
		ELSE
			PRINTFORMW %CALLNAME:TARGET%의 가슴은 %CALLNAME:MASTER%에 의해 개발되어, 민감하게 되어버린 것같다…
		ENDIF
	CASE "음탕한입술"
		PRINTFORMW %타겟은()% 행위가 끝나 떠나려고 하는 %마스터를()% 끌어안고, 몇번이고 입맞춤을 계속하고있다…
	CASE "도착적"
		PRINTFORMW %타겟은()% 평범하지 않은 행위에 의한 흥분을 몸에 익힌 것같다…
	CASE "새드"
		PRINTFORMW %타겟은()% 행위가 끝났음에도 불구하고, 가학적인 미소를 지으며 %마스터를()% 쓰러뜨리려 했다…
	CASE "마조"
		IF TALENT:연모 || TALENT:음란
			PRINTFORMW %타겟은()% 행위가 끝났음에도 불구하고, %CALLNAME:MASTER%에게 더욱 격렬하게 괴롭혀달라고 졸라왔다…
		ELSE
			PRINTFORMW %CALLNAME:TARGET%의 몸은 %CALLNAME:MASTER%에게 고통받는 것에 쾌감을 얻게 된 것같다…
		ENDIF
	ENDSELECT
	SETCOLOR DEF_COLOR("하트핑크")
	PRINTFORMW %타겟은()% 【%ARGS%】를 얻었다
	RESETCOLOR
	RETURN 0
ENDSELECT

SELECTCASE ARGS
CASE "굴복각인"
	IF MARK:ARGS >= 3
		PRINTW 이미 MAX입니다.
		RETURN 0
	ENDIF
CASE "반발각인"
	SIF MARK:ARGS <= 0
		RETURN 0
CASE "기교", "Ｃ감각", "Ｖ감각", "Ａ감각", "Ｂ감각", "Ｍ감각"
	IF ABL:ARGS >= 10 && ARGS:1 == "" && ARGS:2 == ""
		PRINTW 이미 MAX입니다.
		RETURN 0
	ENDIF
CASE "순종", "욕망", "봉사정신", "노출벽", "마조끼", "자위중독", "정액중독", "새드끼"
	IF ABL:ARGS >= 5 && ARGS:1 == ""
		PRINTW 이미 MAX입니다.
		RETURN 0
	ENDIF
CASE "레즈끼"
	SIF TALENT:남자
		RETURN 0
	IF ABL:ARGS >= 5 && ARGS:1 == ""
		PRINTW 이미 MAX입니다.
		RETURN 0
	ENDIF
CASE "ＢＬ끼"
	SIF TALENT:남자 == 0
		RETURN 0
	IF ABL:ARGS >= 5 && ARGS:1 == ""
		PRINTW 이미 MAX입니다.
		RETURN 0
	ENDIF
ENDSELECT

CALL ISABLUP, ARGS

VARSET LOCALS
;共通の必須条件
;SIF M > 0
;	LOCALS =  %EXPNAME:N%が{M}이상（{EXP:N}）
;SIF O > 0
;	LOCALS =  \@ LOCALS != "" ? %LOCALS%, 그리고 # \@%ABLNAME:P%%조사만처리(ABLNAME:P,"가")% {O} 이상（현재 {ABL:P}）
;SIF Q > 0
;	LOCALS =  \@ LOCALS != "" ? %LOCALS%, 그리고 # \@%ABLNAME:R%%조사만처리(ABLNAME:R,"가")% {O} 이상（현재 {ABL:R}）
;SIF S > 0
;	LOCALS =  \@ LOCALS != "" ? %LOCALS%, 그리고 # \@%MARKNAME:T%%조사만처리(MARKNAME:T,"가")% {O} 이상（현재 {MARK:T}）
;
;SIF LOCALS != ""
;	PRINTFORML %LOCALS%

IF M > 0
	PRINTFORM  %조사처리(EXPNAME:N,"가")% {M}이상（현재
	SIF EXP:N < M
		SETCOLOR DEF_COLOR("회색")
	PRINTFORM {EXP:N}
	RESETCOLOR
	PRINTFORM ）
ENDIF
IF O > 0
	PRINTFORM \@ M > 0 ? , 그리고 # \@%조사처리(ABLNAME:P,"가")% {O}이상（현재
	SIF ABL:P < O
		SETCOLOR DEF_COLOR("회색")
	PRINTFORM {ABL:P}
	RESETCOLOR
	PRINTFORM ）
ENDIF
IF Q > 0
	PRINTFORM \@ M + O > 0 ? , 그리고 # \@%조사처리(ABLNAME:R,"가")% {Q}이상（현재
	SIF ABL:R < Q
		SETCOLOR DEF_COLOR("회색")
	PRINTFORM {ABL:R}
	RESETCOLOR
	PRINTFORM ）
ENDIF
IF S > 0
	PRINTFORM \@ M + O + Q > 0 ? , 그리고 # \@%조사처리(MARKNAME:T,"가")% {S}이상（현재
	SIF MARK:T < S
		SETCOLOR DEF_COLOR("회색")
	PRINTFORM {MARK:T}
	RESETCOLOR
	PRINTFORM ）
ENDIF
SIF M > 0 || O > 0 || Q > 0 || S > 0
	PRINTFORML 

SETCOLOR DEF_COLOR("황색")
SELECTCASE ARGS
CASE "Ｖ감각", "Ａ감각", "Ｍ감각"
	IF ABL:ARGS < 10
		;필요経験が多い場合には필요な구슬の量が減る
		SELECTCASE EXP:H
		CASE IS >= G*2
			PRINTFORML （%조사처리(EXPNAME:H,"가")% 필요량의 ２배를 상회하므로, 필요한 %PALAMNAME:B%의 구슬이 50％ 감소합니다）
		CASE IS >= G*3/2
			PRINTFORML （%조사처리(EXPNAME:H,"가")% 필요량의 1.5배를 상회하므로, 필요한 %PALAMNAME:B%의 구슬이 25％ 감소합니다）
		ENDSELECT
	ENDIF
CASE "기교"
	IF ABL:ARGS < 10
		;펠라경험～커닐링구스경험の合計によるEXPLVによる軽減
		SELECTCASE GET_EXP_LV(EXP:키스경험 + (EXP:펠라경험+EXP:수음경험+EXP:풋잡경험+EXP:허리사용경험+EXP:파이즈리경험+EXP:Ａ자극경험+EXP:커닐링구스경험)*5) - ABL:기교
		CASE IS >= 9
			PRINTFORML （기교에 관련되는 경험의 합계 회수가 굉장히 많기 때문에,필요%PALAMNAME:B%의 구슬이 격감하고 있습니다）
		CASE IS >= 6
			PRINTFORML （기교에 관련되는 경험의 합계 회수가 매우 많기 때문에,필요%PALAMNAME:B%의 구슬이 격감하고 있습니다）
		CASE IS >= 3
			PRINTFORML （기교에 관한 경험의 총 횟수가 많으므로, 필요한 %PALAMNAME:B%의 구슬이 감소합니다）
		ENDSELECT
	ENDIF
CASE "봉사정신"
	IF ABL:ARGS < 5
		SELECTCASE EXP:봉사쾌락경험
		CASE IS >= EXP_LV(ABL:봉사정신 + 3)
			PRINTFORML （봉사쾌락경험이 상당히 많으므로, 요구되는 구슬이 50％ 감소합니다）
		CASE IS >= EXP_LV(ABL:봉사정신 + 2)
			PRINTFORML （봉사쾌락경험이 많으므로, 요구되는 구슬이 20％ 감소합니다）
		ENDSELECT
	ENDIF
CASE "새드끼"
	IF ABL:ARGS < 5
		;가학쾌락경험が多いなら필요な구슬の量が軽減される
		SELECTCASE EXP:가학쾌락경험 / MAX(G, 1)
		CASE IS >= 4
			PRINTFORML （가학쾌락경험이 매우 많으므로, 요구되는 구슬이 70％ 감소합니다）
		CASE 3
			PRINTFORML （가학쾌락경험이 꽤 많으므로, 요구되는 구슬이 50％ 감소합니다）
		CASE 2
			PRINTFORML （가학쾌락경험이 많으므로, 요구되는 구슬이 20％ 감소합니다）
		ENDSELECT
	ENDIF
CASE "정액중독"
	IF ABL:ARGS < 5
		;정액경험が필요な値より大幅に多い
		SELECTCASE EXP:정액경험 / MAX(G:0, 1)
		CASE IS >= 4
			PRINTFORML （정액경험이 꽤 많으므로, 요구되는 구슬이 격감합니다）
		CASE 2, 3
			PRINTFORML （정액경험이 많으므로, 요구되는 구슬이 감소합니다）
		ENDSELECT
	ENDIF
ENDSELECT
RESETCOLOR

FOR LCOUNT, 0, 10
	SIF A:LCOUNT <= 0
		CONTINUE
	PRINTFORM  [{LCOUNT, 2}] - %PALAMNAME:(B:LCOUNT)%의 구슬 
	SIF JUEL:(B:LCOUNT) < A:LCOUNT
		SETCOLOR DEF_COLOR("회색")
	PRINTFORM {JUEL:(B:LCOUNT)}
	RESETCOLOR
	PRINTFORML /{A:LCOUNT} ……%PRINT_ABLUP_OK(Z:LCOUNT)%
	IF C:LCOUNT > 0
		PRINTFORM  %PALAMNAME:(D:LCOUNT)%의 구슬 
		SIF JUEL:(D:LCOUNT) < C:LCOUNT
			SETCOLOR DEF_COLOR("회색")
		PRINTFORM {JUEL:(D:LCOUNT)}
		RESETCOLOR
		PRINTFORML /{C:LCOUNT}
	ENDIF
	IF E:LCOUNT > 0
		PRINTFORM  %PALAMNAME:(F:LCOUNT)%의 구슬 
		SIF JUEL:(F:LCOUNT) < E:LCOUNT
			SETCOLOR DEF_COLOR("회색")
		PRINTFORM {JUEL:(F:LCOUNT)}
		RESETCOLOR
		PRINTFORML /{E:LCOUNT}
	ENDIF
	IF G:LCOUNT > 0
		PRINTFORM  %EXPNAME:(H:LCOUNT)% 
		SIF EXP:(H:LCOUNT) < G:LCOUNT
			SETCOLOR DEF_COLOR("회색")
		PRINTFORM {EXP:(H:LCOUNT)}
		RESETCOLOR
		PRINTFORML /{G:LCOUNT}
	ENDIF
	IF I:LCOUNT > 0
		PRINTFORM  %EXPNAME:(J:LCOUNT)% 
		SIF EXP:(J:LCOUNT) < I:LCOUNT
			SETCOLOR DEF_COLOR("회색")
		PRINTFORM {EXP:(J:LCOUNT)}
		RESETCOLOR
		PRINTFORML /{I:LCOUNT}
	ENDIF
	IF K:LCOUNT > 0
		PRINTFORM  %EXPNAME:(L:LCOUNT)% 
		SIF EXP:(L:LCOUNT) < K:LCOUNT
			SETCOLOR DEF_COLOR("회색")
		PRINTFORM {EXP:(L:LCOUNT)}
		RESETCOLOR
		PRINTFORML /{K:LCOUNT}
	ENDIF

	LOCAL:LCOUNT = !(Z:LCOUNT)
NEXT

SELECTCASE ARGS:1
CASE "아르카나"
	PRINTFORM  [10] - 아르카나의 습득 - %PALAMNAME:7%의 구슬 
	SIF JUEL:습득 < NUM("宝구슬：아르카나습득")
		SETCOLOR DEF_COLOR("회색")
	PRINTFORM {JUEL:습득}
	RESETCOLOR
	PRINTFORML /{NUM("宝구슬：아르카나습득")}
	LOCAL:10 = 1
CASE "연모"
	IF CONFIG("연모と음란の任意획득") && COND("연모취득가능")
		PRINTFORML  [10] - 연모에 눈뜬다
		LOCAL:10 = 1
	ENDIF
CASE "음란"
	IF CONFIG("연모と음란の任意획득") && COND("음란취득가능")
		PRINTFORML  [10] - 음란에 눈뜬다
		LOCAL:10 = 1
	ENDIF
CASE "도착적"
	PRINTL 
	CALL ISABLUP, ARGS:1
	PRINTFORML  소질 【도착적】을 획득한다
	PRINTFORM  노출벽과 마조끼와 새드끼의 합계가 9LV（현재 합계
	SIF ABL:7 + ABL:8 + ABL:15 < 9
		SETCOLOR DEF_COLOR("회색")
	PRINTFORM {ABL:7 + ABL:8 + ABL:15}
	RESETCOLOR
	PRINTFORML ）, 
	PRINTFORM   이번 조교 중에 고통쾌락경험과 가학쾌락경험과 수치쾌락경험이 합계 200이상（합계
	SIF EXP_GAIN(30) + EXP_GAIN(32) + EXP_GAIN(36) < 200
		SETCOLOR DEF_COLOR("회색")
	PRINTFORM {EXP_GAIN(30) + EXP_GAIN(32) + EXP_GAIN(36)}
	RESETCOLOR
	PRINTFORML ）
	PRINTFORM  [\@ Z == 0 ? 10 # × \@] - %PALAMNAME:B%의 구슬 
	SIF JUEL:B < A
		SETCOLOR DEF_COLOR("회색")
	PRINTFORM {JUEL:B}
	RESETCOLOR
	PRINTFORML /{A} ……%PRINT_ABLUP_OK(Z)%
	LOCAL:10 = !Z
	CALL ISABLUP, ARGS
CASE "음핵", "음호", "애널광", "음유", "음탕한입술", "새드", "마조"
	PRINTL 
	CALL ISABLUP, ARGS:1
	PRINTFORML  소질 【%ARGS:1%】%조사만처리(ARGS:1,"을")% 획득한다
	IF O > 0
		PRINTFORM  %ABLNAME:P%{O}LV（현재
		SIF ABL:P < O
			SETCOLOR DEF_COLOR("회색")
		PRINTFORM {ABL:P}
		RESETCOLOR
		PRINTFORM ）,
	ENDIF
	IF Q > 0
		PRINTFORM %ABLNAME:R%{Q}LV（현재
		SIF ABL:R < Q
			SETCOLOR DEF_COLOR("회색")
		PRINTFORM {ABL:R}
		RESETCOLOR
		PRINTFORM ）,
	ENDIF
	IF U > 0
		PRINTFORM 이번 조교 중에%EXNAME:V%{U}회（
		SIF EX:V < U
			SETCOLOR DEF_COLOR("회색")
		PRINTFORM {EX:V}
		RESETCOLOR
		PRINTFORM 회）
	ENDIF
	PRINTFORML 
	PRINTFORM  [\@ Z == 0 ? 10 # × \@] - %PALAMNAME:B%의 구슬 
	SIF JUEL:B < A
		SETCOLOR DEF_COLOR("회색")
	PRINTFORM {JUEL:B}
	RESETCOLOR
	PRINTFORML /{A} ……%PRINT_ABLUP_OK(Z)%
	LOCAL:10 = !Z
	CALL ISABLUP, ARGS
ENDSELECT

SELECTCASE ARGS:2
CASE "요도광"
	PRINTL 
	CALL ISABLUP, ARGS:2
	PRINTFORML  소질【%ARGS:2%】를 획득한다
	IF O > 0
		PRINTFORM  %ABLNAME:P%{O}LV（현재
		SIF ABL:P < O
			SETCOLOR DEF_COLOR("회색")
		PRINTFORM {ABL:P}
		RESETCOLOR
		PRINTFORM ）,
	ENDIF
	IF Q > 0
		PRINTFORM %ABLNAME:R%{Q}LV（현재
		SIF ABL:R < Q
			SETCOLOR DEF_COLOR("회색")
		PRINTFORM {ABL:R}
		RESETCOLOR
		PRINTFORM ）,
	ENDIF
	IF U > 0
		PRINTFORM 이번 조교 중에%EXNAME:V%{U}회（
		SIF EX:V < U
			SETCOLOR DEF_COLOR("회색")
		PRINTFORM {EX:V}
		RESETCOLOR
		PRINTFORM 회）
	ENDIF
	PRINTFORML 
	PRINTFORM  [\@ Z == 0 ? 20 # × \@] - %PALAMNAME:B%의 구슬 
	SIF JUEL:B < A
		SETCOLOR DEF_COLOR("회색")
	PRINTFORM {JUEL:B}
	RESETCOLOR
	PRINTFORML /{A} ……%PRINT_ABLUP_OK(Z)%
	LOCAL:20 = !Z
	CALL ISABLUP, ARGS
ENDSELECT

PRINTL [100] - 그만둔다

CALL INPUT_SELECT, 21, 100

IF RESULT == 100
	RETURN 0
ELSEIF LOCAL:RESULT == 0
	RESTART
ELSEIF RESULT == 10
	SWAP ARGS, ARGS:1
	RESTART
ELSEIF RESULT == 20
	SWAP ARGS, ARGS:2
	RESTART
ENDIF

JUEL:(B:RESULT) -= A:RESULT
JUEL:(D:RESULT) -= C:RESULT
JUEL:(F:RESULT) -= E:RESULT

SELECTCASE ARGS
CASE "굴복각인"
	MARK:ARGS += 1
	SIF MARK:ARGS == 3
		CALL KOJO_MESSAGE_MARKCNG, "굴복각인Lv3취득"
	PRINTFORMW %ARGS%의 레벨이 {MARK:ARGS}가 되었습니다.
CASE "반발각인"
	MARK:ARGS -= 1
	;CALL KOJO_MESSAGE_MARKCNG, "반발각인감소"
	PRINTFORMW %ARGS%의 레벨이 {MARK:ARGS}가 되었습니다.
CASEELSE
	ABL:ARGS += 1
	PRINTFORMW %ARGS%의 레벨이 {ABL:ARGS}가 되었습니다.
ENDSELECT

;소질が変動したりするものの체크
SELECTCASE ARGS
;욕망, 순종, 굴복각인の上昇による[억압][저항]の消滅を체크
CASE "순종", "욕망", "굴복각인"
	CALL YOKUBO_UP_CHECK, ARGS
CASE "Ｖ감각"
	;자궁성감
	IF TALENT:자궁성감 == 0 && ABL:Ｖ감각 + TALENT:Ｖ민감*2 >= 5
		PRINTFORM %타겟은()% 
		CALL PRINT_COLORTEXT, "[자궁성감]", DEF_COLOR("황색")
		PRINTFORML 을 얻었다
		TALENT:자궁성감 = 1
		FORCEWAIT
	ENDIF
CASE "Ｍ감각"
	;인후성감
	IF TALENT:인후성감 == 0 && ABL:Ｍ감각 + TALENT:Ｍ민감*2 >= 5
		PRINTFORM %타겟은()% 
		CALL PRINT_COLORTEXT, "[인후성감]", DEF_COLOR("황색")
		PRINTFORML 을 얻었다
		TALENT:인후성감 = 1
		FORCEWAIT
	ENDIF
;[정액에약함]を得るかの체크
CASE "정액중독"
	IF TALENT:정액에약함 == 0
		CALL CALC, "정액에약함", TARGET
		;소질を得た
		IF TALENT:정액에약함 && EXP:조교횟수
			PRINTFORMW %타겟은()% 정액을 수 없이 받아 들이는 사이에, 정액으로 쾌락을 얻는 몸이 되어버린 모양이다…
			PRINTFORM %타겟은()% 
			LOCALS = [%TALENT_NAME(49, TALENT:정액에약함)%]
			CALL PRINT_COLORTEXT, LOCALS, DEF_COLOR("황색")
			PRINTFORMW 을 얻었다
		ENDIF
	ENDIF
ENDSELECT

CALL ISABLUP, ARGS
SIF RESULT
	RESTART

IF ARGS:1 != ""
	CALL ISABLUP, ARGS:1
	SIF RESULT
		RESTART
ENDIF

@ISABLUP, ARGS
;(A:X, B:X)～(E:X, F:X)が(필요な구슬の数, 구슬の種類)
;(G:X, H:X)～(K:X, L:X)が(필요な経験, 経験の種類)
;(M, N)が全体を通して필요な経験
;(O, P), (Q, R)が全体を通して필요な能力
;(S, T)が全体を通して필요な刻印
;(U, V)が全体を通して필요な절정
;Z:Xが上を踏まえた가능かどうかの判定. ただし, 가능な場合が０である

CALL SETFLAG, "一文字関数初期化"

SELECTCASE ARGS
CASE "Ｖ확장"
	SIF ABL:ARGS >= 10 || CONFIG("성기상세표시") == 0
		RETURN 0
	SIF CALCF("Ｖ확장の상한", TARGET) > ABL:Ｖ확장
		RETURN 1
	RETURN 0
CASE "Ａ확장"
	SIF ABL:ARGS >= 10 || CONFIG("성기상세표시") == 0
		RETURN 0
	SIF CALCF("Ａ확장の상한", TARGET) > ABL:Ａ확장
		RETURN 1
	RETURN 0
CASE "아르카나"
	SIF COND("아르카나습득가능")
		RETURN 1
	RETURN 0
CASE "연모"
	SIF CONFIG("연모と음란の任意획득") && COND("연모취득가능")
		RETURN 1
	RETURN 0
CASE "음란"
	SIF CONFIG("연모と음란の任意획득") && COND("음란취득가능")
		RETURN 1
	RETURN 0
CASE "굴복각인"
	SIF MARK:ARGS >= 3
		RETURN 0

	B = 6

	SELECTCASE MARK:ARGS
	CASE 0
		A = 2000
	CASE 1
		A = 10000
	CASE 2
		A = 30000
	ENDSELECT

	SIF TALENT:건방짐 || TALENT:일선을넘지않음
		TIMES A, 1.20
	SIF TALENT:연모
		TIMES A, 0.50

	SIF MARK:반발각인
		TIMES A, 2.00
	SIF CONFIG("ＥＡＳＹ")
		TIMES A, 0.75

	;-----------------
	;ボスキャラ用. 특수な条件を達成しないと굴복각인取得に요정する파라미터量が激増. 
	;-----------------
	IF TALENT:상위몽마
		SELECTCASE MARK:공략각인
		CASE 0
			TIMES A, 4.00
		CASE 1
			TIMES A, 3.00
		CASE 2
			TIMES A, 2.00
		CASE 3
			TIMES A, 1.50
		CASE 4
			TIMES A, 1.20
		CASE 5

		CASE 6
			TIMES A, 0.80
		CASEELSE
			TIMES A, 0.50
		ENDSELECT
	ENDIF

	A:1 = A/3*2
	C:1 = A:1
	B:1 = 4
	D:1 = 6
	A:2 = A*3/2
	B:2 = 4

	;取得したい굴복각인と同じLvの순종が필요
	O = MARK:ARGS
	P = 0
CASE "반발각인"
	SIF MARK:ARGS <= 0
		RETURN 0

	B = 6
	SELECTCASE MARK:ARGS
	CASE 1
		A = 5000
	CASE 2
		A = 10000
	CASE 3
		A = 50000
	ENDSELECT

	SIF TALENT:건방짐
		TIMES A, 1.50
	SIF TALENT:솔직 || TALENT:연모
		TIMES A, 0.50

	;반발각인Lv-1Lvの굴복각인が필요
	IF MARK:ARGS >= 2
		S = MARK:ARGS - 1
		T = 2
	ENDIF

	;반발각인Lv+1Lvの순종が필요
	O = MARK:ARGS + 1
	P = 0
CASE "ＬＶ"
	SIF ABL:ARGS >= NUM("최대레벨", 90)
		RETURN 0

	A:0 = EXP_NEXTLV(TARGET, ABL:ARGS)
	A:2 = 200

	SIF EXP:경험치 < A:0
		SETBIT Z:0, 1
	;몽마はMAX14LV
	SIF ABL:ARGS >= 14 && TALENT:몽마
		SETBIT Z:0, 3

	SIF EXP:경험치 < A:2
		SETBIT Z:2, 1
	SIF ABL:ARGS < 10
		SETBIT Z:2, 4
	SIF TALENT:몽마 == 0 || TALENT:상위몽마
		SETBIT Z:2, 9

	SIF MIN(Z:0, Z:2) == 0
		RETURN 1
	RETURN 0
CASE "음핵"
	A = NUM("宝구슬：음핵취득")
	B = 0
	O = 4
	P = 1
	Q = 4
	R = 3
	U = 20
	V = 0
CASE "요도광"
	A = NUM("宝구슬：요도광취득")
	B = 0
	O = 4
	P = 1
	Q = 0
	R = 0
	U = 10
	V = 37
CASE "음호"
	A = NUM("宝구슬：음호취득")
	B = 1
	O = 4
	P = 1
	Q = 4
	R = 4
	U = 20
	V = 1
CASE "애널광"
	A = NUM("宝구슬：애널광취득")
	B = 2
	O = 4
	P = 1
	Q = 4
	R = 5
	U = 20
	V = 2
CASE "음유"
	A = NUM("宝구슬：음유취득")
	B = 14
	O = 4
	P = 1
	Q = 4
	R = 14
	U = 20
	V = 3
CASE "음탕한입술"
	A = NUM("宝구슬：음탕한입술취득")
	B = 15
	O = 4
	P = 1
	Q = 4
	R = 16
	U = 20
	V = 4
CASE "도착적"
	A = NUM("宝구슬：도착적취득")
	B = 5
	Z = COND("도착적취득불가능")
CASE "새드"
	A = NUM("宝구슬：새드취득")
	B = 5
	O = 4
	P = 1
	Q = 5
	R = 15
CASE "마조"
	A = NUM("宝구슬：마조취득")
	B = 5
	O = 4
	P = 1
	Q = 5
	R = 8
CASE "기교",  "Ｃ감각",  "Ｖ감각",  "Ａ감각", "Ｂ감각", "Ｍ감각"
	SIF ABL:ARGS >= 10
		RETURN 0
CASE "순종", "욕망", "봉사정신", "노출벽", "마조끼", "레즈끼", "자위중독", "정액중독", "ＢＬ끼", "새드끼"
	SIF ABL:ARGS >= 5
		RETURN 0
ENDSELECT

SELECTCASE ARGS
CASE "순종"
	;ＬＶ４から５に올린다ときは이상경험필요（소질：[겁쟁이][솔직][망신][음란化]なら無視できる）
	;[ 0] - 공포의 구슬　{JUEL:공포}/{A:0} ……%PRINT_ABLUP_OK(Z:0)%
	;[ 1] - 순종의 구슬　{JUEL:순종}/{A:1} ……%PRINT_ABLUP_OK(Z:1)%
	;[ 2] - 욕정의 구슬　{JUEL:욕정}/{A:2} ……%PRINT_ABLUP_OK(Z:2)%
	;[ 3] - 굴복의 구슬　{JUEL:굴복}/{A:3} ……%PRINT_ABLUP_OK(Z:3)%

	;A:0 공포의 구슬
	B:0 = 10
	;A:1 순종의 구슬
	B:1 = 4
	;A:2 욕정의 구슬
	B:2 = 5
	;A:3 굴복의 구슬
	B:3 = 6

	SELECTCASE ABL:순종
	CASE 0
		A:0 = 500
		A:1 = 300
		A:2 = 1000
	CASE 1
		A:0 = 2400
		A:1 = 1200
		A:2 = 3000
	CASE 2
		A:0 = 5000
		A:1 = 4000
		A:2 = 5000
	CASE 3
		A:0 = 20000
		A:1 = 15000
		A:2 = 0
	CASE 4
		A:0 = 0
		A:1 = 50000
		A:2 = 0
	ENDSELECT
	A:3 = A:1 * 2

	IF CONFIG("ＥＡＳＹ")
		SELECTCASE ABL:순종
		CASE 0
			A:0 = 10
			A:1 = 10
			A:2 = 300
			A:3 = 200
		CASE 1
			A:0 = 150
			A:1 = 100
			A:2 = 1000
			A:3 = 1200
		CASE 2
			A:0 = 1000
			A:1 = 800
			A:2 = 2000
			A:3 = 3000
		CASE 3
			A:0 = 3000
			A:1 = 3000
			A:2 = 0
			A:3 = 0
		CASE 4
			A:0 = 8000
			A:1 = 15000
			A:2 = 0
			A:3 = 0
		ENDSELECT
	ENDIF

	IF TALENT:일선을넘지않음 && ABL:순종 >= 3
		TIMES A:0, 2.00
		TIMES A:1, 2.00
		TIMES A:2, 2.00
		TIMES A:3, 2.00
	ENDIF
	SIF TALENT:겁쟁이
		TIMES A:0, 0.50
	IF TALENT:꿋꿋함
		TIMES A:0, 3.00
		TIMES A:1, 2.00
		TIMES A:2, 1.50
		TIMES A:3, 1.50
	ENDIF
	IF TALENT:교만
		TIMES A:0, 1.50
		TIMES A:1, 1.50
		TIMES A:2, 1.20
		TIMES A:3, 1.50
	ENDIF
	IF TALENT:건방짐
		TIMES A:0, 1.50
		TIMES A:1, 2.00
		TIMES A:3, 1.50
	ENDIF
	IF TALENT:솔직
		TIMES A:0, 0.50
		TIMES A:1, 0.50
		TIMES A:3, 0.50
	ENDIF
	IF TALENT:반하기쉬움
		TIMES A:0, 0.80
		TIMES A:1, 0.80
		TIMES A:3, 0.80
	ENDIF

	;ＬＶ４から５に올린다ときは이상경험필요（소질：[겁쟁이][솔직][망신][음란化]なら無視できる）
	IF ABL:순종 == 4 && TALENT:겁쟁이 == 0 && TALENT:솔직 == 0 && TALENT:망신 == 0 && TALENT:음란 == 0
		M = 1
		N = 50
	ENDIF
CASE "욕망"
	;A 욕정의 구슬
	B = 5
	;G 절정경험
	H = 2
	SIF TALENT:남자
		H = 3

	SELECTCASE ABL:욕망
	CASE 0
		A = 600
		G = 1
	CASE 1
		A = 2400
		G = 5
	CASE 2
		A = 10000
		G = 30
	CASE 3
		A = 30000
		G = 80
	CASE 4
		A = 60000
		G = 150
	ENDSELECT

	IF CONFIG("ＥＡＳＹ")
		SELECTCASE ABL:욕망
		CASE 0
			A = 5
		CASE 1
			A = 50
		CASE 2
			A = 1000
		CASE 3
			A = 8000
		CASE 4
			A = 24000
		ENDSELECT
		G = 0
	ENDIF
	SIF TALENT:일선을넘지않음 && ABL:욕망 >= 3
		TIMES A, 2.00
	SIF TALENT:청초
		TIMES A, 1.50
	IF TALENT:쾌감에솔직 || TALENT:호색
		TIMES A, 0.80
		TIMES G, 0.80
	ENDIF

	;ＬＶ４から５に올린다ときは이상경험필요（소질：[쾌감에솔직][음란]なら無視できる）
	IF ABL:욕망 == 4 && TALENT:쾌감에솔직 == 0 && TALENT:음란 == 0
		M = 1
		N = 50
	ENDIF
CASE "기교"
	;A 습득의 구슬
	B = 7

	SELECTCASE ABL:기교
	CASE 0
		A = 300
	CASE 1
		A = 3000
	CASE 2
		A = 8000
	CASE 3
		A = 20000
	CASE 4
		A = 50000
	CASEELSE
		A = 50000 * (ABL:기교 - 3)
	ENDSELECT

	IF CONFIG("ＥＡＳＹ")
		SELECTCASE ABL:기교
		CASE 0
			A = 1
		CASE 1
			A = 25
		CASE 2
			A = 200
		CASE 3
			A = 3000
		CASE 4
			A = 20000
		CASEELSE
			A = 20000 * (ABL:기교 - 3)
		ENDSELECT
	ENDIF

	;펠라경험～커닐링구스경험の合計によるEXPLVによる軽減
	SELECTCASE GET_EXP_LV(EXP:키스경험 + (EXP:펠라경험+EXP:수음경험+EXP:풋잡경험+EXP:허리사용경험+EXP:파이즈리경험+EXP:Ａ자극경험+EXP:커닐링구스경험)*5) - ABL:기교
	CASE IS >= 12
		TIMES A, 0.20
	CASE IS >= 9
		TIMES A, 0.40
	CASE IS >= 6
		TIMES A, 0.60
	CASE IS >= 3
		TIMES A, 0.80
	ENDSELECT

	A = MAX(A, 1)

CASE "Ｃ감각", "Ｂ감각"
	;필요とする구슬の数
	SELECTCASE ABL:ARGS
	CASE 0
		A = 100
	CASE 1
		A = 1000
	CASE 2
		A = 10000
	CASE 3
		A = 30000
	CASE 4
		A = 50000
	CASEELSE
		A = 100000 * POWER(2, ABL:ARGS - 5)
	ENDSELECT

	IF CONFIG("ＥＡＳＹ")
		SELECTCASE ABL:ARGS
		CASE 0
			A = 1
		CASE 1
			A = 20
		CASE 2
			A = 400
		CASE 3
			A = 5000
		CASE 4
			A = 20000
		CASEELSE
			A = 50000 * POWER(2, ABL:ARGS - 5)
		ENDSELECT
	ENDIF

	SELECTCASE ARGS
	CASE "Ｃ감각"
		;A 쾌Ｃ의 구슬で올린다
		B = 0
		SIF TALENT:Ｃ민감 || TALENT:음핵
			TIMES A, 0.80
	CASE "Ｂ감각"
		;A 쾌Ｂ의 구슬で올린다
		B = 14
		SIF TALENT:Ｂ민감 || TALENT:음유
			TIMES A, 0.80
	ENDSELECT

	A = MAX(A, 1)
CASE "Ｖ감각", "Ａ감각", "Ｍ감각"
	SELECTCASE ABL:ARGS
	CASE 0
		A = 200
		G = 3
	CASE 1
		A = 2000
		G = 10
	CASE 2
		A = 20000
		G = 25
	CASE 3
		A = 60000
		G = 50
	CASE 4
		A = 100000
		G = 100
	CASEELSE
		A = 200000 * POWER(2, ABL:ARGS - 5)
		G = 200 + 100*(ABL:ARGS - 5)
	ENDSELECT

	IF CONFIG("ＥＡＳＹ")
		SELECTCASE ABL:ARGS
		CASE 0
			A = 1
		CASE 1
			A = 50
			G = 5
		CASE 2
			A = 600
			G = 20
		CASE 3
			A = 7000
			G = 50
		CASE 4
			A = 45000
			G = 150
		CASEELSE
			A = 90000 * POWER(2, ABL:ARGS - 5)
			G = 150 + 50*(ABL:ARGS - 4)
		ENDSELECT
	ENDIF

	SELECTCASE ARGS
	CASE "Ｖ감각"
		;A 쾌Ｖ의 구슬で올린다
		B = 1
		;G Ｖ경험で올린다
		H = 0
		SIF TALENT:Ｖ민감 || TALENT:음호
			TIMES A, 0.80
	CASE "Ａ감각"
		;A 쾌Ａ의 구슬で올린다
		B = 2
		;G Ａ경험で올린다
		H = 1
		SIF TALENT:Ａ민감 || TALENT:애널광
			TIMES A, 0.80
	CASE "Ｍ감각"
		;A 쾌Ｍ의 구슬で올린다
		B = 15
		;G 키스경험で올린다(増えづらい経験なので回数はＶやＡの半分)
		H = 70
		G /= 2
		SIF TALENT:Ｍ민감 || TALENT:음탕한입술
			TIMES A, 0.80
	ENDSELECT

	;필요経験が多い場合には필요な구슬の量が減る
	SELECTCASE EXP:H
	CASE IS >= G*2
		TIMES A, 0.50
	CASE IS >= G*3/2
		TIMES A, 0.75
	ENDSELECT

	A = MAX(A, 1)
	G = MAX(G, 1)
CASE "봉사정신"
	;共通条件で봉사쾌락경험が필요
	;굴복의 구슬　{JUEL:굴복}/{A:0} ……%PRINT_ABLUP_OK(Z:0)%
	;순종의 구슬　{JUEL:순종}/{A:1} ……%PRINT_ABLUP_OK(Z:1)%
	;습득의 구슬　{JUEL:습득}/{A:2} ……%PRINT_ABLUP_OK(Z:2)%

	;A:0 굴복의 구슬で올린다
	B:0 = 6
	;A:1 순종의 구슬で올린다
	B:1 = 4
	;A:2 습득의 구슬
	B:2 = 7

	;M 봉사쾌락경험
	M = EXP_LV(ABL:봉사정신 + 1)
	N = 21

	SELECTCASE ABL:봉사정신
	CASE 0
		A:0 = 500
		A:1 = 300
		A:2 = 1000
	CASE 1
		A:0 = 3000
		A:1 = 2000
		A:2 = 0
	CASE 2
		A:0 = 12000
		A:1 = 6000
		A:2 = 0
	CASE 3
		A:0 = 60000
		A:1 = 30000
		A:2 = 0
	CASEELSE
		A:0 = 120000
		A:1 = 80000
		A:2 = 0
	ENDSELECT

	SELECTCASE EXP:봉사쾌락경험
	CASE IS >= EXP_LV(ABL:봉사정신 + 3)
		TIMES A:1, 0.50
		TIMES A:2, 0.50
	CASE IS >= EXP_LV(ABL:봉사정신 + 2)
		TIMES A:1, 0.80
		TIMES A:2, 0.80
	ENDSELECT

	IF CONFIG("ＥＡＳＹ")
		SELECTCASE ABL:봉사정신
		CASE 0
			A:0 = 100
			A:1 = 20
			A:2 = 100
		CASE 1
			A:0 = 1200
			A:1 = 100
			A:2 = 0
		CASE 2
			A:0 = 5000
			A:1 = 600
			A:2 = 0
		CASE 3
			A:0 = 10000
			A:1 = 2000
			A:2 = 0
		CASE 4
			A:0 = 30000
			A:1 = 8000
			A:2 = 0
		ENDSELECT
	ENDIF

	IF TALENT:반항적
		TIMES A:0, 1.50
		TIMES A:1, 1.50
	ENDIF
	SIF TALENT:꿋꿋함
		TIMES A:0, 1.20
	IF TALENT:솔직
		TIMES A:0, 0.80
		TIMES A:1, 0.80
	ENDIF
	IF TALENT:교만
		TIMES A:0, 2.00
		TIMES A:1, 2.00
	ENDIF
	IF TALENT:헌신
		TIMES A:0, 0.50
		TIMES A:1, 0.50
	ENDIF
	IF TALENT:새드
		TIMES A:0, 1.30
		TIMES A:1, 1.30
	ENDIF
	IF TALENT:소악마
		TIMES A:0, 1.20
		TIMES A:1, 1.20
	ENDIF

	;순종が봉사정신＋１레벨でないといけない
	O = ABL:봉사정신 + 1
	P = 0
CASE "노출벽"
	;욕망が노출벽+1LVかつ４ＬＶ이상にあげるには이상경험필요
	;치정의 구슬　{JUEL:치정}/{A:0} ……%PRINT_ABLUP_OK(Z:0)%
	;　　　　%EXPNAME:10% {EXP:자위경험}/{G:0}
	;치정의 구슬　{JUEL:치정}/{A:1} ……%PRINT_ABLUP_OK(Z:1)%
	;　　　　%EXPNAME:34% {EXP:노출경험}/{G:1}
	;치정의 구슬　{JUEL:치정}/{A:2} ……%PRINT_ABLUP_OK(Z:2)%
	;　　　　%EXPNAME:36%　{EXP:수치쾌락경험}/{G:2}

	;A:0～A:2 치정의 구슬で올린다(A:2だけ他のものより2割減)
	B:0 = 8
	B:1 = 8
	B:2 = 8
	;G:0 자위경험
	H:0 = 10
	;G:1 노출경험
	H:1 = 34
	;G:2 수치쾌락경험
	H:2 = 36

	SELECTCASE ABL:ARGS
	CASE 0
		A:0 = 100
		G:0 = 1
		G:1 = 5
		G:2 = 10
	CASE 1
		A:0 = 1000
		G:0 = 5
		G:1 = 20
		G:2 = 50
	CASE 2
		A:0 = 5000
		G:0 = 20
		G:1 = 50
		G:2 = 150
	CASE 3
		A:0 = 20000
		G:0 = 50
		G:1 = 150
		G:2 = 300
	CASE 4
		A:0 = 50000
		G:0 = 100
		G:1 = 300
		G:2 = 500
	ENDSELECT

	IF CONFIG("ＥＡＳＹ")
		SELECTCASE ABL:ARGS
		CASE 0
			A:0 = 100
		CASE 1
			A:0 = 1000
		CASE 2
			A:0 = 5000
		CASE 3
			A:0 = 15000
		CASE 4
			A:0 = 35000
		ENDSELECT
	ENDIF

	SIF TALENT:청초
		TIMES A:0, 1.50
	IF TALENT:도착적
		TIMES A:0, 0.75
		TIMES G:0, 0.75
		TIMES G:1, 0.75
	ENDIF
	IF TALENT:눈에띄고싶어함
		TIMES A:0, 0.50
		TIMES G:0, 0.50
		TIMES G:1, 0.50
	ENDIF
	IF TALENT:마조
		TIMES A:0, 0.75
		TIMES G:0, 0.50
		TIMES G:1, 0.50
	ENDIF

	G:0 = MAX(G:0, 1)
	G:1 = MAX(G:1, 1)
	G:2 = MAX(G:2, 1)

	;수치쾌락경험の時には치정의 구슬は２割お得
	A:1 = A:0
	A:2 = A:0*4/5

	;욕망が노출벽＋１레벨でないといけない
	O = ABL:ARGS + 1
	P = 1

	;ＬＶ３から４, ４から５に올린다ときは이상경험필요（소질：[눈에띄고싶어함]なら無視できる）
	IF TALENT:눈에띄고싶어함 == 0
		SELECTCASE ABL:ARGS
		CASE 3
			M = 1
			N = 50
		CASE 4
			M = 3
			N = 50
		ENDSELECT
	ENDIF
CASE "마조끼"
	;욕망が마조끼＋１LVかつ４ＬＶ이상にあげるには이상경험필요
	; [ 0] - 치정의 구슬　{JUEL:치정}/{A:0} ……%PRINT_ABLUP_OK(Z:0)%
	;　　　　욕정の구슬　{JUEL:욕정}/{C:0}
	;　　　　%EXPNAME:36% {EXP:수치쾌락경험}/{G:0}
	; [ 1] - 고통의 구슬　{JUEL:고통}/{A:1} ……%PRINT_ABLUP_OK(Z:1)%
	;　　　　굴복의 구슬　{JUEL:굴복}/{C:1}
	;　　　　%EXPNAME:30% {EXP:고통쾌락경험}/{G:1}
	; [ 2] - 고통의 구슬　{JUEL:고통}/{A:2} ……%PRINT_ABLUP_OK(Z:2)%
	;　　　　욕정의 구슬　{JUEL:욕정}/{C:2}
	;　　　　%EXPNAME:30% {EXP:고통쾌락경험}/{G:2}


	;A:0 치정의 구슬
	B:0 = 8
	;A:1, A:2 고통의 구슬で올린다
	B:1 = 9
	B:2 = 9
	;C:1 굴복の구슬
	D:1 = 6
	;C:0, C:2 욕정の구슬で올린다
	D:0 = 5
	D:2 = 5
	;G:0 수치쾌락경험
	H:0 = 36
	;G:1, G:2 고통쾌락경험
	H:1 = 30
	H:2 = 30

	SELECTCASE ABL:ARGS
	CASE 0
		A:0 = 100
		A:2 = 100
		C:0 = 100
		C:2 = 100
		G:0 = 50
		G:1 = 1
	CASE 1
		A:0 = 500
		A:2 = 500
		C:0 = 500
		C:2 = 500
		G:0 = 100
		G:1 = 20
	CASE 2
		A:0 = 1500
		A:2 = 1200
		C:0 = 2000
		C:2 = 1000
		G:0 = 200
		G:1 = 50
	CASE 3
		A:0 = 3000
		A:2 = 0
		C:0 = 4000
		C:2 = 0
		G:0 = 400
		G:1 = 100
	CASE 4
		A:0 = 5000
		A:2 = 0
		C:0 = 8000
		C:2 = 0
		G:0 = 600
		G:1 = 200
	CASEELSE
		A:0 = 8000
		A:2 = 0
		C:0 = 15000
		C:2 = 0
		G:0 = 800
		G:1 = 400
	ENDSELECT

	IF CONFIG("ＥＡＳＹ")
		SELECTCASE ABL:ARGS
		CASE 0
			A:2 = 100
			C:2 = 100
			G:1 = 1
			A:0 = 100
			C:0 = 100
		CASE 1
			A:2 = 500
			C:2 = 500
			G:1 = 2
			A:0 = 500
			C:0 = 300
		CASE 2
			A:2 = 1200
			C:2 = 1000
			G:1 = 5
			A:0 = 1500
			C:0 = 1000
		CASE 3
			A:2 = 0
			C:2 = 0
			G:1 = 10
			A:0 = 3000
			C:0 = 6000
		CASE 4
			A:2 = 0
			C:2 = 0
			G:1 = 50
			A:0 = 5000
			C:0 = 12000
		CASEELSE
			A:2 = 0
			C:2 = 0
			G:1 = 150
			A:0 = 10000
			C:0 = 20000
		ENDSELECT
		TIMES G:0, 0.50
	ENDIF

	LOCAL = 100
	SIF TALENT:교만
		TIMES LOCAL, 2.00
	SIF TALENT:프라이드낮음
		TIMES LOCAL, 0.50
	SIF TALENT:해방
		TIMES LOCAL, 0.50
	SIF TALENT:도착적
		TIMES LOCAL, 0.75
	SIF TALENT:음란
		TIMES LOCAL, 0.80
	SIF TALENT:새드
		TIMES LOCAL, 1.50

	SELECTCASE ABL:새드끼
	CASE IS >= 5
		TIMES LOCAL, 3.00
	CASE 4
		TIMES LOCAL, 2.00
	CASE 3
		TIMES LOCAL, 1.50
	ENDSELECT

	IF LOCAL != 100
		A:0 = A:0 * LOCAL / 100
		A:2 = A:2 * LOCAL / 100
		C:0 = C:0 * LOCAL / 100
		C:1 = C:1 * LOCAL / 100
		G:0 = G:0 * LOCAL / 100
		G:1 = G:1 * LOCAL / 100
	ENDIF

	G:1 = MAX(G:1, 1)

	A:1 = A:0
	C:1 = C:0
	G:2 = G:1

	;욕망が마조끼＋１레벨でないといけない
	O = ABL:ARGS + 1
	P = 1

	;ＬＶ３から４, ４から５に올린다ときは이상경험필요（소질：[해방]なら無視できる）
	IF TALENT:해방 == 0
		SELECTCASE ABL:ARGS
		CASE 3
			M = 1
			N = 50
		CASE 4
			M = 3
			N = 50
		ENDSELECT
	ENDIF
CASE "새드끼"
	;A 욕정의 구슬
	B = 5
	;C 습득의 구슬
	D = 7
	;G 가학쾌락경험
	H = 32

	SELECTCASE ABL:ARGS
	CASE 0
		A = 100
		C = 100
		G = 50
	CASE 1
		A = 1000
		C = 1000
		G = 100
	CASE 2
		A = 10000
		C = 10000
		G = 200
	CASE 3
		A = 30000
		C = 30000
		G = 400
	CASE 4
		A = 100000
		C = 100000
		G = 800
	CASEELSE
		A = 200000
		C = 100000
		G = 1600
	ENDSELECT

	IF CONFIG("ＥＡＳＹ")
		SELECTCASE ABL:ARGS
		CASE 0
			A = 100
			C = 100
			G = 0
		CASE 1
			A = 500
			C = 500
			G = 20
		CASE 2
			A = 1200
			C = 1000
			G = 50
		CASE 3
			A = 6000
			C = 6000
			G = 100
		CASE 4
			A = 10000
			C = 10000
			G = 200
		CASEELSE
			A = 20000
			C = 20000
			G = 500
		ENDSELECT
	ENDIF

	LOCAL = 100
	SIF TALENT:교만
		TIMES LOCAL, 0.50
	SIF TALENT:프라이드낮음
		TIMES LOCAL, 2.00
	SIF TALENT:심술쟁이
		TIMES LOCAL, 0.70
	SIF TALENT:해방
		TIMES LOCAL, 0.50
	SIF TALENT:도착적
		TIMES LOCAL, 0.75
	SIF TALENT:음란
		TIMES LOCAL, 0.80
	SIF TALENT:마조
		TIMES LOCAL, 1.50

	SELECTCASE ABL:마조끼
	CASE IS >= 5
		TIMES LOCAL, 3.00
	CASE 4
		TIMES LOCAL, 2.00
	CASE 3
		TIMES LOCAL, 1.50
	ENDSELECT

	;가학쾌락경험が多いなら필요な구슬の量が軽減される
	SELECTCASE EXP:가학쾌락경험 / MAX(G, 1)
	CASE IS >= 4
		TIMES LOCAL, 0.30
	CASE 3
		TIMES LOCAL, 0.50
	CASE 2
		TIMES LOCAL, 0.80
	ENDSELECT

	IF LOCAL != 100
		A = A * LOCAL / 100
		C = C * LOCAL / 100
		G = G * LOCAL / 100
	ENDIF

	G = MAX(G, 1)

	;욕망が새드끼＋１레벨でないといけない
	O = ABL:ARGS + 1
	P = 1

	;ＬＶ３から４, ４から５に올린다ときは이상경험필요（소질：[해방]なら無視できる）
	IF TALENT:해방 == 0
		SELECTCASE ABL:ARGS
		CASE 3
			M = 1
			N = 50
		CASE 4
			M = 3
			N = 50
		ENDSELECT
	ENDIF
CASE "레즈끼", "ＢＬ끼"
	;A 욕정의 구슬
	B = 5
	SELECTCASE ARGS
	CASE "레즈끼"
		;G 레즈경험
		H = 40
	CASE "ＢＬ끼"
		;G ＢＬ경험
		H = 41
	ENDSELECT

	SELECTCASE ABL:ARGS
	CASE 0
		A = 200
		G = 50
	CASE 1
		A = 1000
		G = 200
	CASE 2
		A = 5000
		G = 400
	CASE 3
		A = 20000
		G = 700
	CASE 4
		A = 80000
		G = 1000
	ENDSELECT

	IF CONFIG("ＥＡＳＹ")
		SELECTCASE ABL:ARGS
		CASE 0
			A = 200
			G = 50
		CASE 1
			A = 1000
			G = 200
		CASE 2
			A = 3000
			G = 500
		CASE 3
			A = 8000
			G = 1000
		CASE 4
			A = 20000
			G = 2000
		ENDSELECT
	ENDIF

	IF TALENT:바이
		TIMES A, 0.25
		TIMES G, 0.25
	ELSEIF ARGS == "ＢＬ끼" && COND("오토코노코")
		TIMES A, 0.50
		TIMES G, 0.50
	ENDIF
	IF TALENT:도착적
		TIMES A, 0.75
		TIMES G, 0.75
	ENDIF

	;ＬＶ３から４, ４から５に올린다ときは이상경험필요（소질：[바이]なら無視できる）
	IF TALENT:바이 == 0
		SELECTCASE ABL:ARGS
		CASE 3
			M = 1
			N = 50
		CASE 4
			M = 3
			N = 50
		ENDSELECT
	ENDIF
CASE "자위중독"
	;A 욕정의 구슬
	B = 5
	;C 치정의 구슬
	D = 8
	;G 자위경험
	H = 10

	SELECTCASE ABL:ARGS
	CASE 0
		A = 3000
		C = 1000
		G = 10
	CASE 1
		A = 8000
		C = 3000
		G = 50
	CASE 2
		A = 20000
		C = 6000
		G = 200
	CASE 3
		A = 40000
		C = 15000
		G = 400
	CASE 4
		A = 80000
		C = 40000
		G = 800
	ENDSELECT

	IF CONFIG("ＥＡＳＹ")
		SELECTCASE ABL:ARGS
		CASE 0
			A = 3000
			C = 1000
			G = 100
		CASE 1
			A = 6000
			C = 3000
			G = 250
		CASE 2
			A = 12000
			C = 6000
			G = 500
		CASE 3
			A = 20000
			C = 15000
			G = 1000
		CASE 4
			A = 40000
			C = 40000
			G = 2000
		ENDSELECT
	ENDIF

	IF TALENT:청초
		TIMES A, 1.20
		TIMES C, 1.20
		TIMES G, 1.20
	ENDIF
	IF TALENT:자위하기쉬움
		TIMES A, 0.25
		TIMES C, 0.25
		TIMES G, 0.25
	ELSEIF TALENT:중독되기쉬움 || PENIS(TARGET)
		TIMES A, 0.50
		TIMES C, 0.50
		TIMES G, 0.50
	ENDIF
	IF TALENT:음란
		TIMES A, 0.80
		TIMES C, 0.80
		TIMES G, 0.80
	ENDIF

	;ＬＶ２から３, ＬＶ３から４, ４から５に올린다ときは이상경험필요（소질：[자위하기쉬움][중독되기쉬움][음란]なら無視できる）
	IF TALENT:자위하기쉬움 == 0 && TALENT:중독되기쉬움 == 0 && TALENT:음란 == 0
		SELECTCASE ABL:ARGS
		CASE 2
			M = 1
			N = 50
		CASE 3
			M = 2
			N = 50
		CASE 4
			M = 4
			N = 50
		ENDSELECT
	ENDIF
CASE "정액중독"
	;A:0 욕정의 구슬
	B:0 = 5
	;A:1 굴복의 구슬
	B:1 = 6
	;G:0, G:1 정액경험
	H:0 = 20
	H:1 = 20

	SELECTCASE ABL:ARGS
	CASE 0
		A:0 = 3000
		A:1 = 6000
		G:0 = 50
	CASE 1
		A:0 = 10000
		A:1 = 20000
		G:0 = 150
	CASE 2
		A:0 = 25000
		A:1 = 35000
		G:0 = 300
	CASE 3
		A:0 = 50000
		A:1 = 70000
		G:0 = 500
	CASE 4
		A:0 = 100000
		A:1 = 120000
		G:0 = 1000
	ENDSELECT

	;EASY모드
	IF CONFIG("ＥＡＳＹ")
		SELECTCASE ABL:ARGS
		CASE 0
			A:0 = 3000
			A:1 = 10000
			G:0 = 50
		CASE 1
			A:0 = 8000
			A:1 = 25000
			G:0 = 150
		CASE 2
			A:0 = 15000
			A:1 = 50000
			G:0 = 300
		CASE 3
			A:0 = 30000
			A:1 = 100000
			G:0 = 500
		CASE 4
			A:0 = 55000
			A:1 = 300000
			G:0 = 1000
		ENDSELECT
	ENDIF

	LOCAL = 100
	SIF TALENT:청초
		TIMES LOCAL, 1.20
	SIF TALENT:악취둔감
		TIMES LOCAL, 0.50
	SIF TALENT:악취민감
		TIMES LOCAL, 2.00
	SIF TALENT:중독되기쉬움
		TIMES LOCAL, 0.50
	SIF TALENT:도착적
		TIMES LOCAL, 0.75
	SIF TALENT:냄새민감
		TIMES LOCAL, 0.75
	SIF COND("몽마")
		TIMES LOCAL, 0.80
	IF LOCAL != 100
		A:0 = A:0 * LOCAL / 100
		A:1 = A:1 * LOCAL / 100
		G:0 = G:0 * LOCAL / 100
	ENDIF

	;정액경험が필요な値より大幅に多い
	SELECTCASE EXP:정액경험 / MAX(G:0, 1)
	CASE IS >= 5
		TIMES A:0, 0.30
		TIMES A:1, 0.20
	CASE 4
		TIMES A:0, 0.50
		TIMES A:1, 0.30
	CASE 3
		TIMES A:0, 0.80
		TIMES A:1, 0.50
	CASE 2
		TIMES A:1, 0.80
	ENDSELECT

	G:1 = G:0

	;ＬＶ２から３, ＬＶ３から４, ４から５に올린다ときは이상경험필요（소질：[악취둔감][중독되기쉬움]なら無視できる）
	IF TALENT:중독되기쉬움 == 0 && TALENT:악취둔감 == 0
		SELECTCASE ABL:ARGS
		CASE 2
			M = 1
			N = 50
		CASE 3
			M = 2
			N = 50
		CASE 4
			M = 3
			N = 50
		ENDSELECT
	ENDIF
ENDSELECT

CALL CHECK_ABLUP_OK
RETURN RESULT

