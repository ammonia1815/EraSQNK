;MASTERの소질変動など
@EVENTCHECK_M
;堕とした人数が5人이상で, 수수께끼의매력が付く
IF FLAG:30 >= 5 && TALENT:MASTER:수수께끼의매력 == 0
	PRINTFORMW %CALLNAME:MASTER%은 【수수께끼의매력】을 몸에 익혔다
	TALENT:MASTER:수수께끼의매력 = 1
ENDIF

;Ａ경험５００이상, Ａ확장경험３５이상で애널광化
IF EXP:MASTER:Ａ경험 >= 500 && EXP:MASTER:Ａ확장경험 >= 35 && TALENT:MASTER:애널광 == 0
	PRINTFORMW 몸의 상태가 이상하다…
	PRINTFORML 엉덩이가 뜨겁고, 욱신거려서 어쩔 수가 없다.
	PRINTFORMW 아무래도 조교에의해 %CALLNAME:MASTER% 자신도 쾌락에 눈을 떠버린 것 같다
	PRINTFORMW %CALLNAME:MASTER%은 【애널광】이 되어버렸다
	TALENT:MASTER:애널광 = 1
ENDIF

;Ｖ경험５００이상で음호化
IF EXP:MASTER:Ｖ경험 >= 500 && TALENT:MASTER:음호 == 0
	PRINTFORMW 몸의 상태가 이상하다…
	PRINTFORML 자궁 부근이 뜨겁고, 욱신거려서 어쩔 수가 없다.
	PRINTFORMW 아무래도 조교에의해 %CALLNAME:MASTER% 자신도 쾌락에 눈을 떠버린 것 같다
	PRINTFORMW %CALLNAME:MASTER%은 【음호】를 얻고 말았다
	TALENT:MASTER:음호 = 1
ENDIF

CALL ABLUP_MASTER, "通常"


@LVUPCHECK_MASTER, ARG
#DIM LCOUNT
;ARG != 0なら텍스트を出力する
FLAG:255 = MAX(ABL:MASTER:ＬＶ, FLAG:255)
;本来の레벨がMAXならＬＶＵＰしない
SIF FLAG:255 >= NUM("최대레벨", 90)
	RETURN 1

;MASTERのLVに基づく最小경험치算出（Extraの時）
SIF NO:MASTER && FLAG:255 >= 2
	EXP:MASTER:경험치 = MAX(EXP_NEXTLV(MASTER, FLAG:255 - 1), EXP:MASTER:경험치)

LOCAL:100 = 0
FOR LCOUNT, 2, CHARANUM
	LOCAL = CALCF("경험치획득", LCOUNT)
	EXP:MASTER:경험치 += LOCAL
	IF ARG && LOCAL && LCOUNT == TARGET
		PRINTFORML %CALLNAME:MASTER%은 경험치를 {LOCAL}Ｐ 얻었습니다
		SIF ABL:MASTER:ＬＶ == FLAG:255 && EXP_NEXTLV(MASTER, ABL:MASTER:ＬＶ) - EXP:MASTER:경험치 > 0
			PRINTFORML 앞으로 {EXP_NEXTLV(MASTER, ABL:MASTER:ＬＶ) - EXP:MASTER:경험치}Ｐ의 경험치로 다음 레벨이 될 수 있습니다.
		WAIT
	ENDIF

	;현재の경험치によるMAXLVの算出
	LOCAL:90 = MASTER_LV()

	;本来の레벨が算出した레벨이상(LVUP発生せず)
	SIF FLAG:255 >= LOCAL:90
		CONTINUE

	;레벨制限中なら, MAXLVだけあがる
	IF ABL:MASTER:ＬＶ < FLAG:255
		LOCAL:100 = 1
	;制限していない
	ELSEIF ABL:MASTER:ＬＶ < LOCAL:90
		ABL:MASTER:ＬＶ = LOCAL:90
		LOCAL:100 = 2
	ENDIF
	;MAXLVの変更
	FLAG:255 = LOCAL:90
NEXT

SETCOLOR DEF_COLOR("노랑")
IF LOCAL:100 == 1
	PRINTFORMW %CALLNAME:MASTER%은 LV의 최대치가 {FLAG:255}\@LOCAL:90 == NUM("최대레벨", 90) ? (MAX!) # \@로 올라갔습니다!
ELSEIF LOCAL:100 == 2
	PRINTFORMW %CALLNAME:MASTER%은 LV이 {ABL:MASTER:ＬＶ}\@LOCAL:90 == NUM("최대레벨", 90) ? (MAX!) # \@로 올라갔습니다!
ENDIF
;달콤한말ゲット
IF ( (FLAG:255 >= 5 && FLAG:30) || ITEM:로즈닐의시집) && TALENT:MASTER:달콤한말 == 0
	PRINTFORMW \@ ITEM:로즈닐의시집 ? 시집을 탐독한 # 경험을 쌓은\@ %CALLNAME:MASTER%은, %TALENTNAME:191%을 습득했다!
	TALENT:MASTER:달콤한말 = 1
ENDIF
RESETCOLOR

@MASTER_LV()
#FUNCTION
;경험치からLVを算出
FOR LOCAL, 1, NUM("최대레벨", 90)
	SIF EXP:MASTER:경험치 < EXP_NEXTLV(MASTER, LOCAL)
		RETURNF LOCAL
NEXT
RETURNF NUM("최대레벨", 90)

@EXP_NEXTLV(ARG:1, ARG)
#FUNCTION
;ARG:1が次のLVに필요な경험치を算出
IF ARG:1 == MASTER
	LOCAL = ARG*(10 + (ARG - 1)*10)
ELSE
	LOCAL = ARG * (2 + ARG/5 + (ARG > 10)*2 + (ARG > 15)*2 + (ARG > 20)*3 + (ARG > 25)*3 + (ARG > 30)*4)
ENDIF
RETURNF LOCAL



;MASTERの技能獲得
@ABLUP_MASTER, ARGS
#DIM MEMO_LINECOUNT
#DIM CHOICE

MEMO_LINECOUNT = LINECOUNT

SELECTCASE ARGS
CASE "通常"
	;LVUP. FLAG:255は本来の레벨. ABL:ＬＶは현재の레벨. ABL:ＬＶは레벨리미터で減ったりする. 
	CALL LVUPCHECK_MASTER, 1
	IF CONFIG("ＶＡ상세표시：수동ＬＶＵＰ") == 0
		ABL:MASTER:Ｖ확장 = CALCF("Ｖ확장の상한", MASTER)
		ABL:MASTER:Ａ확장 = CALCF("Ａ확장の상한", MASTER)
	ENDIF
	CALL EX_ABLUP, MASTER
CASE "주말"
	REDRAW 0

	;CHOICEでどのABLをＬＶ変更候補として選んでいるかを記録
	CHOICE = 0

	$START
	VARSET LOCAL, 0, 0, 100
	PRINTFORML 다음 주차에 대비해, %CALLNAME:MASTER%의 능력의 LV변경을 합니다
	CALL NEW_PRINT_ABL, MASTER
	DRAWLINE
	PRINTFORML 다음 중에서 두개까지 선택해주세요
	PRINTFORML (기교LV의 상승은 손가락놀림은, ~놀림의 LV의 합계가 필요치를 만족시키는 것이 조건입니다)

	;기교ＬＶが上げられるかどうか
	LOCAL:99 = ABL:MASTER:혀놀림+ABL:MASTER:손가락놀림+ABL:MASTER:발놀림+ABL:MASTER:허리놀림+ABL:MASTER:마유
	LOCAL:99 -= TALENT:MASTER:혀놀림+TALENT:MASTER:손가락놀림+TALENT:MASTER:발놀림+TALENT:MASTER:허리놀림+TALENT:MASTER:마유

	SIF ABL:MASTER:기교 < MIN(MAX(2 + LOCAL:99/3, 2), NUM("최대레벨", 2) )
		LOCAL:0 = 1
	PRINTFORM  [\@ LOCAL:0 ?  {0, 2} # × \@] \@ GETBIT(CHOICE, 0) ? ○ # － \@ 기교＋1LV
	SIF LOCAL:0 == 0 && ABL:MASTER:기교 < NUM("최대레벨", 2)
		PRINTFORM 　다음 LV에 필요한 ~놀림 LV의 합계：앞으로 {(ABL:MASTER:기교 - 1) * 3 - LOCAL:99}
	PRINTL 

	SIF ABL:MASTER:기교 > 0
		LOCAL:1 = 1
	PRINTFORML  [\@ LOCAL:1 ?  {1, 2} # × \@] \@ GETBIT(CHOICE, 1) ? ○ # － \@ 기교－1LV
	SIF (ABL:MASTER:마조끼 < 5 || TALENT:MASTER:마조 == 0)
		LOCAL:2 = 1
	PRINTFORML  [\@ LOCAL:2 ?  {2, 2} # × \@] \@ GETBIT(CHOICE, 2) ? ○ # － \@ \@ ABL:MASTER:마조끼 == 5 ? [마조] 획득 # 마조끼＋1LV \@
	SIF ABL:MASTER:마조끼 > 0
		LOCAL:3 = 1
	PRINTFORML  [\@ LOCAL:3 ?  {3, 2} # × \@] \@ GETBIT(CHOICE, 3) ? ○ # － \@ \@ (ABL:MASTER:마조끼 == 5 && TALENT:MASTER:마조) ? [마조]를 잃는다 # 마조끼－1LV \@
	SIF (ABL:MASTER:새드끼 < 5 || TALENT:MASTER:새드 == 0)
		LOCAL:4 = 1
	PRINTFORML  [\@ LOCAL:4 ?  {4, 2} # × \@] \@ GETBIT(CHOICE, 4) ? ○ # － \@ \@ ABL:MASTER:새드끼 == 5 ? [새드] 획득 # 새드끼＋1LV \@
	SIF ABL:MASTER:새드끼 > 0
		LOCAL:5 = 1
	PRINTFORML  [\@ LOCAL:5 ?  {5, 2} # × \@] \@ GETBIT(CHOICE, 5) ? ○ # － \@ \@ (ABL:MASTER:새드끼 == 5 && TALENT:MASTER:새드) ? [새드]를 잃는다 # 새드끼－1LV \@
	IF TALENT:MASTER:남자 == 0
		SIF ABL:MASTER:레즈끼 < 5
			LOCAL:6 = 1
		PRINTFORML  [\@ LOCAL:6 ?  {6, 2} # × \@] \@ GETBIT(CHOICE, 6) ? ○ # － \@ 레즈끼＋1LV
		SIF ABL:MASTER:레즈끼 > 0
			LOCAL:7 = 1
		PRINTFORML  [\@ LOCAL:7 ?  {7, 2} # × \@] \@ GETBIT(CHOICE, 7) ? ○ # － \@ 레즈끼－1LV
	ELSE
		SIF ABL:MASTER:ＢＬ끼 < 5
			LOCAL:8 = 1
		PRINTFORML  [\@ LOCAL:8 ?  {8, 2} # × \@] \@ GETBIT(CHOICE, 8) ? ○ # － \@ ＢＬ끼＋1LV
		SIF ABL:MASTER:ＢＬ끼 > 0
			LOCAL:9 = 1
		PRINTFORML  [\@ LOCAL:9 ?  {9, 2} # × \@] \@ GETBIT(CHOICE, 9) ? ○ # － \@ ＢＬ끼－1LV
	ENDIF
	IF CONFIG("ＶＡ상세표시：수동ＬＶＵＰ")
		IF TALENT:MASTER:남자 == 0
			SIF CALCF("Ｖ확장の상한", MASTER) > ABL:MASTER:Ｖ확장
				LOCAL:10 = 1
			PRINTFORML  [\@ LOCAL:10 ?  {10, 2} # × \@] \@ GETBIT(CHOICE, 10) ? ○ # － \@ Ｖ확장＋1LV
		ENDIF
		SIF CALCF("Ａ확장の상한", MASTER) > ABL:MASTER:Ａ확장
			LOCAL:11 = 1
		PRINTFORML  [\@ LOCAL:11 ?  {11, 2} # × \@] \@ GETBIT(CHOICE, 11) ? ○ # － \@ Ａ확장＋1LV
	ENDIF

	PRINTFORML [100] 종료

	CALL INPUT_SELECT, 12, 100

	IF RESULT == 100
		IF GETBIT(CHOICE, 0)
			ABL:MASTER:기교 += 1
			PRINTL 기교가 1LV 올라갔다!
		ELSEIF GETBIT(CHOICE, 1)
			ABL:MASTER:기교 -= 1
			PRINTL 기교가 1LV 내려갔다!
		ENDIF
		IF GETBIT(CHOICE, 2)
			IF ABL:MASTER:마조끼 == 5
				TALENT:MASTER:마조 = 1
				PRINTL 소질：[마조]를 얻었다!
			ELSE
				ABL:MASTER:마조끼 += 1
				PRINTL 마조끼가 1LV 올라갔다!
			ENDIF
		ELSEIF GETBIT(CHOICE, 3)
			IF ABL:MASTER:마조끼 == 5 && TALENT:MASTER:마조
				TALENT:MASTER:마조 = 0
				PRINTL 소질：[마조]를 잃었다!
			ELSE
				ABL:MASTER:마조끼 -= 1
				PRINTL 마조끼가 1LV 내려갔다!
			ENDIF
		ENDIF
		IF GETBIT(CHOICE, 4)
			IF ABL:MASTER:새드끼 == 5
				TALENT:MASTER:새드 = 1
				PRINTL 소질：[새드]를 얻었다!
			ELSE
				ABL:MASTER:새드끼 += 1
				PRINTL 새드끼가 1LV 올라갔다!
			ENDIF
		ELSEIF GETBIT(CHOICE, 5)
			IF ABL:MASTER:새드끼 == 5 && TALENT:MASTER:새드
				TALENT:MASTER:새드 = 0
				PRINTL 소질：[새드]를 잃었다!
			ELSE
				ABL:MASTER:새드끼 -= 1
				PRINTL 새드끼가 1LV 내려갔다!
			ENDIF
		ENDIF
		IF GETBIT(CHOICE, 6)
			ABL:MASTER:레즈끼 += 1
			PRINTL 레즈끼가 1LV 올라갔다!
		ELSEIF GETBIT(CHOICE, 7)
			ABL:MASTER:레즈끼 -= 1
			PRINTL 레즈끼가 1LV 내려갔다!
		ENDIF
		IF GETBIT(CHOICE, 8)
			ABL:MASTER:ＢＬ끼 += 1
			PRINTL ＢＬ끼가 1LV 올라갔다!
		ELSEIF GETBIT(CHOICE, 9)
			ABL:MASTER:ＢＬ끼 -= 1
			PRINTL ＢＬ끼가 1LV 내려갔다!
		ENDIF

		IF GETBIT(CHOICE, 10)
			ABL:MASTER:Ｖ확장 += 1
			PRINTL Ｖ확장이 1LV 올라갔다! 
		ENDIF
		IF GETBIT(CHOICE, 11)
			ABL:MASTER:Ａ확장 += 1
			PRINTL Ａ확장이 1LV 올라갔다! 
		ENDIF

		IF CHOICE
			WAIT
		ELSE
			PRINTW 능력의 변경을 보류했습니다
		ENDIF
		REDRAW 1
		RETURN 0
	ELSEIF LOCAL:RESULT
		CLEARBIT CHOICE, RESULT + 1 - (RESULT%2)*2
		IF GETBIT(CHOICE, RESULT) == 0 && NUM_BIT(CHOICE) == 2
			PRINTW 선택 가능한 것은 두 개까지 입니다. 무언가 하나를 줄여주세요
		ELSE
			INVERTBIT CHOICE, RESULT
		ENDIF
	ENDIF
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	GOTO START
ENDSELECT


@EX_ABLUP, ARG
#DIM LCOUNT
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

VARSET LOCAL
;30,혀놀림 31,손가락놀림 32,발놀림 33,허리놀림 34,마유
FOR LCOUNT, 30, 36
	LOCAL:LCOUNT = ABL:ARG:LCOUNT
NEXT
CALL GET_EXABL, ARG

FOR LCOUNT, 30, 35
	IF ABL:ARG:LCOUNT > LOCAL:LCOUNT
		IF LINECOUNT == MEMO_LINECOUNT
			DRAWLINE
		ELSE
			PRINTFORML 
		ENDIF
		PRINTFORML %NAME:ARG%의 %ABLNAME:LCOUNT%의 LV이 올라갔다!
		PRINTFORMW %ABLNAME:LCOUNT%가 LV{ABL:ARG:LCOUNT}이 되었다!
	ENDIF
NEXT

LOCAL = TALENT:ARG:흡정체질
LOCAL:1 = TALENT:ARG:가창기능

CALL GET_EXTALENT, ARG

IF TALENT:ARG:흡정체질 > LOCAL
	SIF LINECOUNT == MEMO_LINECOUNT
		DRAWLINE
	PRINTFORMW %CALLNAME:ARG%의 흡정체질이 {TALENT:ARG:흡정체질}LV로 올라갔다!
ENDIF
IF TALENT:ARG:가창기능 > LOCAL:1 && EXP:ARG:조수경험 + EXP:ARG:조교횟수
	SIF LINECOUNT == MEMO_LINECOUNT
		DRAWLINE
	PRINTFORMW %CALLNAME:ARG%의 가창기능이 {TALENT:ARG:가창기능}LV로 올라갔다!
ENDIF

SIF LINECOUNT > MEMO_LINECOUNT
	DRAWLINE

;경험치の増加処理。ARGには主にCOND("절정", PLAYER)、ARG:1は경험치の受け取り손が入る
@GET_EXPERIENCE, ARG, ARG:1
;레벨差による보정をかける
LOCAL = MAX(MULTIPLY(ARG, 100 + (ABL:PLAYER:ＬＶ - ABL:(ARG:1):ＬＶ)*10), 1 + ARG/3)
EXP:(ARG:1):경험치 += LOCAL

@GET_TALENT, ARG
REDRAW 0
;ARGはHARDの時専用
PRINTL 
IF CONFIG("ＨＡＲＤ") && ARG > 0
	PRINTFORML {ABL:ＬＶ/5*5}LV\@ ABL:ＬＶ == ABL:ＬＶ/5*5 ? 이 되었기 # 을 넘었기 \@때문에, %타겟은()% 소질을 앞으로 {ARG}개 획득할 수 있게 되었습니다
	PRINTFORML 변화를 좋아하지 않는다면 [999] 돌아간다 를 선택해주세요
ELSE
	PRINTFORM 경험치를
	SETCOLOR DEF_COLOR("황색")
	PRINTFORM {NUM("경험치：소질획득")}Ｐ
	RESETCOLOR
	PRINTFORML 사용해서 몇 개의 소질을 획득하거나 잃을 수 있습니다
	;PRINTFORML （ただし、ここで소질を変化させる度に、필요な경험치は200Ｐづつ増えていきます）
	PRINTFORML 앞으로 {EXP:경험치}Ｐ의 경험치가 있습니다
ENDIF
PRINTFORML 
VARSET LOCAL

PRINTFORML [  0] 체력+200＆기력+200
LOCAL:0 = 1

IF COND("유두비대") < 3
	PRINTFORML [  8] 유두를 비대화 시킨다（[유두비대{COND("유두비대") + 1}LV](이)가 됩니다）
	LOCAL:8 = 1
ENDIF
IF TALENT:젖기어려움
	PRINTFORML [ 43] [젖기어려움] 를 잃는다
	LOCAL:43 = 1
ELSEIF TALENT:젖기쉬움 == 0
	PRINTFORML [ 42] [젖기쉬움] 를 얻는다
	LOCAL:42 = 1
ENDIF
IF TALENT:장액분비체질 == 0
	IF EXP:Ａ경험 >= 100
		PRINTFORML [ 48] [장액분비체질] 를 얻는다
		LOCAL:48 = 1
	ELSE
		CALL PRINT_STR, "暗회색_[ ×] [장액분비체질] 를 얻는다（요정：Ａ경험100）_L"
	ENDIF
ENDIF

IF TALENT:구토반사내성 == 0
	IF EXP:펠라경험 >= 300
		PRINTFORML [ 52] [구토반사내성] 를 얻는다
		LOCAL:52 = 1
	ELSE
		CALL PRINT_STR, "暗회색_[ ×] [구토반사내성] 를 얻는다（요정：펠라경험300）_L"
	ENDIF
ENDIF

IF TALENT:조합지식 == 0
	IF TALENT:현명함
		PRINTFORML [ 55] [조합지식] 를 얻는다
		LOCAL:55 = 1
	ELSE
		CALL PRINT_STR, "暗회색_[ ×] [조합지식] 를 얻는다（요정：[현명함]）_L"
	ENDIF
ENDIF
IF TALENT:확장적정 == 0
	IF MAX(EXP:Ｖ확장경험, EXP:Ａ확장경험, EXP:요도확장경험) >= 20
		PRINTFORML [ 59] [확장적정] 를 얻는다
		LOCAL:59 = 1
	ELSE
		CALL PRINT_STR, "暗회색_[ ×] [확장적정] 를 얻는다（요정：Ｖ확장경험20 or Ａ확장경험20 or 요도확장경험20）_L"
	ENDIF
ENDIF

IF TALENT:불결무시 == 0
	PRINTFORML [ 64] [불결무시] 를 얻는다
	LOCAL:64 = 1
ENDIF

IF TALENT:Ｍ둔감
	PRINTFORML [ 96] [Ｍ둔감] 를 잃는다
	LOCAL:96 = 1
ELSEIF TALENT:Ｍ민감 == 0
	PRINTFORML [ 97] [Ｍ민감] 를 얻는다
	LOCAL:97 = 1
ENDIF

IF TALENT:요정 || COND("子孫") || COND("몽마") == 0
ELSE
	IF TALENT:작은몸집 == 0 && TALENT:오토코노코 == 0
		PRINTFORML [ 99] \@ TALENT:장신 ? 키를 줄인다（[장신]을 잃음） # 키를 높인다 [장신]） \@
		LOCAL:99 = 1
	ENDIF
	IF TALENT:장신 == 0
		PRINTFORML [100] \@ TALENT:작은몸집 ? 보통의 키로 큰다（[작은몸집]을 잃음） # 키를 줄인다（[작은몸집]） \@
		LOCAL:100 = 1
	ENDIF
ENDIF

IF TALENT:Ｃ둔감
	PRINTFORML [101] [Ｃ둔감] 를 잃는다
	LOCAL:101 = 1
ELSEIF TALENT:Ｃ민감 == 0
	PRINTFORML [102] [Ｃ민감] 를 얻는다
	LOCAL:102 = 1
ENDIF

IF TALENT:남자 == 0
	IF TALENT:Ｖ둔감
		PRINTFORML [103] [Ｖ둔감] 를 잃는다
		LOCAL:103 = 1
	ELSEIF TALENT:Ｖ민감 == 0
		PRINTFORML [104] [Ｖ민감] 를 얻는다
		LOCAL:104 = 1
	ENDIF
ENDIF
IF TALENT:Ａ둔감
	PRINTFORML [105] [Ａ둔감] 를 잃는다
	LOCAL:105 = 1
ELSEIF TALENT:Ａ민감 == 0
	PRINTFORML [106] [Ａ민감] 를 얻는다
	LOCAL:106 = 1
ENDIF

IF TALENT:남자 == 0
	IF TALENT:Ｂ둔감
		PRINTFORML [107] [Ｂ둔감] 를 잃는다
		LOCAL:107 = 1
	ELSEIF TALENT:Ｂ민감 == 0
		PRINTFORML [108] [Ｂ민감] 를 얻는다
		LOCAL:108 = 1
	ENDIF
ENDIF

IF TALENT:남자 || COND("子孫") || COND("몽마") == 0
ELSE
	IF TALENT:거유 == 0
		PRINTFORML [109] \@ TALENT:빈유 ? 가슴을 크게 한다（빈유라고 할수 없어！） # 가슴을 작게한다（[빈유]） \@
		LOCAL:109 = 1
	ENDIF
	IF TALENT:빈유 == 0
		PRINTFORM [110] 
		IF TALENT:거유 >= 2
			PRINTFORML 가슴을 작게 한다（\@ TALENT:거유 >= 3 ? [초유]→[폭유] # [폭유]→[거유] \@）
		ELSEIF TALENT:거유
			PRINTFORML 가슴을 더 크게 만들거나, 작게만든다
		ELSE
			PRINTFORML 가슴을 크게 한다（[거유]）
		ENDIF
		LOCAL:110 = 1
	ENDIF
ENDIF

IF TALENT:회복느림
	PRINTFORML [112] [회복느림] 를 잃는다
	LOCAL:112 = 1
ELSEIF TALENT:회복빠름 == 0
	PRINTFORML [111] [회복빠름] 를 얻는다
	LOCAL:111 = 1
ENDIF

IF TALENT:치료 == 0
	IF ABL:기교 >= 3 || ABL:봉사정신 >= 5
		PRINTFORML [117] [치료] 를 얻는다
		LOCAL:117 = 1
	ELSE
		CALL PRINT_STR, "暗회색_[ ×] [치료] 를 얻는다（요정：기교3LV or 봉사정신5LV）_L"
	ENDIF
ENDIF
IF TALENT:금단의지식 == 0
	IF TALENT:현명함 && TALENT:조합지식 && TALENT:마법사 && ABL:기교 >= 5
		PRINTFORML [125] [금단의지식] 을 얻는다
		LOCAL:125 = 1
	ELSE
		CALL PRINT_STR, "暗회색_[ ×] [금단의지식] 을 얻는다（요정：[현명함][조합지식][마법사]기교5LV）_L"
	ENDIF
ENDIF
IF TALENT:구현 == 0
	IF TALENT:금단의지식
		PRINTFORML [127] [구현] 을 얻는다
		LOCAL:127 = 1
	ELSE
		CALL PRINT_STR, "暗회색_[ ×] [구현] 을 얻는다（요정：[금단의지식]）_L"
	ENDIF
ENDIF
IF TALENT:남자 || COND("子孫") || COND("몽마") == 0
ELSEIF TALENT:모유체질 == 0
	IF ABL:Ｂ감각 >= 5 || TALENT:거유
		PRINTFORML [130] [모유체질] 을 얻는다
		LOCAL:130 = 1
	ELSE
		CALL PRINT_STR, "暗회색_[ ×] [모유체질] 을 얻는다（요정：[거유] or Ｂ감각5LV）_L"
	ENDIF
ENDIF
IF TALENT:인후성감 == 0
	IF ABL:Ｍ감각 >= 3
		PRINTFORML [140] [인후성감] 을 얻는다
		LOCAL:140 = 1
	ELSE
		CALL PRINT_STR, "暗회색_[ ×] [인후성감] 을 얻는다（요정：Ｍ감각3LV）_L"
	ENDIF
ENDIF
IF TALENT:자궁성감 == 0 && TALENT:남자 == 0
	IF ABL:Ｖ감각 >= 3
		PRINTFORML [141] [자궁성감] 을 얻는다
		LOCAL:141 = 1
	ELSE
		CALL PRINT_STR, "暗회색_[ ×] [자궁성감] 을 얻는다（요정：Ｖ감각3LV）_L"
	ENDIF
ENDIF
IF TALENT:요도성감 == 0
	IF ABL:Ｃ감각 >= 3
		PRINTFORML [142] [요도성감] 을 얻는다
		LOCAL:142 = 1
	ELSE
		CALL PRINT_STR, "暗회색_[ ×] [요도성감] 을 얻는다（요정：Ｃ감각3LV）_L"
	ENDIF
ENDIF

IF TALENT:냄새민감 == 0
	PRINTFORML [144] [냄새민감] 을 얻는다\@ TALENT:악취둔감 ? （동시에 [악취둔감]을 잃음）# \@
	LOCAL:144 = 1
ENDIF

IF TALENT:마법사 == 0
	IF ABL:기교 >= 5 && TALENT:현명함
		PRINTFORML [151] [마법사] 를 얻는다
		LOCAL:151 = 1
	ELSE
		CALL PRINT_STR, "暗회색_[ ×] [마법사] 를 얻는다（요정：[현명함]기교5LV）_L"
	ENDIF
ENDIF

IF TALENT:미련함 == 0
	PRINTFORML [185] [조교지식] 을\@ TALENT:조교지식 ? 변경한다 # 얻는다 \@
	LOCAL:185 = 1
ENDIF
IF TALENT:비젼 == 0 && BASE:종족사고 == 22
	PRINTFORML [243] [분신] 을 체득한다
	LOCAL:243 = 1
ENDIF

PRINTL [999] 돌아간다

CALL INPUT_SELECT, 250, 999

IF RESULT == 999
	REDRAW 1
	RETURN 0
ELSEIF LOCAL:RESULT == 0
	RESTART
ELSEIF CONFIG("ＨＡＲＤ") && ARG > 0
ELSEIF EXP:경험치 < NUM("경험치：소질획득")
	PRINTW 경험치가 부족합니다
	REDRAW 1
	RETURN 0
ENDIF

SELECTCASE RESULT
CASE 0
	MAXBASE:체력 += 200
	MAXBASE:기력 += 200
	BASE:체력 += 200
	BASE:기력 += 200
	PRINTFORMW 체력과 기력이 200씩 상승했습니다
CASE 8
	SELECTCASE COND("유두비대")
	CASE 0
		CLEARBIT TALENT:유두, 0
		SETBIT TALENT:유두, 1
	CASE 1
		CLEARBIT TALENT:유두, 1
		SETBIT TALENT:유두, 2
	CASE 2
		CLEARBIT TALENT:유두, 2
		SETBIT TALENT:유두, 3
	ENDSELECT
	PRINTFORMW [유두비대\@ COND("유두비대") >= 2 ? LV{COND("유두비대")} # \@]를 획득했습니다
CASE 185
	LOCAL = TALENT:조교지식
	CALL SETFLAG, "조교지식선택", TARGET
	SIF TALENT:조교지식 == LOCAL
		RESTART
;純粋獲得
CASE 42, 48, 52, 55, 59, 64, 97, 102, 104, 106, 108, 111, 117, 125, 127, 140, 141, 142, 151, 189
	TALENT:RESULT = 1
	PRINTFORMW [%TALENTNAME:RESULT%]를 획득했습니다
;분신
CASE 243
	TALENT:RESULT = 2
	PRINTFORMW [%TALENT_NAME(RESULT, TALENT:ARG:RESULT)%]를 체득했습니다

;生まれつきかどうかで違いがある소질の獲得
CASE 130
	TALENT:RESULT = 2
	PRINTFORMW [%TALENTNAME:RESULT%]를 획득했습니다
;純粋喪失
CASE 43, 96, 101, 103, 105, 107, 112
	TALENT:RESULT = 0
	PRINTFORMW [%TALENTNAME:RESULT%]를 잃었습니다
;반전
CASE 99
	TALENT:장신 = !TALENT:장신
	IF TALENT:장신
		CALL CALC, "장신化", TARGET
		PRINTFORMW [장신]을 획득했습니다
	ELSE
		CALL CALC, "보통体型化", TARGET
		PRINTFORMW [장신]을 잃었습니다
	ENDIF
CASE 100
	TALENT:작은몸집 = !TALENT:작은몸집
	IF TALENT:작은몸집
		CALL CALC, "小柄化", TARGET
		PRINTFORMW [작은몸집]을 획득했습니다
	ELSE
		CALL CALC, "보통体型化", TARGET
		PRINTFORMW [작은몸집]을 잃었습니다
	ENDIF
CASE 109
	TALENT:빈유 = !TALENT:빈유
	IF TALENT:빈유
		CALL CALC, "빈유化", TARGET
		PRINTFORMW [빈유]를 획득했습니다
	ELSE
		CALL CALC, "평유化", TARGET
		PRINTFORMW [빈유]를 잃었습니다
	ENDIF
CASE 110
	IF TALENT:거유 == 1
		PRINTL [ 0] 폭유가 된다
		PRINTL [ 1] 가슴을 작게 만든다（거유를 잃음）
		PRINTL [ 2] 돌아간다
		CALL INPUT_SELECT, 3
		IF RESULT == 0
			CALL CALC, "폭유化", TARGET
			PRINTFORMW [폭유]가 되었습니다
		ELSEIF RESULT == 1
			CALL CALC, "평유化", TARGET
			PRINTFORMW [거유]를 잃었습니다
		ELSE
			RESTART
		ENDIF
	ELSEIF TALENT:거유 >= 3
		CALL CALC, "폭유化", TARGET
		PRINTFORMW [폭유]가 되었습니다
	ELSE
		CALL CALC, "거유化", TARGET
		PRINTFORMW [거유]가 되었습니다
	ENDIF
CASE 144
	TALENT:RESULT = 1
	IF TALENT:악취둔감
		TALENT:악취둔감 = 0
		PRINTFORM [악취둔감]을 잃어,
	ENDIF
	PRINTFORMW [%TALENTNAME:RESULT%]%조사만처리(TALENTNAME:RESULT,"를")% 획득했습니다
ENDSELECT

IF CONFIG("ＨＡＲＤ") && ARG > 0
	ARG -= 1
	IF ARG == 0
		REDRAW 1
		RETURN 0
	ELSE
		RESTART
	ENDIF
ELSE
	EXP:경험치 -= NUM("경험치：소질획득")
	SIF COND("소질획득가능")
		RESTART
ENDIF
REDRAW 1

