;能力アップ可否判定追加版
@SHOW_JUEL
#DIM MEMO_FLAG99
;조교中플래그記憶
MEMO_FLAG99 = FLAG:99

PRINTL 
;조교中플래그ON
SETBIT FLAG:99, 10

CALL NEW_PRINT_STATE, TARGET, "ＬＶＵＰ"

;조교中플래그戻す
FLAG:99 = MEMO_FLAG99

@SHOW_ABLUP_SELECT
CALL PRINT_SHOW_ABLUP_SELECT


;ARGSに"予定"と入っていると레벨업가능かどうかなどだけ表示される（宝구슬取得予定数で用いる）
@PRINT_SHOW_ABLUP_SELECT, ARGS
#DIM LCOUNT
#DIM ABL_NUM

;ABL_NUMを用いて, 表示順番とＡＢＬの番号を調整して表示する
FOR LCOUNT, 0, 18
	SELECTCASE LCOUNT
	CASE 4
		ABL_NUM = LCOUNT
		SIF TALENT:남자
			CONTINUE
	CASE 6
		ABL_NUM = 14
	CASE 7
		ABL_NUM = 16
	CASE 8
		ABL_NUM = 6
	CASE 9
		ABL_NUM = 7
	CASE 10
		ABL_NUM = 8
	CASE 11
		ABL_NUM = 15
	CASE 12
		SIF TALENT:남자
			CONTINUE
		ABL_NUM = 9
	CASE 13
		ABL_NUM = 10
	CASE 14
		ABL_NUM = 11
	CASE 15
		SIF TALENT:남자 == 0
			CONTINUE
		ABL_NUM = 13
	CASE 16
		SIF TALENT:남자
			CONTINUE
		;상한を上回っている場合には戻す
		ABL:Ｖ확장 = MIN(CALCF("Ｖ확장の상한", TARGET), ABL:Ｖ확장)
		;ＶＡ확장度を손動であげない場合は, 自動的に최대레벨になる
		IF CONFIG("ＶＡ상세표시：수동ＬＶＵＰ") == 0
			ABL:Ｖ확장 = CALCF("Ｖ확장の상한", TARGET)
			CONTINUE
		ELSE
			ABL_NUM = 23
		ENDIF
	CASE 17
		;상한を上回っている場合には戻す
		ABL:Ａ확장 = MIN(CALCF("Ａ확장の상한", TARGET), ABL:Ａ확장)
		;ＶＡ확장度を손動であげない場合は, 自動的に최대레벨になる
		IF CONFIG("ＶＡ상세표시：수동ＬＶＵＰ") == 0
			ABL:Ａ확장 = CALCF("Ａ확장の상한", TARGET)
			CONTINUE
		ELSE
			ABL_NUM = 26
		ENDIF
	CASEELSE
		ABL_NUM = LCOUNT
	ENDSELECT

	VARSET LOCALS

	LOCALS = %ABLNAME:ABL_NUM%
	CALL ISABLUP, LOCALS
	SIF RESULT && ABL:ABL_NUM < NUM("최대레벨", ABL_NUM)
		LOCALS:1 = ☆레벨업가능☆%BL(1)%

	;특수な습득
	SELECTCASE ABL_NUM
	CASE 0
		SIF CONFIG("연모と음란の任意획득") && COND("연모취득가능")
			LOCALS:1 = %LOCALS:1%☆연모☆
	CASE 1
		SIF CONFIG("연모と음란の任意획득") && COND("음란취득가능")
			LOCALS:1 = %LOCALS:1%☆음란☆
	CASE 2
		SIF COND("아르카나습득가능")
			LOCALS:1 = %LOCALS:1%☆아르카나습득가능☆
	CASE 3
		SIF COND("음핵취득불가능") == 0
			LOCALS:1 = %LOCALS:1%☆음핵☆
		SIF COND("요도광취득불가능") == 0
			LOCALS:1 = %LOCALS:1%☆요도광☆
	CASE 4
		SIF COND("음호취득불가능") == 0
			LOCALS:1 = %LOCALS:1%☆음호☆
	CASE 5
		SIF COND("애널광취득불가능") == 0
			LOCALS:1 = %LOCALS:1%☆애널광☆
	CASE 7
		SIF COND("도착적취득불가능") == 0
			LOCALS:1 = %LOCALS:1%☆도착적☆
	CASE 8
		SIF COND("마조취득불가능") == 0
			LOCALS:1 = %LOCALS:1%☆마조☆
	CASE 14
		SIF COND("음유취득불가능") == 0
			LOCALS:1 = %LOCALS:1%☆음유☆
	CASE 15
		SIF COND("새드취득불가능") == 0
			LOCALS:1 = %LOCALS:1%☆새드☆
	CASE 16
		SIF COND("음탕한입술취득불가능") == 0
			LOCALS:1 = %LOCALS:1%☆음탕한입술☆
	ENDSELECT

	SELECTCASE ARGS
	CASE "予定"
		IF LOCALS:1 != ""
			PRINTFORM %TEXT_RJ(LOCALS, 8)%
			SETCOLOR DEF_COLOR("노랑")
			PRINTFORML  %LOCALS:1%
			RESETCOLOR
		ENDIF
	CASEELSE
		PRINTFORM  [{LCOUNT,2}] %TEXT_RJ(LOCALS, 8)%-{ABL:ABL_NUM, 2}LV 
		CALL PRINT_BAR, "황색게이지", ABL:ABL_NUM, NUM("최대레벨", ABL_NUM), 5
		IF LOCALS:1 != ""
			SETCOLOR DEF_COLOR("노랑")
			PRINTFORM  %LOCALS:1%
			RESETCOLOR
		ENDIF
		PRINTL 
	ENDSELECT
NEXT

SELECTCASE ARGS
CASE "予定"
	VARSET LOCALS
	CALL ISABLUP, "ＬＶ"
	SIF RESULT
		LOCALS = ☆레벨업가능☆%BL(1)%
	SIF COND("소질획득가능")
		LOCALS = %LOCALS%☆소질획득가능☆%BL(1)%
	SIF COND("성장가능")
		LOCALS = %LOCALS%☆성장가능☆
	IF LOCALS != ""
		PRINTFORM %TEXT_RJ("레벨", 8)%
		SETCOLOR DEF_COLOR("노랑")
		PRINTFORML  %LOCALS%
		RESETCOLOR
	ENDIF

	VARSET LOCALS
	CALL ISABLUP, "굴복각인"
	SIF RESULT
		LOCALS = ☆레벨업가능☆
	IF LOCALS != ""
		PRINTFORM %TEXT_RJ("굴복각인", 8)%
		SETCOLOR DEF_COLOR("노랑")
		PRINTFORML  %LOCALS%
		RESETCOLOR
	ENDIF

	VARSET LOCALS
	CALL ISABLUP, "반발각인"
	SIF RESULT
		LOCALS = ☆레벨다운가능☆
	IF LOCALS != ""
		PRINTFORM %TEXT_RJ("반발각인", 8)%
		SETCOLOR DEF_COLOR("노랑")
		PRINTFORML  %LOCALS%
		RESETCOLOR
	ENDIF
CASEELSE
	PRINTFORM  [90]   레벨-{ABL:ＬＶ, 2}LV 
	CALL PRINT_BAR, "황색게이지", ABL:ＬＶ, NUM("최대레벨", 90), 5
	SETCOLOR DEF_COLOR("노랑")
	CALL ISABLUP, "ＬＶ"
	SIF RESULT
		PRINT  ☆레벨업가능☆
	SIF COND("소질획득가능")
		PRINT  ☆소질획득가능☆
	SIF COND("성장가능")
		PRINT  ☆성장가능☆
	RESETCOLOR
	PRINTL  

	PRINTFORM  [98] 굴복각인-{MARK:굴복각인, 2}LV 
	CALL PRINT_BAR, "황색게이지", MARK:굴복각인, 3, 3
	SETCOLOR DEF_COLOR("노랑")
	CALL ISABLUP, "굴복각인"
	SIF RESULT
		PRINT    ☆레벨업가능☆
	RESETCOLOR
	PRINTL  

	IF MARK:반발각인
		PRINTFORM  [99] 반발각인-{MARK:반발각인, 2}LV 
		CALL PRINT_BAR, "황색게이지", MARK:반발각인, 3, 3
		SETCOLOR DEF_COLOR("노랑")
		CALL ISABLUP, "반발각인"
		SIF RESULT
			PRINT    ☆레벨다운가능☆
		RESETCOLOR
		PRINTL  
	ENDIF

	PRINTL [200] 불필요한 구슬을 경험치로 전환 한다
	PRINTL [999] 능력치업 종료
ENDSELECT

@USERABLUP
SELECTCASE RESULT
CASE 999
	;TARGETの소질変動など
	SIF TARGET
		CALL EVENTCHECK_T
	;ASSIの소질変動など
	CALL EVENTCHECK_A
	;MASTERの소질変動など
	CALL EVENTCHECK_M
	SELECTCASE FLAG:22
	CASE 1
		FLAG:22 = 2
		FLAG:21 = TARGET
		TARGET = ASSI
		BEGIN AFTERTRAIN
	CASE 2
		FLAG:22 = 0
		ASSI = TARGET
		TARGET = FLAG:21
	ENDSELECT
	BEGIN TURNEND
	RETURN 1
CASE 200
	CALL JUEL_TO_EXP, 10000
ENDSELECT

RETURN 0

;ARGは１度に交換する単位. 初期は１万
@JUEL_TO_EXP, ARG
#DIM LCOUNT
#DIM MEMO_EXP
#DIM MEMO_JUEL, 16

;まずは경험치と各種구슬の量を記録
MEMO_EXP = EXP:경험치
FOR LCOUNT, 0, 16
	MEMO_JUEL:LCOUNT = JUEL:LCOUNT
NEXT

CALL JUEL_TO_EXP_CALC, ARG, JUEL:0, JUEL:1, JUEL:2, JUEL:3, JUEL:4, JUEL:5, JUEL:6, JUEL:7, JUEL:8, JUEL:9, JUEL:10, JUEL:11, JUEL:12, JUEL:13, JUEL:14, JUEL:15

;やり直す？
IF RESULT == 99
	EXP:경험치 = MEMO_EXP
	FOR LCOUNT, 0, 16
		JUEL:LCOUNT = MEMO_JUEL:LCOUNT
	NEXT
	RESTART
ENDIF

;交換単位は最小１万. ARG < 0ならば全部交換
@JUEL_TO_EXP_CALC, ARG, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARG:7, ARG:8, ARG:9, ARG:10, ARG:11, ARG:12, ARG:13, ARG:14, ARG:15, ARG:16
#DIM LCOUNT
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
REDRAW 0

PRINTFORML 
PRINTFORML 여기에선 각종 구슬 1만개를 경험치 10P로 교환하는 장소 입니다.
PRINTFORML 조교로 쌓는 경험치 보다 좋은것은 아니지만, 남는 구슬을 처리할때 유용합니다.
PRINTFORML 
PRINTFORM 현재 경험치의{EXP:경험치}Ｐ에서, 
IF ARG < 0
	PRINTFORML 선택된 구슬을 교환합니다. 
	PRINTFORML 어느것을 교환 하겠습니까？
ELSE
	PRINTFORML 교환 단위는{ARG/10000}만입니다. 어느 것을 교환 하시겠습니까？
	PRINTFORML （교환 단위 미만의 경우 １만 단위로 교환 가능합니다.）
ENDIF

FOR LCOUNT, 0, 16
	SELECTCASE LCOUNT
	CASE 0 TO 2, 4 TO 10, 14, 15
		SELECTCASE ARG:(LCOUNT+1)
		CASE IS >= 10000
			PRINTFORM  [{LCOUNT, 2}] 
		CASEELSE
			SETCOLOR DEF_COLOR("회색")
			PRINTFORM  [×] 
		ENDSELECT

		PRINTFORM %PALAMNAME:LCOUNT%の구슬:{ARG:(LCOUNT+1), 8}
		SIF JUEL:LCOUNT != ARG:(LCOUNT+1)
			PRINTFORM  → {JUEL:LCOUNT, 8} {(ARG:(LCOUNT+1) - JUEL:LCOUNT)/1000, 5}Ｐ get
		PRINTL 
		RESETCOLOR
	ENDSELECT
NEXT

SELECTCASE ARG
CASE 10000 TO 1000000
	PRINTFORML  [90] 교환 단위를{ARG/1000}만으로 증가
CASE 10000000
	PRINTFORML  [90] 모든 구슬을 경험치로 교환한다
CASEELSE
	PRINTFORML  [90] 교환 단위를 １만으로 돌린다
ENDSELECT

PRINTFORML  [99] 교환 상태를 재설정
PRINTFORML [100] 종료

CALL INPUT_SELECT, 16, 90, 99, 100

SELECTCASE RESULT
CASE 100
	REDRAW 1
	RETURN 1
CASE 99
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	REDRAW 1
	RETURN 99
CASE 90
	SELECTCASE ARG
	CASE 10000 TO 1000000
		ARG *= 10
	CASE 10000000
		ARG = -1
	CASEELSE
		ARG = 10000
	ENDSELECT
CASEELSE
	SELECTCASE RESULT
	CASE 0 TO 2, 4 TO 10, 14, 15
		IF JUEL:RESULT < 10000

		ELSEIF ARG > 0 && JUEL:RESULT >= ARG
			JUEL:RESULT -= ARG
			EXP:경험치 += 10 * ARG / 10000
		ELSE
			LOCAL = JUEL:RESULT / 10000

			JUEL:RESULT -= 10000 * LOCAL
			EXP:경험치 += 10 * LOCAL
		ENDIF
	ENDSELECT
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART



@CHECK_ABLUP_OK
#DIM LCOUNT

;各種구슬や経験などの체크
FOR LCOUNT, 0, 10
	;(A:X, B:X)～(E:X, F:X)が(필요な구슬の数, 구슬の種類)
	SIF A:LCOUNT == 0
		SETBIT Z:LCOUNT, 9
	SIF JUEL:(B:LCOUNT) < A:LCOUNT || JUEL:(D:LCOUNT) < C:LCOUNT || JUEL:(F:LCOUNT) < E:LCOUNT
		SETBIT Z:LCOUNT, 0

	;(G:X, H:X)～(K:X, L:X)が(필요な経験, 経験の種類)
	SIF EXP:(H:LCOUNT) < G:LCOUNT || EXP:(J:LCOUNT) < I:LCOUNT || EXP:(L:LCOUNT) < K:LCOUNT
		SETBIT Z:LCOUNT, 1

	;(M, N)が全体を通して필요な経験
	SIF EXP:N < M
		SETBIT Z:LCOUNT, 1

	;(O, P), (Q, R)が全体を通して필요な能力
	SIF ABL:P < O || ABL:R < Q
		SETBIT Z:LCOUNT, 2

	;(S, T)が全体を通して필요な刻印
	SIF MARK:T < S
		SETBIT Z:LCOUNT, 6

	;(U, V)が全体を通して필요な절정
	SIF EX:V < U
		SETBIT Z:LCOUNT, 5
NEXT

FOR LCOUNT, 0, 10
	;Z:Xが上を踏まえて가능かどうかの判定. ただし, 가능な場合が０である
	SIF Z:LCOUNT == 0
		RETURN 1
NEXT

@PRINT_ABLUP_OK(ARG)
#FUNCTIONS
VARSET LOCALS
IF ARG == 0
	LOCALS = ＯＫ
ELSE
	SIF GETBIT(ARG, 0)
		LOCALS = %LOCALS%구슬부족%BL(1)%
	SIF GETBIT(ARG, 1)
		LOCALS = %LOCALS%경험부족%BL(1)%
	SIF GETBIT(ARG, 2)
		LOCALS = %LOCALS%능력부족%BL(1)%
	SIF GETBIT(ARG, 3)
		LOCALS = %LOCALS%소질부족%BL(1)%
	SIF GETBIT(ARG, 4)
		LOCALS = %LOCALS%레벨부족%BL(1)%
	SIF GETBIT(ARG, 5)
		LOCALS = %LOCALS%절정부족%BL(1)%
	SIF GETBIT(ARG, 6)
		LOCALS = %LOCALS%낙인부족%BL(1)%
	;GETBIT(ARG, 9)はとにかく무리な場合に使用
ENDIF
RETURNF LOCALS

@ABLUP0
IF CONFIG("연모と음란の任意획득") && COND("연모취득가능")
	CALL ABL_UP, "순종", "연모"
ELSE
	CALL ABL_UP, "순종"
ENDIF

@ABLUP1
IF CONFIG("연모と음란の任意획득") && COND("음란취득가능")
	CALL ABL_UP, "욕망", "음란"
ELSE
	CALL ABL_UP, "욕망"
ENDIF

@ABLUP2
IF COND("아르카나습득가능")
	CALL ABL_UP, "기교", "아르카나"
ELSE
	CALL ABL_UP, "기교"
ENDIF

@ABLUP3
VARSET LOCALS
SIF TALENT:음핵 == 0 && ABL:Ｃ감각 >= 4
	LOCALS = 음핵
SIF TALENT:요도광 == 0 && TALENT:요도성감
	LOCALS:1 = 요도광

CALL ABL_UP, "Ｃ감각", LOCALS, LOCALS:1

;IF TALENT:음핵 == 0 && ABL:Ｃ감각 >= 4
;	CALL ABL_UP, "Ｃ감각", "음핵"
;ELSE
;	CALL ABL_UP, "Ｃ감각"
;ENDIF

@ABLUP4
IF TALENT:음호 == 0 && ABL:Ｖ감각 >= 4
	CALL ABL_UP, "Ｖ감각", "음호"
ELSE
	CALL ABL_UP, "Ｖ감각"
ENDIF

@ABLUP5
IF TALENT:애널광 == 0 && ABL:Ａ감각 >= 4
	CALL ABL_UP, "Ａ감각", "애널광"
ELSE
	CALL ABL_UP, "Ａ감각"
ENDIF

@ABLUP6
IF TALENT:음유 == 0 && ABL:Ｂ감각 >= 4
	CALL ABL_UP, "Ｂ감각", "음유"
ELSE
	CALL ABL_UP, "Ｂ감각"
ENDIF

@ABLUP7
IF TALENT:음탕한입술 == 0 && ABL:Ｍ감각 >= 4
	CALL ABL_UP, "Ｍ감각", "음탕한입술"
ELSE
	CALL ABL_UP, "Ｍ감각"
ENDIF

@ABLUP8
CALL ABL_UP, "봉사정신"

@ABLUP9
IF TALENT:도착적 == 0 && ABL:노출벽 + ABL:마조끼 + ABL:새드끼 >= 7
	CALL ABL_UP, "노출벽", "도착적"
ELSE
	CALL ABL_UP, "노출벽"
ENDIF

@ABLUP10
IF TALENT:마조 == 0 && ABL:마조끼 >= 4
	CALL ABL_UP, "마조끼", "마조"
ELSE
	CALL ABL_UP, "마조끼"
ENDIF

@ABLUP11
IF TALENT:새드 == 0 && ABL:새드끼 >= 4
	CALL ABL_UP, "새드끼", "새드"
ELSE
	CALL ABL_UP, "새드끼"
ENDIF

@ABLUP12
CALL ABL_UP, "레즈끼"

@ABLUP13
CALL ABL_UP, "자위중독"

@ABLUP14
CALL ABL_UP, "정액중독"

@ABLUP15
CALL ABL_UP, "ＢＬ끼"

@ABLUP16
CALL ABL_UP, "Ｖ확장"

@ABLUP17
CALL ABL_UP, "Ａ확장"

@ABLUP90
CALL ABL_UP, "ＬＶ", "ＬＶ以外"

@ABLUP98
CALL ABL_UP, "굴복각인"

@ABLUP99
CALL ABL_UP, "반발각인"


;TARGETの소질変動など
@EVENTCHECK_T
;-------------------------------------------------------------------------------------------------
;技能の成長
;-------------------------------------------------------------------------------------------------
CALL EX_ABLUP, TARGET
;-------------------------------------------------------------------------------------------------
;ネガティブな소질の克옷
;-------------------------------------------------------------------------------------------------
;반항적ではなくなる이벤트（ある程度の好感度と、今までに반발각인を1LVまでしかつけたことが無い、というのが条件）
;もしくは、순종4LV+반발각인0LV
IF TALENT:반항적
	LOCAL = 0
	IF CFLAG:2 >= 100 + MARK:반발각인의취득이력*400 && MARK:반발각인의취득이력 <= 1
		LOCAL = 1
	ELSEIF ABL:순종 >= 4 && MARK:반발각인 == 0
		LOCAL = 2
	ENDIF
	IF LOCAL
		IF LOCAL == 1
			PRINTFORM 상냥하게 접한 덕분인가,%CALLNAME:TARGET%하
			PRINTFORMW \@EXP:조교횟수 > 1 ? 最近%CALLNAME:MASTER%에 막역해 온 것 같다… # 어깨가 힘을 뺀 것 같다… \@
		ENDIF
		PRINTFORMW %CALLNAME:TARGET%하【반항적】는 아니게 되었습니다
		TALENT:반항적 = 0
	ENDIF
ENDIF

IF MARK:반발각인 == 0
	;好感度が4000이상で연모か음란を持っていると、매각가능に
	IF CFLAG:2 >= 4000 && COND("매각가능플래그") == 0 && (TALENT:음란 || TALENT:연모)
		PRINTFORMW %CALLNAME:TARGET%의 이쪽을 보는 눈이 이상하다…
		PRINTFORML %CALLNAME:TARGET%는 몸도 마음도%CALLNAME:MASTER%에 의존해 버리고 있는 것 같다…
		IF ABL:순종 < 5
			ABL:순종 = 5
			PRINTL 순종하지만LV5(이)가 되었다
		ENDIF
		PRINTFORMW %CALLNAME:TARGET%의매각가능(이)가 되었습니다
		SETBIT CFLAG:1, 0
	;好感度が2500이상、순종が3이상かつ봉사쾌락경험500이상で연모
	ELSEIF CONFIG("연모と음란の任意획득") == 0 && COND("연모취득가능")
		CALL SETFLAG, "연모"
	;욕망が4이상、절정경험や정액경험などの合計が800이상、자위중독と정액중독の合計3이상、今回の절정数が20이상で음란
	ELSEIF CONFIG("연모と음란の任意획득") == 0 && COND("음란취득가능")
		CALL SETFLAG, "음란"
	ENDIF
ENDIF

;가슴の大きさＵＰ
CALL CALC, "바스트UP"

;매각や조수化が가능になったか
SIF ABL:순종 + ABL:욕망 < 5
	RETURN 0
SIF MAX(ABL:Ｃ감각, ABL:Ｖ감각, ABL:Ａ감각, ABL:Ｂ감각, ABL:Ｍ감각) < 3
	RETURN 0
SIF ABL:순종 < 4 && (TALENT:반항적 || TALENT:꿋꿋함 || TALENT:저항)
	RETURN 0
SIF ABL:욕망 < 4 && (TALENT:냉정 || TALENT:억압 || TALENT:저항)
	RETURN 0

IF COND("매각가능플래그") == 0
	IF ABL:순종 >= 5 || ABL:욕망 >= 5 || (ABL:기교 >= 3 && ABL:봉사정신 >= 3) || (ABL:노출벽 >= 3 && ABL:자위중독 >= 2) || ABL:마조끼 >= 3 || ABL:Ｃ감각+ABL:Ｖ감각+ABL:Ａ감각+ABL:Ｂ감각+ABL:Ｍ감각 >= 13
		PRINTFORMW %CALLNAME:TARGET%의매각가능(이)가 되었습니다
		SETBIT CFLAG:1, 0
	ENDIF
ENDIF

SIF MARK:반발각인
	RETURN 0

IF COND("조수가능플래그") == 0
	IF ABL:순종 >= 3 && (ABL:기교 >= 4 || (ABL:욕망 >= 3 && ABL:기교 >= 3) || ABL:순종 >= 5 || ABL:레즈끼 >= 3)
		PRINTFORMW %CALLNAME:TARGET%의%TEXTS("조수の명칭")%(이)가 될 수 있게 되었습니다
		SETBIT CFLAG:1, 1
	ENDIF
ENDIF

