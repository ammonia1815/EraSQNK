;複数조교と大人数対応を合成. 複数조교用キャラカード체크を削除
;커맨드メニュー及び条件を変更
;｢@SELECT_TARGET｣を削除
;｢@SHOW_CHARADATA｣＝ステータス表示
;｢@CHARA_SALE｣＝キャラ매각
;｢@SALEITEM_CHECK｣｢@USE_ITEM｣＝아이템の購入と使用
;-------------------------------------------------
;낮や밤の始まりに一度だけ起きる이벤트はここで記述
;-------------------------------------------------
@EVENTSHOP
#PRI
#DIM LCOUNT
;안전위험일判定(생일依存でいってみる)
CALL SETFLAG, "안전위험일判定"

;아침
IF TIME == 0
	CALL YOBAI_ASAGAKE

	SELECTCASE CONFIG("조교종료時の복장")
	;終了時に마음에듬の옷に着替えてもらうなら
	CASE 2
		FOR LCOUNT, 2, CHARANUM
			;낮用の衣装に着替えてもらう
			IF GETBIT(IS_FAVORITE_DRESS(LCOUNT), 5)
				CALL SET_FAVORITE_DRESS, LCOUNT, 5
				SIF CONFIG("팬티を몽마の好きにさせる")
					CALL RAND_PANTU, LCOUNT
			ELSEIF GETBIT(IS_FAVORITE_DRESS(LCOUNT), 0)
				CALL SET_FAVORITE_DRESS, LCOUNT, 0
				SIF CONFIG("팬티を몽마の好きにさせる")
					CALL RAND_PANTU, LCOUNT
			ENDIF
			;팬티の着用禁止
			SIF COND("身嗜み：노팬티", LCOUNT)
				CALL SET_PANTIES, LCOUNT
		NEXT
	ENDSELECT
;밤
ELSE
	SELECTCASE CONFIG("조교종료時の복장")
	;終了時に마음에듬の옷に着替えてもらうなら
	CASE 2
		FOR LCOUNT, 2, CHARANUM
			;밤用の衣装に着替えてもらう
			IF GETBIT(IS_FAVORITE_DRESS(LCOUNT), 6)
				CALL SET_FAVORITE_DRESS, LCOUNT, 6
				SIF CONFIG("팬티を몽마の好きにさせる")
					CALL RAND_PANTU, LCOUNT
			ELSEIF GETBIT(IS_FAVORITE_DRESS(LCOUNT), 0)
				CALL SET_FAVORITE_DRESS, LCOUNT, 0
				SIF CONFIG("팬티を몽마の好きにさせる")
					CALL RAND_PANTU, LCOUNT
			ENDIF
			;팬티の着用禁止
			SIF COND("身嗜み：노팬티", LCOUNT)
				CALL SET_PANTIES, LCOUNT
		NEXT
	ENDSELECT
ENDIF

;서류작업のフラグ
FLAG:12 = 0
;一言이벤트の発言フラグリセット
CALL RESET_SEXUHARAFLAG

;極短시간の記録用のCEVENT消去
FOR LCOUNT, 1, CHARANUM
	CALL DEL_CEVENT_GROUP, "一言이벤트短期", LCOUNT
NEXT

DRAWLINE
DRAWLINE

;-------------------------------------------------
;まだ조교していないキャラへの플래그설정
;-------------------------------------------------
@CHECK_FLAG_MUMA
#DIM LCOUNT

FOR LCOUNT, 1, CHARANUM
	;옷のずれなどを直す
	CALL RESETFLAG_CLO, LCOUNT
	CALL SETFLAG, "페니스상세설정"
	CALL SETFLAG, "속성", LCOUNT

	;유니크캐릭터의判定
	SIF COND("유니크", LCOUNT)
		FLAG:(1000 + NO:LCOUNT) = 1
	;남자なら胎内の정액を消去. 
	IF TALENT:LCOUNT:남자
		BASE:LCOUNT:자궁정자량 = 0
		BASE:LCOUNT:태내정액량 = 0
	ENDIF

	;キャラ独自の식별번호を設置
	CALL SETFLAG, "식별번호", LCOUNT

	;매각価格の計算
	CALL SETFLAG, "매각価格", LCOUNT

	;既に조교した経験があるならば이하は判定済み
	SIF EXP:LCOUNT:조교횟수
		CONTINUE

	;男, 女の判定
	SELECTCASE PENIS(LCOUNT)
	CASE 0
		TALENT:LCOUNT:여자 = 1
	CASEELSE
		TALENT:LCOUNT:여자 = 0
	ENDSELECT

	;スリーサイズの判定や変化
	CALL SET_3SIZE, LCOUNT
	;特技LV
	CALL GET_EXABL, LCOUNT
	CALL GET_EXTALENT, LCOUNT
	;衣装の설정
	IF CHECK_CLO("全裸", LCOUNT)
		CALL COORDINATE_MUMA, LCOUNT
		CALL MEMO_FAVORITE_DRESS, LCOUNT
	ENDIF
	CALL SETFLAG, "음모と겨털", LCOUNT
	CALL SETFLAG, "유두の상세설정", LCOUNT
	CALL CALC, "선호체위", LCOUNT
	;생일の설정
	SIF BASE:LCOUNT:생일 == 0
		BASE:LCOUNT:생일 = RAND:120 + 1
	;상성の설정
	SIF RELATION:LCOUNT:(NO:LCOUNT) < 120
		RELATION:LCOUNT:(NO:LCOUNT) = 120
	SIF BASE:LCOUNT:배설물 == 0
		BASE:LCOUNT:배설물 = RAND:4 + RAND:4
NEXT

@SHOW_SHOP
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM NEXT_LINE
#DIM MEMO_TARGET
#DIM MEMO_ASSI
#DIM ACTOR
#DIMS MENU_SHOP

;-------------------------------------------------
;バージョンアップ
;-------------------------------------------------
CALL VERUP_ERASQN, TARGET


REDRAW 0

;실행치の表示/비표시(0で비표시, 1で表示)
FLAG:8 = 1
;TFLAGをリセット
VARSET TFLAG
;各캐릭터의行動等をリセット(bit)
;0夜に妊婦と過ごした, 1다방出演, 2하룻밤개방実行, 3同衾이벤트, 4日常이벤트出演済み, 5난교参加, 6다방挨拶
CVARSET CFLAG, 95, 0
;조수画面をデフォに戻す
FLAG:45 = 0
;능력표시画面の３サイズ表示を初期化
FLAG:46 = 0
;조교대상切り替え画面の설정を初期化
FLAG:47 = 0
;조교中フラグなどなどOFF
FLAG:99 = 0
;비표시커맨드の表示状態をリセット
CLEARBIT FLAG:210, 21
;キャラ変更가능플래그リセット
FLAG:253 = 0
;PLAYERをMASTERに戻す（念のため）
MASTER = 1
PLAYER = MASTER
;複数キャラを使う이벤트用の대상フラグリセット
VARSET TARGET, 0, 1, 4
;TARGETとASSIが-1となることを禁止
TARGET = MAX(TARGET, 0)
ASSI = MAX(ASSI, 0)
;레벨리미터체크
CALL SETFLAG, "레벨리미터"

;-------------------------------------------------
;幾つかの플래그をここで설정（主に新規キャラ参入のときに働く）
;-------------------------------------------------
CALL CHECK_FLAG_MUMA

;정력と스톡のセット
CALL SET_COUNTER_BASE
;조교커맨드実行前の파라미터の記録
CALL SAVE_PALAM_BEFORECOM

;-------------------------------------------------
;さぁ온천여행! 
;-------------------------------------------------
IF FLAG:60 && DAYEV("土曜", "ゲーム内") && TEXTS("시간") == "낮"
	MEMO_TARGET = TARGET
	MEMO_ASSI = ASSI
	CALL EVENT_TRAVEL
	;낮밤の플래그変動
	FOR LOCAL, 0, 2
		FOR LCOUNT, 1, 5
			CALL EVENTTURNEND_FLAG, LCOUNT
		NEXT

		TIME = (TIME + 1)%2
	NEXT
	;온천플래그リセット
	VARSET FLAG, 0, 60, 70
	CVARSET CFLAG, 70, 0
	DAY += 1
	CALL SETFLAG, "안전위험일判定"
	TARGET = MEMO_TARGET
	ASSI = MEMO_ASSI
	RESTART
ENDIF
;-------------------------------------------------
;SHOP画面
;-------------------------------------------------
;この値をもとにCLEARLINEを行う
FLAG:2 = LINECOUNT

PRINTL 
DRAWLINE

;ここから계절, 年表示部分
FLAG:871 = (DAY - 1)%120 + 1
FLAG:872 = (DAY+119)/120
FLAG:873 = (FLAG:871 - 1) / 30

PRINTFORM {DAY}일째({FLAG:872}년{FLAG:871}일째) %TEXTS("날짜표시")% 소지금:%MONEY_C(MONEY)%
SIF FLAG:40
	PRINTFORM  [%NAMES("食事", FLAG:40)%]

SIF ITEM:정액저축량
	PRINTFORM (보온병의 정액：{ITEM:정액저축량}) 
PRINTL 

IF TEXTS("날짜対応이벤트") != ""
	SETCOLOR DEF_COLOR("황색")
	PRINTFORM %TEXTS("날짜対応이벤트")%
	;크리스마스ツリー체크
	IF DAYEV("크리스마스期間") && NUM("크리스마스ツリー")
		SETCOLOR DEF_COLOR("하트핑크")
		FOR LCOUNT, 0, NUM("크리스마스ツリー")
			PRINTFORM %UNICODE(0xD83C)%%UNICODE(0xDF84)%
		NEXT
	ENDIF

	RESETCOLOR
	PRINTL 
ENDIF

IF TARGET
	PRINTFORM %NAME:TARGET% 조교안 
	CALL TEXT, "生理周期", TARGET
	PRINT 체력
	BASE:체력 = MAX(BASE:체력, 0)
	CALL PRINT_BAR, "감소게이지", BASE:체력, MAXBASE:체력, 5
	PRINTFORML ({BASE:체력}/{MAXBASE:체력}) 호감도:{CFLAG:2} 봉사쾌락:{EXP:봉사쾌락경험}
	;갈아입히기表示
	CALL PRINT_DRESS, TARGET, "短縮표시"
	PRINTL 
ENDIF
IF ASSI
	PRINTFORM %TEXTS("조수の명칭")%：%NAME:ASSI% 
	CALL TEXT, "生理周期", ASSI
	CALL ASSI_LIST_ABL, ASSI, 0
	PRINTFORML 
	;갈아입히기表示
	CALL PRINT_DRESS, ASSI, "短縮표시"
	PRINTL 
	;지시されているならそれも表示
	IF ABL:ASSI:조수 && CFLAG:ASSI:35
		PRINT 지시：
		CALL ASSI_LIST_INDICATE, ASSI
		PRINTL 
	ENDIF
ENDIF

PRINTFORM 주인：%NAME:MASTER%
IF CONFIG("레벨리미터")
	SETCOLOR DEF_COLOR("황색")
	IF CONFIG("레벨리미터：－10ＬＶ")
		PRINTFORM (상대의LV-10)
	ELSEIF CONFIG("레벨리미터：－５ＬＶ")
		PRINTFORM (상대의LV-５)
	ELSEIF CONFIG("레벨리미터：상대に合わせる")
		PRINTFORM (상대의LV에 맞춘다)
	ELSEIF CONFIG("레벨리미터：＋５ＬＶ")
		PRINTFORM (상대의LV+５)
	ELSEIF CONFIG("레벨리미터：＋10ＬＶ")
		PRINTFORM (상대의LV+10)
	ELSE
		PRINTFORM ({CONFIG("레벨리미터")}LV에 제한)
	ENDIF
	RESETCOLOR
ENDIF
PRINTFORM  정력{BASE:MASTER:정력}/{MAXBASE:MASTER:정력}
SIF MAXBASE:MASTER:스톡
	PRINTFORM ×({BASE:MASTER:스톡}/{MAXBASE:MASTER:스톡})
PRINT  
CALL TEXT, "生理周期", MASTER
CALL ASSI_LIST_ABL, MASTER, 0

PRINTFORML 

;ここでの아이템販売は中止
;CALL SALEITEM_CHECK
;0～99番のITEMSALESを1にする
VARSET ITEMSALES, 1, 0, 100

DRAWLINE

VARSET MENU_SHOP

SIF TARGET == 0
	MENU_SHOP = %MENU_SHOP%회색/
IF TARGET && GETBIT(CFLAG:201, 4)
	MENU_SHOP = %MENU_SHOP%[100] 만나러 간다/
ELSE
	MENU_SHOP = %MENU_SHOP%[100] 조교한다/
ENDIF

MENU_SHOP = %MENU_SHOP%[101] 능력 표시/

SIF TARGET == 0 || CONFIG("조교상대변경불가")
	MENU_SHOP = %MENU_SHOP%회색/
MENU_SHOP = %MENU_SHOP%[102] 휴식/

SIF ASSI == 0 || CONFIG("조교상대변경불가")
	MENU_SHOP = %MENU_SHOP%회색/
MENU_SHOP = %MENU_SHOP%[108] %조사처리(TEXTS("조수の명칭"),"를")% 상대한다/

SIF CHARA_NUM("조수가능") == 0 || CONFIG("조교상대변경불가")
	MENU_SHOP = %MENU_SHOP%회색/
MENU_SHOP = %MENU_SHOP%[109] %TEXTS("조수の명칭")% 변경/

SIF CHARA_NUM("控えキャラ") == 0 || CONFIG("조교상대변경불가")
	MENU_SHOP = %MENU_SHOP%회색/
MENU_SHOP = %MENU_SHOP%[110] 조교 대상 변경/

IF CHARANUM >= 100 || CONFIG("조교상대변경불가")
	MENU_SHOP = %MENU_SHOP%회색/
ELSEIF CHARANUM <= 2
	MENU_SHOP = %MENU_SHOP%하트핑크/
ENDIF
MENU_SHOP = %MENU_SHOP%[111] 몽마소환/

SIF CHARA_NUM("매각가능") == 0 || CONFIG("조교상대변경불가")
	MENU_SHOP = %MENU_SHOP%회색/
MENU_SHOP = %MENU_SHOP%[112] 몽마매각/

SIF CHARANUM <= 2
	MENU_SHOP = %MENU_SHOP%회색/
MENU_SHOP = %MENU_SHOP%[113] 코스튬 확인/

MENU_SHOP = %MENU_SHOP%물색/[114] 어용 상인을 부른다/

SIF NUM("総衣装数") == NUM("所持衣装")
	MENU_SHOP = %MENU_SHOP%회색/
MENU_SHOP = %MENU_SHOP%물색/[115] 고급 의상점으로/

MENU_SHOP = %MENU_SHOP%물색/[116] 아르카나 상점으로/

MENU_SHOP = %MENU_SHOP%물색/[117] 악세사리 상점으로/

SIF FLAG:40
	MENU_SHOP = %MENU_SHOP%회색/
MENU_SHOP = %MENU_SHOP%물색/[118] 주점으로/

SIF CHARA_NUM("하룻밤개방가능") == 0
	MENU_SHOP = %MENU_SHOP%회색/
MENU_SHOP = %MENU_SHOP%[120] 외출 허가를 내준다/

MENU_SHOP = %MENU_SHOP%[121] 탐색하러 간다/

IF TALENT:MASTER:조합지식  || (ASSI && TALENT:ASSI:조합지식)
	MENU_SHOP = %MENU_SHOP%[122] 소재 조합/
ELSE
	MENU_SHOP = %MENU_SHOP%회색/[122] 소재 조합/
ENDIF

SIF FLAG:12 || CHARA_NUM("서류작업") == 0
	MENU_SHOP = %MENU_SHOP%회색/
MENU_SHOP = %MENU_SHOP%[123] 서류작업/

SIF ASSI == 0 || CEVENT("조수で성처리：おあずけ", ASSI) || CONFIG("조교상대변경불가")
	MENU_SHOP = %MENU_SHOP%회색/
MENU_SHOP = %MENU_SHOP%[124] %조사처리(TEXTS("조수の명칭"),"로")% 성욕처리/

SIF TARGET == 0 || COND("온천여행가능") == 0
	MENU_SHOP = %MENU_SHOP%회색/
MENU_SHOP = %MENU_SHOP%[125] 여행 예약(금요일 밤)/

SIF FLAG:208
	MENU_SHOP = %MENU_SHOP%회색/
MENU_SHOP = %MENU_SHOP%[131] 마음에 듬＆성감대 설정/


SIF NUM("所持아이템") + NUM("所持衣装") == 0
	MENU_SHOP = %MENU_SHOP%회색/
MENU_SHOP = %MENU_SHOP%[140] 아이템 & 의상 목록/
SIF NUM("매각가능아이템") == 0
	MENU_SHOP = %MENU_SHOP%회색/
MENU_SHOP = %MENU_SHOP%[141] 아이템 매각/


;SIF TARGET <= 0
;	MENU_SHOP = %MENU_SHOP%회색/
;MENU_SHOP = %MENU_SHOP%[142] 애널라이즈/

IF NUM("所持店舗")
	MENU_SHOP = %MENU_SHOP%[160] 다방의 준비/
ELSE
	SIF NUM("運営가능店舗") == 0
		MENU_SHOP = %MENU_SHOP%회색/
	MENU_SHOP = %MENU_SHOP%[160] 다방을 시작한다/
ENDIF

MENU_SHOP = %MENU_SHOP%改行/[200] ＳＡＶＥ/

MENU_SHOP = %MENU_SHOP%[300] ＬＯＡＤ/

SIF ITEM:호접지몽 == 100
	MENU_SHOP = %MENU_SHOP%[330] 꿈에서 깨어난다/

SIF ITEM:레벨리미터
	MENU_SHOP = %MENU_SHOP%[333] 레벨리미터/

MENU_SHOP = %MENU_SHOP%[555] ＣＯＮＦＩＧ/

;엔딩突入(호접지몽や온천여행企画中は駄目)
SIF COND("엔딩선택가능")
	MENU_SHOP = %MENU_SHOP%[600] 엔딩을 본다/
SIF NUM("ＥＮＤ閲覧済")
	MENU_SHOP = %MENU_SHOP%[650] 개별 END의 회상/

SIF TARGET && COND("子孫") && (NAME:TARGET == "딸" || NAME:TARGET == "아들")
	MENU_SHOP = %MENU_SHOP%改行/[700] %NAME:TARGET%에게 이름을 붙인다/


VARSET LOCALS
SPLIT MENU_SHOP, "/", LOCALS


NEXT_LINE = 0

;SHOP画面の表示
FOR LCOUNT, 0, 100
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASE "회색", "물색", "하트핑크"
		SETCOLOR DEF_COLOR(LOCALS:LCOUNT)
		CONTINUE
	CASE "改行"
		IF NEXT_LINE%3
			NEXT_LINE = 0
			PRINTL 
		ENDIF
		CONTINUE
	CASE "強制改行"
		NEXT_LINE = 0
		PRINTL 
		CONTINUE
	ENDSELECT

	PRINTFORM %TEXT_LJ(LOCALS:LCOUNT, 27)%

	RESETCOLOR

	NEXT_LINE += 1
	SIF NEXT_LINE%3 == 0
		PRINTL 
NEXT
SIF NEXT_LINE%3
	PRINTL 

VARSET LOCALS
;날짜이벤트があるキャラを示唆する試み
SELECTCASE TEXTS("날짜対応이벤트")
;날짜固有이벤트フラグは이벤트が설정されていない日に全消去
CASE ""
	FOR LCOUNT, 1, CHARANUM
		CALL DEL_CEVENT_GROUP, "날짜", LCOUNT
	NEXT
CASEELSE
	;こっちは主人のフラグは見ない
	FOR LCOUNT, 2, CHARANUM
		SIF CDAYEV(TEXTS("날짜対応이벤트"), LCOUNT)
			LOCALS = %LOCALS%[{LCOUNT, 2}]%CALLNAME:LCOUNT%と
	NEXT
ENDSELECT

IF LOCALS != ""
	PRINTL 
	SETCOLOR DEF_COLOR("황색")
	PRINTFORML 어쩐지,%SUBSTRING(LOCALS, 0, STRLENS(LOCALS) - 2)%하지만 안절부절 하고 있다……
	PRINTFORML 오늘만이 가능한 이야기가 있을 듯 합니다
	RESETCOLOR
ENDIF

MEMO_LINECOUNT = LINECOUNT

;一言이벤트など
IF CHARANUM >= 3 && COND("一言이벤트中") == 0
	ACTOR = RANDF(CHARANUM - 2) + 2

	PRINTL 
	;台詞有る場合に、台詞の名前表示をする？
	SIF CONFIG("대사の名前비표시") == 0
		PRINTFORML - %CALLNAME:ACTOR% -

	MEMO_LINECOUNT = LINECOUNT

	TRYCALLFORM GUEST_KOJO_K{NO:ACTOR}_{CFLAG:ACTOR:209}, "一言이벤트", ACTOR

	;台詞が無いなら辻褄あわせ
	IF LINECOUNT == MEMO_LINECOUNT
		CLEARLINE 1
		SIF CONFIG("대사の名前비표시") == 0
			CLEARLINE 1
		MEMO_LINECOUNT = LINECOUNT
	ELSEIF LINECOUNT > MEMO_LINECOUNT
		FLAG:23 = ACTOR
		;一言이벤트中フラグ
		SETBIT FLAG:23, 10
		CALL INPUT_SEXUHARA, ACTOR
	ENDIF

	IF COND("一言이벤트中") == 0 && LINECOUNT == MEMO_LINECOUNT && RAND:3 == 0
		IF COND("온천여행가능", ACTOR)
			PRINTL 
			PRINTFORML （%NAME:ACTOR%로부터의 뜨거운 시선을 느낀다….온천라도 데려 가 줄까？）
		ELSEIF ACTOR != TARGET && COND("욕구불만", ACTOR)
			PRINTL 
			PRINTFORML （%NAME:ACTOR%로부터의 뜨거운 시선을 느낀다…. 가끔 씩은 상관해 주어야 할 것인가도 모른다）
		ENDIF
	ENDIF
ENDIF

SIF LINECOUNT == MEMO_LINECOUNT
	PRINTL 


;-------------------------------------------------
;SHOP画面入力の0～99は@EVENTBUYへ、100이상は@USERSHOPへと行く
;ただし、@EVENTBUYではBOUGHTにRESULTが代入され、RESULT自体は0になっている
;-------------------------------------------------
@EVENTBUY
#DIM CHOICE
;システムで自動的に行われている購入処理を부정
ITEM:BOUGHT -= 1
MONEY += ITEMPRICE:BOUGHT

CHOICE = BOUGHT

SIF COND("一言이벤트中")
	CALL EVENTBUY_SEXUHARA, CHOICE

;SHOP表示をCLEAR
CLEARLINE LINECOUNT - FLAG:2
;一言이벤트中フラグもCLEAR
CLEARBIT FLAG:23, 10

RETURN 0

@USERSHOP
#DIM CHOICE
#DIM MEMO_TARGET
#DIM MEMO_ASSI

CHOICE = RESULT
TFLAG:102 = RESULT

SIF COND("一言이벤트中")
	CALL USERSHOP_SEXUHARA, CHOICE

;[100]조교する
IF CHOICE == 100
	IF TARGET
		REDRAW 1
		BEGIN TRAIN
		RETURN 1
	ELSE
		PRINTW 조교할 상대가 선택되어 있지 않습니다
	ENDIF
;[101]能力の表示
ELSEIF CHOICE == 101
	;キャラ変更가능플래그
	SIF CONFIG("조교상대변경불가")
		FLAG:253 = 1
	;1ページ目を表示
	CALL SHOW_CHARADATA, 0, 0
;[102]休憩
ELSEIF CHOICE == 102
	IF TARGET && CONFIG("조교상대변경불가") == 0
		REDRAW 1
		;휴식플래그を立てる
		FLAG:0 = 1
		TRYCALL EVENT_REST_PRE
		BEGIN TURNEND
		RETURN 1
	ELSEIF CONFIG("조교상대변경불가")
		PRINTW 지금은 휴식을 선택할 수 없습니다
	ELSEIF TARGET == 0
		PRINTW 조교상대가 선택되어 있지 않습니다
	ENDIF
;ELSEIF CHOICE == 103
;	CALL SHOW_CHARA_ANALYSIS, 0, 0
;[108]조수を相手する
ELSEIF CHOICE == 108
	IF ASSI && CONFIG("조교상대변경불가") == 0
		LOCAL = ASSI
		ASSI = 0
		SIF TARGET && COND("조수가능")
			ASSI = TARGET
		TARGET = LOCAL
	ELSEIF ASSI && CONFIG("조교상대변경불가")
		PRINTFORMW 지금은%TEXTS("조수の명칭")%를 상대로 할 수 있는 상황이 아닙니다
	ELSEIF ASSI == 0
		PRINTFORMW %TEXTS("조수の명칭")%하지만 없습니다
	ENDIF
;[109]조수変更
ELSEIF CHOICE == 109
	MEMO_ASSI = ASSI
	IF CHARA_NUM("조수가능") && CONFIG("조교상대변경불가") == 0
		;キャラ変更가능플래그
		FLAG:253 = 1
		;1ページ目を表示
		CALL SELECT_ASSI, 0, 0
	ELSEIF CONFIG("조교상대변경불가")
		PRINTFORMW 지금은%TEXTS("조수の명칭")%를 변경할 수 있는 상황이 아닙니다
	ELSEIF CHARA_NUM("조수가능") == 0
		PRINTFORMW %TEXTS("조수の명칭")%하지만 없습니다
	ENDIF
	IF ASSI && ASSI != MEMO_ASSI
		TRYCALLFORM GUEST_KOJO_K{NO:ASSI}_{CFLAG:ASSI:209}, "조수に抜擢", ASSI
		SIF EXP:ASSI:조수경험 == 0
			EXP:ASSI:조수경험 += 1
	ENDIF
;[110]조교대상切り替え
ELSEIF CHOICE == 110
	MEMO_TARGET = TARGET
	IF CHARA_NUM("控えキャラ") && CONFIG("조교상대변경불가") == 0
		;キャラ変更가능플래그
		FLAG:253 = 1
		;1ページ目を表示
		CALL CHANGE_TARGET, 0, 0
	ENDIF
	;조교대상を変えた＆一度は상대をしたことがある
	SIF TARGET && TARGET != MEMO_TARGET && EXP:조교횟수
		TRYCALLFORM GUEST_KOJO_K{NO:TARGET}_{CFLAG:209}, "조교상대に選ばれた", TARGET
;[111]몽마소환
ELSEIF CHOICE == 111
	IF CHARANUM < 100 && CONFIG("조교상대변경불가") == 0
		CALL CHARA_BUY
	ELSEIF CONFIG("조교상대변경불가")
		PRINTW 지금은 소환을 할 수 있는 상황이 아닙니다
	ELSEIF CHARANUM >= 100
		PRINTW 저택에 사람이 한가득입니다! 
	ENDIF
;[112]몽마매각
ELSEIF CHOICE == 112
	IF CHARA_NUM("매각가능") && CONFIG("조교상대변경불가") == 0
		;매각価格のCFLAGを前もって計算. 最高価格をMONEY:1, 最高評価をMONEY:3とする
		MONEY:1 = 0
		MONEY:3 = 0
		FOR LOCAL, 2, CHARANUM
			CALL SETFLAG, "매각価格", LOCAL
			MONEY:1 = MAX(MONEY:1, CFLAG:LOCAL:40)
			MONEY:3 = MAX(MONEY:3, CFLAG:LOCAL:44)
		NEXT
		;歴代最高額の更新
		MONEY:2 = MAX(MONEY:1, MONEY:2)
		MONEY:4 = MAX(MONEY:3, MONEY:4)
		CALL CHARA_SALE, 0, 0
	ELSEIF CONFIG("조교상대변경불가")
		PRINTW 지금은 매각따위를 할 수있는 상황이 아닙니다
	ELSEIF CHARA_NUM("매각가능") == 0
		PRINTW 없는 걸 팔 수 없습니다
	ENDIF
;[113]코스튬 확인
ELSEIF CHOICE == 113
	IF CHARANUM > 2
		;1ページ目を表示
		CALL COSPLAY_LIST, 0, 0
	ENDIF
;[114]御用商人を呼ぶ
ELSEIF CHOICE == 114
	CALL EVENT_SHOP
;[115]高級옷飾店『クヴァラットミルク』
ELSEIF CHOICE == 115
	IF NUM("総衣装数") > NUM("所持衣装")
		CALL COSPLAY_SHOP
	ELSE
		PRINTW 간판에 『ＳＯＬＤ ＯＵＴ』이라는 패가 걸려있습니다…
	ENDIF
;[116]아르카나 상점『魔女の家』
ELSEIF CHOICE == 116
	CALL ARCANA_SHOP
;[117]악세사리 상점『おぼろげ屋』
ELSEIF CHOICE == 117
	CALL ACCESSORY_SHOP
;[118]酒場『一角獣の角』
ELSEIF CHOICE == 118
	IF FLAG:40 == 0
		CALL EAT_LUNCH
	ELSE
		PRINTFORMW 이것이상는 과식합니다
	ENDIF
;[120]外出許可を与える
ELSEIF CHOICE == 120
	IF CHARA_NUM("하룻밤개방가능")
		CALL SET_CHARA_NIGHTWALKER, 0, 0

		;DRAWLINE
		;몽마이벤트
		;CALL NIGHTWALKER
		;BEGIN TURNEND
		;RETURN 1
	ELSE
		PRINTL 외출 허가를 바란다or외출을 시켜도 문제 없는 캐릭터가 없습니다
		PRINTL 성행위에 익숙해 있어[호색]or[음란]or노출벽하지만 높은 캐릭터가 외출을 바랍니다
		PRINTW 순종야굴복각인노LV하지만 높지 않으면 허가를 낼 수 없는 것도 있습니다
	ENDIF
;[121]탐색に出かける
ELSEIF CHOICE == 121
	REDRAW 1
	CALL EXPLORER, 0, 0
;[122]素材の調合
ELSEIF CHOICE == 122
	IF TALENT:MASTER:조합지식 || (ASSI && TALENT:ASSI:조합지식)
		CALL COMPOUND_ITEM
	ELSE
		PRINTFORMW 주인인가%TEXTS("조수の명칭")%에[조합지식]하지만필요입니다
	ENDIF
;[123]서류작업をする
ELSEIF CHOICE == 123
	IF CHARA_NUM("서류작업")
		IF FLAG:12 == 0
			CALL EVENT_DESKWORK
		ELSE
 			PRINTW 일은, 이 정도로 해 둡시다
		ENDIF
	ELSE
		PRINTL 정리하는 만큼 서류가 모여 있지 않습니다
		PRINTW （이벤트의 발생에는조교(이)가 진행된 캐릭터가필요입니다）
	ENDIF
;[124]조수に성욕処理を頼む
ELSEIF CHOICE == 124
	IF ASSI && CONFIG("조교상대변경불가") == 0
		TRYCALL PLAY_WITH_ASSI
	ELSEIF ASSI && CONFIG("조교상대변경불가")
		PRINTFORMW 지금은%TEXTS("조수の명칭")%를 상대로 할 수 있는 상황이 아닙니다
	ELSEIF ASSI == 0
		PRINTFORMW %TEXTS("조수の명칭")%하지만 없습니다
	ENDIF
;[125]土曜日に온천여행を予約する
ELSEIF CHOICE == 125
	IF TARGET && COND("온천여행가능")
		CALL EVENT_TRAVEL_SELECT
	ELSE
		PRINTL 금요일밤에$3,000를 준비해[연모]or[음란]의 캐릭터를조교상대로 하면선택할 수 있습니다
		PRINTW 다만, [처녀]인 경우에는 [상사상애]일 필요가 있습니다
	ENDIF
;[131]마음에듬や성감대等の설정
ELSEIF CHOICE == 131
	IF FLAG:208 == 0
		;설정の記録
		TFLAG:0 = TALENT:MASTER:성감대
		TFLAG:1 = TALENT:MASTER:성벽
		FOR LOCAL, 2, CHARANUM
			TFLAG:LOCAL = TALENT:LOCAL:마음에듬
		NEXT
		CALL SET_CHARA_LOVE, 0, 0
	ELSE
		PRINTFORMW 앞으로 {FLAG:208}일 경과하지 않으면 변경할 수 없습니다
	ENDIF
;[140]아이템＆衣装일람
ELSEIF CHOICE == 140
	IF NUM("所持아이템") + NUM("所持衣装")
		CALL SHOW_ITEM_HAVE
	ENDIF
;[141]아이템매각
ELSEIF CHOICE == 141
	IF NUM("매각가능아이템")
		CALL SELL_ITEM
	ENDIF
;[142]애널라이즈（一応残しておいてみる）
ELSEIF CHOICE == 142
	IF TARGET
		CALL ANALYSIS
	ELSE
		PRINTW 애널라이즈하고싶은 상대를 조교대상으로 선택해주세요
	ENDIF
;[160]다방의 준비
ELSEIF CHOICE == 160
	IF NUM("運営가능店舗") || NUM("所持店舗")
		CALL SHOP_EX
	ELSE
		PRINTFORML 다방의 웨이트레스가 생길 것 같은 캐릭터가부족하고 있습니다
		PRINTFORML （겉모습이 여성인 일과[연모]없고순종하지만 높은 일이 기본 조건이 되고 있습니다）
		PRINTFORML 한 점포당 네명이 필요하므로, 앞으로 {4 - CHARA_NUM("バイト가능")}명이 필요합니다
		PRINTFORMW 거기에 더해 개점자금으로 $10,000이 필요합니다
	ENDIF
ELSEIF CHOICE == 200
	CALL SAVEGAME_EX
ELSEIF CHOICE == 300
	CALL LOADGAME_EX
;[330]夢から覚める
ELSEIF CHOICE == 330 && ITEM:호접지몽 == 100
	PRINTFORML 호접지몽를 끝냅니다
	PRINTFORML ……정말로 꿈으로부터 깨어도 괜찮습니까？
	PRINTFORML [ 0] 네
	PRINTFORML [ 1] 아니오
	CALL INPUT_SELECT, 2
	SIF RESULT == 0
		CALL EVENT_KOTYOUNOYUME, "夢の終わり"
ELSEIF CHOICE == 333 && ITEM:레벨리미터
	CALL LV_LIMITTER
ELSEIF CHOICE == 555
	CALL SET_CONFIGURE
ELSEIF CHOICE == 600
	;ノーマル모드での엔딩突入
	IF COND("엔딩선택가능")
		REDRAW 1
		IF MONEY < NUM("목표금액")
			PRINTL 아직 목표의 금액까지 벌고 있지 않습니다만……
		ELSEIF CALCF("最愛の상대")
			PRINTPLAINFORM %CALLNAME:MASTER%의 가장 사랑하는 상대는……[{CALCF("最愛の상대"), 2}]%CALLNAME:CALCF("最愛の상대")%입니다
			CALL CHECK_SINGLE_ENDING
			IF RESULT
				CALL PRINT_STR, "하트핑크_（個別ＥＮＤ有）_L_"
			ELSE
				PRINTL 
			ENDIF
		ENDIF
		PRINTL 정말로 엔딩을 봐도 좋습니까?
		PRINTL [ 0] 예
		PRINTL [ 1] 아니오
		CALL INPUT_SELECT, 2
		SIF RESULT
			RETURN 0
		CALL ENDING_CHECK
	ENDIF
ELSEIF CHOICE == 650 && NUM("ＥＮＤ閲覧済")
	MEMO_TARGET = TARGET
	CALL SHOW_CHARA_END
	TARGET = MEMO_TARGET
ELSEIF CHOICE == 700 && TARGET && COND("子孫") && (NAME:TARGET == "딸" || NAME:TARGET == "아들")
	CALL GIVE_NAME

;상위몽마소환
ELSEIF CHOICE == 666 && CHARANUM < 100
	IF CONFIG("조교상대변경불가") == 0
		FLAG:666 = 1
		IF CONFIG("소환사고") == 0
			FLAG:221 = 1
			CALL CHARA_BUY
			FLAG:221 = 0
		ELSE
			CALL CHARA_BUY
		ENDIF
	ENDIF
	FLAG:666 = 0
ELSEIF CHOICE == 777
	CALL NAME_CONVERT, 0, 0
ELSEIF CHOICE == 888
	SIF TARGET == 0
		RETURN 0
	;실행가능명령の確認
	CALL CHECK_COM_ABLE, TARGET
	CALL EVENT_RANKOU_SET, "요바이난교"
	IF CSTR:요바이 != ""
		CSTR:난교 = %CSTR:요바이%
		CALL EVENT_RANKOU, "요바이난교"
	ENDIF
ELSEIF CHOICE == 88224646
	MEMO_TARGET = TARGET

	SIF TARGET <= 0
		TARGET = 1
	TRYCALL DEBUG_MODE

	TARGET = MEMO_TARGET
	;3サイズと신장
	FOR LOCAL, 1, CHARANUM
		CALL SET_3SIZE, LOCAL
	NEXT
ENDIF

RETURN 0

@EVENT_SHOP
REDRAW 0

PRINTL 
PRINTFORML 「좋은 것을 가지런히 해 있어요」
CALL EVENT_SHOP_BUY

@EVENT_SHOP_BUY
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
PRINTL 
PRINTFORML 어떤 것을 매입하십니까？ (나머지%MONEY_C(MONEY)%)
CALL SALEITEM_CHECK
DRAWLINE
PRINTFORML [100] 가게를 나간다

CALL INPUT_SELECT, 100, 100

SELECTCASE RESULT
CASE 100
	PRINTFORMW 「다음 이용을 기다리고 있습니다」
	REDRAW 1
	RETURN 0
CASEELSE
	;売ってない番号を선택したらやり直し
	IF ITEMSALES:RESULT == 0
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
	ELSE
		BOUGHT = RESULT
		CALL NEW_EVENTBUY
	ENDIF
ENDSELECT

RESTART

@SHOW_ITEM_HAVE
#DIM NEXT_LINE
#DIM LCOUNT

PRINTL □ 소지아이템일람 □---------------------------------------------------------

NEXT_LINE = 0
FOR LCOUNT, 0, 30
	SIF ITEM:LCOUNT == 0
		CONTINUE
	SIF NEXT_LINE == 0
		PRINTL □ 조교도구 □-----------------------------------------------------------------
	SELECTCASE ITEM:LCOUNT
	CASE 1
		PRINTFORM %ITEMNAME:LCOUNT% 
	CASEELSE
		PRINTFORM %ITEMNAME:LCOUNT%({ITEM:LCOUNT}) 
	ENDSELECT
	NEXT_LINE += 1
NEXT
IF NEXT_LINE
	PRINTL 
	PRINTL 
ENDIF

NEXT_LINE = 0
FOR LCOUNT, 30, 50
	SIF ITEM:LCOUNT == 0
		CONTINUE
	SIF NEXT_LINE == 0
		PRINTL □ 소모품＆인센스 □-------------------------------------------------------
	SELECTCASE ITEM:LCOUNT
	CASE 1
		PRINTFORM %ITEMNAME:LCOUNT% 
	CASEELSE
		PRINTFORM %ITEMNAME:LCOUNT%({ITEM:LCOUNT}) 
	ENDSELECT
	NEXT_LINE += 1
NEXT
IF NEXT_LINE
	PRINTL 
	PRINTL 
ENDIF


NEXT_LINE = 0
FOR LCOUNT, 90, 100
	SIF ITEM:LCOUNT == 0
		CONTINUE
	SIF NEXT_LINE == 0
		PRINTL □ 특수한 물품 □---------------------------------------------------------------
	SELECTCASE ITEM:LCOUNT
	CASE 1
		PRINTFORM %ITEMNAME:LCOUNT% 
	CASEELSE
		PRINTFORM %ITEMNAME:LCOUNT%({ITEM:LCOUNT}) 
	ENDSELECT
	NEXT_LINE += 1
NEXT
IF NEXT_LINE
	PRINTL 
	PRINTL 
ENDIF


NEXT_LINE = 0
FOR LCOUNT, 300, 320
	SIF ITEM:LCOUNT == 0
		CONTINUE
	SIF NEXT_LINE == 0
		PRINTL □ 수집품 □-------------------------------------------------------------------
	SELECTCASE ITEM:LCOUNT
	CASE 1
		PRINTFORM %ITEMNAME:LCOUNT% 
	CASEELSE
		PRINTFORM %ITEMNAME:LCOUNT%({ITEM:LCOUNT}) 
	ENDSELECT
	NEXT_LINE += 1
NEXT
IF NEXT_LINE
	PRINTL 
	PRINTL 
ENDIF

IF ITEM:음마의물방울
	PRINTL □ 조합재료 □-----------------------------------------------------------------
	PRINTFORML 음마의물방울({ITEM:음마의물방울})
	PRINTL 
ENDIF

NEXT_LINE = 0
FOR LCOUNT, 500, 600
	SIF ITEM:LCOUNT == 0
		CONTINUE
	SIF NEXT_LINE == 0
		PRINTL □ 코스프레의상 □-------------------------------------------------------------
	PRINTFORM %TEXT_LJ(ITEMNAME:LCOUNT, 20)%
	NEXT_LINE += 1
	SIF NEXT_LINE%4 == 0
		PRINTFORML 
NEXT

SIF NEXT_LINE%4
	PRINTFORML 

DRAWLINE
WAIT


@COMPOUND_ITEM
#DIM LCOUNT
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
PRINTL 
DRAWLINE
PRINTFORML 음마의물방울를 원료로서, 약이나아이템등을조합합니다(소지우：{ITEM:음마의물방울}개)

VARSET LOCAL
;조합아이템の実際に使う상대がいるかなどを判定
;건강해지는약
SIF FIND_USE_ITEM("건강해지는약")
	LOCAL:0 = 1
;ドロップ
FOR LCOUNT, 1, 5
	SIF FIND_USE_ITEM(ITEMNAME:(LCOUNT + 54) )
		LOCAL:LCOUNT = 1
NEXT
;330番台の아이템
FOR LCOUNT, 5, 16
	SIF FIND_USE_ITEM(ITEMNAME:(LCOUNT + 325) )
		LOCAL:LCOUNT = 1
NEXT
;정액각인
SIF FIND_USE_ITEM("정액각인刻印가능")
	LOCAL:30 = 1


SIF ITEM:프랭킨센스 < 99
	LOCAL:40 = 1
SIF ITEM:문드롭스 < 99
	LOCAL:50 = 1

;음마의물방울は발りてる？
SELECTCASE ITEM:음마의물방울
CASE IS < 3
	VARSET LOCAL
CASE IS < 10
	FOR LCOUNT, 0, 5
		LOCAL:LCOUNT = 0
	NEXT
	LOCAL:30 = 0
	LOCAL:40 = 0
	LOCAL:50 = 0
CASE IS < 30
	LOCAL:30 = 0
ENDSELECT

;음마의물방울を買い발す？
SIF ITEM:음마의물방울 < 999 && MONEY >= ITEMPRICE:음마의물방울
	LOCAL:90 = 1

PRINTFORML  [\@ LOCAL:0 ? {0, 2} # × \@] %TEXT_LJ("후타나리약", 16)% 10개 후타나리가 되거나, 돌아오거나
PRINTFORML  [\@ LOCAL:1 ? {1, 2} # × \@] %TEXT_LJ("파란드롭스", 16)% 10개 모유체질이 된다
PRINTFORML  [\@ LOCAL:2 ? {2, 2} # × \@] %TEXT_LJ("빨간드롭스", 16)% 10개 처녀막을 재생 or 모유체질을 상실
PRINTFORML  [\@ LOCAL:3 ? {3, 2} # × \@] %TEXT_LJ("검은드롭스", 16)% 10개 성별이 반전된다
PRINTFORML  [\@ LOCAL:4 ? {4, 2} # × \@] %TEXT_LJ("하얀드롭스", 16)% 10개 실금버릇이나, 체내의 손상 등을 치료한다
PRINTFORML  [\@ LOCAL:5 ? {5, 2} # × \@] %TEXT_LJ("거근약", 16)% ３개 말할 필요도 없이, 커지게하는 약입니다!
PRINTFORML  [\@ LOCAL:6 ? {6, 2} # × \@] %TEXT_LJ("축근약", 16)% ３개 그것을 작게만드는 약입니다
PRINTFORML  [\@ LOCAL:7 ? {7, 2} # × \@] %TEXT_LJ("박피약", 16)% ３개 그곳 껍데기를 줄여줍니다
PRINTFORML  [\@ LOCAL:8 ? {8, 2} # × \@] %TEXT_LJ("신피약", 16)% ３개 그곳 껍데기를 늘려줍니다
PRINTFORML  [\@ LOCAL:9 ? {9, 2} # × \@] %TEXT_LJ("풍유환", 16)% ３개 가슴을 키워줍니다
PRINTFORML  [\@ LOCAL:10 ? {10, 2} # × \@] %TEXT_LJ("축유환", 16)% ３개 가슴을 줄여줍니다
PRINTFORML  [\@ LOCAL:11 ? {11, 2} # × \@] %TEXT_LJ("거인의약", 16)% ３개 몸을 크게 만들어줍니다
PRINTFORML  [\@ LOCAL:12 ? {12, 2} # × \@] %TEXT_LJ("소인의약", 16)% ３개 몸을 작게 만들어줍니다
PRINTFORML  [\@ LOCAL:13 ? {13, 2} # × \@] %TEXT_LJ("무지개의약", 16)% ３개 피부색을 변화시킵니다
PRINTFORML  [\@ LOCAL:14 ? {14, 2} # × \@] %TEXT_LJ("풍둔약", 16)% ３개 엉덩이를 키워줍니다
PRINTFORML  [\@ LOCAL:15 ? {15, 2} # × \@] %TEXT_LJ("축둔약", 16)% ３개 엉덩이를 줄여줍니다

PRINTFORML  [\@ LOCAL:30 ? {30, 2} # × \@] %TEXT_LJ("정액각인", 16)% 30색욕을 강하게 하는 문장

PRINTFORML  [\@ LOCAL:40 ? {40, 2} # × \@] %TEXT_LJ("프랭킨센스", 16)% 10달콤하고 고귀한 향기를 가진다인센스（소지：{ITEM:프랭킨센스}개）
PRINTFORML  [\@ LOCAL:50 ? {50, 2} # × \@] %TEXT_LJ("문드롭스", 16)% 10몽마를 아주 좋아하는 눈깔사탕（소지：{ITEM:문드롭스}개）

PRINTFORML  [\@ LOCAL:90 ? {90, 2} # × \@] 음마의물방울を%MONEY_C(ITEMPRICE:음마의물방울)%그리고 구매발스（소지금：%MONEY_C(MONEY)%）

PRINTFORML [100] 돌아간다

CALL INPUT_SELECT, 16, 30, 40, 50, 90, 100

IF RESULT != 100 && LOCAL:RESULT == 0
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDIF

SELECTCASE RESULT
CASE 100
	RETURN 0
CASE 0
	BOUGHT = 51
	CALL USE_ITEM, "調合"
CASE 1 TO 4
	BOUGHT = RESULT + 54
	CALL USE_ITEM, "調合"
CASE 5 TO 29
	BOUGHT = RESULT + 325
	CALL USE_ITEM, "調合"
CASE 30
	BOUGHT = 350
	CALL USE_ITEM, "調合"
CASE 40
	BOUGHT = 49
	PRINTFORMW 프랭킨센스를 조합했습니다
	ITEM:프랭킨센스 += 1
	ITEM:음마의물방울 -= 10
CASE 50
	BOUGHT = 99
	PRINTFORMW 문드롭스를 조합했습니다
	ITEM:문드롭스 += 1
	ITEM:음마의물방울 -= 10
CASE 90
	ITEM:음마의물방울 += 1
	MONEY -= ITEMPRICE:음마의물방울
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
ENDSELECT

RESTART

@SELL_ITEM, ARGS
#DIM LCOUNT
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT

PRINTL 
DRAWLINE
PRINTFORML 필요없는 아이템을 매각합니다 소지금：%MONEY_C(MONEY)%
DRAWLINE
VARSET LOCAL
IF FLAG:259
	;조교아이템
	FOR LCOUNT, 0, 30
		SIF ITEM:LCOUNT <= 0
			CONTINUE
		PRINTFORML  [{LCOUNT, 2}] %TEXT_LJ(ITEMNAME:LCOUNT, 23)%×{ITEM:LCOUNT, 3} (%MONEY_C(ITEMPRICE:LCOUNT / 2)%)
		LOCAL:LCOUNT = 1
	NEXT
ELSE
	;消耗아이템
	FOR LCOUNT, 30, 100
		SIF ITEM:LCOUNT <= 0
			CONTINUE
		SELECTCASE LCOUNT
		CASE 30 TO 49, 96, 99
			PRINTFORML  [{LCOUNT, 2}] %TEXT_LJ(ITEMNAME:LCOUNT, 23)%×{ITEM:LCOUNT, 3} (%MONEY_C(ITEMPRICE:LCOUNT / 2)%)
			LOCAL:LCOUNT = 1
		ENDSELECT
	NEXT
	;収集専用아이템
	FOR LCOUNT, 300, 330
		SIF ITEM:LCOUNT <= 0
			CONTINUE
		PRINTFORML [{LCOUNT, 3}] %TEXT_LJ(ITEMNAME:LCOUNT, 23)%×{ITEM:LCOUNT, 3} (%MONEY_C(ITEMPRICE:LCOUNT / 2)%)
		LOCAL:LCOUNT = 1
	NEXT
ENDIF
PRINTFORML [900] \@ FLAG:259 ? 소모품 표시로 돌아간다 # 조교도구 등을 매각한다 \@
PRINTFORML [999] 돌아간다
SIF ARGS != ""
	PRINTFORML %ARGS%

$INPUT_LOOP
INPUT

IF RESULT == 999
	PRINTFORMW 무언가 진귀한 물건이 있으면 잘 부탁해요
	FLAG:259 = 0
	RETURN 0
ELSEIF RESULT == 900
	INVERTBIT FLAG:259, 0
ELSEIF RESULT >= 0 && RESULT < 999 && ITEM:RESULT && LOCAL:RESULT
	ARGS = %ITEMNAME:RESULT%をひとつ%MONEY_C(ITEMPRICE:RESULT / 2)%で매각しました
	ITEM:RESULT -= 1
	MONEY += ITEMPRICE:RESULT / 2
ELSE
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@COSPLAY_SHOP
DRAWLINE
PRINTL  □ 고급옷식점 『쿠바랏트미르크』 □
DRAWLINE
PRINTFORML 「어서오세요.
PRINTFORML 당점에서는 세계 각국의 여성정장을 풍부하게 취급하고 있습니다.
PRINTFORMW 오늘에 한정해, 일착 무려%MONEY_C(ITEMPRICE:여교사)%에서 준비해요」
CALL COSPLAY_SHOP_BUY

@COSPLAY_SHOP_BUY
#DIM LCOUNT

PRINTL 
PRINTFORML 어떤 것을 매입하십니까？ (나머지%MONEY_C(MONEY)%)

VARSET LOCAL
FOR LCOUNT, 500, 600
	SIF ITEM:LCOUNT
		CONTINUE

	IF STRLENS(ITEMNAME:LCOUNT)
		PRINTFORML  [{LCOUNT - 500,2}] %ITEMNAME:LCOUNT%
		LOCAL:(LCOUNT - 500) = 1
	ENDIF
NEXT


PRINTFORML [100] 가게를 나간다
$INPUT_LOOP
INPUT
IF RESULT == 100
	PRINTW 「다음 방문을 기다리겠습니다」
	RETURN 0
ELSEIF RESULT >= 0 && RESULT <= 99 && ITEM:(RESULT + 500) == 0 && LOCAL:RESULT
	IF MONEY >= ITEMPRICE:(RESULT + 500)
		MONEY -= ITEMPRICE:(RESULT + 500)
		PRINTFORML ＜%조사선택(ITEMNAME:(RESULT + 500), "를")% 구입했습니다＞
		ITEM:(RESULT + 500) = 1
	ELSE
		PRINTL 「죄송합니다만, 대금이 부족한 것같네요」
		PRINTW 「다음 방문을 기다리겠습니다」
		RETURN 0
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

IF MONEY >= ITEMPRICE:여교사 && NUM("総衣装数") > NUM("所持衣装") 
	PRINTW 「다른 코디네이트도 어떻습니까?」
	RESTART
ELSE
	PRINTW 「다음 방문을 기다리겠습니다」
	RETURN 0
ENDIF

@ARCANA_SHOP
DRAWLINE
PRINTL  □ 아르카나 상점『마녀의 집』 □
DRAWLINE
;토曜の밤は魔女のお祭り
IF DAYEV("土曜") && TEXTS("시간") == "밤"
	PRINTFORML 「어머, 어서와. 
	PRINTFORML  여기는 마도서를 저렴하게 판매하고 있는 아주 양심적인 가게야. 
	PRINTFORMW  ……뭐어, 어려워서 못읽을 지는 모르지만？ 쿡쿡쿡」
	PRINTFORML 
	PRINTFORML  [마법사]의 소질을 갖고있다면 돈을 내고 아르카나를 습득할 수 있습니다
	PRINTFORML  다만, 주인 이외는 1개 기억하는 것에 대해습득의 구슬이1,000필요됩니다
	CALL PRINT_COLORTEXT, " 다른 색깔", DEF_COLOR("물색")
	PRINTFORML 은 중급이상의 마도서입니다.[현명함]없고[상위몽마]가 필요입니다
	PRINTFORMW （어떤 (뜻)이유인가,스톡(와)과바인드는 주인에게는 팔아 주지 않는 것 같습니다……）
	CALL ARCANA_SHOP_BUY, "中級"
	PRINTFORMW 「다음에 또봐」
ELSE
	PRINTFORML 「어머, 어서와. 
	PRINTFORML  여기는 값싸고 간단한 마도서를 팔고있는 아주 양심적인 가게야. 
	PRINTFORMW  ……근데, 머리가 나쁜 아이라면 읽기 어려울지도？」
	PRINTFORML 
	PRINTFORML  [마법사]의 소질을 갖고있다면 금을 내고 아르카나를 습득할 수 있습니다
	PRINTFORML  다만, 주인 이외는 1개 기억하는 것에 대해습득의 구슬이1,000필요됩니다
	CALL PRINT_COLORTEXT, " 다른 색깔", DEF_COLOR("물색")
	PRINTFORML 은 중급이상의 마도서입니다. [현명함]없고[상위몽마]가 필요입니다
	PRINTFORMW （어떤 (뜻)이유인가,스톡(와)과바인드는 주인에게는 팔아 주지 않는 것 같습니다……）
	CALL ARCANA_SHOP_BUY, "中級"
	PRINTFORMW 「또봐」
ENDIF


@ARCANA_SHOP_BUY, ARGS
#DIM LCOUNT
#DIM CHOICE
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT
REDRAW 0

VARSET ITEMSALES, 0, 600, 700
VARSET LOCAL

;売られてる아르카나の判定
FOR LCOUNT, 600, 600 + NUM("存在する아르카나")
	SIF MONEY < ITEMPRICE:LCOUNT
		CONTINUE

	FOR LOCAL, 1, CHARANUM
		SIF TALENT:LOCAL:마법사 != 1
			CONTINUE

		SELECTCASE LOCAL
		CASE MASTER
			SELECTCASE ITEMNAME:LCOUNT
			CASE "스톡", "바인드"
				CONTINUE
			ENDSELECT
		CASEELSE
			SIF JUEL:LOCAL:습득 < 1000
				CONTINUE
		ENDSELECT

		SELECTCASE ITEMNAME:LCOUNT
		CASE "스톡", "투와이스", "아르스이사", "바인드", "아르스군", "인라지", "비젼"
			SIF TALENT:LOCAL:현명함 == 0 && TALENT:LOCAL:상위몽마 == 0
				CONTINUE
		ENDSELECT

		SIF TALENT:LOCAL:(ITEMNAME:LCOUNT)
			CONTINUE

		ITEMSALES:LCOUNT = 1
		BREAK
	NEXT
NEXT

PRINTL 
PRINTFORML 무엇을 구입합니까? (소지금：%MONEY_C(MONEY)%)
FOR LCOUNT, 0, NUM("存在する아르카나")
	LOCAL = LCOUNT + 600

	SELECTCASE ITEMNAME:LOCAL
	CASE "바인드"
		ITEMSALES:LOCAL = 0
		CONTINUE
	CASE "스톡", "투와이스", "아르스이사", "아르스군", "인라지", "비젼"
		SELECTCASE ARGS
		CASE "初級"
			ITEMSALES:LOCAL = 0
			CONTINUE
		CASEELSE
			SETCOLOR DEF_COLOR("물색")
		ENDSELECT
	ENDSELECT

	PRINTFORML  [\@ ITEMSALES:LOCAL ? {LCOUNT, 2} # × \@] %TEXT_LJ(ITEMNAME:LOCAL, 12)% %TEXT_RJ(MONEY_C(ITEMPRICE:LOCAL), 7)% %TEXTS("아르카나解説", LCOUNT + 230)%
	RESETCOLOR
NEXT

PRINTFORML [100] 가게를 나간다

CALL INPUT_SELECT, NUM("存在する아르카나"), 100

SELECTCASE RESULT
CASE 100
	REDRAW 1
	RETURN 0
CASEELSE
	IF RESULT >= 0 && RESULT < NUM("存在する아르카나") && ITEMSALES:(RESULT + 600)
		BOUGHT = RESULT + 600
		PRINTFORML 누가 %조사처리(ITEMNAME:BOUGHT,"를")% 배웁니까?  (구입 후의 소지금：%MONEY_C(MONEY - ITEMPRICE:BOUGHT)%)

		VARSET LOCAL
		FOR LCOUNT, 1, CHARANUM
			SIF TALENT:LCOUNT:마법사 != 1
				CONTINUE

			SELECTCASE LCOUNT
			CASE MASTER
				SELECTCASE ITEMNAME:BOUGHT
				CASE "스톡", "바인드"
					CONTINUE
				ENDSELECT
			CASEELSE
				SIF JUEL:LCOUNT:습득 < 1000
					CONTINUE
			ENDSELECT

			SELECTCASE ITEMNAME:BOUGHT
			CASE "스톡", "투와이스", "아르스이사", "바인드", "아르스군", "인라지", "비젼"
				SIF TALENT:LCOUNT:현명함 == 0 && TALENT:LCOUNT:상위몽마 == 0
					CONTINUE
			ENDSELECT

			SIF TALENT:LCOUNT:(ITEMNAME:BOUGHT)
				CONTINUE

			CALL PRINT_NUM_NAME, "체력", LCOUNT
			LOCAL:LCOUNT = 1
		NEXT
		PRINTL  [100]돌아간다

		CALL INPUT_SELECT, CHARANUM, 100
		CHOICE = RESULT

		SELECTCASE CHOICE
		CASE 100
		CASE IS < CHARANUM
			IF LOCAL:CHOICE
				LOCALS = 
				;覚えている아르카나の表示
				FOR LCOUNT, 230, 230 + NUM("存在する아르카나")
					SIF TALENT:CHOICE:LCOUNT
						LOCALS = %LOCALS%[%TALENTNAME:LCOUNT%]
				NEXT
				IF LOCALS != ""
					PRINTFORML %조사처리(CALLNAME:CHOICE,"는")% 현재 %조사처리(LOCALS,"를")% 습득한 상태입니다
					PRINTFORML 새롭게[%ITEMNAME:BOUGHT%]를 기억해도 좋습니까？\@ CHOICE != MASTER ? （습득の구슬：{JUEL:CHOICE:습득}） # \@
				ELSE
					PRINTFORML %조사처리(CALLNAME:CHOICE,"는")% 아르카나를 하나도 배울 수 없습니다
					PRINTFORML [%ITEMNAME:BOUGHT%]를 기억해도 좋습니까？\@ CHOICE != MASTER ? （습득の구슬：{JUEL:CHOICE:습득}） # \@
				ENDIF

				PRINTFORML [ 0] 예
				PRINTFORML [ 1] 역시 그만둔다
				CALL INPUT_SELECT, 2
				SELECTCASE RESULT
				CASE 1
					RESTART
				ENDSELECT

				PRINTFORML ＜%조사처리(CALLNAME:CHOICE,"는")% [%ITEMNAME:BOUGHT%]%조사만처리(ITEMNAME:BOUGHT,"를")% 배웠다＞
				TALENT:CHOICE:(ITEMNAME:BOUGHT) = 1
				IF CHOICE != MASTER
					PRINTFORMW 습득의 구슬-1000
					JUEL:CHOICE:습득 -= 1000
				ENDIF
				MONEY -= ITEMPRICE:BOUGHT
			ENDIF
		ENDSELECT
	ELSE
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
	ENDIF
ENDSELECT
RESTART

;-------------------------------------------------
;ARGSを使う事が出来るキャラが一人でもいたら１を返す。いない場合には０
;-------------------------------------------------
@FIND_USE_ITEM(ARGS)
#FUNCTION

SIF TARGET && COND(ARGS)
	RETURNF 1
SIF COND(ARGS, MASTER) || FIND_COND(ARGS)
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;아이템出現条件
;ARGSに"표시なし"とすれば아이템일람は出さない
;-------------------------------------------------
@SALEITEM_CHECK, ARGS
#DIM LCOUNT
#DIM NEXT_LINE

CALL SETFLAG, "아이템플래그調整"

ITEMSALES:촉수사역술 = 0

ITEMSALES:애널비즈 = 0
SIF ITEM:애널바이브 && ITEM:애널비즈 == 0
	ITEMSALES:애널비즈 = 1
;보온병
ITEMSALES:보온병 = 0
SIF FLAG:91 && ITEM:보온병 == 0
	ITEMSALES:보온병 = 1

ITEMSALES:배란유발제 = 0
ITEMSALES:근육이완제 = 0

;50番以降は, 初期状態では売られていない
VARSET ITEMSALES, 0, 50, 100


;주인か조수が『조합지식』を持っている
IF TALENT:MASTER:조합지식 || (ASSI && TALENT:ASSI:조합지식)
	SIF FIND_USE_ITEM("건강해지는약")
		ITEMSALES:건강해지는약 = 1
	SIF FIND_USE_ITEM("후타나리약")
		ITEMSALES:후타나리약 = 1
ENDIF

;주인か조수が『금단의지식』を持っている
IF TALENT:MASTER:금단의지식 || (ASSI && TALENT:ASSI:금단의지식)
	SIF ITEM:근육이완제 < 99
		ITEMSALES:근육이완제 = 1
	;촉수사역술
	SIF ITEM:촉수사역술 == 0
		ITEMSALES:촉수사역술 = 1

	;ドロップ
	FOR LCOUNT, 55, 59
		SIF FIND_USE_ITEM(ITEMNAME:LCOUNT)
			ITEMSALES:LCOUNT = 1
	NEXT

	FOR LCOUNT, 1, CHARANUM
		IF TALENT:LCOUNT:서약 == 0 && TALENT:LCOUNT:상사상애 == 0
			ITEMSALES:서약의두루마리 = 1
			BREAK
		ENDIF
	NEXT
	FOR LCOUNT, 1, CHARANUM
		IF COND("성장성", LCOUNT)
			ITEMSALES:비뚤어진시계 = 1
			BREAK
		ENDIF
	NEXT
	FOR LCOUNT, 1, CHARANUM
		;온천予約中はダメ
		SIF FLAG:60
			BREAK
		IF ITEM:호접지몽 == 0 && COND("임신이벤트中", LCOUNT) == 0 && MARK:LCOUNT:반발각인 == 0 && ABL:LCOUNT:순종 >= 4
			ITEMSALES:호접지몽 = 1
			BREAK
		ENDIF
	NEXT
	SIF ITEM:레벨리미터 == 0
		ITEMSALES:레벨리미터 = 1
	SIF CHARANUM >= 3
		ITEMSALES:기억소거약 = 1
ENDIF


;소질系아이템の処理
SIF ABL:MASTER:기교 < 3
	ITEMSALES:☆기교Lv = 1
SIF TALENT:MASTER:손대중 == 0
	ITEMSALES:☆손대중 = 1
SIF TALENT:MASTER:불결무시 == 0
	ITEMSALES:☆불결무시 = 1
SIF TALENT:MASTER:금단의지식 == 0
	ITEMSALES:☆금단의지식 = 1
;☆조합대전. 賢くて, 조합지식を持っていないものがいる. 습득의 구슬も5万필요
FOR LCOUNT, 1, CHARANUM
	SIF TALENT:LCOUNT:현명함 == 0 || TALENT:LCOUNT:조합지식 || JUEL:LCOUNT:습득 < ITEMPRICE:65
		CONTINUE
	ITEMSALES:☆조합대전 = 1
	BREAK
NEXT
;☆조교지식. 현명함orＭＡＳＴＥＲ. 습득의 구슬も1万필요
FOR LCOUNT, 1, CHARANUM
	IF LCOUNT == MASTER
		SIF FLAG:240
			CONTINUE
	ELSEIF TALENT:LCOUNT:현명함 == 0 || JUEL:LCOUNT:습득 < ITEMPRICE:66
		CONTINUE
	ENDIF
	ITEMSALES:☆조교지식 = 1
	BREAK
NEXT

;インのモウサポートセンター
IF CONFIG("음모と겨털")
	FOR LCOUNT, 1, CHARANUM
		IF COND("음모", LCOUNT) && COND("음모", LCOUNT) < 7
			ITEMSALES:발모제 = 1
			BREAK
		ENDIF
	NEXT
	FOR LCOUNT, 1, CHARANUM
		IF COND("음모", LCOUNT) >= 4
			ITEMSALES:탈모제 = 1
			BREAK
		ENDIF
	NEXT
ENDIF

;임신패치をONにしている
IF CONFIG("임신패치")
	ITEMSALES:배란유발제 = 1
	;피임약を使う意味のあるキャラがいるか
	FOR LCOUNT, 1, CHARANUM
		;남자or임신中 or피임中or인간형
		SIF TALENT:LCOUNT:남자 || TALENT:LCOUNT:임신 || TALENT:LCOUNT:마도인형 || COND("着床확인", LCOUNT)
			CONTINUE

		ITEMSALES:긴급피임약 = 1

		SIF TALENT:LCOUNT:처녀 == 0
			ITEMSALES:피임의부적 = 1

		SIF TALENT:LCOUNT:피임의부적 == 0
			ITEMSALES:피임약 = 1
	NEXT
ENDIF

;複数반지が渡せる설정orまだ渡していない
IF CONFIG("상사상애：人数제한없음") || TALENT:MASTER:상사상애 == 0
	FOR LCOUNT, 1, CHARANUM
		SIF TALENT:LCOUNT:연모 == 0 || TALENT:LCOUNT:상사상애
			CONTINUE
		ITEMSALES:서약의반지 = 1
		BREAK
	NEXT
ENDIF

;음마의물방울は999個まで持てる
SIF ITEM:음마의물방울 < 999
	ITEMSALES:음마의물방울 = 1

;非売品
ITEMSALES:프랭킨센스 = 0
ITEMSALES:사고연마의돌 = 0
ITEMSALES:문드롭스 = 0

FOR LCOUNT, 30, 50
	IF ITEM:LCOUNT >= 99
		ITEM:LCOUNT = 99
		ITEMSALES:LCOUNT = 0
	ENDIF
NEXT

SIF ARGS == "표시なし"
	RETURN 0

DRAWLINE
;0-99に自動的に売り物が表示される. 
;PRINT_SHOPITEM
NEXT_LINE = 0
FOR LCOUNT, 0, 100
	SIF ITEMSALES:LCOUNT == 0 || ITEMNAME:LCOUNT == ""
		CONTINUE

	PRINTFORMLC  [{LCOUNT, 2}]%TEXT_LJ(ITEMNAME:LCOUNT, 14)%%TEXT_RJ(MONEY_C(ITEMPRICE:LCOUNT), MIN(21 - STRLENS(ITEMNAME:LCOUNT), 7) )%

	NEXT_LINE += 1
	SIF NEXT_LINE%3 == 0
		PRINTL 
NEXT
SIF NEXT_LINE%3
	PRINTL 

;-------------------------------------------------
;아이템購入
;-------------------------------------------------
@NEW_EVENTBUY
;조교아이템
SELECTCASE BOUGHT
CASE 0 TO 29, 97
	PRINTFORMW ＜%조사처리(ITEMNAME:BOUGHT,"를")% 구입했습니다＞
	ITEM:BOUGHT += 1
	MONEY -= ITEMPRICE:BOUGHT

	;補발があるならここで
	SELECTCASE BOUGHT
	;요도고문키트
	CASE 24
		PRINTFORML □ 보발 □---------------------------------------------------------------------
		PRINTFORM 요도를 확장할 때에는 , 
		CALL PRINT_COLORTEXT, "[조교지식(성기확장)]", DEF_COLOR("황색")
		PRINTFORM 과
		CALL PRINT_COLORTEXT, "[확장적정]", DEF_COLOR("황색")
		PRINTFORML 이 존재하면 순조롭게 됩니다
		PRINTFORM 확장의 하기 쉬움에는 영향이 없다소질그렇지만,
		CALL PRINT_COLORTEXT, "[도착적]", DEF_COLOR("황색")
		PRINTFORML 하지만 있으면반감를 억제 당하는 일이지요
		PRINTFORML 
		PRINTFORML 에서의 쾌감은Ｃ감각에 의존하고 있습니다……하지만, 초기는아픔하지만 강하고 대부분 쾌락을 얻을 수 있지 않습니다
		PRINTFORML 경험을 쌓게해아픔를 줄여, 를 만지작거려지는 일에 익숙하게 하는 것이 겹요정됩니다
		PRINTFORM 요도를 졸라대면서 절정을 경험하는 것이고 , 
		CALL PRINT_COLORTEXT, "[요도성감]", DEF_COLOR("황색")
		PRINTFORML 에 일깨우는 일이 됩니다.
		CALL PRINT_COLORTEXT, "[요도성감]", DEF_COLOR("황색")
		PRINTFORMW 없음이라면 기본적으로는 기분의 좋지 않다조교그래서, 빠른 취득이 겹요정그렇겠지요
	ENDSELECT
;로션など, 소비아이템. それに加えて음마의물방울も
CASE 30 TO 49, 78
	CALL BUY_PLURAL

;소질아이템【기교Lv】
CASE 60
	VARSET LOCAL
	PRINTL  [0] 기교Lv을 올린다
	IF ABL:MASTER:기교 > 1
		PRINTL  [1] 기교Lv을 내린다(대금이 들지 않음)
		LOCAL:1 = 1
	ENDIF
	PRINTL [100] 돌아간다
	CALL INPUT_SELECT, 1, LOCAL:1, 100
	SELECTCASE RESULT
	CASE 100

	CASE 0
		MONEY -= ITEMPRICE:BOUGHT
		ABL:MASTER:기교 += 1
		PRINTFORMW ＜%NAME:MASTER%의 기교Lv이 1 올라갔다 현재의 기교Lv：{ABL:MASTER:기교}＞
	CASE 1
		ABL:MASTER:기교 -= 1
		PRINTFORMW ＜%NAME:MASTER%의 기교Lv이 1 내려갔다 현재의 기교Lv：{ABL:MASTER:기교}＞
	ENDSELECT

;MASTERが소질を覚える아이템
CASE 61 TO 64
	SELECTCASE ITEMNAME:BOUGHT
	CASE "☆손대중"
		PRINTFORMW ＜%조사처리(NAME:MASTER,"는")% [손대중]을 익혔다＞
		TALENT:MASTER:손대중 = 1
	CASE "☆불결무시"
		PRINTFORMW ＜%조사처리(NAME:MASTER,"는")% [불결무시]를 몸에 익혔습니다＞
		TALENT:MASTER:불결무시 = 1
	;새드は현재封印中
	CASE "☆새드"
		PRINTFORMW ＜%조사처리(NAME:MASTER,"는")% [새드]가 되었습니다＞
		TALENT:MASTER:새드 = 1
	CASE "☆금단의지식"
		PRINTFORMW ＜%조사처리(NAME:MASTER,"는")% [금단의 지식]을 손에 넣었습니다＞
		TALENT:MASTER:금단의지식 = 1
	ENDSELECT
	MONEY -= ITEMPRICE:BOUGHT

;使用아이템, 소질아이템
CASE 50 TO 91, 94, 95, 98
	CALL USE_ITEM
ENDSELECT


;-------------------------------------------------
;複数個所持가능な아이템について, 同時購入個数を선택
;-------------------------------------------------
@BUY_PLURAL
#DIM MEMO

;買う前の所持数を記録
MEMO = ITEM:BOUGHT

DRAWLINE
CALL SHOW_BUY_ITEM, BOUGHT

;１０個이상買った？
IF ITEM:BOUGHT - MEMO >= 10
	LOCAL = (ITEM:BOUGHT - MEMO)/10
	PRINTFORMW 한가득 사준 답례로 도구상 주인이 {LOCAL}개 덤으로 주었습니다
	ITEM:BOUGHT += LOCAL
ENDIF
PRINTL 

;-------------------------------------------------
;소지금に対応して, ITEM:ARGを何個買うかの선택지を表示する関数
;-------------------------------------------------
@SHOW_BUY_ITEM, ARG
#DIM LCOUNT
#DIM MEMO_MAX

;買える최대個数
MEMO_MAX = MIN(99 - ITEM:ARG, MONEY/ITEMPRICE:ARG)
;음마의물방울
SIF ARG == 78
	MEMO_MAX = MIN(999 - ITEM:ARG, MONEY/ITEMPRICE:ARG)

PRINTFORML %조사선택(ITEMNAME:ARG, "를")%  몇개 삽니까？ (소지하고 있는 수：{ITEM:ARG} 소지금：%MONEY_C(MONEY)%)
PRINTFORML （사고 싶은 수를 직접 입력에서도 구입가능. 0or999그리고 돌아온다）

FOR LCOUNT, 1, MEMO_MAX+1
	SIF MONEY < ITEMPRICE:ARG * LCOUNT
		BREAK
	SELECTCASE LCOUNT
	CASE IS <= 5, 10, 20, 50, MEMO_MAX
		PRINTFORML  [{LCOUNT, 2}] {LCOUNT, 2}개 (%MONEY_C(ITEMPRICE:ARG * LCOUNT)%)
	ENDSELECT
NEXT
PRINTFORML [999] 돌아간다

$INPUT_LOOP
INPUT

IF RESULT == 0 || RESULT == 999
	RETURN 0
ELSEIF ITEMPRICE:ARG * RESULT > MONEY
	PRINTL 돈이 부족합니다
	GOTO INPUT_LOOP
ELSEIF RESULT > MEMO_MAX
	PRINTL 그렇게 많이 가질 수 없습니다
	GOTO INPUT_LOOP
ELSEIF RESULT >= 1 && RESULT <= MEMO_MAX
	PRINTFORMW ＜%조사선택(ITEMNAME:ARG, "를")%  \@ RESULT > 1 ? {RESULT}개 # \@ 구입했습니다＞
ELSE
	GOTO INPUT_LOOP
ENDIF

ITEM:ARG += RESULT
MONEY -= ITEMPRICE:ARG * RESULT

RETURN RESULT

;-------------------------------------------------
;아이템使用。ARGS == "調合"だと음마의물방울が通貨扱いになる
;-------------------------------------------------
@USE_ITEM, ARGS
#DIM LCOUNT
#DIM CHOICE, 2
#DIM MEMO, 10

DRAWLINE
;아이템の効果を表示する. 
SELECTCASE BOUGHT
CASE 65
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%):조합지식을 익히게합니다（필요：현명함＆습득의 구슬{ITEMPRICE:BOUGHT}）
CASE 66
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%):여러가지 조교에 쓰이는 지식을 기억하게합니다
	PRINTFORML MASTER인 경우에는 한번 변경할때마다 일주일간 \@ FLAG:240 ? (앞으로 {FLAG:240}일) # \@재설정을 할 수 없게되고, 
	PRINTFORML 그 이외의 사람의 경우에는, 현명함 ＆ 습득의 구슬 %조사처리(ITEMNAME:BOUGHT,"가")% 필요합니다
CASE 70
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%):그곳\@ CONFIG("음모") ? 과 %TEXTS("겨드랑이")% # \@의 털을 자라게합니다
CASE 71
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%):그곳\@ CONFIG("음모") ? 과 %TEXTS("겨드랑이")% # \@의 털을 탈모시킵니다
CASE 72
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%):마시는 타입의 사후피임약. 효과는 10일정도입니다
CASE 73
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%):긴급피임약. 최근에 한 성행위의 착상을 막습니다
CASE 74
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%):피임 마법이 걸린 T자형의 작은 부적입니다
	SETCOLOR DEF_COLOR("하트핑크")
	PRINTFORML ※효과는 영속적(영구적)이지만, 자궁 안에 장착합니다※
	RESETCOLOR
	PRINTFORML 이미 장착된 캐릭터를 선택하면, 피임의부적을 파괴합니다
CASE 90
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%):처분하지 않을 것을 서약합니다
CASE 91
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%):사랑을 전합니다
	CALL PRINT_STR, "H_個別ＥＮＤが用意されているキャラは_하트핑크_名前の色が変わります_H_L"
CASE 94
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%):특정공간의 시간을 조종합니다(아이를 단번에 성장시킵니다)
CASE 95
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%):기묘한 분위기의 책. 두권 세트.
	PRINTFORML 선택한 두 명의 신체를 바꿔 넣는 효과가 있습니다
CASE 98
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%):조교의 기억을 지웁니다
;이하は조합系
CASE 50
	PRINTFORML %ITEMNAME:BOUGHT%:체력(와)과정력를 전회복합니다（스톡도입니다！）
CASE 51
	PRINTFORML %ITEMNAME:BOUGHT%:여자를 후타나리로 만들거나, 되돌리거나 합니다
CASE 55
	PRINTFORML %ITEMNAME:BOUGHT%:모유체질로 하거나 함몰유두를 치료하거나 합니다
CASE 56
	PRINTFORML %ITEMNAME:BOUGHT%:처녀막을 재생시키고 모유체질을 잃게합니다
CASE 57
	PRINTFORML %ITEMNAME:BOUGHT%:성반전합니다
CASE 58
	PRINTFORML %ITEMNAME:BOUGHT%:실금 버릇과 몸 안의 손상을 치료합니다
ENDSELECT

SELECTCASE BOUGHT
CASE 72, 95
	PRINTFORML 누구에게 전해줍니까? (소지금：%MONEY_C(MONEY)%)
;음문
CASE 350
	PRINTFORML 하복부에, 마력이 깃든 정욕을 불러일으키는 문장을 새깁니다
	PRINTFORML (최대로 ４LV. LV이 오를수록, 자궁이 쾌락과 정력을 요구하게 됩니다)
	PRINTFORML 누구에게 각인 합니까？ (소지 물방울：{ITEM:음마의물방울}개)
CASE 55 TO 59
	SELECTCASE ARGS
	CASE "調合"
		PRINTFORML 누구에게 빨게 합니까？ (소지우：{ITEM:음마의물방울}개)
	CASEELSE
		PRINTFORML 누구에게 빨게 합니까？ (소지금：%MONEY_C(MONEY)%)
	ENDSELECT
;조합약
CASE 330 TO 340
	PRINTFORML 누구에게 %ITEMNAME:BOUGHT%를놀림인가？ (소지우：{ITEM:음마의물방울}개)
CASEELSE
	SELECTCASE ARGS
	CASE "調合"
		PRINTFORML 누구에게 사용합니까？ (소지우：{ITEM:음마의물방울}개)
	CASEELSE
		PRINTFORML 누구에게 사용합니까？ (소지금：%MONEY_C(MONEY)%)
	ENDSELECT
ENDSELECT

VARSET LOCAL
FOR LCOUNT, 1, CHARANUM
	LOCAL:LCOUNT = 0

	SELECTCASE BOUGHT
	CASE 65
		SIF TALENT:LCOUNT:현명함 == 0 || TALENT:LCOUNT:조합지식 || JUEL:LCOUNT:습득 < ITEMPRICE:BOUGHT
			CONTINUE
	CASE 66
		IF LCOUNT == MASTER
			SIF FLAG:240
				CONTINUE
		ELSEIF TALENT:LCOUNT:현명함 == 0 || JUEL:LCOUNT:습득 < ITEMPRICE:BOUGHT
			CONTINUE
		ENDIF
	CASE 73
		SIF TALENT:LCOUNT:남자 || TALENT:LCOUNT:임신 || TALENT:LCOUNT:마도인형 || COND("着床확인", LCOUNT)
			CONTINUE
	CASE 85
		SIF LCOUNT == MASTER
			CONTINUE
	CASE 90
		SIF LCOUNT == MASTER || TALENT:LCOUNT:서약 || TALENT:LCOUNT:상사상애
			CONTINUE
	CASE 91
		SIF LCOUNT == MASTER || TALENT:LCOUNT:연모 == 0 || TALENT:LCOUNT:상사상애
			CONTINUE
	CASE 94
		SIF !COND("성장성", LCOUNT)
			CONTINUE
	;호접지몽
	CASE 95
		SIF LCOUNT > MASTER && (COND("임신이벤트中", LCOUNT) || MARK:LCOUNT:반발각인 || ABL:LCOUNT:순종 < 4)
			CONTINUE
	;MASTERが記憶を失ってどうする…経験を失うのは面하양いかも？
	CASE 98
		SIF LCOUNT == MASTER
			CONTINUE
	ENDSELECT

	;登録番号・名前・체력・その他ステータスをまとめて表示
	SELECTCASE BOUGHT
	CASE 50
		SIF COND(ITEMNAME:BOUGHT, LCOUNT) == 0
			CONTINUE
		CALL PRINT_NUM_NAME, "체력と정력", LCOUNT
	CASE 51
		SIF TALENT:LCOUNT:남자
			CONTINUE
		CALL PRINT_NUM_NAME, "후타나리약", LCOUNT
	CASE 55 TO 59
		SIF COND(ITEMNAME:BOUGHT, LCOUNT) == 0
			CONTINUE
		CALL PRINT_NUM_NAME, ITEMNAME:BOUGHT, LCOUNT
	CASE 66
		CALL PRINT_NUM_NAME, "조교지식", LCOUNT
	CASE 70
		SIF (COND("음모", LCOUNT) == 0 || COND("음모", LCOUNT) >= 7) && (COND("겨털", LCOUNT) == 0 || COND("겨털", LCOUNT) >= 7)
			CONTINUE
		CALL PRINT_NUM_NAME, "음모＆겨털", LCOUNT
	CASE 71
		SIF COND("음모", LCOUNT) <= 2 && COND("겨털", LCOUNT) <= 2
			CONTINUE
		CALL PRINT_NUM_NAME, "음모＆겨털", LCOUNT
	CASE 72
		SIF TALENT:LCOUNT:남자 || TALENT:LCOUNT:임신 || TALENT:LCOUNT:마도인형 || TALENT:LCOUNT:피임의부적 || COND("着床확인", LCOUNT)
			CONTINUE
		CALL PRINT_NUM_NAME, "避妊措置", LCOUNT
	CASE 74
		SIF TALENT:LCOUNT:남자 || TALENT:LCOUNT:임신 || TALENT:LCOUNT:마도인형 || TALENT:LCOUNT:처녀 || COND("着床확인", LCOUNT)
			CONTINUE
		CALL PRINT_NUM_NAME, "避妊措置", LCOUNT
	CASE 91
		CALL PRINT_NUM_NAME, "서약의반지", LCOUNT
	CASE 330 TO 333
		SIF PENIS(LCOUNT) == 0
			CONTINUE
		CALL PRINT_NUM_NAME, ITEMNAME:BOUGHT, LCOUNT
	CASE 334, 335, 339, 340
		SIF TALENT:LCOUNT:남자
			CONTINUE
		CALL PRINT_NUM_NAME, ITEMNAME:BOUGHT, LCOUNT
	CASE 330 TO 359
		CALL PRINT_NUM_NAME, ITEMNAME:BOUGHT, LCOUNT
	CASEELSE
		CALL PRINT_NUM_NAME, "체력", LCOUNT
	ENDSELECT
	LOCAL:LCOUNT = 1
NEXT
PRINTL  [100]돌아간다

;특수な선택지
SELECTCASE BOUGHT
CASE 72
	MEMO = 0
	FOR LCOUNT, 1, CHARANUM
		SIF LOCAL:LCOUNT == 0 || COND("피임中", LCOUNT)
			CONTINUE
		MEMO += 1
	NEXT
	IF MEMO >= 2
		PRINTFORML  [200]임신위험이 있는 사람 전부에게 준다
		LOCAL:200 = 1
	ENDIF
ENDSELECT

$INPUT_LOOP
INPUT

IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 200 && LOCAL:200

ELSEIF RESULT < MASTER || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ELSEIF LOCAL:RESULT == 0
	GOTO INPUT_LOOP
ENDIF

;선택キャラをCHOICEに保存
CHOICE = RESULT


;お会計はSELECTCASEの終了後なので、RESTARTさせる場合にはその前に代金を頂くこと
SELECTCASE BOUGHT
;건강해지는약
CASE 50
	PRINTFORMW ＜%NAME:CHOICE%노체력(와)과정력하지만 완쾌 했다＞

	BASE:CHOICE:체력 = MAX(BASE:CHOICE:체력, MAXBASE:CHOICE:체력)
	BASE:CHOICE:정력 = MAX(BASE:CHOICE:정력, MAXBASE:CHOICE:정력)
	BASE:CHOICE:스톡 = MAX(BASE:CHOICE:스톡, MAXBASE:CHOICE:스톡)

	MONEY -= ITEMPRICE:BOUGHT

	SIF FIND_USE_ITEM(ITEMNAME:BOUGHT)
		RESTART
	RETURN 0
;후타나리化
CASE 51
	IF TALENT:CHOICE:후타나리 == 0
		PRINTFORMW ＜%NAME:CHOICE%를후타나리화했다＞
		TALENT:CHOICE:여자 = 0
		TALENT:CHOICE:후타나리 = 2
		CALL EXP50_CHECK, CHOICE
	ELSE
		PRINTFORMW ＜%NAME:CHOICE%하후타나리는 아니게 되었다＞
		TALENT:CHOICE:여자 = 1
		TALENT:CHOICE:후타나리 = 0
	ENDIF
	SWAP TARGET, CHOICE
	CALL SELF_KOJO, "후타나리약"
	SWAP TARGET, CHOICE

	SELECTCASE ARGS
	CASE "調合"
		ITEM:음마의물방울 -= 10
	CASEELSE
		MONEY -= ITEMPRICE:BOUGHT
	ENDSELECT

	RETURN 0
;모유체질にするor陥没유두を治す
CASE 55
	IF TALENT:CHOICE:모유체질 == 0
		PRINTL  [ 0] 모유체질로 한다
	ELSE
		CALL PRINT_STR, "회색_ [×] 모유체질にする_L"
	ENDIF
	IF COND("함몰유두", CHOICE)
		PRINTL  [ 1] 함몰유두를 고친다
	ELSE
		CALL PRINT_STR, "회색_ [×] 함몰유두を治す_L"
	ENDIF
	PRINTFORML [100] 돌아온다

	CALL INPUT_SELECT, 2, 100

	IF RESULT == 100
		RETURN 0
	ELSEIF RESULT == 0 && TALENT:CHOICE:모유체질 == 0
		PRINTFORMW ＜%NAME:CHOICE%는 모유가 나오게 되었다＞
		TALENT:CHOICE:모유체질 = 2
	ELSEIF RESULT == 1 && COND("함몰유두", CHOICE)
		PRINTFORMW ＜%NAME:CHOICE%의 함몰유두하지만 나았다＞
		SETBIT TALENT:CHOICE:유두, 10
		CLEARBIT TALENT:CHOICE:유두, 11
		CLEARBIT TALENT:CHOICE:유두, 12
	ENDIF

	SELECTCASE ARGS
	CASE "調合"
		ITEM:음마의물방울 -= 10
	CASEELSE
		MONEY -= ITEMPRICE:BOUGHT
	ENDSELECT

	SIF FIND_USE_ITEM(ITEMNAME:BOUGHT)
		RESTART
	RETURN 0

;처녀膜再生or모유체질喪失
CASE 56
	IF TALENT:CHOICE:처녀 == 0
		PRINTL  [ 0] 처녀막을 재생한다
	ELSE
		CALL PRINT_STR, "회색_ [×] 처녀膜を再生する_L"
	ENDIF
	IF TALENT:CHOICE:모유체질
		PRINTL  [ 1] 모유체질를 잃게 한다
	ELSE
		CALL PRINT_STR, "회색_ [×] 모유체질を失わせる_L"
	ENDIF
	PRINTFORML [100] 돌아온다

	CALL INPUT_SELECT, 2, 100

	IF RESULT == 100
		RETURN 0
	ELSEIF RESULT == 0 && TALENT:CHOICE:처녀 == 0
		PRINTFORMW ＜%NAME:CHOICE%노처녀막이 재생되었다＞
		TALENT:CHOICE:처녀 = 2
	ELSEIF RESULT == 1 && TALENT:CHOICE:모유체질
		PRINTFORMW ＜%NAME:CHOICE%하모유체질를 잃었다＞
		TALENT:CHOICE:모유체질 = 0
	ENDIF

	SELECTCASE ARGS
	CASE "調合"
		ITEM:음마의물방울 -= 10
	CASEELSE
		MONEY -= ITEMPRICE:BOUGHT
	ENDSELECT

	SIF FIND_USE_ITEM(ITEMNAME:BOUGHT)
		RESTART
	RETURN 0
;성전환
CASE 57
	CALL CHANGE_SEX, CHOICE
	IF TALENT:CHOICE:후타나리
		PRINTFORM ＜%NAME:CHOICE%하후타나리
	ELSEIF TALENT:CHOICE:여자
		PRINTFORM ＜%NAME:CHOICE%하여자
	ELSE
		PRINTFORM ＜%NAME:CHOICE%하남자
	ENDIF
	PRINTFORMW 에\@ CFLAG:CHOICE:4 ? なりました # 戻りました \@＞
	IF TALENT:CHOICE:남자
		PRINTL 
		PRINTFORML ……%CALLNAME:CHOICE%를 이른바"오토코노코"로 합니까？
		PRINTFORML [ 0] 오토코노코로 한다\@ TALENT:CHOICE:장신 ? (장신は失います) # \@
		PRINTFORML [ 1] 하지않는다
		CALL INPUT_SELECT, 2
		IF RESULT == 0
			TALENT:CHOICE:오토코노코 = 1
			SIF TALENT:CHOICE:장신
				CALL CALC, "普通体型化", CHOICE
			PRINTFORMW ＜%CALLNAME:CHOICE%하"오토코노코"(이)가 되었습니다＞
		ENDIF
		PRINTL 
	ENDIF
	CALL SET_3SIZE, CHOICE

	SWAP TARGET, CHOICE
	CALL SELF_KOJO, "검은드롭스"
	SWAP TARGET, CHOICE

	SELECTCASE ARGS
	CASE "調合"
		ITEM:음마의물방울 -= 10
	CASEELSE
		MONEY -= ITEMPRICE:BOUGHT
	ENDSELECT

	RETURN 0
;치료
CASE 58
	PRINTFORM ＜%NAME:CHOICE%하
	IF TALENT:CHOICE:오줌싸개버릇 > 0
		PRINTFORM %TALENTNAME:57%
		TALENT:CHOICE:오줌싸개버릇 -= 1
		;次になるのは, 50回실금を追加でした時とする
		CFLAG:CHOICE:34 = EXP:CHOICE:실금경험 + 20
		SIF TALENT:CHOICE:Ｖ손상 + TALENT:CHOICE:Ａ손상 > 0
			PRINT 과
	ENDIF
	IF TALENT:CHOICE:Ｖ손상 + TALENT:CHOICE:Ａ손상 > 0
		TALENT:CHOICE:Ｖ손상 = 0
		TALENT:CHOICE:Ａ손상 = 0
		PRINTFORM 체내의 손상
	ENDIF
	PRINTFORMW 이 치료되었다＞
	CALL GET_EXABL2, CHOICE

	SELECTCASE ARGS
	CASE "調合"
		ITEM:음마의물방울 -= 10
	CASEELSE
		MONEY -= ITEMPRICE:BOUGHT
	ENDSELECT

	RETURN 0
;조합지식
CASE 65
	PRINTFORMW ＜%NAME:CHOICE%하조합지식를 얻었다＞
	TALENT:CHOICE:조합지식 = 1
	JUEL:CHOICE:습득 -= ITEMPRICE:BOUGHT
CASE 66
	CHOICE:1 = TALENT:CHOICE:조교지식
	CALL SETFLAG, "조교지식の선택", CHOICE
	SIF TALENT:CHOICE:조교지식 == CHOICE:1
		RESTART
;음모を生やす
CASE 70
	IF COND("음모", CHOICE) == 0 || COND("음모", CHOICE) >= 7
		RESULT = 1
	ELSEIF COND("겨털", CHOICE) == 0 || COND("겨털", CHOICE) >= 7
		RESULT = 0
	ELSE
		PRINTFORML 음모와 %TEXTS("겨드랑이")%털 중 어느쪽을 기릅니까？
		PRINTFORML [ 0] 음모
		PRINTFORML [ 1] %TEXTS("겨드랑이")%털
		CALL INPUT_SELECT, 2
	ENDIF
	IF RESULT == 0
		PRINTFORMW ＜%NAME:CHOICE%의 저기의 털을 폈습니다＞
		SELECTCASE COND("음모", CHOICE)
		CASE IS <= 5
			BASE:CHOICE:음모 = 600
		CASE 6
			BASE:CHOICE:음모 = 700
		ENDSELECT
	ELSE
		PRINTFORMW ＜%NAME:CHOICE%노%TEXTS("겨드랑이")%털을 폈습니다＞
		BASE:CHOICE:겨털 = 700
	ENDIF
	SIF TALENT:CHOICE:마도인형
		PRINTFORMW 다만,%NAME:CHOICE%하마도인형때문에 성장하거나는 하지 않습니다. 자발적으로손넣고도 하지 않습니다.
;음모を失くす
CASE 71
	IF COND("음모", CHOICE) <= 2
		RESULT = 1
	ELSEIF COND("겨털", CHOICE) <= 2
		RESULT = 0
	ELSE
		PRINTFORML 음모와 %TEXTS("겨드랑이")%털 중 어느쪽을 탈모시킵니까？
		PRINTFORML [ 0] 음모
		PRINTFORML [ 1] %TEXTS("겨드랑이")%털
		CALL INPUT_SELECT, 2
	ENDIF
	IF RESULT == 0
		PRINTFORMW ＜%NAME:CHOICE%의 저기의 털을 탈모했습니다＞
		BASE:CHOICE:음모 = 200
		;마도인형なら, パイパン化
		SIF TALENT:CHOICE:마도인형
			BASE:CHOICE:음모 = 100
	ELSE
		PRINTFORMW ＜%NAME:CHOICE%노%TEXTS("겨드랑이")%털을 탈모했습니다＞
		BASE:CHOICE:겨털 = 200
		SIF TALENT:CHOICE:마도인형
			BASE:CHOICE:겨털 = 100
	ENDIF
;피임약
CASE 72
	SELECTCASE CHOICE
	;全員に配る場合
	CASE 200
		MEMO = 0
		FOR LCOUNT, 1, CHARANUM
			SIF LOCAL:LCOUNT == 0 || COND("피임中", LCOUNT)
				CONTINUE
			MEMO += 1
		NEXT
		PRINTFORML 피임을 하고 있지 않는 인원수가{MEMO}사람인 것으로%MONEY_C(ITEMPRICE:BOUGHT * MEMO)%하지만필요입니다
		;お금が발りている
		IF MONEY >= ITEMPRICE:BOUGHT*MEMO
			PRINTFORML [ 0] 일괄구입한다 (구입 후의 소지금：%MONEY_C(MONEY - ITEMPRICE:BOUGHT*MEMO)%)
			PRINTFORML [ 1] 역시 사지않는다
			CALL INPUT_SELECT, 2
			IF RESULT == 0
				MONEY -= ITEMPRICE:BOUGHT*MEMO
				FOR LCOUNT, 1, CHARANUM
					SIF LOCAL:LCOUNT == 0 || COND("피임中", LCOUNT)
						CONTINUE
					TALENT:LCOUNT:피임약복용 += 10
				NEXT
				PRINTFORMW ＜피임약을 구입하고 썻습니다＞
			ENDIF
		ELSE
			PRINTW …아무래도 소지금이 부족한 모양이네요
		ENDIF
	CASEELSE
		TALENT:CHOICE:피임약복용 += 10
		IF CHOICE == MASTER
			PRINTFORMW ＜%NAME:CHOICE%의 피임 기간이{TALENT:CHOICE:피임약복용}날이 되었다＞
		ELSE
			PRINTFORMW ＜%NAME:CHOICE%에피임약를 건네주었습니다. 피임 기간이{TALENT:CHOICE:피임약복용}날이 되었다＞
		ENDIF

		SWAP TARGET, CHOICE
		CALL SELF_KOJO, "피임약"
		SWAP TARGET, CHOICE

		MONEY -= ITEMPRICE:BOUGHT
	ENDSELECT
	IF MONEY >= ITEMPRICE:BOUGHT
		RESTART
	ELSE
		RETURN 0
	ENDIF
;긴급피임약
CASE 73
	CALL SETFLAG, "避妊", CHOICE
	PRINTFORMW ＜%NAME:CHOICE%노임신의 위험을 없앴습니다＞

	SWAP TARGET, CHOICE
	CALL SELF_KOJO, "긴급피임약"
	SWAP TARGET, CHOICE
;피임의부적
CASE 74
	;付けている場合には外す
	IF TALENT:CHOICE:피임의부적
		PRINTFORMW ＜%NAME:CHOICE%노자궁입로부터 늘어지고 있는 끈을 잡아당겨,피임의부적를 파괴했습니다＞
		TALENT:CHOICE:피임의부적 = 0
	ELSE
		PRINTFORML ＜%NAME:CHOICE%노자궁안에피임의부적를 장착했습니다＞
		PRINTFORMW 자궁구에서는 잡아 당기는일로 부적을 제거할수 있는 부드러운 끈이 늘어지고 있습니다…
		;즉시 피임효과가 나타났다
		CALL SETFLAG, "避妊", CHOICE
		TALENT:CHOICE:피임의부적 = 1
		MONEY -= ITEMPRICE:BOUGHT
	ENDIF

	SWAP TARGET, CHOICE
	CALL SELF_KOJO, "피임의부적"
	SWAP TARGET, CHOICE

	;피임약は回収
	IF TALENT:CHOICE:피임약복용 > 0
		PRINTL 
		PRINTFORML 필요없어진 피임약을 교환했습니다. 
		PRINTFORMW 소지금＋%MONEY_C(TALENT:CHOICE:피임약복용*150)%
		MONEY += TALENT:CHOICE:피임약복용*150
		TALENT:CHOICE:피임약복용 = 0
	ENDIF

	IF MONEY >= ITEMPRICE:BOUGHT
		RESTART
	ELSE
		RETURN 0
	ENDIF
;서약의두루마리
CASE 90
	PRINTFORMW ＜%NAME:CHOICE%를매각할 수 없게 되었습니다＞
	TALENT:CHOICE:서약 = 1

	SWAP TARGET, CHOICE
	CALL SELF_KOJO, "서약의두루마리"
	SWAP TARGET, CHOICE
;서약의반지
CASE 91
	PRINTFORMW ＜%NAME:CHOICE%에 반지를 주었습니다＞
	TALENT:CHOICE:서약 = 0
	TALENT:CHOICE:상사상애 = 1
	TALENT:MASTER:상사상애 = 1

	;告하양이벤트
	SWAP TARGET, CHOICE
	CALL SELF_KOJO, "서약의반지"
	SWAP TARGET, CHOICE

	SETCOLOR DEF_COLOR("노랑")
	PRINTL 
	PRINTFORMW %CALLNAME:CHOICE%(와)과상사상애(이)가 되었습니다！
	RESETCOLOR
	;반지をとっておく
	ITEM:서약의반지 += 1
;비뚤어진시계
CASE 94
	PRINTFORML ＜%NAME:CHOICE%시에는 진행되었습니다＞
	PRINTFORMW 1인분이 되기 위해서(때문에) 충분한 경험을 쌓으면, 새로운 성장의가능성이 보이겠지요……
	SETBIT CFLAG:CHOICE:484, 0
;호접지몽（体の入れ換え）
CASE 95
	CALL KOTYOUNOYUME, CHOICE
	;호접지몽を使わなかった
	SIF ITEM:호접지몽 == 0
		RETURN 0
;기억소거약
CASE 98
	PRINTFORMW ＜%NAME:CHOICE%는 지금까지의 일을 잊었습니다＞
	CALL MEMORY_LOST, CHOICE
;身体変化系の効果
CASE 330 TO 349
	SIF COND(ITEMNAME:BOUGHT, CHOICE) == 0
		RESTART

	;元の体型を記憶
	LOCALS = %CONDS("体型", CHOICE)%

	SELECTCASE BOUGHT
	CASE 330
		IF GETBIT(TALENT:CHOICE:페니스, 4)
			SETBIT TALENT:CHOICE:페니스, 5
			CLEARBIT TALENT:CHOICE:페니스, 4
		ELSEIF GETBIT(TALENT:CHOICE:페니스, 3)
			SETBIT TALENT:CHOICE:페니스, 4
			CLEARBIT TALENT:CHOICE:페니스, 3
		ELSEIF GETBIT(TALENT:CHOICE:페니스, 2)
			SETBIT TALENT:CHOICE:페니스, 3
			CLEARBIT TALENT:CHOICE:페니스, 2
		ELSEIF GETBIT(TALENT:CHOICE:페니스, 1)
			SETBIT TALENT:CHOICE:페니스, 0
			CLEARBIT TALENT:CHOICE:페니스, 1
		ELSEIF GETBIT(TALENT:CHOICE:페니스, 0)
			SETBIT TALENT:CHOICE:페니스, 2
			CLEARBIT TALENT:CHOICE:페니스, 0
		ENDIF
		PRINTFORMW ＜%NAME:CHOICE%노페니스의 크기는[%TALENT_NAME(2, TALENT:CHOICE:페니스, CHOICE)%](이)가 되었습니다＞
	CASE 331
		IF GETBIT(TALENT:CHOICE:페니스, 5)
			SETBIT TALENT:CHOICE:페니스, 4
			CLEARBIT TALENT:CHOICE:페니스, 5
		ELSEIF GETBIT(TALENT:CHOICE:페니스, 4)
			SETBIT TALENT:CHOICE:페니스, 3
			CLEARBIT TALENT:CHOICE:페니스, 4
		ELSEIF GETBIT(TALENT:CHOICE:페니스, 3)
			SETBIT TALENT:CHOICE:페니스, 2
			CLEARBIT TALENT:CHOICE:페니스, 3
		ELSEIF GETBIT(TALENT:CHOICE:페니스, 2)
			SETBIT TALENT:CHOICE:페니스, 0
			CLEARBIT TALENT:CHOICE:페니스, 2
		ELSEIF GETBIT(TALENT:CHOICE:페니스, 0)
			SETBIT TALENT:CHOICE:페니스, 1
			CLEARBIT TALENT:CHOICE:페니스, 0
		ENDIF
		PRINTFORMW ＜%NAME:CHOICE%노페니스의 크기는[%TALENT_NAME(2, TALENT:CHOICE:페니스, CHOICE)%](이)가 되었습니다＞
	CASE 332
		IF GETBIT(TALENT:CHOICE:페니스, 12)
			SETBIT TALENT:CHOICE:페니스, 11
			CLEARBIT TALENT:CHOICE:페니스, 12
		ELSEIF GETBIT(TALENT:CHOICE:페니스, 11)
			CLEARBIT TALENT:CHOICE:페니스, 11
		ENDIF
		PRINTFORMW ＜%NAME:CHOICE%노페니스상태는[%TALENT_NAME(2, TALENT:CHOICE:페니스, CHOICE)%](이)가 되었습니다＞
	CASE 333
		IF GETBIT(TALENT:CHOICE:페니스, 11)
			SETBIT TALENT:CHOICE:페니스, 12
			CLEARBIT TALENT:CHOICE:페니스, 11
		ELSE
			SETBIT TALENT:CHOICE:페니스, 11
		ENDIF
		PRINTFORMW ＜%NAME:CHOICE%노페니스상태는[%TALENT_NAME(2, TALENT:CHOICE:페니스, CHOICE)%](이)가 되었습니다＞
	CASE 334
		IF TALENT:CHOICE:거유 >= 2
			PRINTFORMW ＜%NAME:CHOICE%노가슴의 사이즈는, 있을 수 없는 크기의 초유가 되었습니다＞
			CALL CALC, "초유化", CHOICE
		ELSEIF TALENT:CHOICE:거유
			PRINTFORMW ＜%NAME:CHOICE%노가슴의 사이즈는, 터질듯한폭유(이)가 되었습니다＞
			CALL CALC, "폭유化", CHOICE
		ELSEIF TALENT:CHOICE:빈유
			PRINTFORMW ＜%NAME:CHOICE%노가슴의 사이즈는 특히 희소 가치도 없는 보통의 크기가 되었습니다＞
			CALL CALC, "並乳化", CHOICE
		ELSE
			PRINTFORMW ＜%NAME:CHOICE%노가슴의 사이즈는[거유](이)가 되었습니다＞
			CALL CALC, "거유化", CHOICE
		ENDIF
	CASE 335
		IF TALENT:CHOICE:거유 >= 3
			PRINTFORMW ＜%NAME:CHOICE%노가슴의 사이즈는[폭유](이)가 되었습니다＞
			CALL CALC, "폭유化", CHOICE
		ELSEIF TALENT:CHOICE:거유 == 2
			PRINTFORMW ＜%NAME:CHOICE%노가슴의 사이즈는[거유](이)가 되었습니다＞
			CALL CALC, "거유化", CHOICE
		ELSEIF TALENT:CHOICE:거유
			PRINTFORMW ＜%NAME:CHOICE%노가슴의 사이즈는거유로부터 일전해 보통의 크기가 되었습니다＞
			CALL CALC, "並乳化", CHOICE
		ELSE
			PRINTFORMW ＜%NAME:CHOICE%노가슴의 사이즈는[빈유](이)가 되었습니다＞
			CALL CALC, "빈유化", CHOICE
		ENDIF
	CASE 336
		IF TALENT:CHOICE:작은몸집
			PRINTFORMW ＜%NAME:CHOICE%하[작은몸집]를 잃었습니다＞
			CALL CALC, "普通体型化", CHOICE
		ELSE
			PRINTFORMW ＜%NAME:CHOICE%하[장신]를 얻었습니다＞
			CALL CALC, "장신化", CHOICE
			SIF TALENT:CHOICE:오토코노코
				PRINTFORMW ＜…역시 오토코노코라는 것은 무리가 있어보입니다＞
		ENDIF
	CASE 337
		IF TALENT:CHOICE:장신
			PRINTFORMW ＜%NAME:CHOICE%하[장신]를 잃었습니다＞
			CALL CALC, "普通体型化", CHOICE
		ELSE
			PRINTFORMW ＜%NAME:CHOICE%하[작은몸집](이)가 되었습니다＞
			CALL CALC, "小柄化", CHOICE
		ENDIF
	CASE 338
		PRINTFORM 현재[{CHOICE, 2}]%CALLNAME:CHOICE%노피부색하
		IF TALENT:CHOICE:피부색
			PRINTFORML %CONDS("피부색", CHOICE)%입니다
		ELSE
			PRINTFORML 보통입니다
		ENDIF
		PRINTFORML 어느 색으로 바꿉니까？
		PRINTFORML  [ 0] 보통색깔
		PRINTFORML  [ 1] 갈색
		PRINTFORML  [ 2] 흰피부
		PRINTFORML  [ 3] 파랑
		PRINTFORML [100] 역시 바꾸지않는다
		CALL INPUT_SELECT, 4, 100
		SELECTCASE RESULT
		CASE 100
			RETURN 0
		CASE 0
			PRINTFORMW ＜%CALLNAME:CHOICE%노피부색를 말[살색]에 변화시켰습니다＞
		CASE 1
			PRINTFORMW ＜%CALLNAME:CHOICE%노피부색를 건강할 것 같은[갈색]에 변화시켰습니다＞
		CASE 2
			PRINTFORMW ＜%CALLNAME:CHOICE%노피부색를 빠지는 것 같은[백옥]에 변화시켰습니다＞
		CASE 3
			PRINTFORMW ＜%CALLNAME:CHOICE%노피부색를 보통에서는 뵙기 힘들다[파랑]에 변화시켰습니다＞
		ENDSELECT
		IF TALENT:CHOICE:피부색 == RESULT
			PRINTFORMW …근데 원래색이랑 똑같군요. 소재는 돌려드립니다
			RETURN 0
		ELSE
			TALENT:CHOICE:피부색 = RESULT
		ENDIF
	CASE 339
		PRINTFORM ＜%NAME:CHOICE%노엉덩이하지만{BASE:CHOICE:힙}cm로부터{BASE:CHOICE:신장*3/100}cm커져,
		BASE:CHOICE:힙 += BASE:CHOICE:신장*3/100
		PRINTFORMW {BASE:CHOICE:힙}cm(이)가 되었습니다＞
		IF TALENT:CHOICE:큰엉덩이 == 2
			CALL SETFLAG, "힙설정", CHOICE
			SIF TALENT:CHOICE:큰엉덩이 != 2
				PRINTFORMW ＜%NAME:CHOICE%하[%CONDS("엉덩이", CHOICE)%](이)가 되었습니다＞
		ELSEIF TALENT:CHOICE:큰엉덩이 == 1
			CALL SETFLAG, "힙설정", CHOICE
			SIF TALENT:CHOICE:큰엉덩이 != 1
				PRINTFORMW ＜%NAME:CHOICE%하[%CONDS("엉덩이", CHOICE)%](이)가 되었습니다＞
		ELSEIF TALENT:CHOICE:작은엉덩이
			CALL SETFLAG, "힙설정", CHOICE
			SIF TALENT:CHOICE:작은엉덩이 == 0
				PRINTFORMW ＜%NAME:CHOICE%하[작은엉덩이]를 잃었습니다＞
		ELSEIF TALENT:CHOICE:큰엉덩이 == 0
			CALL SETFLAG, "힙설정", CHOICE
			SIF TALENT:CHOICE:큰엉덩이 != 0
				PRINTFORMW ＜%NAME:CHOICE%하[%CONDS("엉덩이", CHOICE)%](이)가 되었습니다＞
		ENDIF
	CASE 340
		PRINTFORM ＜%NAME:CHOICE%노엉덩이하지만{BASE:CHOICE:힙}cm로부터{BASE:CHOICE:신장*3/100}cm작아져,
		BASE:CHOICE:힙 -= BASE:CHOICE:신장*3/100
		PRINTFORMW {BASE:CHOICE:힙}cm(이)가 되었습니다＞
		IF TALENT:CHOICE:큰엉덩이 == 3
			CALL SETFLAG, "힙설정", CHOICE
			SIF TALENT:CHOICE:큰엉덩이 != 3
				PRINTFORMW ＜%NAME:CHOICE%하[초고]를 잃었습니다＞
		ELSEIF TALENT:CHOICE:큰엉덩이 == 2
			CALL SETFLAG, "힙설정", CHOICE
			SIF TALENT:CHOICE:큰엉덩이 != 2
				PRINTFORMW ＜%NAME:CHOICE%하[폭고]를 잃었습니다＞
		ELSEIF TALENT:CHOICE:큰엉덩이 == 1
			CALL SETFLAG, "힙설정", CHOICE
			SIF TALENT:CHOICE:큰엉덩이 != 1
				PRINTFORMW ＜%NAME:CHOICE%하[큰엉덩이]를 잃었습니다＞
		ELSEIF TALENT:CHOICE:작은엉덩이 == 0
			CALL SETFLAG, "힙설정", CHOICE
			SIF TALENT:CHOICE:작은엉덩이
				PRINTFORMW ＜%NAME:CHOICE%하[%CONDS("엉덩이", CHOICE)%](이)가 되었습니다＞
		ENDIF
	ENDSELECT
	;3サイズを체크
	CALL SET_3SIZE, CHOICE

	IF LOCALS != CONDS("体型", CHOICE)
		PRINTFORM ＜변화에 수반해,%NAME:CHOICE%의 체형은
		SETCOLOR DEF_COLOR("황색")
		PRINTFORM [%CONDS("体型", CHOICE)%]
		RESETCOLOR
		PRINTFORMW 되었습니다＞
	ENDIF

	SWAP TARGET, CHOICE
	CALL SELF_KOJO, ITEMNAME:BOUGHT
	SWAP TARGET, CHOICE

	ITEM:음마의물방울 -= ITEMPRICE:BOUGHT

	;所持雫が발りていて, 使う대상が居るならもう一回キャラリスト表示
	IF ITEM:음마의물방울 >= ITEMPRICE:BOUGHT
		FOR LCOUNT, 1, CHARANUM
			SIF COND(ITEMNAME:BOUGHT, LCOUNT) == 0
				CONTINUE

			RESTART
		NEXT
	ENDIF

	RETURN 0

;정액각인
CASE 350
	SIF COND("정액각인刻印가능", CHOICE) == 0
		RESTART

	;흡정체질の上昇判定などに使う
	MEMO = TALENT:CHOICE:흡정체질
	MEMO:1 = COND("요도구弛緩度", CHOICE)

	SELECTCASE MARK:CHOICE:정액각인
	CASE 0
		PRINTFORMW 음마의물방울를 이용해,%NAME:CHOICE%의 하복부에 사랑스러운 하트형의 문장을 새겼습니다
	CASE 1
		PRINTFORMW 이미 새겨지고 있는 하트의 원포인트에 장식을 더해,%NAME:CHOICE%노자궁를 본뜬 문양을 음각했다
	CASE 2
		PRINTFORMW %NAME:CHOICE%의 하복부…확실히자궁(이)가 있는 장소에 조각해 붐비어진 각인에, 난관과 난소를 나타내는 모양을 추가하여 그렸습니다
	CASE 3
		PRINTFORMW %NAME:CHOICE%노자궁를 틈새를 만들었는지의 같은 음미로운 문장에,자궁경부를 구상한 무늬를 조각해발해정액각인를 완성시켰습니다
	CASE 4
		GOTO INPUT_LOOP
	ENDSELECT

	MARK:CHOICE:정액각인 += 1
	TALENT:CHOICE:흡정체질 = SET_EXTALENT("흡정체질", CHOICE)

	SWAP TARGET, CHOICE
	CALL SELF_KOJO, "정액각인"
	SWAP TARGET, CHOICE

	SETCOLOR DEF_COLOR("하트핑크")
	PRINTFORMW ＜%NAME:CHOICE%하정액각인LV{MARK:CHOICE:정액각인}를 얻었다＞
	RESETCOLOR

	;소질の変化
	IF TALENT:CHOICE:젖기어려움
		TALENT:CHOICE:젖기어려움 = 0
		PRINTFORMW ＜%NAME:CHOICE%하[젖기어려움]를 잃었다＞
	ENDIF
	IF TALENT:CHOICE:Ｖ둔감 && MARK:CHOICE:정액각인 >= 4
		TALENT:CHOICE:Ｖ둔감 = 1
		PRINTFORMW ＜%NAME:CHOICE%하[Ｖ둔감]를 잃었다＞
	ENDIF

	IF TALENT:CHOICE:젖기쉬움 == 0 && MARK:CHOICE:정액각인 >= 2
		TALENT:CHOICE:젖기쉬움 = 1
		PRINTFORMW ＜%NAME:CHOICE%하[젖기쉬움]를 얻었다＞
	ENDIF
	IF TALENT:CHOICE:자궁성감 == 0 && MARK:CHOICE:정액각인 >= 2
		TALENT:CHOICE:자궁성감 = 1
		PRINTFORMW ＜%NAME:CHOICE%하[자궁성감]를 얻었다＞
	ENDIF

	IF TALENT:CHOICE:흡정체질 > MEMO
		PRINTFORMW ＜%NAME:CHOICE%노흡정체질하LV{TALENT:CHOICE:흡정체질}로 상승했다＞
	ENDIF
	IF COND("요도구弛緩度", CHOICE) > MEMO:1
		PRINTFORMW ＜%NAME:CHOICE%노자궁입의 유연성이 상승했다＞
	ENDIF

	ITEM:음마의물방울 -= ITEMPRICE:BOUGHT

	;所持雫が발りていて, 使う대상が居るならもう一回キャラリスト表示
	IF ITEM:음마의물방울 >= ITEMPRICE:BOUGHT
		FOR LCOUNT, 1, CHARANUM
			SIF TALENT:LCOUNT:남자 || COND("임신이벤트中", LCOUNT)
				CONTINUE
			RESTART
		NEXT
	ENDIF

	RETURN 0
ENDSELECT

;代금はここで支払う（一部例外あり）
MONEY -= ITEMPRICE:BOUGHT

