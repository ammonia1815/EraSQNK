@EAT_LUNCH

REDRAW 0

PRINTL 
DRAWLINE
PRINTL  □ 술집 『일각수의 모퉁이』 □
DRAWLINE
PRINTFORML 보통의 식사에 무언가 메뉴를 더해 심신을 알차게 하자!  (소지금:%MONEY_C(MONEY)%)
PRINTFORML 덧붙여서 식사에 의한 체력의 회복은 100％를 넘습니다
PRINTFORML 마시면 조교의 실행치가 상승합니다만, 술주정이나 이상한 행동을 이따금 취합니다
PRINTFORML （우단의 [해설]을 클릭or식사의 명칭을 입력하는 것으로 해설을 ON/OFF할 수 있습니다）
;食器で회복量UP
IF ITEM:미스릴스푼
	PRINTFORML 미스릴스푼을 소지하고 있으므로, 체력＆정력회복효과가 60％ 높아지고 있습니다.
ELSEIF ITEM:은스푼
	PRINTFORML 은스푼을 소지하고 있으므로, 체력＆정력회복효과가 40％ 높아지고 있습니다.
ELSEIF ITEM:도자기스푼
	PRINTFORML 도자기스푼울 소지하고 있으므로, 체력＆정력회복효과 20％ 높아지고 있습니다.
ENDIF

CALL EAT_LUNCH_BUY

@EAT_LUNCH_BUY
#DIM MEMO_LINECOUNT
#DIM PRICE, 100

MEMO_LINECOUNT = LINECOUNT

PRINTL 
;メインディッシュ
PRINTFORM  [ 0] %"시프리샌드", 24, LEFT% ＄９０ %TEXT_LJ("체력小회복＆정력小회복", 33)%
PRINTBUTTON "[해설]", "시프리샌드"
PRINTL 
SIF GETBIT(FLAG:41, 0)
	CALL PRINTL_COLORTEXT, " 치즈와 충분한 양파를 실은 시프리 명물 오픈 샌드", DEF_COLOR("황색")
PRICE:0 = 90

PRINTFORM  [ 1] %"마운틴스테이크", 24, LEFT% ＄９０ %TEXT_LJ("체력中회복", 33)%
PRINTBUTTON "[해설]", "마운틴스테이크"
PRINTL 
SIF GETBIT(FLAG:41, 1)
	CALL PRINTL_COLORTEXT, "   염소의 고기를 다양한 허브와 함께 구운 스테이크", DEF_COLOR("황색")
PRICE:1 = 90

;軽食やサラダ
PRINTFORM  [10] %"과일샐러드", 24, LEFT% ＄６０ %TEXT_LJ("체력小회복＆에올군", 33)%
PRINTBUTTON "[해설]", "과일샐러드"
PRINTL 
SIF GETBIT(FLAG:41, 10)
	CALL PRINTL_COLORTEXT, "   숲에서 얻는 너트를 충분하게 사용한 샐러드", DEF_COLOR("황색")
PRICE:10 = 60

PRINTFORM  [11] %"훈제연어", 24, LEFT% ＄７５ %TEXT_LJ("체력小회복＆고양이좋아함", 33)%
PRINTBUTTON "[해설]", "훈제연어"
PRINTL 
SIF GETBIT(FLAG:41, 11)
	CALL PRINTL_COLORTEXT, "   연어의 생선 토막을 훈제로 한 요리. 그대로도 맛좋지만, 식품 재료로서도 가치가 높다", DEF_COLOR("황색")
PRICE:11 = 75

;お菓子やデザート
PRINTFORM  [20] %"허니비스킷", 24, LEFT% ＄１０ %TEXT_LJ("체력小회복＆호감도小상승", 33)%
PRINTBUTTON "[해설]", "허니비스킷"
PRINTL 
SIF GETBIT(FLAG:41, 20)
	CALL PRINTL_COLORTEXT, "   벌꿀을 발라 딱딱하게 구운 구운 과자.손가벼운 간식으로서 넓고인기(이)가 있다", DEF_COLOR("황색")
PRICE:20 = 10

PRINTFORM  [21] %"베이크드애플", 24, LEFT% ＄３０ %TEXT_LJ("체력小회복＆호감도小상승", 33)%
PRINTBUTTON "[해설]", "베이크드애플"
PRINTL 
SIF GETBIT(FLAG:41, 21)
	CALL PRINTL_COLORTEXT, " 시나몬등을 채웠다린고를 양주로 향기 내기 해 오븐으로 구운 과자", DEF_COLOR("황색")
PRICE:21 = 30

;お酒や飲み物
PRINTFORM  [30] %"벌꿀술", 24, LEFT% ＄６０ %TEXT_LJ("드렁크(술주정率：並)", 33)%
PRINTBUTTON "[해설]", "벌꿀술"
PRINTL 
SIF GETBIT(FLAG:41, 30)
	CALL PRINTL_COLORTEXT, "   벌꿀을 발효 시켜 만든 밀술. 전국적으로 자주(잘) 마셔지는 일반적인 술", DEF_COLOR("황색")
PRICE:30 = 60
;칼피스 사워의 패러디로 보인다
PRINTFORM  [31] %"파두르사워", 24, LEFT% ＄４５ %TEXT_LJ("드렁크(술주정率：高)", 33)%
PRINTBUTTON "[해설]", "파두르사워"
PRINTL 
SIF GETBIT(FLAG:41, 31)
	CALL PRINTL_COLORTEXT, " 파두르보리를 발효 시켜 만든 맥주. 서민에게인기(이)가 있다", DEF_COLOR("황색")
PRICE:31 = 45

PRINTFORM  [32] %"개다래와인", 24, LEFT% ＄７０ %TEXT_LJ("드렁크(술주정率：並)＆고양이좋아함", 33)%
PRINTBUTTON "[해설]", "개다래와인"
PRINTL 
SIF GETBIT(FLAG:41, 32)
	CALL PRINTL_COLORTEXT, " 마타타비의 열매로부터 만든 과실주. 자양 강장에 효과가 있어, 고양이의 좋아하는 것이기도 하다", DEF_COLOR("황색")
PRICE:32 = 70

IF ITEM:문드롭스
	PRINTFORM  [50] %"문드롭스", 24, LEFT% 
ELSE
	SETCOLOR DEF_COLOR("회색")
	PRINTPLAINFORM  [×] %"문드롭스", 24, LEFT% 
ENDIF
PRINT 나머지
SIF ITEM:문드롭스 < 10
	PRINT  
PRINTFORM {ITEM:문드롭스} %TEXT_LJ("체력中회복＆호감도大상승", 33)%
PRINTBUTTON "[해설]", "문드롭스"
PRINTL 
SIF GETBIT(FLAG:41, 50)
	CALL PRINTL_COLORTEXT, " 몽마(이)가 좋아하는 이상한 캔디.몽마(이)가 아닌 사람은 빠는 것이 할 수 없다", DEF_COLOR("황색")

RESETCOLOR

PRINTFORM [100] 돌아온다      
PRINTBUTTON "[해설ＡＬＬON]", "해설ＡＬＬON"
PRINT 　
PRINTBUTTON "[해설ＡＬＬOFF]", "해설ＡＬＬOFF"
PRINTL 

$INPUT_LOOP

INPUTS

SELECTCASE RESULTS
CASE "해설ＡＬＬON"
	FLAG:41 = SETBITS(FLAG:41, 0, 63)
CASE "해설ＡＬＬOFF"
	FLAG:41 = 0
;入力する人は居ないだろうが念のため
CASE "×"

CASE "시프리샌드"
	INVERTBIT FLAG:41, 0
CASE "마운틴스테이크"
	INVERTBIT FLAG:41, 1

CASE "과일샐러드"
	INVERTBIT FLAG:41, 10
CASE "훈제연어"
	INVERTBIT FLAG:41, 11

CASE "허니비스킷"
	INVERTBIT FLAG:41, 20
CASE "베이크드애플"
	INVERTBIT FLAG:41, 21

CASE "벌꿀술"
	INVERTBIT FLAG:41, 30
CASE "파두르사워"
	INVERTBIT FLAG:41, 31
CASE "개다래와인"
	INVERTBIT FLAG:41, 32

CASE "문드롭스"
	INVERTBIT FLAG:41, 50

CASEELSE
	SELECTCASE TOINT(RESULTS)
	CASE  100
		REDRAW 1
		RETURN 0
	CASE 0 TO 1, 10 TO 11, 20 TO 21, 30 TO 32
		IF MONEY >= PRICE:TOINT(RESULTS)
			MONEY -= PRICE:TOINT(RESULTS)
		ELSE
			PRINTFORML 돈이발선
			GOTO INPUT_LOOP
		ENDIF
	CASE 50
		IF ITEM:문드롭스
			ITEM:문드롭스 -= 1
		ELSE
			PRINTFORML 문드롭스를 가지고 있지 않습니다
			GOTO INPUT_LOOP
		ENDIF
	CASEELSE
		GOTO INPUT_LOOP
	ENDSELECT

	;食事フラグＯＮ
	SETBIT FLAG:40, TOINT(RESULTS)
	CALL EAT_LUNCH_2
	;一言이벤트の発言フラグリセット
	FLAG:23 = 0
	REDRAW 1
	RETURN 0
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART


;食べたものはFLAG:40で記録されている
@EAT_LUNCH_2
#DIM LCOUNT
;0は체력％、1は정력％、2は호감도、3は술주정率
#DIM DYNAMIC LUNCH_BONUS, 5
#DIM MOON_DROP
#DIM CAT_FAVOR

MOON_DROP = 0
CAT_FAVOR = 0

;食事等による체력회복
SELECTCASE NAMES("食事", FLAG:40)
CASE "시프리샌드"
	LUNCH_BONUS:0 = 25
	LUNCH_BONUS:1 = 25
CASE "마운틴스테이크"
	LUNCH_BONUS:0 = 50

CASE "과일샐러드"
	LUNCH_BONUS:0 = 25
CASE "훈제연어"
	LUNCH_BONUS:0 = 25
	CAT_FAVOR = 50

CASE "허니비스킷"
	LUNCH_BONUS:0 = 25
	LUNCH_BONUS:2 = 50
CASE "베이크드애플"
	LUNCH_BONUS:0 = 25
	LUNCH_BONUS:2 = 50

CASE "벌꿀술", "MEAD", "ミョッズ"
	LUNCH_BONUS:3 = 25
CASE "파두르사워"
	LUNCH_BONUS:3 = 50
CASE "개다래와인"
	LUNCH_BONUS:3 = 25
	CAT_FAVOR = 100

CASE "문드롭스"
	LUNCH_BONUS:0 = 50
	;これはムーンドロップ専用処理
	;LUNCH_BONUS:2 = 100
	MOON_DROP = 100
CASEELSE
	RETURN 0
ENDSELECT

;食器で회복量UP
IF ITEM:미스릴스푼
	TIMES LUNCH_BONUS:0, 1.60
	TIMES LUNCH_BONUS:1, 1.60
ELSEIF ITEM:은스푼
	TIMES LUNCH_BONUS:0, 1.40
	TIMES LUNCH_BONUS:1, 1.40
ELSEIF ITEM:도자기스푼
	TIMES LUNCH_BONUS:0, 1.20
	TIMES LUNCH_BONUS:1, 1.20
ENDIF

FOR LCOUNT, 1, CHARANUM
	;체력회복
	CALL CALC, "체력회복상한무시", MAXBASE:LCOUNT:체력 * LUNCH_BONUS:0 / 100, LCOUNT

	;정력회복
	CALL CALC, "정력회복", MAXBASE:LCOUNT:정력 * LUNCH_BONUS:1 / 100, LCOUNT

	;酔っ払い
	SIF LUNCH_BONUS:3
		CALL ADD_STATE, LCOUNT, "드렁크"

	;MASTERは호감도関係なし
	SIF LCOUNT == MASTER
		CONTINUE

	;猫専用メニュー
	SIF CAT_FAVOR && BASE:LCOUNT:종족 == 6
		CFLAG:LCOUNT:2 += CAT_FAVOR

	;문드롭스
	SIF MOON_DROP && COND("몽마", LCOUNT)
		CFLAG:LCOUNT:2 += MOON_DROP

	;호감도ＵＰ
	CFLAG:LCOUNT:2 += LUNCH_BONUS:2
NEXT

IF MOON_DROP
	PRINTFORMW 몽마들에게문드롭스를 행동했다！
ELSEIF LUNCH_BONUS:3
	PRINTFORML 모두가%NAMES("食事", FLAG:40)%를 즐겼다！
	CALL PRINT_STR, "핑크_※드렁크状態だと조교が무드が高い상태で개시されます※_W"
ELSE
	PRINTFORMW 모두가%NAMES("食事", FLAG:40)%에 입맛을 다셨다！
ENDIF

LOCALS = 
SIF LUNCH_BONUS:0
	LOCALS = %LOCALS%\@ LOCALS != "" ? ＆ # \@체력{LUNCH_BONUS:0}％회복！
SIF LUNCH_BONUS:1
	LOCALS = %LOCALS%\@ LOCALS != "" ? ＆ # \@정력{LUNCH_BONUS:1}％회복！

IF LUNCH_BONUS:2
	LOCALS = %LOCALS%\@ LOCALS != "" ? ＆ # \@호감도{LUNCH_BONUS:2}ＵＰ！
ELSEIF MOON_DROP
	LOCALS = %LOCALS%\@ LOCALS != "" ? ＆ # \@몽마の호감도{MOON_DROP}ＵＰ！
ELSEIF CAT_FAVOR
	LOCALS = %LOCALS%\@ LOCALS != "" ? ＆ # \@猫の호감도{CAT_FAVOR}ＵＰ！
ENDIF

SIF LOCALS != ""
	PRINTFORMW %LOCALS%


;お酒飲んだら술주정判定
IF LUNCH_BONUS:3
	LOCALS = 
	FOR LCOUNT, 1, CHARANUM
		;술주정判定(약독내성持ちは술주정しづらい)
		IF RAND:100 * (1 + TALENT:LCOUNT:약독내성) < LUNCH_BONUS:3
			CALL ADD_STATE, LCOUNT, "술주정"
			LOCALS = %LOCALS%\@ LOCALS != "" ? と # …… \@%CALLNAME:LCOUNT%
		ENDIF
	NEXT
	SIF LOCALS != ""
		PRINTFORMW %LOCALS%하,술주정해 버렸다…
ENDIF

