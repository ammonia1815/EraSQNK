;-------------------------------------------------
;キャラを羅列する表示はここで一括管理. 種類はARGSで区別する. 
;LOCAL:LCOUNTでLOCAL番のキャラが条件CLEARかどうかを管理する. 
;-------------------------------------------------
@SHOW_CHARA_LIST, ARGS, ARG, ARG:1
#DIM LCOUNT

IF ARGS != "コス확인" && CONFIG("세대別표시")
	IF ARG:1 == 0
		PRINTL [2001]아이의 표시(1세대)
	ELSEIF ARG:1 == 1
		PRINTL [2000]신부의 표시
		PRINTL [2001]다음 세대 아이의 표시	
	ELSEIF ARG:1 > 1
		PRINTL [2000]신부의 표시
		PRINTL [2001]다음 세대 아이의 표시
		PRINTL [2002]전 세대 아이의 표시
	ENDIF
ENDIF

VARSET LOCAL
FOR LCOUNT, 1, CHARANUM
	IF ARGS != "コス확인" && CONFIG("세대別표시")
		IF ARG:1 == 0
			SIF BASE:LCOUNT:세대
				CONTINUE
		ELSE
			SIF BASE:LCOUNT:세대 != ARG:1
				CONTINUE
		ENDIF
	ENDIF
	SELECTCASE ARGS
	CASE "조수변경"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "조교대상변경"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "매각"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "コス확인"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "탐색"
		SIF LCOUNT > MASTER && COND("조수가능", LCOUNT) == 0
			CONTINUE
	CASE "마음에듬"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "다방"
		SIF COND("バイト가능", LCOUNT) == 0
			CONTINUE
	CASE "애널라이즈"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "하룻밤개방허가"
		SIF COND("하룻밤개방가능", LCOUNT) == 0
			CONTINUE
	ENDSELECT
	LOCAL:LCOUNT = 1
NEXT

Z = 0
DRAWLINE
FOR LCOUNT, 1, CHARANUM
	SIF LOCAL:LCOUNT == 0
		CONTINUE
	IF LCOUNT > MASTER
		Z += 1
		SIF (Z-1 < CONFIG("표시人数")*ARG || Z > CONFIG("표시人数")*(ARG+1)) && CONFIG("표시人数")
			CONTINUE
	ENDIF

	SELECTCASE ARGS
	CASE "능력표시"
		IF LCOUNT == MASTER
			IF CONFIG("능력표시で３사이즈閲覧") == 0 || TALENT:MASTER:남자
				PRINTFORM  [{MASTER, 2}]
				IF STATE("술주정", MASTER)
					SETCOLOR DEF_COLOR("파랑")
				ELSEIF STATE("드렁크", MASTER)
					SETCOLOR DEF_COLOR("하트핑크")
				ENDIF
				PRINTFORM %TEXT_LJ(NAME:MASTER, 16)%
				RESETCOLOR
			ELSE
				PRINTFORM %BL(22)%
			ENDIF
			IF FLAG:31
				PRINTFORM  [999]클리어이력 
			ELSE
				PRINTFORM  %BL(16)%
			ENDIF
			IF CONFIG("３사이즈：표시")
				PRINTFORM [200]\@ CONFIG("능력표시で３사이즈閲覧") ? 상태 표시 # 3size표시 \@ %BL(12)%
			ELSE
				PRINTFORM %BL(28)%
			ENDIF
			SIF CONFIG("능력표시で３사이즈閲覧") == 0 && TALENT:MASTER:남자 == 0
				CALL PRINT_N, LCOUNT
			PRINTL 
			IF CONFIG("능력표시で３사이즈閲覧") == 0 || TALENT:MASTER:남자
				DRAWLINE
				CONTINUE
			ENDIF
		ENDIF
		IF CONFIG("능력표시で３사이즈閲覧")
			CALL PRINT_NUM_NAME, "３사이즈閲覧", LCOUNT
		ELSE
			CALL PRINT_NUM_NAME, "능력표시", LCOUNT
		ENDIF
	CASE "조교대상변경"
		IF CONFIG("조교대상변경で애널라이즈")
			CALL PRINT_NUM_NAME, "애널라이즈", LCOUNT
		ELSE
			CALL PRINT_NUM_NAME, "조교대상변경", LCOUNT
		ENDIF
	CASE "마음에듬", "名前변경"
		CALL PRINT_NUM_NAME, "체력", LCOUNT
	CASEELSE
		CALL PRINT_NUM_NAME, ARGS, LCOUNT
	ENDSELECT
NEXT

;선택지の表示
IF CONFIG("표시人数") && Z > CONFIG("표시人数")
	IF ARG
		PRINTFORM [{1000 + ARG}]이전 page로 
	ELSE
		PRINTPLAINFORM [----]이전 page로 
	ENDIF
ENDIF

SIF ARGS != "コス확인" || (CONFIG("표시人数") && Z > CONFIG("표시人数"))
	PRINTFORM  [100]돌아간다

SELECTCASE ARGS
CASE "다방"
	SIF CHARA_NUM("バイト가능") >= 2
		PRINTFORM  [200]전 캐릭터 일괄 지시
CASE "하룻밤개방허가"
	SIF CHARA_NUM("하룻밤개방가능") >= 2
		PRINTFORM  [200]전 캐릭터 일괄 지시
CASE "능력표시"
	SIF CHARA_NUM("조수가능") && CONFIG("조교상대변경불가") == 0
		PRINTFORM  [109]조수변경
	SIF CHARA_NUM("控えキャラ") && CONFIG("조교상대변경불가") == 0
		PRINTFORM  [110]조교 대상 변경
CASE "조수변경"
	IF CONFIG("탐색동행자선택中") == 0
		PRINTFORM  [101]능력표시
		SIF CHARA_NUM("控えキャラ") && CONFIG("조교상대변경불가") == 0
			PRINTFORM  [110]조교대상변경
	ENDIF
CASE "조교대상변경"
	PRINTFORM  [101]능력표시
	SIF CHARA_NUM("조수가능") && CONFIG("조교상대변경불가") == 0
		PRINTFORM  [109]%TEXTS("조수の명칭")% 변경
ENDSELECT

SIF CONFIG("표시人数") && Z > CONFIG("표시人数") * (ARG + 1)
	PRINTFORM  [{1000 + ARG + 2}]다음 page

PRINTL 

;全ページ数
IF CONFIG("표시人数")
	Q = (Z / CONFIG("표시人数")) + 1
ELSE
	Q = 1
ENDIF

@SHOW_CHARA_LIST_PRE, ARGS
PRINTL 
SELECTCASE ARGS
CASE "다방", "탐색", "유두선택"
CASEELSE
	SIF TARGET || (ASSI && TARGET <= 0)
		PRINTFORM 현재
	SIF TARGET
		PRINTPLAINFORM [{TARGET}]%NAME:TARGET%의 상대를 하고있고, 
	IF ASSI
		PRINTPLAINFORM [{ASSI}]%조수가("0")% %TEXTS("조수の명칭")%입니다
	ELSEIF TARGET
		PRINTFORM %조사선택(TEXTS("조수の명칭"), "는")% 임명하지 않았습니다.
	ENDIF
	SIF ARGS == "매각"
		PRINTFORM  (소지금:%MONEY_C(MONEY)%)
	PRINTL 
ENDSELECT

SELECTCASE ARGS
CASE "능력표시"
	PRINTL 누구의 능력을 봅니까? 
	IF CHARA_NUM("드렁크")
		PRINT 덧붙여서
		CALL PRINT_COLORTEXT, "색이 파란 캐릭터는 술주정中", DEF_COLOR("파랑")
		PRINT , 
		CALL PRINT_COLORTEXT, "핑크의 캐릭터는 드렁크中", DEF_COLOR("하트핑크")
		PRINTL 입니다
	ENDIF
	PRINTL 능력 표시 화면의 각 항목(□ 소질 □같은)은 클릭해서 열고 닫을 수 있습니다
CASE "조수변경"
	IF CONFIG("조수への지시")
		PRINTL []의 수 만큼 지시를 할 수 있습니다. 
		PRINTL 누구의 지시를 변경합니까？
	ELSE
		PRINTFORML 누구를 %TEXTS("조수の명칭")%로 합니까？（선택 끝난 캐릭터를 선택하면 %TEXTS("조수の명칭")%에서 해임합니다）
		IF CONFIG("조수の탐색능력표시")
			PRINTFORML %TEXTS("조수の명칭")%의 능력(재보 발견 등)은 클릭해서 능력의 설명을 볼 수 있습니다.
		ELSE
			PRINTFORML %TEXTS("조수の명칭")%의 능력(조제 등)는 클릭 하는 것으로 효과의 해설을 볼 수가 있습니다
			PRINTFORML （습득하고 있는 아르카나를 확인하려면 마법사 or 아르카나 를 클릭 해 주세요）
		ENDIF
	ENDIF
	DRAWLINE
	LOCALS = %TEXTS("조수の명칭")%없음
	PRINTFORM  [ 0]%TEXT_LJ(LOCALS, 16)% 
	IF CONFIG("탐색동행자선택中") == 0
		IF CHARA_NUM("지시가능な조수")
			PRINTFORM [200]\@ CONFIG("조수への지시") ? 변경 화면 # 지시 화면 \@ 
		ELSE
			CLEARBIT FLAG:45, 0
		ENDIF
		PRINTFORM [300]\@ CONFIG("조수の탐색능력표시") ? 조교시의 능력 # 탐색시의 능력 \@ 
		PRINTFORM [400]%TEXTS("조수の명칭")%의 명칭변경
	ENDIF
	PRINTL 
CASE "조교대상변경"
	IF CONFIG("조교대상변경で애널라이즈")
		PRINTL 누구를 조교합니까？（강함은 100점 만점으로, 50점이라면 주인과 호각입니다）
		DRAWLINE
		PRINTFORM  [200]통상정보를표시
		PRINTPLAINFORM  욕구불만  호감도   방치  민감도  공격력 주인기준강함
	ELSE
		PRINTL 누구를 조교합니까？（☆마크는 마음에듬의 표시입니다）
		DRAWLINE
		PRINTFORM  [200]욕구불만도,호감도,방치기간,구체적인 강함…등을 표시한다
	ENDIF
	PRINTL 
CASE "매각"
	PRINTL 누구를 팝니까? 
	PRINTL 계산식은 능력×변동하지 않는 소질×변동하는 소질＝평가pt→가격으로 되어있습니다
CASE "탐색"
	PRINTFORML 주인또는 %TEXTS("조수の명칭")%만 탐색할 수 있습니다. 누가 갑니까？
	IF ASSI
		PRINTPLAINFORM （[ 1]%마스터를()% 선택하면 
		SETCOLOR DEF_COLOR("노랑")
		PRINTPLAINFORM %TEXTS("조수の명칭")%[{ASSI, 2}]%NAME:ASSI%
		RESETCOLOR
		PRINTL 도 동행합니다）
		PRINTFORML %TEXTS("조수の명칭")%의 능력(재보 발견등)은 클릭으로 능력설명을 볼 수 있습니다.
	ENDIF
	;食器で회복量UP
	IF ITEM:미스릴찻잔
		PRINTFORML 미스릴찻잔을 소지하고 있으므로, 전투종료시 휴식효과가 6Ｐ 높아집니다. 
	ELSEIF ITEM:미스릴스푼
		PRINTFORML 미스릴스푼을 소지하고 있으므로,전투종료시 휴식효과가 5Ｐ 높아집니다. 
	ELSEIF ITEM:은찻잔
		PRINTFORML 은찻잔을 소지하고 있으므로, 전투종료시 휴식효과가 4Ｐ 높아집니다. 
	ELSEIF ITEM:은스푼
		PRINTFORML 은스푼을 소지하고 있으므로, 전투종료시 휴식효과가 3Ｐ 높아집니다. 
	ELSEIF ITEM:도자기찻잔
		PRINTFORML 도자기찻잔을 소지하고 있으므로, 전투종료시 휴식효과가 2Ｐ 높아집니다. 
	ELSEIF ITEM:도자기스푼
		PRINTFORML 도자기스푼을 소지하고 있으므로, 전투종료시 휴식효과가 1Ｐ 높아집니다. 
	ENDIF
CASE "마음에듬"
	PRINTFORML 누구를 마음에 드는 인물로 설정합니까? 
	PRINTFORML ※한번 설정을 변경하면, 일주일간 변경할 수 없습니다※
	PRINTFORML (마음에 드는 인물은 ☆로 기록됩니다. 그 상대로부터 쾌락을 더 많이 받을 수 있습니다…)
	PRINTFORML 또한, [ 0]이나[ 1]을 입력해서, %CALLNAME:MASTER%의 성감대나 성벽을 설정하는 것도 가능합니다
	PRINTFORML (취향에 직격인 상대에게는, 받을 수 있는 쾌락이 더 많아집니다)
	PRINTFORML 
	PRINTFORM  [ 0] 성감대를 설정한다 현재의 성감대:
	SELECTCASE NUM_BIT(TALENT:MASTER:성감대)
	CASE NUM("성감대")
		PRINTFORM [모두약함]
	CASE IS >= 4
		PRINTFORM 잔뜩
	CASE 0
		PRINTFORM 없음
	CASEELSE
		FOR LOCAL, 0, NUM("성감대")
			SIF GETBIT(TALENT:MASTER:성감대, LOCAL)
				PRINTFORM %NAMES("성감대", LOCAL)%
		NEXT
	ENDSELECT
	PRINTL 

	PRINTFORM  [ 1] 성벽을 설정한다 현재의 성벽:
	SELECTCASE NUM_BIT(TALENT:MASTER:성벽)
	CASE IS >= 6
		PRINTFORM 잔뜩
	CASE 0
		PRINTFORM 없음
	CASEELSE
		FOR LOCAL, 0, NUM("속성")
			SIF GETBIT(TALENT:MASTER:성벽, LOCAL)
				PRINTFORM %NAMES("속성", LOCAL)%
		NEXT
	ENDSELECT
	PRINTL 
CASE "名前변경"
	PRINTL 누구의 이름을 변경합니까? 
CASE "다방"
	PRINTL ～점원명단～
	PRINTL 근무태도를 지시합니까? 
CASE "애널라이즈"
	PRINTL 누구를 애널라이즈 합니까? 
CASE "하룻밤개방허가"
	PRINTL 누구의 외출을 허가합니까? 
CASE "유두선택"
	PRINTFORML 비대, 함몰, 유륜 아래 열의 문자 클릭으로,
	PRINTFORML 유두비대,유두의 함몰 상태, 유륜의 크기를 변화시킬 수가 있습니다.
	PRINTFORML １클릭으로 １LV이 변화하므로 신중히 조정해 주세요.
	DRAWLINE
	PRINTFORML             비대 함몰 유륜
ENDSELECT

;-------------------------------------------------
;스테이터스 표시（P의 초기치는 0）
;-------------------------------------------------
@SHOW_CHARADATA, P, M
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "능력표시"
CALL SHOW_CHARA_LIST, "능력표시", P, M

$INPUT_LOOP
INPUT
IF RESULT == 100
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 109 && CHARA_NUM("조수가능") && CONFIG("조교상대변경불가") == 0
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	CALL SELECT_ASSI, 0, 0
	RETURN 0
ELSEIF RESULT == 110 && CHARA_NUM("控えキャラ") && CONFIG("조교상대변경불가") == 0
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	CALL CHANGE_TARGET, 0, 0
	RETURN 0
ELSEIF RESULT == 200 && CONFIG("３사이즈：표시")
	INVERTBIT FLAG:46, 0
	RESTART
;CLEAR캐릭터표시（잠정）
ELSEIF RESULT == 999 && FLAG:31
	CALL SHOW_CHARA_CAPTURED
	RESTART
ELSEIF RESULT == 2000 && CONFIG("세대別표시")
	M = 0
	P = 0
	RESTART
ELSEIF RESULT == 2001 && CONFIG("세대別표시")
	M += 1
	P = 0
	RESTART
ELSEIF RESULT == 2002 && CONFIG("세대別표시")
	M -= 1
	P = 0
	RESTART
ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("표시人数") && Q >= 2 && P != RESULT - 1001
	P = RESULT - 1001
	RESTART
ELSEIF RESULT < MASTER || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF

CALL NEW_PRINT_STATE, RESULT, "通常"

RESTART

;-------------------------------------------------
;조수선택
;-------------------------------------------------
@SELECT_ASSI, P, M
#DIM LCOUNT
#DIM CHOICE
#DIMS CHOICES, 2
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
VARSET CHOICES

REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "조수변경"
CALL SHOW_CHARA_LIST, "조수변경", P, M

$INPUT_LOOP
INPUTS

SPLIT RESULTS, "/", CHOICES

SELECTCASE CHOICES
CASE "상성:☆"
	IF TARGET
		PRINTFORML %조사처리(CALLNAME:TARGET,"와")% 궁합이 최고로 좋습니다！
		PRINTFORMW (커맨드의 실행치에 30의 보너스가 붙습니다)
	ENDIF
CASE "상성:◎"
	IF TARGET
		PRINTFORML %조사처리(CALLNAME:TARGET,"와")% 궁합이 훌륭합니다！
		PRINTFORMW (커맨드의 실행치에 15의 보너스가 붙습니다)
	ENDIF
CASE "상성:〇"
	IF TARGET
		PRINTFORML %조사처리(CALLNAME:TARGET,"와")% 궁합이 좋습니다
		PRINTFORMW (커맨드의 실행치에 ５의 보너스가 붙습니다)
	ENDIF
CASE "바늘"
	PRINTFORMW 바늘커맨드의 효과가 오릅니다
CASE "調合"
	PRINTFORML 미약커맨드의 효과가 뚜렷하게 오릅니다
	PRINTFORMW 또한, 음마의 물방울를 조제해 여러가지 약물을 만들 수 있습니다
CASE "새드"
	PRINTFORMW 일반적으로 말하는 ＳＭ에 있어 Ｓ커맨드와 행위을 실행하는 것에 자신있습니다
CASE "요호"
	PRINTFORMW 조교종료시에 미약를 1개 받을 수 있습니다
CASE "슬라임"
	PRINTFORMW 조교종료시에 로션를 1개 받을 수 있습니다
CASE "드라이어드"
	PRINTFORMW 조교종료시에 재스민를 1개 받을 수 있습니다
CASE "치료"
	PRINTFORMW 시간경과마다 조교대상의 체력이 500만큼 회복합니다
CASE "고무"
	PRINTFORML %조사선택(TEXTS("조수の명칭"),"이")%, 힘내라 힘내라 하는 것으로 옆에서 응원해 줍니다
	PRINTFORMW (커맨드의 실행치에 10의 보너스가 붙습니다)
CASE "후타나리"
	PRINTFORMW 후타나리입니다
CASE "♂"
	PRINTFORMW 남자입니다
CASE "금단의지식"
	PRINTFORML 가게에서 위험한 상품을 살 수 있게 됩니다
	PRINTFORMW 또,조합지식과 같이 미약의 효과가 강해집니다
CASE "구현"
	PRINTFORMW 여성에게 페니스를 기를 수 있습니다
CASE "大마법사"
	PRINTFORMW 모든 아르카나를 사용할 수가 있습니다. 마법사로서는 최강이라고 쳐도 됩니다.
CASE "마법사"
	PRINTFORML 마도서를 읽는 것으로 아르카나를 기억할 수가 있습니다. 또, 소비정력도 적습니다
	PRINTFORML -%조사처리(CALLNAME:TOINT(CHOICES:1),"이")% 기억하고 있는 아르카나의 목록-
	FOR LCOUNT, 230, 260
		SIF TALENT:TOINT(CHOICES:1):LCOUNT
			PRINTFORML %TEXT_LJ(TALENT_NAME(LCOUNT, TALENT:TOINT(CHOICES:1):LCOUNT, TOINT(CHOICES:1)), 20)% %TEXTS("아르카나解説", LCOUNT)%
	NEXT
	WAIT
CASE "아르카나"
	PRINTFORML 전문가가 아니지만, 아르카나를 사용할 수가 있습니다
	PRINTFORML -%조사처리(CALLNAME:TOINT(CHOICES:1),"이")% 기억하고 있는 아르카나의 목록-
	FOR LCOUNT, 230, 260
		SIF TALENT:TOINT(CHOICES:1):LCOUNT
			PRINTFORML %TEXT_LJ(TALENT_NAME(LCOUNT, TALENT:TOINT(CHOICES:1):LCOUNT, TOINT(CHOICES:1)), 20)% %TEXTS("아르카나解説", LCOUNT)%
	NEXT
	WAIT
CASE "애무・봉사지식"
	PRINTFORML 봉사와 애무로 주는 쾌감이 5할 증가하고, 두려움과 반감 등의 파라미터를 반감시킵니다
	PRINTFORMW 다만, %TEXTS("조수の명칭")%자신이 상대를 조교하지 않으면 의미가 없습니다
CASE "教え上手"
	PRINTFORML 봉사될 때에 상대보다 기교LV등이 높으면 상대가 획득하는 경험을 각+１시킵니다
	PRINTFORML 또, 조교대상의 획득하는 봉사쾌락경험을 1.5배로 합니다
	PRINTFORMW 다만, %TEXTS("조수の명칭")%자신이 직접 상대를 조교하지 않으면 의미가 없습니다
CASE "ＳＭ지식"
	PRINTFORML 스팽킹등,ＳＭ플레이에 속하는 커맨드의 실행시에만 효과가 있는 지식입니다
	PRINTFORML 욕정과 굴복의 파라미터가 ５할 증가하고, 공포와 반감을 절반으로 합니다
	PRINTFORML 또, 상대의 획득하는 가학쾌락경험과 고통쾌락경험을 1.5배로 합니다
	PRINTFORMW 다만, %TEXTS("조수の명칭")%자신이 직접 상대를 조교하지 않으면 의미가 없습니다
CASE "Ａ조교지식"
	PRINTFORML 애널을 희롱하는 커맨드를 실행시에 Ａ쾌락과 욕정의 파라미터를 ５할 증가하고,
	PRINTFORML 고통과 반감등의 마이너스파라미터의 발생을 반감시킵니다
	PRINTFORMW 다만, %TEXTS("조수の명칭")%자신이 직접 상대를 조교하지 않으면 의미가 없습니다
CASE "성기확장지식"
	PRINTFORML 성기를 확장시킬 때의 고통과 공포를 반감시키고, 확장 경험의 증가량에 보너스를 얻습니다
	PRINTFORMW 다만, %TEXTS("조수の명칭")%자신이 직접 상대를 조교하지 않으면 의미가 없습니다
CASE "도구使い"
	PRINTFORML 바이브등 도구를 이용하는 커맨드실행시에 쾌락과 욕정의 파라미터를 ５할 증가하고,
	PRINTFORML 공포와 반감등의 마이너스파라미터의 발생을 반감시킵니다
	PRINTFORMW 다만, %TEXTS("조수の명칭")%자신이 직접 상대를 조교하지 않으면 의미가 없습니다
CASE "노출지식"
	PRINTFORML 조교대상의 치정 파라미터를 항상 ５할증가하고, 노출경험과 수치쾌락경험의 획득량을 늘립니다
	PRINTFORMW 이 지식은,%조사처리(TEXTS("조수の명칭"),"이")% 가지고 있는 것으로도 효과를 발휘합니다
CASE "달콤한말"
	PRINTFORMW 달콤한말을 속삭일 수 있습니다
CASE "손대중"
	PRINTFORMW 손대중을 실시할 수 있습니다
CASE "歌唱"
	PRINTFORMW 여러가지 효과의 노래를 연주할 수 있습니다

;탐색での能力
CASE "遭遇回避"
	CALL PRINT_STR, "몽마와 조우율이_황색_５％감소_합니다_W"
CASE "遭遇증가"
	CALL PRINT_STR, "몽마와 조우율이_파랑_５％증가_합니다_W"
CASE "罠解除"
	CALL PRINT_STR, "보물 상자를 열 때 함정 발동율이_황색_25%_가 됩니다_W"
CASE "罠誘発"
	CALL PRINT_STR, "보물 상자를 열 때 함정 발동율이_파랑_2.5배_이 됩니다_W"
CASE "財宝発見"
	CALL PRINT_STR, "보물상자를 찾을 확률이_황색_５％증가_합니다_W"
CASE "財宝見逃し"
	CALL PRINT_STR, "보물상자를 찾을 확률이_파랑_５％감소_합니다_W"
CASE "階段発見"
	CALL PRINT_STR, "다음층의 문을 찾을 확률이_황색_５％상승_합니다_W"
CASE "회복"
	CALL PRINT_STR, "각 행동마다 정력이_황색_１Ｐ회복_하고、전투종료시_황색_２Ｐ회복_합니다_W"
CASE "戦闘補助"
	CALL PRINT_STR, "수치만큼 탐색자의 전투력이 증가합니다_W"
CASE "상태異常無効"
	CALL PRINT_STR, "보물상자 함정의 상태 이상을 해제합니다_W"

;それ以外は数値入力へ変換
CASEELSE
	RESULT = TOINT(CHOICES)
	IF RESULT == 100
		REDRAW 1
		RETURN 0
	ELSEIF RESULT == 101 && CONFIG("탐색동행자선택中") == 0
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
		;1ページ目を表示
		CALL SHOW_CHARADATA, 0, 0
		RETURN 0
	ELSEIF RESULT == 200 && CHARA_NUM("지시가능な조수") && CONFIG("탐색동행자선택中") == 0
		INVERTBIT FLAG:45, 0
		RESTART
	ELSEIF RESULT == 300 && CONFIG("탐색동행자선택中") == 0
		CLEARBIT FLAG:45, 0
		INVERTBIT FLAG:45, 1
		RESTART
	ELSEIF RESULT == 400
		PRINTFORML 이른바 "조수"의 직함명을 변경합니다.
		PRINTFORML 현재의 직함명은 『%TEXTS("조수の명칭")%』입니다. 무엇으로 변경합니까？
		PRINTFORML  [ 0] 조수
		PRINTFORML  [ 1] 비서
		PRINTFORML  [ 2] 부관
		PRINTFORML  [ 3] 메이드장
		PRINTFORML  [ 4] 측근
		PRINTFORML  [ 5] 참모
		PRINTFORML  [99] 직접 입력한다
		PRINTFORML [100] 변경하지 않는다

		CALL INPUT_SELECT, 6, 99, 100

		CHOICE = RESULT

		SELECTCASE RESULT
		CASE 100
			RESTART
		CASE 0
			LOCALS = 조수の명칭：조수
		CASE 1
			LOCALS = 조수の명칭：비서
		CASE 2
			LOCALS = 조수の명칭：부관
		CASE 3
			LOCALS = 조수の명칭：메이드장
		CASE 4
			LOCALS = 조수の명칭：측근
		CASE 5
			LOCALS = 조수の명칭：참모
		CASE 99
			PRINTFORML 전각８문자(반각16문자)이내로 입력해 주세요
			CALL PRINT_STR, "황색_※추천은 전각 5문자 이내입니다※_L"
			PRINTFORML 아무것도 입력하지 않으면 변경하지 않습니다.

			CALL INPUTS_SELECT, 16

			SELECTCASE RESULTS
			CASE ""
				RESTART
			CASEELSE
				LOCALS = 조수の명칭：%RESULTS%
			ENDSELECT
		ENDSELECT
		;조수の명칭フラグリセット
		CALL DEL_CEVENT_GROUP, "조수の명칭", MASTER
		CALL SET_CEVENT, LOCALS, MASTER

		PRINTFORMW 직함명을 %조사처리("『"+TEXTS("조수の명칭")+"』", "로")% 변경합니다.

		RESTART
	ELSEIF RESULT == 110 && CHARA_NUM("控えキャラ") && CONFIG("조교상대변경불가") == 0 && CONFIG("탐색동행자선택中") == 0
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
		CALL CHANGE_TARGET, 0, 0
		RETURN 0
	ELSEIF RESULT == 2000 && CONFIG("세대別표시")
		M = 0
		P = 0
		RESTART
	ELSEIF RESULT == 2001 && CONFIG("세대別표시")
		M += 1
		P = 0
		RESTART
	ELSEIF RESULT == 2002 && CONFIG("세대別표시")
		M -= 1
		P = 0
		RESTART
	ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("표시人数") && Q >= 2 && P != RESULT - 1001
		P = RESULT - 1001
		RESTART
	ELSEIF RESULT == 0
		ASSI = 0
		REDRAW 1
		RETURN 0
	ELSEIF RESULT < MASTER || RESULT >= CHARANUM
		GOTO INPUT_LOOP
	ELSEIF RESULT == ASSI

	ELSEIF COND("조수가능", RESULT) == 0
		GOTO INPUT_LOOP
	ELSEIF CONFIG("조수への지시") && ABL:RESULT:조수 == 0
		GOTO INPUT_LOOP
	ENDIF

	;指示変更
	IF CONFIG("조수への지시")
		CALL ASSI_CHANGE_INDICATE, RESULT
	;조수変更
	ELSEIF RESULT == ASSI
		ASSI = 0
	ELSE
		ASSI = RESULT
		SIF ASSI == TARGET
			TARGET = 0
	ENDIF
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;-------------------------------------------------
;조교대상선택
;-------------------------------------------------
@CHANGE_TARGET, P, M
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "조교대상변경"
CALL SHOW_CHARA_LIST, "조교대상변경", P, M

$INPUT_LOOP
INPUT
IF RESULT == 100
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 101
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	;1ページ目を表示
	CALL SHOW_CHARADATA, 0, 0
	RETURN 0
ELSEIF RESULT == 109 && CHARA_NUM("조수가능") && CONFIG("조교상대변경불가") == 0
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	CALL SELECT_ASSI, 0, 0
	RETURN 0
ELSEIF RESULT == 200
	INVERTBIT FLAG:47, 0
	RESTART
ELSEIF RESULT == 2000 && CONFIG("세대別표시")
	M = 0
	P = 0
	RESTART
ELSEIF RESULT == 2001 && CONFIG("세대別표시")
	M += 1
	P = 0
	RESTART
ELSEIF RESULT == 2002 && CONFIG("세대別표시")
	M -= 1
	P = 0
	RESTART
ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("표시人数") && Q >= 2 && P != RESULT - 1001
	P = RESULT - 1001
	RESTART
ELSEIF RESULT <= MASTER || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF

SIF RESULT != 0
	TARGET = RESULT
SIF TARGET == ASSI
	ASSI = 0

REDRAW 1
RETURN 1

;CLEARLINE LINECOUNT - MEMO_LINECOUNT
;RESTART

;-------------------------------------------------
;매각の処理
;-------------------------------------------------
@CHARA_SALE, P, M
CALL SHOW_CHARA_LIST_PRE, "매각"
CALL SHOW_CHARA_LIST, "매각", P, M

$INPUT_LOOP
INPUT

IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 2000 && CONFIG("세대別표시")
	M = 0
	P = 0
	RESTART
ELSEIF RESULT == 2001 && CONFIG("세대別표시")
	M += 1
	P = 0
	RESTART
ELSEIF RESULT == 2002 && CONFIG("세대別표시")
	M -= 1
	P = 0
	RESTART
ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("표시人数") && Q >= 2 && P != RESULT - 1001
	P = RESULT - 1001
	RESTART
ELSEIF RESULT <= MASTER || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ELSEIF TALENT:RESULT:서약 || TALENT:RESULT:상사상애
	IF TALENT:RESULT:상사상애
		IF TALENT:RESULT:몽마 || TALENT:RESULT:상위몽마 || TALENT:RESULT:권속
			PRINTFORML 사랑하는 몽마를 판다는 것은 있을 수 없다! 
		ELSE
			PRINTFORML 사랑하는 사람을 판다는 것은 있을 수 없다! 
		ENDIF
	ELSEIF TALENT:RESULT:서약
		PRINTFORML 서약에 의해 %조사처리(NAME:RESULT,"를")% 파는 것은 불가능합니다
	ENDIF
	GOTO INPUT_LOOP
ELSEIF COND("매각가능", RESULT) == 0
	PRINTFORML 매각조건을 만족시키지 못했습니다
	GOTO INPUT_LOOP
ELSE
	LOCAL = TARGET
	LOCAL:1 = RESULT
	TARGET = RESULT

	;内訳の表示
	PRINTFORML ～가격계산의 상세내역～
	CALL PRIZE_CHARA, 2
	PRINTFORML 종합평가：{CFLAG:44}pt
	PRINTFORML  평가액：%MONEY_C(CFLAG:40)%
	PRINTFORML 정말로 [{TARGET}]%조사처리(CALLNAME:TARGET,"를")% 팝니까? 
	PRINTL  [ 0] 판다
	PRINTL [100] 역시 그만둔다
	CALL INPUT_SELECT, 1, 100
	IF RESULT == 100
		TARGET = LOCAL
		RESTART
	ENDIF

	LOCAL:2 = 0
	;목표금액に到達したか
	SIF MONEY < NUM("목표금액")
		LOCAL:2 = 1

	;매각処理・구상表示デバグ
	DRAWLINE
	CALL SELF_KOJO, "매각した"
	DRAWLINE
	PRINTFORMW 매각 결과, %조사처리(CALLNAME:TARGET,"는")% %MONEY_C(CFLAG:40)%에 팔렸습니다. 
	MONEY += CFLAG:40

	TARGET = LOCAL

	CALL DELETE_CHARA, LOCAL:1

	;목표금액に到達したか
	IF MONEY >= NUM("목표금액") && LOCAL:2
		PRINTL 
		PRINTPLAIN ☆ＥＮＤ조건 달성-SHOP 화면[600]을 선택해 ENDING을 봅니다-☆
		PRINTW
	ENDIF
ENDIF

;能力×소질＝＄表示
@PRIZE_CHARA, ARG
;ARG = 0 텍스트表示なし
;ARG = 1 (能力)×(소질) = 表示
;ARG = 2 매각時の表示
;CFLAG:41に能力による価値が代入される
CALL CALC, "매각価格：능력", ARG
;CFLAG:43に獲得or喪失しうる소질による価値の倍率が代入される
CALL CALC, "매각価格：変動소질", ARG
;CFLAG:42に殆ど変動のない소질による価値の倍率が代入される
CALL CALC, "매각価格：基礎", ARG

IF ARG == 1
	SELECTCASE CFLAG:41
	;100万未満
	CASE IS < 1000000
		PRINTFORM  {CFLAG:41, 8}
	;100万이상, 100億未満
	CASE IS < 1000000 * 10000
		PRINTFORM  {CFLAG:41/10000, 6}만
	;100億이상, 100兆未満
	CASE IS < 1000000 * 10000 * 10000
		PRINTFORM  {CFLAG:41/(10000 * 10000), 6}억
	;100兆이상, 100京未満
	CASE IS < 1000000 * 10000 * 10000 * 10000
		PRINTFORM  {CFLAG:41/(10000 * 10000 * 10000), 6}조
	CASEELSE
		PRINTFORM  {CFLAG:41/(10000 * 10000 * 10000 * 10000), 6}경
	ENDSELECT

	PRINTFORM ×{CFLAG:42 / 1000, 2}.{CFLAG:42%1000/100}{CFLAG:42%100/10}
	PRINTFORM ×{CFLAG:43 / 1000, 2}.{CFLAG:43%1000/100}{CFLAG:43%100/10}＝

	SELECTCASE CFLAG:44
	;100万未満
	CASE IS < 1000000
		PRINTFORM {CFLAG:44, 8}
	;100万이상, 100億未満
	CASE IS < 1000000 * 10000
		PRINTFORM {CFLAG:44/10000, 6}만
	;100億이상, 100兆未満
	CASE IS < 1000000 * 10000 * 10000
		PRINTFORM {CFLAG:44/(10000 * 10000), 6}억
	;100兆이상, 100京未満
	CASE IS < 1000000 * 10000 * 10000 * 10000
		PRINTFORM {CFLAG:44/(10000 * 10000 * 10000), 6}존
	CASEELSE
		PRINTFORM {CFLAG:44/(10000 * 10000 * 10000 * 10000), 6}경
	ENDSELECT

	PRINTFORM pt → %MONEY_C(CFLAG:40, MONEY:1)%
ENDIF

;-------------------------------------------------
;코스튬の일람表示
;-------------------------------------------------
@COSPLAY_LIST, P, M
DRAWLINE
PRINTL □ 코스튬일람 □------------------------------------------------------------
CALL SHOW_CHARA_LIST, "コス확인", P, M

IF Z <= CONFIG("표시人数") || CONFIG("표시人数") == 0
	WAIT
	RETURN 0
ENDIF

$INPUT_LOOP
INPUT
IF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("표시人数") && Q >= 2 && P != RESULT - 1001
	P = RESULT - 1001
	RESTART
;100以外でもSHOP画面に戻합니다
ELSE
	RETURN 0
ENDIF


;-------------------------------------------------
;마음에듬の설정をする
;-------------------------------------------------
@SET_CHARA_LOVE, P, M
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE

MEMO_LINECOUNT = LINECOUNT
REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "마음에듬"
CALL SHOW_CHARA_LIST, "마음에듬", P, M

$INPUT_LOOP
INPUT

CHOICE = RESULT

IF CHOICE == 100
	FOR LCOUNT, 1, CHARANUM
		SELECTCASE LCOUNT
		CASE MASTER
			SIF TFLAG:0 == TALENT:MASTER:성감대 && TFLAG:1 == TALENT:MASTER:성벽
				CONTINUE
		CASEELSE
			SIF TFLAG:LCOUNT == TALENT:LCOUNT:마음에듬
				CONTINUE
		ENDSELECT
		PRINTW 설정을 변경했기에, 일주일간 재설정할 수 없게 되었습니다
		FLAG:208 = 7
		BREAK
	NEXT
	REDRAW 1
	RETURN 0
ELSEIF CHOICE == 2000 && CONFIG("세대別표시")
	M = 0
	P = 0
ELSEIF CHOICE == 2001 && CONFIG("세대別표시")
	M += 1
	P = 0
ELSEIF CHOICE == 2002 && CONFIG("세대別표시")
	M -= 1
	P = 0
ELSEIF CHOICE >= 1001 && CHOICE <= 1000+Q && CONFIG("표시人数") && Q >= 2 && P != CHOICE - 1001
	P = CHOICE - 1001
	RESTART
ELSEIF CHOICE == 0
	CALL SETCONFIG, "성감대"
ELSEIF CHOICE == 1
	CALL SETCONFIG, "성벽"
ELSEIF CHOICE < MASTER || CHOICE >= CHARANUM
	GOTO INPUT_LOOP
ELSEIF TALENT:CHOICE:마음에듬
	TALENT:CHOICE:마음에듬 = 0
ELSE
	TALENT:CHOICE:마음에듬 = 1
	PRINTFORMW %조사처리(NAME:CHOICE,"를")% 마음에 든다고 했습니다
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;ステータス表示（Pは初期値0）
@CAFE_LIST, P, M
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE, 2

MEMO_LINECOUNT = LINECOUNT

REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "다방"
CALL SHOW_CHARA_LIST, "다방", P, M

$INPUT_LOOP
INPUT

CHOICE = RESULT

IF CHOICE == 100
	REDRAW 1
	RETURN 0
ELSEIF CHOICE == 200
ELSEIF CHOICE == 2000 && CONFIG("세대別표시")
	M = 0
	P = 0
	RESTART
ELSEIF CHOICE == 2001 && CONFIG("세대別표시")
	M += 1
	P = 0
	RESTART
ELSEIF CHOICE == 2002 && CONFIG("세대別표시")
	M -= 1
	P = 0
	RESTART
ELSEIF CHOICE >= 1001 && CHOICE <= 1000+Q && CONFIG("표시人数") && Q >= 2 && P != CHOICE - 1001
	P = CHOICE - 1001
	RESTART
ELSEIF CHOICE < MASTER || CHOICE >= CHARANUM || COND("バイト가능", CHOICE) == 0
	GOTO INPUT_LOOP
ENDIF

IF CHOICE == 200
	PRINTFORML 전원의 근무 태도（엣치한 행동의 상한등）를 지시해 주세요.
ELSE
	PRINTFORML %NAME:CHOICE%의 근무 태도（엣치한 행동의 상한등）를 지시해 주세요.
ENDIF
PRINTFORML  [ 0] 마음대로 하게 한다
PRINTFORML  [ 1] 펠라치오 정도까지
PRINTFORML  [ 2] 접촉（가슴에 닿거나）정도까지
PRINTFORML  [ 3] 판치라 정도까지
PRINTFORML  [10] 야한 일은 전면 금지
PRINTFORML  [99] 바이트시키지 않는다
PRINTFORML [100] 변경하지 않는다

CALL INPUT_SELECT, 4, 10, 99, 100
CHOICE:1 = RESULT

IF CHOICE:1 < 100
	IF CHOICE == 200
		FOR LCOUNT, 2, CHARANUM
			SIF COND("하룻밤개방가능", LCOUNT)
				CFLAG:LCOUNT:31 = CHOICE:1
		NEXT
	ELSE
		CFLAG:CHOICE:31 = CHOICE:1
	ENDIF
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;-------------------------------------------------
;하룻밤개방허가の설정をする
;-------------------------------------------------
@SET_CHARA_NIGHTWALKER, P, M
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE, 2

MEMO_LINECOUNT = LINECOUNT

REDRAW 0
CALL SHOW_CHARA_LIST_PRE, "하룻밤개방허가"
CALL SHOW_CHARA_LIST, "하룻밤개방허가", P, M

$INPUT_LOOP
INPUT

CHOICE = RESULT

IF CHOICE == 100
	REDRAW 1
	RETURN 0
ELSEIF CHOICE == 200
ELSEIF CHOICE == 2000 && CONFIG("세대別표시")
	M = 0
	P = 0
	RESTART
ELSEIF CHOICE == 2001 && CONFIG("세대別표시")
	M += 1
	P = 0
	RESTART
ELSEIF CHOICE == 2002 && CONFIG("세대別표시")
	M -= 1
	P = 0
	RESTART
ELSEIF CHOICE >= 1001 && CHOICE <= 1000+Q && CONFIG("표시人数") && Q >= 2 && P != CHOICE - 1001
	P = CHOICE - 1001
	RESTART
ELSEIF CHOICE <= MASTER || CHOICE >= CHARANUM || COND("하룻밤개방가능", CHOICE) == 0
	GOTO INPUT_LOOP
ENDIF

PRINTFORML 어떻게 조건을 붙입니까？
PRINTFORML  [ 0] 외출하게 하지 않는다
PRINTFORML  [ 1] 좋아하게 외출하게 한다
PRINTFORML  [ 2] 피임中만 허가한다
PRINTFORML  [ 3] 임신하기 쉬운 날은 외출하게 하지 않는다（안전일or피임中이라면ＯＫ）
PRINTFORML  [ 4] 임신하기 쉬운 날만 허가한다（위험일이나 발정중이라면ＯＫ.피임中은 NO）
CALL INPUT_SELECT, 5
CHOICE:1 = RESULT

IF CHOICE == 200
	FOR LCOUNT, 2, CHARANUM
		SIF COND("하룻밤개방가능", LCOUNT)
			CFLAG:LCOUNT:49 = CHOICE:1
	NEXT
ELSE
	CFLAG:CHOICE:49 = CHOICE:1
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;-------------------------------------------------
;유두선택
;-------------------------------------------------
@RESELECT_ERECT, P, M
#DIM LCOUNT
#DIM CHOICE
#DIMS CHOICES, 2
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
VARSET CHOICES

REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "유두선택"
CALL SHOW_CHARA_LIST, "유두선택", P, M

$INPUT_LOOP
INPUTS

SPLIT RESULTS, "/", CHOICES

CHOICE = TOINT(CHOICES:1)
IF CHOICE >= CHARANUM
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDIF

SELECTCASE CHOICES
CASE "유두비대없음"
		CLEARBIT TALENT:CHOICE:유두, 0
		SETBIT TALENT:CHOICE:유두, 1
CASE "유두비대LV１"
		CLEARBIT TALENT:CHOICE:유두, 1
		SETBIT TALENT:CHOICE:유두, 2
CASE "유두비대LV２"
		CLEARBIT TALENT:CHOICE:유두, 2
		SETBIT TALENT:CHOICE:유두, 3
CASE "유두비대LV３"
		CLEARBIT TALENT:CHOICE:유두, 3
		SETBIT TALENT:CHOICE:유두, 0

CASE "보통유두"
		CLEARBIT TALENT:CHOICE:유두, 10
		SETBIT TALENT:CHOICE:유두, 11
CASE "가성함몰"
		CLEARBIT TALENT:CHOICE:유두, 11
		SETBIT TALENT:CHOICE:유두, 12
CASE "진성함몰"
		CLEARBIT TALENT:CHOICE:유두, 12
		SETBIT TALENT:CHOICE:유두, 10

CASE "거유륜없음"
		CLEARBIT TALENT:CHOICE:유두, 20
		SETBIT TALENT:CHOICE:유두, 21
CASE "거유륜LV１"
		CLEARBIT TALENT:CHOICE:유두, 21
		SETBIT TALENT:CHOICE:유두, 22
CASE "거유륜LV２"
		CLEARBIT TALENT:CHOICE:유두, 22
		SETBIT TALENT:CHOICE:유두, 23
CASE "거유륜LV３"
		CLEARBIT TALENT:CHOICE:유두, 23
		SETBIT TALENT:CHOICE:유두, 20

;それ以外は数値入力へ変換
CASEELSE
	RESULT = TOINT(CHOICES)
	IF RESULT == 100
		SETBIT FLAG:11, 2
		REDRAW 1
		RETURN 0
	ELSEIF RESULT == 2000 && CONFIG("세대別표시")
		M = 0
		P = 0
	ELSEIF RESULT == 2001 && CONFIG("세대別표시")
		M += 1
		P = 0
	ELSEIF RESULT == 2002 && CONFIG("세대別표시")
		M -= 1
		P = 0
	ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("표시人数") && Q >= 2 && P != RESULT - 1001
		P = RESULT - 1001
	ENDIF
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;-------------------------------------------------
;연모or음란にしたキャラを일람表示(名前の異なる상위몽마などにも個別対応)
;-------------------------------------------------
@SHOW_CHARA_CAPTURED
#DIM LCOUNT
#DIM LCOUNT_2
#DIM NEXT_LINE
PRINTL □ CLEAR캐릭터일람 □---------------------------------------------------------

;改行の判定
NEXT_LINE = 0

FOR LCOUNT, 1, 99
	VARSET LOCALS
	FOR LCOUNT_2, 0, 5
		SIF GETBIT(FLAG:(1099 + LCOUNT), LCOUNT_2) == 0
			CONTINUE

		LOCALS = %NAMES("종족", LCOUNT, LCOUNT_2)%
		;同じ名前なら飛ばす
		SIF LOCALS == LOCALS:1 || LOCALS == LOCALS:2 || LOCALS == LOCALS:3 || LOCALS == LOCALS:4 || LOCALS == LOCALS:5
			CONTINUE

		SIF FLAG:(1200 + LCOUNT) >= 1
			SETCOLOR DEF_COLOR("노랑")
		PRINTFORM  %TEXT_LJ(LOCALS, 16)%
		SIF FLAG:(1200 + LCOUNT) >= 1
			RESETCOLOR

		SIF NEXT_LINE%4 == 3
			PRINTL 
		NEXT_LINE += 1
		LOCALS:(LCOUNT_2 + 1) = %LOCALS%
	NEXT
NEXT

;最後のループで改行してなかったら改行
SIF NEXT_LINE%4
	PRINTL 
WAIT

