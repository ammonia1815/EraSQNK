;악세사리 상점『신기루당』
;FLAG:300番台を使用. 下一桁によって用途を分ける. 
;0	악세사리の種類と名前
;1～5	効果
;8	특수플래그
;0bit 배추흰나비で使用
;9	레벨
@ACCESSORY_SHOP
DRAWLINE
PRINTL  □ 악세사리 상점『신기루당』 □
DRAWLINE
PRINTL 「오우, 어서옵셔.
PRINTL  여긴 아무것도 없는 가게다.
PRINTW  하지만 인기있다고. 모두에게」
CALL ACCESSORY_SHOP_BUY

@ACCESSORY_SHOP_BUY
PRINTL 
PRINTFORML 무엇을 구입합니까? (소지금：%MONEY_C(MONEY)%)
SIF NUM("未鑑定の악세사리")
	PRINTFORML 혹은 무언가 감정합니까？（악세사리의 감정가격은 일률적으로 $100입니다）
PRINTFORML  [ 0] %TEXT_LJ("아련한 팔찌", 30)% 정신력 중시（$2,000）\@ ITEM:아련한팔찌 ? 미감정：{ITEM:아련한팔찌, 2}개 # \@
PRINTFORML  [ 1] %TEXT_LJ("아련한 소매장식", 30)% 매력 중시 （$2,000）\@ ITEM:아련한소매장식 ? 미감정：{ITEM:아련한소매장식, 2}개 # \@
PRINTFORML  [ 2] %TEXT_LJ("아련한 장갑", 30)% 기교 중시 （$2,000）\@ ITEM:아련한장갑 ? 미감정：{ITEM:아련한장갑, 2}개 # \@
PRINTFORML  [ 3] %TEXT_LJ("아련한 장식띠", 30)% 정력 중시 （$2,000）\@ ITEM:아련한장식띠 ? 미감정：{ITEM:아련한장식띠, 2}개 # \@
PRINTFORML  [ 4] %TEXT_LJ("아련한 반지", 30)% 효과 수 중시（$5,000）\@ ITEM:아련한반지 ? 미감정：{ITEM:아련한반지, 2}개 # \@
SIF ITEM:아련한보석
	PRINTFORML  [ 5] %TEXT_LJ("아련한 보석", 30)%  \@ ITEM:아련한보석 ? 미감정：{ITEM:아련한보석, 2}개 # \@

PRINTFORML  [10] %TEXT_LJ("오더메이드 악세사리", 30)%  （$5,000～）
IF NUM("所持악세사리")
	PRINTFORML  [20] %TEXT_LJ("사고연마의 돌", 30)% ＬＶＵＰ! （$5,000）
	PRINTFORML  [\@ ITEM:사고연마의돌 ? 80 # × \@] 사고연마의 돌을 사용한다  （{ITEM:사고연마의돌, 2}개）
	PRINTFORML  [\@ ITEM:사고를태우는불꽃 ? 81 # × \@] 사고를 태우는 불꽃을 사용한다 （{ITEM:사고를태우는불꽃, 2}개）
	PRINTFORML  [\@ ITEM:배추흰나비 && NUM("所持악세사리") >= 2 ? 82 # × \@] 배추흰나비를 사용한다 （{ITEM:배추흰나비, 2}마리）
	PRINTFORML  [90] 악세사리 관리（주인의 능력 표시 화면으로부터도 할 수 있습니다）
ENDIF
PRINTFORML [100] 가게를 나간다

$INPUT_LOOP
INPUT

;악세사리を限界まで持っていると購入できない
IF RESULT == 100
	CALL ACCESSORY_SET_TALENT
	PRINTW 「또 와줘」
	RETURN 0
;악세사리に付加される効果を決定
ELSEIF RESULT >= 0 && RESULT <= 4
	IF ACCESSORY_BUY()
		;鑑定
		IF ITEM:(320+RESULT)
			LOCAL = 100
		ELSE
			LOCAL = ITEMPRICE:(320+RESULT)
		ENDIF
		;소지금が발りないと購入できない
		IF MONEY < LOCAL
			PRINTW 「어이어이, 돈이 부족하잖아?」
		ELSE
			MONEY -= LOCAL
			;鑑定なら未鑑定の악세사리数を減らす
			IF ITEM:(320+RESULT)
				ITEM:(320+RESULT) -= 1
				CALL ACCESSORY_IDENTIFY, RESULT*10, "鑑定"
			ELSE
				CALL ACCESSORY_IDENTIFY, RESULT*10
			ENDIF
		ENDIF
	ELSE
		PRINTW 「더는 가질 수 없을것 같은데」
	ENDIF
;鑑定
ELSEIF RESULT == 5 && ITEM:아련한보석
	IF ACCESSORY_BUY()
		;소지금が발りないと購入できない
		IF MONEY < 100
			PRINTW 「어이어이, 돈이 부족하잖아?」
		ELSE
			MONEY -= 100
			;未鑑定の악세사리数を減らす
			ITEM:325 -= 1
			CALL ACCESSORY_IDENTIFY, RESULT*10, "鑑定"
		ENDIF
	ELSE
		PRINTW 「더는 가질 수 없을것 같은데」
	ENDIF
ELSEIF RESULT == 10
	IF ACCESSORY_BUY()
		CALL ACCESSORY_ORDERMADE_MENU
	ELSE
		PRINTW 「안됐지만, 더는 가질 수 없을것 같은데」
	ENDIF
ELSEIF RESULT == 20 && NUM("所持악세사리")
	IF MONEY < ITEMPRICE:사고연마의돌
		PRINTW 「어이어이, 돈이 부족하잖아?」
	ELSE
		PRINTFORML 「몇개 사갈거야?」
		CALL SHOW_BUY_ITEM, 96
		IF RESULT == 0
			PRINTW 「뭐야, 놀리는거냐」
		ELSE
			PRINTFORMW 「{RESULT}개에 %MONEY_C(ITEMPRICE:사고연마의돌 * RESULT)%다, 고마워.  소중히 쓰라고」
		ENDIF
	ENDIF
ELSEIF RESULT == 80 && ITEM:사고연마의돌 && NUM("所持악세사리")
	CALL ACCESSORY_LVUP
ELSEIF RESULT == 81 && ITEM:사고를태우는불꽃 && NUM("所持악세사리")
	CALL ACCESSORY_FIRE
ELSEIF RESULT == 82 && ITEM:배추흰나비 && NUM("所持악세사리") >= 2
	CALL ACCESSORY_PIERISRAPAE
ELSEIF RESULT == 90 && NUM("所持악세사리")
	CALL ACCESSORY_SELECT
ELSE
	GOTO INPUT_LOOP
ENDIF

RESTART

;-------------------------------------------------
;악세사리ーの種類と性能を記録する
;ARGには0から10刻みで値が代入されている
;ARGSには"鑑定"などが入る
;-------------------------------------------------
@ACCESSORY_IDENTIFY, ARG, ARGS
#DIM LCOUNT
;まずは何個目かを判定
FOR LCOUNT, 1, 9
	IF FLAG:(300 + LCOUNT*10) == 0
		LOCAL = LCOUNT
		BREAK
	ENDIF
NEXT

SELECTCASE ARGS
;拾ってきたものの場合ＬＶが高いことがある
CASE "鑑定"
	SELECTCASE RAND:10
	CASE 0 TO 5
	CASE 6
		FLAG:(300 + LOCAL*10 + 9) += 1
	CASE 7
		FLAG:(300 + LOCAL*10 + 9) += 2
	CASE 8
		FLAG:(300 + LOCAL*10 + 9) += 3 + RAND:4
	CASEELSE
		FLAG:(300 + LOCAL*10 + 9) += 4 + RAND:6
	ENDSELECT
ENDSELECT

;LOCAL個めの악세사리の形状と修飾語を記録
SELECTCASE ARG
CASE 0
	LOCALS = 팔찌
	FLAG:(300 + LOCAL*10) = ARG + RAND:4 + 1
CASE 10
	LOCALS = 소매장식
	FLAG:(300 + LOCAL*10) = ARG + RAND:4 + 1
CASE 20
	LOCALS = 장갑
	FLAG:(300 + LOCAL*10) = ARG + RAND:3 + 1
CASE 30
	LOCALS = 장식띠
	FLAG:(300 + LOCAL*10) = ARG + RAND:3 + 1
;반지は必ず２個이상の보너스を持つ
CASE 40
	LOCALS = 반지
	FLAG:(300 + LOCAL*10) = ARG + RAND:2 + 1
CASE 50
	LOCALS = 보석
	FLAG:(300 + LOCAL*10) = ARG
	;ＬＶは必ず１
	FLAG:(300 + LOCAL*10 + 9) = 0
ENDSELECT

PRINTFORMW 아련한%LOCALS%의 정체는……

;能力をセット
FOR LCOUNT, 1, 6
	;２個目以降の性能は低확률
	SELECTCASE FLAG:(300 + LOCAL*10)/10
	;竜石
	CASE 5
		BREAK
	;반지
	CASE 4
		SIF LCOUNT >= 3 && RAND:(LCOUNT - 1)
			BREAK
	;それ以外
	CASEELSE
		SIF LCOUNT >= 2 && RAND:LCOUNT
			BREAK
	ENDSELECT

	FLAG:(300 + LOCAL*10 + LCOUNT) = ACCESSORY_ABL()
NEXT

;악세사리の詳細表示
CALL PRINT_ACCESSORY, LOCAL

PRINTFORMW %조사만처리(LASTWORD, "이었")%다!


;-------------------------------------------------
;ARG番目の악세사리ーを表示する
;-------------------------------------------------
@PRINT_ACCESSORY, ARG
#DIM LCOUNT
;브레슬릿	매력+5 정신력+25		매력+5 정신력+10
;커프스 	매력+25 정신력+5		매력+10 정신력+5
;건틀렛	매력+15 정신력+5 기교+15	매력+5 정신력+5 기교+5
;새시	매력+15 정신력+5 정력+15	매력+5 정신력+5 정력+5
;반지		全ステータス+5			全ステータス+5
;竜石		全ステータス+25			全ステータス+10

LOCAL = 300 + ARG*10
SIF FLAG:LOCAL == 0
	RETURN 0

PRINTFORM  [{ARG, 2}] %PRINT_ACCESSORY_NAME(FLAG:LOCAL)% ＬＶ{FLAG:(LOCAL + 9) + 1, 2}
IF FLAG:399 == ARG
	PRINT [
	CALL PRINT_COLORTEXT, "Ｅ", DEF_COLOR("황색")
	PRINT ]
ENDIF
;배추흰나비での合成ベース
IF GETBIT(FLAG:(LOCAL + 8), 0)
	PRINT [
	CALL PRINT_COLORTEXT, "합성베이스", DEF_COLOR("황색")
	PRINT ]
ENDIF
PRINTL 

;악세사리のＬＶ
LOCAL = FLAG:(300 + ARG*10 + 9)

SELECTCASE FLAG:(300 + ARG*10)/10
CASE 0
	PRINTFORML  정신력＋{25 + LOCAL * 10}％ 매력＋{5 + LOCAL * 5}％
CASE 1
	PRINTFORML  정신력＋{5 + LOCAL * 5}％ 매력＋{25 + LOCAL * 10}％
CASE 2
	PRINTFORML  기교＋{15 + LOCAL * 5}％ 정신력＋{5 + LOCAL * 5}％ 매력＋{15 + LOCAL * 5}％
CASE 3
	PRINTFORML  정력＋{15 + LOCAL * 5}％ 정신력＋{5 + LOCAL * 5}％ 매력＋{15 + LOCAL * 5}％
CASE 4
	PRINTFORML  기교＆정력＆정신력＆매력＋{5 + LOCAL * 5}％
CASE 5
	PRINTFORML  기교＆정력＆정신력＆매력＋{25 + LOCAL * 10}％
ENDSELECT

SETCOLOR DEF_COLOR("황색")
FOR LCOUNT, 1, 6
	SIF FLAG:(300 + ARG*10 + LCOUNT)
		PRINTFORML %BL(5)% %ACCESSORY_EFFECT(FLAG:(300 + ARG*10 + LCOUNT) )%
NEXT
RESETCOLOR
LASTWORD = %PRINT_ACCESSORY_NAME(FLAG:LOCAL)%

@ACCESSORY_LVUP
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

REDRAW 0
DRAWLINE
PRINTL 
PRINTFORML ＬＶ 한계는 30입니다. 
PRINTFORML 어느 악세사리에 사용합니까？ (사고연마의돌：{ITEM:사고연마의돌}개)

;全ての악세사리を羅列
FOR LOCAL, 1, 9
	CALL PRINT_ACCESSORY, LOCAL
NEXT

;사고연마의돌が無い
IF ITEM:사고연마의돌 <= 0
	PRINTL
	PRINTW 사고연마의 돌이 없어졌으므로 되돌아갑니다
	CALL ACCESSORY_EQUIP
	REDRAW 1
	RETURN 0
ENDIF

PRINTL [100] 돌아간다

$INPUT_LOOP
INPUT

IF RESULT == 100
	CALL ACCESSORY_EQUIP
	REDRAW 1
	RETURN 0
ELSEIF 1 <= RESULT && RESULT <= 8 && FLAG:(300 + RESULT*10)
	IF FLAG:(300 + RESULT*10 + 9) <= 28
		ITEM:사고연마의돌 -= 1
		FLAG:(300 + RESULT*10 + 9) += 1
	ELSE
		PRINTW 이미 ＬＶ이 ＭＡＸ입니다. 
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@ACCESSORY_FIRE
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT
REDRAW 0

DRAWLINE
PRINTL 
PRINTFORM 악세사리의 
CALL PRINT_COLORTEXT, "이 색깔", DEF_COLOR("황색")
PRINTFORML 로 표시된 효과를 위에서부터 한 개 제거합니다. 
PRINTFORML 어느 악세사리에 사용합니까？ (사고를 태우는 불꽃：{ITEM:사고를태우는불꽃}개)

;全ての악세사리を羅列
FOR LOCAL, 1, 9
	CALL PRINT_ACCESSORY, LOCAL
NEXT

;사고를태우는불꽃が無い
IF ITEM:사고를태우는불꽃 <= 0
	PRINTL
	PRINTW 사고를 태우는 불꽃이 없어졌으므로 되돌아갑니다
	CALL ACCESSORY_EQUIP
	REDRAW 1
	RETURN 0
ENDIF

PRINTL [100] 돌아간다

$INPUT_LOOP
INPUT

IF RESULT == 100
	CALL ACCESSORY_EQUIP
	REDRAW 1
	RETURN 0
ELSEIF 1 <= RESULT && RESULT <= 8 && FLAG:(300 + RESULT*10)
	IF FLAG:(300 + RESULT*10 + 1)
		ITEM:사고를태우는불꽃 -= 1
		;効果を一個づつずらして詰める
		FLAG:(300 + RESULT*10 + 1) = 0
		FOR LOCAL, 1, 5
			SWAP FLAG:(300 + RESULT*10 + LOCAL), FLAG:(300 + RESULT*10 + LOCAL + 1)
		NEXT
	ELSE
		PRINTW 능력이 없으므로 지울 수 없습니다 
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART


@ACCESSORY_PIERISRAPAE
#DIM LCOUNT, 2
#DIM CHOICE, 3
#DIM MEMO_LINECOUNT
#DIM TABLENGTH = 44

VARSET CHOICE
MEMO_LINECOUNT = LINECOUNT
REDRAW 0

DRAWLINE
PRINTL 
PRINTFORML 두 개의 악세사리를 하나로 합성합니다. 
PRINTFORML 다음 중에서 두 개를 선택해주세요.  (배추흰나비：{ITEM:배추흰나비}마리)
PRINTFORML （먼저 고른 쪽이 베이스가 됩니다）

;全ての악세사리を羅列
FOR LCOUNT, 1, 9
	CALL PRINT_ACCESSORY, LCOUNT
NEXT

;악세사리が一つしか無い
IF NUM("所持악세사리") <= 1
	PRINTL
	PRINTW 악세사리가 하나 밖에 없으므로 되돌아갑니다
	FOR LCOUNT, 1, 9
		;배추흰나비플래그消去
		CLEARBIT FLAG:(300 + LCOUNT*10 + 8), 0
	NEXT
	CALL ACCESSORY_EQUIP
	REDRAW 1
	RETURN 0
ENDIF
PRINTL [100] 돌아간다

$INPUT_LOOP
INPUT

IF RESULT == 100
	FOR LCOUNT, 1, 9
		;배추흰나비플래그 소거
		CLEARBIT FLAG:(300 + LCOUNT*10 + 8), 0
	NEXT
	CALL ACCESSORY_EQUIP
	REDRAW 1
	RETURN 0
ELSEIF 1 <= RESULT && RESULT <= 8 && FLAG:(300 + RESULT*10)
	CHOICE:2 = 300 + RESULT * 10
	INVERTBIT FLAG:(CHOICE:2 + 8), 0
	;합성 개시？
	FOR LCOUNT, 1, 9
		SIF GETBIT(FLAG:(300 + LCOUNT * 10 + 8), 0) == 0
			CONTINUE
		SIF (300 + LCOUNT * 10) != CHOICE:2
			CHOICE:1 = 300 + LCOUNT * 10
	NEXT
	;2개가 선택되어 있으면 합성 개시
	IF CHOICE:1 && CHOICE:2
		PRINTL 
		;합성 전 아이템 소개
		LOCALS = [{ARG, 2}] %PRINT_ACCESSORY_NAME(FLAG:(CHOICE:1))% ＬＶ{FLAG:(CHOICE:1 + 9) + 1, 2}
		PRINTFORM %BL(1)%%LOCALS, TABLENGTH-5, LEFT%
		LOCALS = +
		PRINTFORM %TOFULL(LOCALS)%%BL(2)%

		LOCALS = [{ARG, 2}] %PRINT_ACCESSORY_NAME(FLAG:(CHOICE:2))% ＬＶ{FLAG:(CHOICE:2 + 9) + 1, 2}
		PRINTFORM %BL(1)%%LOCALS, TABLENGTH-1, LEFT%

		PRINTL
		FOR LCOUNT, 1, 3
			LOCAL = FLAG:(CHOICE:LCOUNT + 9)
			SELECTCASE FLAG:(CHOICE:LCOUNT)/10
			CASE 0
				LOCALS = 정신력＋{25 + LOCAL * 10}％ 매력＋{5 + LOCAL * 5}％
			CASE 1
				LOCALS = 정신력＋{5 + LOCAL * 5}％ 매력＋{25 + LOCAL * 10}％
			CASE 2
				LOCALS = 기교＋{15 + LOCAL * 5}％ 정신력＋{5 + LOCAL * 5}％ 매력＋{15 + LOCAL * 5}％
			CASE 3
				LOCALS = 정력＋{15 + LOCAL * 5}％ 정신력＋{5 + LOCAL * 5}％ 매력＋{15 + LOCAL * 5}％
			CASE 4
				LOCALS = 기교＆정력＆정신력＆매력＋{5 + LOCAL * 5}％
			CASE 5
				LOCALS = 기교＆정력＆정신력＆매력＋{25 + LOCAL * 10}％
			ENDSELECT
			PRINTFORM %BL(1)%%LOCALS, TABLENGTH-1, LEFT%
		NEXT
		PRINTL

		SETCOLOR DEF_COLOR("황색")
		FOR LCOUNT:1, 1, 6
			FOR LCOUNT, 1, 3
				LOCALS =
				SIF FLAG:(CHOICE:LCOUNT + LCOUNT:1)
					LOCALS = %BL(5)% %ACCESSORY_EFFECT(FLAG:(CHOICE:LCOUNT + LCOUNT:1) )%
				PRINTFORM %LOCALS, TABLENGTH, LEFT%
			NEXT
			SIF LINEISEMPTY() == 0
				PRINTL
		NEXT
		RESETCOLOR


		;악세사리 합성
		;ＬＶＵＰ, 최대레벨 30
		FLAG:(CHOICE:1 + 9) += (FLAG:(CHOICE:2 + 9) + 1) / 2
		SIF FLAG:(CHOICE:1 + 9) > 29
			FLAG:(CHOICE:1 + 9) = 29
		;효과를 이동
		FOR LCOUNT, 1, 6
			;베이스 아이템에 옵션이 없는 플래그를 찾는다.
			SIF FLAG:(CHOICE:1 + LCOUNT)
				CONTINUE
			;옵션이 없는 플래그에 아이템2의 옵션을 차례대로 추가
			;하나의 아이템은 최대 5개의 옵션 보유 가능
			FOR LCOUNT:1, 0, 5
				SIF LCOUNT + LCOUNT:1 > 5
					BREAK
				FLAG:(CHOICE:1 + LCOUNT + LCOUNT:1) = FLAG:(CHOICE:2 + 1 + LCOUNT:1)
			NEXT
			BREAK
		NEXT
		PRINTL
		REUSELASTLINE 합성 중.........
		WAIT
		REUSELASTLINE 합성 중......
		WAIT
		REUSELASTLINE 합성 중...
		WAIT
		PRINTW 합성 완료!
		;배추흰나비플래그消去
		CLEARBIT FLAG:(CHOICE:1 + 8), 0
		CLEARBIT FLAG:(CHOICE:2 + 8), 0
		ITEM:배추흰나비 -= 1

		CALL PRINT_ACCESSORY, (CHOICE:1 - 300) / 10
		PRINTW 

		;합성한 악세사리를 없앤다
		;(CHOICE:1 - 300) / 10 는 악세사리 번호
		IF FLAG:399 == (CHOICE:2 - 300) / 10
			FLAG:399 = (CHOICE:1 - 300) / 10
			CALL ACCESSORY_EQUIP
		ELSEIF FLAG:399 > (CHOICE:2 - 300) / 10
			FLAG:399 -= 1
		ENDIF
		;FLAG리셋
		VARSET FLAG, 0, CHOICE:2, CHOICE:2 + 10
		;번호를 변경 
		;예) 6번 악세사리를 소모 시, 7번 8번 악세사리가 6번 7번이 된다 
		FOR LCOUNT, 0, 8
			LOCAL = CHOICE:2 + LCOUNT * 10

			SIF LOCAL >= 380
				BREAK

			FOR LCOUNT:1, 0, 10
				SWAP FLAG:(LOCAL + LCOUNT:1), FLAG:(LOCAL + 10 + LCOUNT:1)
			NEXT
		NEXT
	ELSE
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
		RESTART
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

RESTART


@ACCESSORY_SELECT
#DIM MEMO_LINECOUNT
;악세사리を全部捨てた
SIF NUM("所持악세사리") == 0
	RETURN 0

MEMO_LINECOUNT = LINECOUNT
REDRAW 0

DRAWLINE
PRINTL 
PRINTL 어느 악세사리를 장비합니까?
PRINTL 이미 장비중인 것을 선택하면 악세사리를 벗습니다.

;全ての악세사리を羅列
FOR LOCAL, 1, 9
	CALL PRINT_ACCESSORY, LOCAL
NEXT

PRINTFORML  [\@ ITEM:사고연마의돌 ? 80 # × \@] 사고연마의 돌을 사용한다（{ITEM:사고연마의돌, 2}개）
PRINTFORML  [\@ ITEM:사고를태우는불꽃 ? 81 # × \@] 사고를 태우는 불꽃을 사용한다（{ITEM:사고를태우는불꽃, 2}개）
PRINTFORML  [\@ ITEM:배추흰나비 && NUM("所持악세사리") >= 2 ? 82 # × \@] 배추흰나비를 사용한다 （{ITEM:배추흰나비, 2}마리）
PRINTL  [98] 악세사리를 버린다
PRINTL  [99] 악세사리의 효과 일람표를 본다
PRINTL [100] 돌아간다

$INPUT_LOOP
INPUT

IF RESULT == 100
	CALL ACCESSORY_SET_TALENT
	REDRAW 1
	RETURN 0
ELSEIF 1 <= RESULT && RESULT <= 8 && FLAG:(300 + RESULT*10)
	IF FLAG:399 == RESULT
		CALL ACCESSORY_EQUIP, "外す"
	ELSE
		FLAG:399 = RESULT
		CALL ACCESSORY_EQUIP
	ENDIF
ELSEIF RESULT == 80 && ITEM:사고연마의돌
	CALL ACCESSORY_LVUP
ELSEIF RESULT == 81 && ITEM:사고를태우는불꽃
	CALL ACCESSORY_FIRE
ELSEIF RESULT == 82 && ITEM:배추흰나비 && NUM("所持악세사리") >= 2
	CALL ACCESSORY_PIERISRAPAE
ELSEIF RESULT == 98
	CALL ACCESSORY_TRASH
ELSEIF RESULT == 99
	CALL ACCESSORY_HELP
ELSE
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART


@ACCESSORY_TRASH
#DIM LCOUNT, 2
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT
REDRAW 0

DRAWLINE
PRINTL 
PRINTL 어느 악세사리를 버립니까?

;全ての악세사리を羅列
FOR LOCAL, 1, 9
	CALL PRINT_ACCESSORY, LOCAL
NEXT

PRINTL [100] 돌아간다

$INPUT_LOOP
INPUT

IF RESULT == 100
	CALL ACCESSORY_SET_TALENT
	REDRAW 1
	RETURN 0
ELSEIF 1 <= RESULT && RESULT <= 8 && FLAG:(300 + RESULT*10)
	;装備しているものを捨てたなら
	IF FLAG:399 == RESULT
		CALL ACCESSORY_EQUIP, "外す"
	ELSEIF FLAG:399 > RESULT
		FLAG:399 -= 1
	ENDIF

	;捨てるもののFLAGリセット
	VARSET FLAG, 0, 300 + RESULT*10, 310 + RESULT*10
	;番号をつめる
	FOR LCOUNT, 0, 8
		LOCAL = 300 + RESULT*10 + LCOUNT*10

		SIF LOCAL >= 380
			BREAK

		FOR LCOUNT:1, 0, 10
			SWAP FLAG:(LOCAL + LCOUNT:1), FLAG:(LOCAL + 10 + LCOUNT:1)
		NEXT
	NEXT
	;악세사리を全部捨てた
	IF NUM("所持악세사리") == 0
		REDRAW 1
		RETURN 0
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@ACCESSORY_HELP
PRINTL ～악세사리 부가효과 일람～
PRINTL 
PRINTFORML  %"ＥＰ ＋○％", 20, LEFT% : 사정게이지의 ＭＡＸ가 ○％ 증가
PRINTFORML  %"정력 ＋○％", 20, LEFT% : 정력의 ＭＡＸ가 ○％ 증가
PRINTFORML  %"기교 ＋○％", 20, LEFT% : 주는 쾌감 등이 ○％ 증가
PRINTFORML  %"정신력＋○％", 20, LEFT% : 받는 쾌감＆황홀확률 등이 ○％ 감소
PRINTFORML  %"매력 ＋○％", 20, LEFT% : 상대에게 주는 불쾌감이 ○％ 감소＆20％이상이라면 호감도＋１
PRINTFORML  %"스톡증가", 20, LEFT% : 스톡을 １개 추가（중복 불가）
PRINTFORML  %"(부위)강화＋○％", 20, LEFT% : 대응하는 부위에 받는 쾌감이 ○％ 증가
PRINTFORML  %"무드＋○", 20, LEFT% : 무드 상승이 ○ 증가
PRINTFORML  %"실행치＋○", 20, LEFT% : 커맨드 실행치가 ○ 증가
PRINTFORML  %"받는쾌감＋○％", 20, LEFT% : 받는 쾌감 등이 ○％ 증가
PRINTFORML  %"밀려넘어짐＋○％", 20, LEFT% : 밀려넘어질 확률이 ○％ 증가
PRINTFORML  %"획득경험치＋○％", 20, LEFT% : 장비자가 획득하는 경험치가 ○％ 증가
PRINTFORML  %"항상(상태명)", 20, LEFT% : 항상 대응되는 상태가 된다
PRINTFORML  %"은밀＋○％", 20, LEFT% : 탐색 중에 적과 만날 확률이 ○％ 감소
PRINTFORML  %"보물상자출현율＋○％", 20, LEFT% : 탐색 중에 보물상자를 발견할 확률이 ○％ 증가
PRINTFORML  %"문발견율＋○％", 20, LEFT% : 탐색 중에 문을 발견할 확률이 ○％ 증가
WAIT


@ACCESSORY_EQUIP, ARGS
;ARGS == "外す" なら装備をはずす処理
SELECTCASE ARGS
CASE "外す"
	FLAG:399 = 0
	VARSET FLAG, 0, 300, 310
CASEELSE
	FOR LOCAL, 0, 10
		FLAG:(300 + LOCAL) = FLAG:(300 + FLAG:399*10 + LOCAL)
	NEXT
ENDSELECT

@ACCESSORY_SET_TALENT
#DIM LCOUNT
;브레슬릿	매력+5 정신력+25		매력+5 정신력+10
;커프스 	매력+25 정신력+5		매력+10 정신력+5
;건틀렛	매력+15 정신력+5 기교+15	매력+5 정신력+5 기교+5
;새시	매력+15 정신력+5 정력+15	매력+5 정신력+5 정력+5
;반지		全ステータス+5			全ステータス+5
;竜石		全ステータス+25			全ステータス+10

VARSET TALENT:MASTER:0, 0, 500, 600

IF FLAG:399 == 0
	CALL ACCESSORY_EQUIP, "外す"
	RETURN 0
ELSE
	CALL ACCESSORY_EQUIP
ENDIF

FOR LCOUNT, 1, 6
	LOCAL = 300 + LCOUNT

	SIF FLAG:LOCAL/1000 == 0
		CONTINUE

	TALENT:MASTER:(500 + FLAG:LOCAL/1000 ) += MAX(FLAG:LOCAL%1000, 1)
NEXT

;항상미니멈と항상인라지で相殺
IF TALENT:MASTER:항상미니멈 && TALENT:MASTER:항상인라지
	TALENT:MASTER:항상미니멈 = 0
	TALENT:MASTER:항상인라지 = 0
ENDIF

SELECTCASE (FLAG:300 / 10)
CASE 0
	TALENT:MASTER:정신력강화 += 25 + FLAG:309 * 10
	TALENT:MASTER:매력강화 += 5 + FLAG:309 * 5
CASE 1
	TALENT:MASTER:정신력강화 += 5 + FLAG:309 * 5
	TALENT:MASTER:매력강화 += 25 + FLAG:309 * 10
CASE 2
	TALENT:MASTER:기교강화 += 15 + FLAG:309 * 5
	TALENT:MASTER:정신력강화 += 5 + FLAG:309 * 5
	TALENT:MASTER:매력강화 += 15 + FLAG:309 * 5
CASE 3
	TALENT:MASTER:정력강화 += 15 + FLAG:309 * 5
	TALENT:MASTER:정신력강화 += 5 + FLAG:309 * 5
	TALENT:MASTER:매력강화 += 15 + FLAG:309 * 5
CASE 4
	TALENT:MASTER:정력강화 += 5 + FLAG:309 * 5
	TALENT:MASTER:기교강화 += 5 + FLAG:309 * 5
	TALENT:MASTER:정신력강화 += 5 + FLAG:309 * 5
	TALENT:MASTER:매력강화 += 5 + FLAG:309 * 5
CASE 5
	TALENT:MASTER:정력강화 += 25 + FLAG:309 * 10
	TALENT:MASTER:기교강화 += 25 + FLAG:309 * 10
	TALENT:MASTER:정신력강화 += 25 + FLAG:309 * 10
	TALENT:MASTER:매력강화 += 25 + FLAG:309 * 10
ENDSELECT


@PRINT_ACCESSORY_NAME(ARG)
#FUNCTIONS
SELECTCASE ARG/10
CASE 0
	SELECTCASE ARG%10
	CASE 1
		LOCALS = 상아의브레슬릿
	CASE 2
		LOCALS = 청동의브레슬릿
	CASE 3
		LOCALS = 백은의브레슬릿
	CASE 4
		LOCALS = 소울실버의브레슬릿
	ENDSELECT
CASE 1
	SELECTCASE ARG%10
	CASE 1
		LOCALS = 산호초커프스
	CASE 2
		LOCALS = 포실커프스
	CASE 3
		LOCALS = 제이드커프스
	CASE 4
		LOCALS = 앰버커프스
	ENDSELECT
CASE 2
	SELECTCASE ARG%10
	CASE 1
		LOCALS = 실크건틀렛
	CASE 2
		LOCALS = 새튼건틀렛
	CASE 3
		LOCALS = 빌로드건틀렛
	ENDSELECT
CASE 3
	SELECTCASE ARG%10
	CASE 1
		LOCALS = 숫말의새시
	CASE 2
		LOCALS = 숫사슴의새시
	CASE 3
		LOCALS = 숫토끼의새시
	ENDSELECT
CASE 4
	SELECTCASE ARG%10
	CASE 1
		LOCALS = 크리스탈링
	CASE 2
		LOCALS = 다이아몬드링
	ENDSELECT
CASE 5
	LOCALS = 용석
ENDSELECT

RETURNF LOCALS


@ACCESSORY_ABL()
#FUNCTION
;LOCALに XXXを効果の種類, AAAを効力として, XXXAAA の形で数値を入れる

SELECTCASE RAND:100
;殆どの場合はステータス変化(種類は정력,기교,정신력,매력)
CASE IS <= 50
	;매력
	IF RAND:5 == 0
		LOCAL = 5000
	;정신력
	ELSEIF RAND:4 == 0
		LOCAL = 4000
	;기교
	ELSEIF RAND:3 == 0
		LOCAL = 3000
	;정력
	ELSEIF RAND:2
		LOCAL = 2000
	;ＥＰ
	ELSE
		LOCAL = 1000
	ENDIF

	SELECTCASE RAND:10
	CASE IS < 2
		LOCAL += 10
	CASE IS < 4
		LOCAL += 20
	CASE IS < 6
		LOCAL += 30
	CASE IS < 8
		LOCAL += 40
	CASEELSE
		LOCAL += 50
	ENDSELECT
;특수効果
CASE IS <= 79
	SELECTCASE RAND:11
	;○○강화 該当部位をせめる場合に与える快感が強くなる 種類はキッス,바스트,힙,ヴァッジ 
	CASE 0 TO 2
		;Ｃ
		IF RAND:5 == 0
			LOCAL = 10000
		;Ｖ
		ELSEIF RAND:4 == 0
			LOCAL = 11000
		;Ａ
		ELSEIF RAND:3 == 0
			LOCAL = 12000
		;Ｂ
		ELSEIF RAND:2
			LOCAL = 13000
		;Ｍ
		ELSE
			LOCAL = 14000
		ENDIF
		LOCAL += 50
	;실행치アップ+○ 種類は+4,+7,+10
	CASE 3
		LOCAL = 21000
		SELECTCASE RAND:10
		CASE IS <= 6
			LOCAL += 4
		CASE IS <= 8
			LOCAL += 7
		CASEELSE
			LOCAL += 10
		ENDSELECT
	;무드 업+○ 무드 업+○
	CASE 4
		LOCAL = 20000
		SELECTCASE RAND:10
		CASE IS <= 6
			LOCAL += 1
		CASE IS <= 8
			LOCAL += 2
		CASEELSE
			LOCAL += 3
		ENDSELECT
	;넘어뜨려질확률+○％
	CASE 5
		LOCAL = 23000
		SELECTCASE RAND:10
		CASE IS <= 6
			LOCAL += 10
		CASE IS <= 8
			LOCAL += 20
		CASEELSE
			LOCAL += 30
		ENDSELECT
	;받는쾌감+○％ 種類は+10,+20,+50
	CASE 6
		LOCAL = 22000
		SELECTCASE RAND:10
		CASE IS <= 6
			LOCAL += 10
		CASE IS <= 8
			LOCAL += 20
		CASEELSE
			LOCAL += 50
		ENDSELECT
	;○○に強い その종족に強くなる 種類は소녀,요정,악마,ケモノ,도마뱀 

	;常に아르카나
	CASE 7, 8
		;항상인라지
		IF RAND:4 == 0
			LOCAL = 33000
		;항상센시브
		ELSEIF RAND:3 == 0
			LOCAL = 32000
		;항상미니멈 ステート異常미니멈になる
		ELSEIF RAND:2
			LOCAL = 31000
		;항상우르즈군
		ELSE
			LOCAL = 30000
		ENDIF
	;경험치増加
	CASE 9
		LOCAL = 24000
		SELECTCASE RAND:10
		CASE IS <= 6
			LOCAL += 20
		CASE IS <= 8
			LOCAL += 50
		CASEELSE
			LOCAL += 100
		ENDSELECT
	;스톡증가
	CASEELSE
		LOCAL = 6000
	ENDSELECT
;主に탐색で効くもの
CASEELSE
	;은밀+○ 敵に出会う확률軽減. 
	IF RAND:3 == 0
		LOCAL = 50000
	;보물상자출현율アップ
	ELSEIF RAND:2
		LOCAL = 51000
	;문발견율アップ
	ELSE
		LOCAL = 52000
	ENDIF
	IF RAND:4 == 0
		LOCAL += 20
	ELSE
		LOCAL += 40
	ENDIF
ENDSELECT
RETURNF LOCAL

@ACCESSORY_EFFECT(ARG)
#FUNCTIONS

SELECTCASE ARG/1000
CASE 1
	LOCALS = ＥＰ＋{ARG%1000}％
CASE 2
	LOCALS = 정력＋{ARG%1000}％
CASE 3
	LOCALS = 기교＋{ARG%1000}％
CASE 4
	LOCALS = 정신력＋{ARG%1000}％
CASE 5
	LOCALS = 매력＋{ARG%1000}％
CASE 6
	LOCALS = 스톡증가

CASE 10
	LOCALS = Ｃ강화＋{ARG%1000}％
CASE 11
	LOCALS = Ｖ강화＋{ARG%1000}％
CASE 12
	LOCALS = Ａ강화＋{ARG%1000}％
CASE 13
	LOCALS = Ｂ강화＋{ARG%1000}％
CASE 14
	LOCALS = Ｍ강화＋{ARG%1000}％

CASE 20
	LOCALS = 무드＋{ARG%1000}
CASE 21
	LOCALS = 실행치＋{ARG%1000}
CASE 22
	LOCALS = 받는쾌감＋{ARG%1000}％
CASE 23
	LOCALS = 밀려넘어짐＋{ARG%1000}％
CASE 24
	LOCALS = 획득경험치＋{ARG%1000}％

CASE 30
	LOCALS = 항상우르즈군
CASE 31
	LOCALS = 항상미니멈
CASE 32
	LOCALS = 항상센시브
CASE 33
	LOCALS = 항상인라지

CASE 50
	LOCALS = 은밀＋{ARG%1000}％
CASE 51
	LOCALS = 보물상자출현율＋{ARG%1000}％
CASE 52
	LOCALS = 문발견율＋{ARG%1000}％
ENDSELECT

RETURNF LOCALS


;악세사리をまだ持てるか判定
@ACCESSORY_BUY()
#FUNCTION
SIF FLAG:380 == 0
	RETURNF 1
RETURNF 0
