;-------------------------------------------------
;보디슈츠を着せる関数
;손가락定番号によって名前がCSTR:44に代入される. 
;99番までは, その番号の몽마の下着. 
;EQUIP:보디슈츠は보디슈츠の特徴플래그. ビット管理する
;-------------------------------------------------
@SET_BODYS, ARG, ARG:1
;ARGの보디슈츠をARG:1番のものにする. ARG:1==0なら보디슈츠を脱がす
;汚れ＆ずれたりした플래그等を初期化して新品にする
STAIN:ARG:보디슈츠바텀 = 0
STAIN:ARG:보디슈츠탑 = 0
TCVAR:ARG:26 = 0

IF ARG:1 == 0 || COND("다리 사용", ARG) == 0
	EQUIP:ARG:보디슈츠 = 0
	CSTR:ARG:44 = 
	RETURN 0
ENDIF

;名前記録
CSTR:ARG:44 = %PALAM_BODYS(ARG:1, ARG, 1)%

;SIF EQUIP:ARG:웃옷
;	CALL SET_SHIRT, ARG
SIF EQUIP:ARG:겉옷
	CALL SET_OUTER, ARG
;SIF EQUIP:ARG:겉옷 && !CHECK_OUTER("팬티스타킹", ARG)
;	CALL SET_OUTER, ARG
SIF EQUIP:ARG:브래지어
	CALL SET_BRA, ARG
IF EQUIP:ARG:보디슈츠 && EQUIP:ARG:팬티
	SIF CHECK_BODYS("食い込み", ARG) && !CHECK_PANTIES("食い込み", ARG) && !CHECK_PANTIES("Ｔ백", ARG) && !CHECK_PANTIES("Ｖ백", ARG)
		CALL SET_PANTIES, ARG
	SIF CHECK_BODYS("수영복", ARG) || CHECK_BODYS("파스너", ARG) || CHECK_BODYS("Ｖ노출", ARG) || CHECK_BODYS("Ａ노출", ARG)
		CALL SET_PANTIES, ARG
ENDIF


TCVAR:ARG:26 = 0
RETURN ARG:1
;-------------------------------------------------
;ARGの穿いている보디슈츠の名前. ARG:1==0なら正式名称 by CSTR. ARG:1==2なら色요정素なし
;ARG:1==1なら短縮名称 by EQUIP. 
;-------------------------------------------------
@BODYS(ARG, ARG:1)
#FUNCTIONS
SELECTCASE ARG:1
CASE 0
	LOCALS = %CSTR:ARG:44%
	SIF CONFIG("衣装の細かい色표시の금지")
		RETURNF LOCALS
	LOCALS:1 = %TEXTS("衣装の色", EQUIP:ARG:보디슈츠)%
	SIF LOCALS:1 != "" && (SUBSTRING(CSTR:ARG:44, 0, 2) != SUBSTRING(LOCALS:1, 0, 2) )
		LOCALS = %LOCALS%(%LOCALS:1%)
CASE 1
	IF CHECK_BODYS("경기용수영복", ARG)
		LOCALS = 경기용수영복
	ELSEIF CHECK_BODYS("원피스수영복", ARG)
		IF CHECK_BODYS("食い込み", ARG)
			LOCALS = 하이레그수영복
		ELSE
			LOCALS = 원피스
		ENDIF
	ELSEIF CHECK_BODYS("본디지", ARG)
		LOCALS = 본디지
	ELSEIF CHECK_BODYS("바니슈트", ARG)
		LOCALS = 바니슈트
	ELSEIF CHECK_BODYS("학교수영복", ARG)
		LOCALS = 학교수영복
	ELSEIF CHECK_BODYS("레오타드", ARG)
		LOCALS = 레오타드
	ELSE
		LOCALS = 보디슈츠
	ENDIF
CASE 2
	LOCALS = %CSTR:ARG:44%
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;ARG番の보디슈츠の正式名称
;ARG:1 > 0ならARG:1は캐릭터번호として働く
;-------------------------------------------------
@NAME_BODYS(ARG, ARG:1)
#FUNCTIONS
LOCALS = %PALAM_BODYS(ARG, ARG:1)%
RETURNF LOCALS

;-------------------------------------------------
;ARG番の보디슈츠の詳細パラ
;ARG:1 > 0ならARG:1は캐릭터번호として働き, ARG:1 > 0 && ARG:2 > 0ならEQUIPを定める
;-------------------------------------------------
@PALAM_BODYS(ARG, ARG:1, ARG:2)
#FUNCTIONS
VARSET LOCAL
VARSET LOCALS
IF ARG:1 > 0 && COND("다리 사용", ARG:1) == 0
	SIF ARG:2 > 0
		EQUIP:(ARG:1):보디슈츠 = LOCAL
	RETURNF LOCALS
ENDIF

SELECTCASE ARG
CASE 5
	IF CFLAG:(ARG:1):8 == 1
		LOCALS = 감색학교수영복
		LOCAL = SETFLAG_BODYS("ズリ불가", "학교수영복", "감색")
	ENDIF
CASE 7
	IF TALENT:(ARG:1):사족보행
		LOCALS = 人늑대の레오타드
		LOCAL = SETFLAG_BODYS("ズリ불가", "하이레그", "레오타드", "모피", "검정")
	ENDIF
CASE 11
	IF CFLAG:(ARG:1):8 != 2
		LOCALS = 妖鳥の레오타드
		LOCAL = SETFLAG_BODYS("하이레그", "Ｔ백", "레오타드", "날개", "가슴골노출")
	ENDIF
CASE 12
	IF CFLAG:(ARG:1):8 == 4
		LOCALS = 레오타드
		LOCAL = SETFLAG_BODYS("ズリ불가", "Ｖ백", "레오타드", "검정")
	ELSEIF CFLAG:(ARG:1):8 != 2
		LOCALS = 레오타드
		LOCAL = SETFLAG_BODYS("ズリ불가", "Ｖ백", "레오타드", "연록")
	ENDIF
CASE 17
	IF CFLAG:(ARG:1):8 == 1
		LOCALS = 에나멜레오타드
		LOCAL = SETFLAG_BODYS("Ｖ백", "레오타드", "エナメル", "검정", "가슴골노출")
	ENDIF
CASE 29
	LOCALS = 슬라임スキン
	LOCAL = SETFLAG_BODYS("레오타드", "연분홍", "가슴골노출")
CASE 35
	LOCALS = Ｈ한보디슈츠
	SIF ARG:1 > 0
		LOCALS = %CALLNAME:(ARG:1)%의복장
	LOCAL = SETFLAG_BODYS("ズリ불가", "하이레그", "Ｔ백", "보디슈츠", "보라")
CASE 41
	LOCALS = 不思議な레오타드
	LOCAL = SETFLAG_BODYS("하이레그", "Ｔ백", "레오타드", "파스너", "검정", "가슴골노출")
CASE 42
	IF CFLAG:(ARG:1):209 == 1
		LOCALS = 不思議な레오타드
		LOCAL = SETFLAG_BODYS("하이레그", "Ｔ백", "레오타드", "파스너", "검정", "가슴골노출")
	ENDIF
CASE 48
	LOCALS = 드림슈라우드
	LOCAL = SETFLAG_BODYS("하이레그", "Ｔ백", "레오타드", "보라", "가슴골노출")
CASE 100
	LOCALS = 레오타드
	LOCAL = SETFLAG_BODYS("ズリ불가", "레오타드")
CASE 101
	LOCALS = 섹시레오타드
	LOCAL = SETFLAG_BODYS("하이레그", "Ｖ백", "레오타드", "가슴골노출")
CASE 102
	LOCALS = 에나멜레오타드
	LOCAL = SETFLAG_BODYS("하이레그", "Ｔ백", "레오타드", "エナメル", "파스너", "가슴골노출")
CASE 103
	IF ITEM:위험한레오타드
		LOCALS = 위험한레오타드
		LOCAL = SETFLAG_BODYS("하이레그", "Ｖ백", "위험한레오타드", "ＶＡ노출", "가슴골노출", "유두노출")
	ENDIF
CASE 104
	IF ITEM:바니슈트
		LOCALS = 바니슈트
		LOCAL = SETFLAG_BODYS("Ｖ백", "바니슈트", "바니컬러")
	ENDIF
CASE 105
	IF ITEM:바니슈트
		LOCALS = 하이레그바니슈트
		LOCAL = SETFLAG_BODYS("하이레그", "Ｔ백", "바니슈트", "エナメル", "바니컬러")
	ENDIF
CASE 106
	IF ITEM:본디지
		LOCALS = 본디지
		LOCAL = SETFLAG_BODYS("본디지", "가죽", "파스너", "가슴골노출", "유두노출")
	ENDIF
CASE 107
	IF ITEM:학교수영복
		LOCALS = 학교수영복
		LOCAL = SETFLAG_BODYS("ズリ불가", "학교수영복")
		IF RAND:2
			LOCAL += SETFLAG_BODYS("감색")
		ELSE
			LOCAL += SETFLAG_BODYS("하양")
		ENDIF
	ENDIF
CASE 108
	IF ITEM:원피스수영복
		LOCALS = 원피스수영복
		LOCAL = SETFLAG_BODYS("ズリ불가", "원피스수영복", "하양")
	ENDIF
CASE 109
	IF ITEM:원피스수영복
		LOCALS = 하이레그수영복
		LOCAL = SETFLAG_BODYS("하이레그", "Ｔ백", "ズリ불가", "원피스수영복")
	ENDIF
CASE 110
	IF ITEM:원피스수영복
		LOCALS = 경기용수영복
		LOCAL = SETFLAG_BODYS("하이레그", "Ｖ백", "ズリ불가", "경기용수영복")
		IF RAND:2
			LOCAL += SETFLAG_BODYS("감색")
		ELSE
			LOCAL += SETFLAG_BODYS("검정")
		ENDIF
	ENDIF
ENDSELECT

SIF ARG:1 > 0 && ARG:2 > 0
	EQUIP:(ARG:1):보디슈츠 = LOCAL

RETURNF LOCALS

;-------------------------------------------------
;지시されたとおりの보디슈츠の플래그を설정するために使う
;-------------------------------------------------
@SETFLAG_BODYS(ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7, ARGS:8, ARGS:9, ARGS:10)
#FUNCTION
#DIM LCOUNT

LOCAL = 0
FOR LCOUNT, 1, 11
	ARGS = %ARGS:LCOUNT%
	SELECTCASE ARGS
	CASE "보디슈츠"
		SETBIT LOCAL, 0
	CASE "하이레그"
		SETBIT LOCAL, 1
	CASE "Ｔ프론트", "T프론트"
		SETBIT LOCAL, 2
	CASE "Ｖ백", "V백"
		SETBIT LOCAL, 3
	CASE "Ｔ백", "T백"
		SETBIT LOCAL, 4
	CASE "레오타드"
		SETBIT LOCAL, 5
	CASE "학교수영복"
		SETBIT LOCAL, 6
		LOCAL |= SETFLAG_COMMON("수영복")
	CASE "바니슈트"
		SETBIT LOCAL, 7
		LOCAL |= SETFLAG_COMMON("가슴골노출")
	CASE "본디지"
		SETBIT LOCAL, 8
	CASE "원피스수영복"
		SETBIT LOCAL, 9
		LOCAL |= SETFLAG_COMMON("수영복")
	CASE "경기용수영복"
		SETBIT LOCAL, 10
		LOCAL |= SETFLAG_COMMON("수영복")
	CASE "위험한레오타드"
		SETBIT LOCAL, 5
		SETBIT LOCAL, 11
	CASE "바니컬러"
		IF RAND:3 == 0
			LOCAL |= SETFLAG_COMMON("하양")
		ELSEIF RAND:2
			LOCAL |= SETFLAG_COMMON("진홍")
		ELSE
			LOCAL |= SETFLAG_COMMON("검정")
		ENDIF
	CASEELSE
		SIF STRLENS(ARGS)
			LOCAL |= SETFLAG_COMMON(ARGS)
	ENDSELECT
	SIF STRLENS(ARGS:(LCOUNT + 1) ) == 0
		RETURNF LOCAL
NEXT
RETURNF LOCAL

;-------------------------------------------------
;ARGの보디슈츠が条件ARGSを満たすか確認する
;-------------------------------------------------
@CHECK_BODYS(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
SIF EQUIP:ARG:보디슈츠 == 0
	RETURNF 0
SELECTCASE ARGS
CASE "はだけ可"
	SIF SETFLAG_BODYS("はだけた")&EQUIP:ARG:보디슈츠
		RETURNF 0
	SIF SETFLAG_BODYS("はだけ불가")&EQUIP:ARG:보디슈츠
		RETURNF 0
	RETURNF 1
;ずれ不可でなく, ずらしていない
CASE "ずらし可", "ずらせる"
	SIF SETFLAG_BODYS("Ｖ노출")&EQUIP:ARG:보디슈츠
		RETURNF 0
	IF SETFLAG_BODYS("파스너")&EQUIP:ARG:보디슈츠
		SIF SETFLAG_BODYS("파스너開")&EQUIP:ARG:보디슈츠
			RETURNF 0
		SIF CHECK_BODYS("파스너閉")
			RETURNF 1
	ENDIF
	SIF SETFLAG_BODYS("ずらした")&EQUIP:ARG:보디슈츠
		RETURNF 0
	SIF SETFLAG_BODYS("ずれ불가")&EQUIP:ARG:보디슈츠
		RETURNF 0
	RETURNF 1
;ファスナーがあり, あけていない
CASE "파스너閉"
	SIF SETFLAG_BODYS("파스너開")&EQUIP:ARG:보디슈츠
		RETURNF 0
	SIF SETFLAG_BODYS("파스너")&EQUIP:ARG:보디슈츠
		RETURNF 1
CASE "クロッチ"
	SIF SETFLAG_BODYS("파스너開", "ずらした", "破いた", "Ｖ노출", "슬릿")&EQUIP:ARG:보디슈츠
		RETURNF 0
	RETURNF 1
CASE "Ａクロッチ"
	SIF SETFLAG_BODYS("파스너開", "ずらした", "破いた", "Ａ노출", "슬릿")&EQUIP:ARG:보디슈츠
		RETURNF 0
	RETURNF 1
CASE "食い込み"
	SIF SETFLAG_BODYS("ずらした", "파스너開", "Ｖ노출", "슬릿", "破いた")&EQUIP:ARG:보디슈츠
		RETURNF 0
	SIF SETFLAG_BODYS("하이레그", "Ｔ프론트")&EQUIP:ARG:보디슈츠
		RETURNF 1
CASE "蒸れ"
	SIF SETFLAG_BODYS("ずらした", "파스너開", "Ｖ노출", "슬릿", "破いた")&EQUIP:ARG:보디슈츠
		RETURNF 0
	SIF CHECK_STAIN(STAIN:ARG:보디슈츠바텀, "蒸れ")
		RETURNF 1
CASE "染み"
	SIF SETFLAG_BODYS("ずらした", "파스너開", "Ｖ노출", "슬릿", "破いた", "通気性없음")&EQUIP:ARG:보디슈츠
		RETURNF 0
	SIF CHECK_STAIN(STAIN:ARG:보디슈츠바텀, "濡れ")
		RETURNF 1
CASEELSE
	SIF (SETFLAG_BODYS(ARGS)&EQUIP:ARG:보디슈츠) == SETFLAG_BODYS(ARGS)
		RETURNF 1
ENDSELECT

RETURNF 0
