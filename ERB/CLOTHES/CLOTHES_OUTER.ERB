;-------------------------------------------------
;바지など, 腰辺りから발목까지を覆うものを穿かせる関数. 팬티스타킹なども含む
;손가락定番号によって名前がCSTR:45に代入される. 
;99番までは, その番号の몽마の下着. 
;EQUIP:겉옷は바지の特徴플래그. ビット管理する
;-------------------------------------------------
@SET_OUTER, ARG, ARG:1
;ARGの바지をARG:1番のものにする. ARG:1==0なら바지を脱がす
;汚れ＆ずれたりした플래그等を初期化して新品にする
STAIN:ARG:겉옷 = 0
TCVAR:ARG:26 = 0

IF ARG:1 == 0 || COND("다리 사용", ARG) == 0
	IF CHECK_OUTER("つなぎ", ARG)
		EQUIP:ARG:웃옷 = 0
		CSTR:ARG:42 = 
	ENDIF
	EQUIP:ARG:겉옷 = 0
	CSTR:ARG:45 = 
	RETURN 0
ENDIF

;名前記録
CSTR:ARG:45 = %PALAM_OUTER(ARG:1, ARG, 1)%
IF CHECK_OUTER("つなぎ", ARG) == 0 && CHECK_SHIRT("つなぎ", ARG)
	EQUIP:ARG:웃옷 = 0
	CSTR:ARG:42 = 
ENDIF

SIF EQUIP:ARG:겉옷 && !CHECK_OUTER("팬티스타킹", ARG)
	CALL SET_BODYS, ARG
IF EQUIP:ARG:겉옷 && EQUIP:ARG:팬티
	SIF CHECK_OUTER("食い込み", ARG) && !CHECK_PANTIES("食い込み", ARG) && !CHECK_PANTIES("Ｔ백", ARG) && !CHECK_PANTIES("Ｖ백", ARG)
		CALL SET_PANTIES, ARG
	SIF CHECK_OUTER("파스너", ARG) || CHECK_OUTER("Ｖ노출", ARG) || CHECK_OUTER("Ａ노출", ARG)
		CALL SET_PANTIES, ARG
ENDIF
SIF EQUIP:ARG:겉옷 && CHECK_OUTER("팬티스타킹", ARG)
	CALL SET_SOCKS, ARG

TCVAR:ARG:26 = 0
RETURN ARG:1
;-------------------------------------------------
;ARGの穿いている바지の名前. ARG:1==0なら正式名称 by CSTR. ARG:1==1なら短縮名称 by EQUIP. 
;-------------------------------------------------
@OUTER(ARG, ARG:1)
#FUNCTIONS
IF ARG:1 == 0
	LOCALS = %CSTR:ARG:45%
	SIF CONFIG("衣装の細かい色표시の금지")
		RETURNF LOCALS
	LOCALS:1 = %TEXTS("衣装の色", EQUIP:ARG:겉옷)%
	SIF LOCALS:1 != "" && (SUBSTRING(CSTR:ARG:45, 0, 2) != SUBSTRING(LOCALS:1, 0, 2) )
		LOCALS = %LOCALS%(%LOCALS:1%)
ELSE
	IF CHECK_OUTER("블루머", ARG)
		LOCALS = 블루머
	ELSEIF CHECK_OUTER("핫팬츠", ARG)
		LOCALS = 핫팬츠
	ELSEIF CHECK_OUTER("스패츠", ARG)
		LOCALS = 스패츠
	ELSEIF CHECK_OUTER("팬티스타킹", ARG)
		LOCALS = 팬티스타킹
	ELSEIF CHECK_OUTER("레깅스", ARG)
		LOCALS = 레깅스
	ELSEIF CHECK_OUTER("파자마", ARG)
		LOCALS = 파자마
	ELSEIF CHECK_OUTER("ハーフ팬티", ARG)
		LOCALS = ハーフ팬티
	ELSE
		LOCALS = 바지
	ENDIF
ENDIF
RETURNF LOCALS

;-------------------------------------------------
;ARG番の바지の正式名称
;ARG:1 > 0ならARG:1は캐릭터번호として働く
;-------------------------------------------------
@NAME_OUTER(ARG, ARG:1)
#FUNCTIONS

LOCALS = %PALAM_OUTER(ARG, ARG:1)%
RETURNF LOCALS

;-------------------------------------------------
;ARG番の바지の詳細パラ
;ARG:1 > 0ならARG:1は캐릭터번호として働き, ARG:1 > 0 && ARG:2 > 0ならEQUIPを定める
;-------------------------------------------------
@PALAM_OUTER(ARG, ARG:1, ARG:2)
#FUNCTIONS
VARSET LOCAL
VARSET LOCALS

IF ARG:1 > 0 && COND("다리 사용", ARG:1) == 0
	SIF ARG:2 > 0
		EQUIP:(ARG:1):겉옷 = LOCAL
	RETURNF LOCALS
ENDIF

SELECTCASE ARG
CASE 17
	IF CFLAG:(ARG:1):8 != 1
		LOCALS = 핫팬츠
		LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "핫팬츠", "가죽", "通気性없음", "검정")
	ENDIF
CASE 23
	IF CFLAG:(ARG:1):8 == 2
		LOCALS = 로우라이즈팬츠
		LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "바지", "로우라이즈", "주황")
	ELSE
		LOCALS = 로우라이즈팬츠
		LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "바지", "로우라이즈", "연록")
	ENDIF
CASE 34
	LOCALS = 핫팬츠
	LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "핫팬츠", "연록")
CASE 42
	IF CFLAG:(ARG:1):209 == 2
		LOCALS = 핫팬츠
		LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "핫팬츠", "검정")
	ENDIF
CASE 58
	LOCALS = 로우라이즈팬츠
	LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "바지", "로우라이즈", "주황")
CASE 100
	LOCALS = 블루머
	LOCAL = SETFLAG_OUTER("블루머", "블루머컬러")
CASE 101
	LOCALS = 하이레그블루머
	LOCAL = SETFLAG_OUTER("하이레그", "Ｖ백", "블루머", "블루머컬러")
CASE 102
	LOCALS = 스패츠
	LOCAL = SETFLAG_OUTER("ずれ불가", "스패츠")
CASE 103
	LOCALS = 핫팬츠
	LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "핫팬츠")
CASE 104
	LOCALS = 팬티스타킹
	LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "팬티스타킹")
CASE 105
	LOCALS = 망사팬티스타킹
	LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "팬티스타킹", "레이스")
CASE 106
	LOCALS = 오픈팬티스타킹
	LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "팬티스타킹", "ＶＡ노출")
CASE 107
	LOCALS = 레깅스
	LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "레깅스")
CASE 108
	IF ITEM:파자마 && TALENT:작은몸집
		LOCALS = 파자마바지
		LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "파자마", "바지")
	ENDIF
CASE 109
	LOCALS = ハーフ팬티
	LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "ハーフ팬티")

CASE 120
	LOCALS = 블루머
	LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "블루머", "ＶＡ노출", "블루머컬러")
CASE 125
	LOCALS = 스패츠
	LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "스패츠", "ＶＡ노출")
CASE 130
	LOCALS = 핫팬츠
	LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "핫팬츠", "Ａ노출")
CASE 135
	LOCALS = 핫팬츠
	LOCAL = SETFLAG_OUTER("ずれ불가", "コキ불가", "핫팬츠", "슬릿")
ENDSELECT

SIF ARG:1 > 0 && ARG:2 > 0
	EQUIP:(ARG:1):겉옷 = LOCAL

RETURNF LOCALS

;-------------------------------------------------
;지시されたとおりの바지の플래그を설정するために使う
;-------------------------------------------------
@SETFLAG_OUTER(ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7, ARGS:8, ARGS:9, ARGS:10)
#FUNCTION
#DIM LCOUNT

LOCAL = 0
FOR LCOUNT, 1, 11
	ARGS = %ARGS:LCOUNT%
	SELECTCASE ARGS
	CASE "바지"
		SETBIT LOCAL, 0
		LOCAL |= SETFLAG_COMMON("ずれ불가")
		LOCAL |= SETFLAG_COMMON("コキ불가")
	CASE "스패츠"
		SETBIT LOCAL, 1
		LOCAL |= SETFLAG_COMMON("ずれ불가")
	CASE "핫팬츠"
		SETBIT LOCAL, 2
		SETBIT LOCAL, 0
		LOCAL |= SETFLAG_COMMON("ずれ불가")
		LOCAL |= SETFLAG_COMMON("コキ불가")
	CASE "블루머"
		SETBIT LOCAL, 3
	CASE "하이레그"
		SETBIT LOCAL, 4
	CASE "Ｔ프론트", "T프론트", "T-front"
		SETBIT LOCAL, 5
	CASE "Ｖ백", "V백", "V-back"
		SETBIT LOCAL, 6
	CASE "Ｔ백", "T백", "T-back"
		SETBIT LOCAL, 7
	CASE "로우라이즈", "浅履き"
		SETBIT LOCAL, 8
	CASE "팬티스타킹"
		SETBIT LOCAL, 9
		LOCAL |= SETFLAG_COMMON("시스루")
	CASE "레깅스"
		SETBIT LOCAL, 10
	CASE "파자마"
		SETBIT LOCAL, 11
	CASE "ハーフ팬티"
		SETBIT LOCAL, 12

	CASE "블루머컬러"
		IF RAND:2
			LOCAL |= SETFLAG_COMMON("빨강")
		ELSE
			LOCAL |= SETFLAG_COMMON("감색")
		ENDIF
	CASEELSE
		SIF STRLENS(ARGS)
			LOCAL |= SETFLAG_COMMON(ARGS)
	ENDSELECT
	SIF STRLENS(ARGS:(LCOUNT + 1) ) == 0
		RETURNF LOCAL
NEXT
RETURNF LOCAL

;-------------------------------------------------
;ARGの바지が条件ARGSを満たすか確認する
;-------------------------------------------------
@CHECK_OUTER(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
SIF EQUIP:ARG:겉옷 == 0
	RETURNF 0
SELECTCASE ARGS
;ずれ不可でなく, ずらしていない
CASE "ずらし可", "ずらせる"
	SIF SETFLAG_OUTER("Ｖ노출")&EQUIP:ARG:겉옷
		RETURNF 0
	IF SETFLAG_OUTER("파스너")&EQUIP:ARG:겉옷
		SIF SETFLAG_OUTER("파스너開")&EQUIP:ARG:겉옷
			RETURNF 0
		SIF CHECK_OUTER("파스너閉")
			RETURNF 1
	ENDIF
	SIF SETFLAG_OUTER("ずらした")&EQUIP:ARG:겉옷
		RETURNF 0
	SIF SETFLAG_OUTER("ずれ불가")&EQUIP:ARG:겉옷
		RETURNF 0
	RETURNF 1
;팬티스타킹で, 破いていない
CASE "破ける"
	SIF SETFLAG_OUTER("破いた")&EQUIP:ARG:겉옷
		RETURNF 0
	SIF SETFLAG_OUTER("팬티스타킹")&EQUIP:ARG:겉옷
		RETURNF 1
;파스너があり, あけていない
CASE "파스너閉"
	SIF SETFLAG_OUTER("파스너開")&EQUIP:ARG:겉옷
		RETURNF 0
	SIF SETFLAG_OUTER("파스너")&EQUIP:ARG:겉옷
		RETURNF 1
CASE "クロッチ"
	SIF SETFLAG_OUTER("파스너開", "ずらした", "破いた", "Ｖ노출", "슬릿")&EQUIP:ARG:겉옷
		RETURNF 0
	RETURNF 1
CASE "Ａクロッチ"
	SIF SETFLAG_OUTER("파스너開", "ずらした", "破いた", "Ａ노출", "슬릿")&EQUIP:ARG:겉옷
		RETURNF 0
	RETURNF 1
CASE "食い込み"
	SIF SETFLAG_OUTER("ずらした", "파스너開", "Ｖ노출", "슬릿", "破いた")&EQUIP:ARG:겉옷
		RETURNF 0
	SIF SETFLAG_OUTER("하이레그", "Ｔ프론트")&EQUIP:ARG:겉옷
		RETURNF 1
CASE "蒸れ"
	SIF SETFLAG_OUTER("ずらした", "파스너開", "Ｖ노출", "슬릿", "破いた")&EQUIP:ARG:겉옷
		RETURNF 0
	SIF CHECK_STAIN(STAIN:ARG:겉옷, "蒸れ")
		RETURNF 1
CASE "染み"
	SIF SETFLAG_OUTER("ずらした", "파스너開", "Ｖ노출", "슬릿", "破いた", "通気性없음")&EQUIP:ARG:겉옷
		RETURNF 0
	SIF CHECK_STAIN(STAIN:ARG:겉옷, "濡れ")
		RETURNF 1
CASEELSE
	SIF (SETFLAG_OUTER(ARGS)&EQUIP:ARG:겉옷) == SETFLAG_OUTER(ARGS)
		RETURNF 1
ENDSELECT
RETURNF 0
