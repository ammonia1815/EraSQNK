;-------------------------------------------------
;팬티を穿かせる関数
;손가락定番号によって名前がCSTR:40に代入される. 
;99番までは, その番号の몽마の下着. 
;EQUIP:팬티は팬티の特徴플래그. ビット管理する
;-------------------------------------------------
@SET_PANTIES, ARG, ARG:1, ARG:2
;ARGの팬티をARG:1番のものにする. ARG:1==0なら팬티を脱がす
;ARG:2は브래지어セット플래그. ==1なら見合った브래지어をつける
;汚れ＆ずれたりした플래그等を初期化して新品にする
STAIN:ARG:팬티 = 0
TCVAR:ARG:26 = 0
SIF TALENT:ARG:남자
	ARG:2 = 0

IF ARG:1 == 0
	EQUIP:ARG:팬티 = 0
	CSTR:ARG:40 = 
	RETURN 0
ENDIF

;발が使えない
IF COND("다리 사용", ARG) == 0
	EQUIP:ARG:팬티 = 0
	CSTR:ARG:40 = 
	CALL SET_BRA, ARG, ARG:1
ELSE
	;名前とEQUIPを記録
	CSTR:ARG:40 = %PALAM_PANTIES(ARG:1, ARG, 1)%
	;Ｈ한팬티はリネーム
	SIF CHECK_PANTIES("超Ｈ", ARG)
		CALL RENAME_PANTIES, ARG
	;브래지어もつける？
	IF ARG:2
		CALL SET_BRA, ARG, ARG:1
		;素材などを揃える
		IF CHECK_PANTIES("시스루", ARG)
			EQUIP:ARG:브래지어 |= SETFLAG_COMMON("시스루")
		ELSEIF CHECK_BRA("시스루", ARG)
			EQUIP:ARG:브래지어 -= SETFLAG_COMMON("시스루")
		ENDIF
		IF CHECK_PANTIES("레이스", ARG)
			EQUIP:ARG:브래지어 |= SETFLAG_COMMON("레이스")
		ELSEIF CHECK_BRA("레이스", ARG)
			EQUIP:ARG:브래지어 -= SETFLAG_COMMON("레이스")
		ENDIF
		IF CHECK_PANTIES("실크", ARG)
			EQUIP:ARG:브래지어 |= SETFLAG_COMMON("실크")
		ELSEIF CHECK_BRA("실크", ARG)
			EQUIP:ARG:브래지어 -= SETFLAG_COMMON("실크")
		ENDIF
		;色をそろえる
		CALL SAME_COLOR, ARG, 40, 41
		;Ｈ한브래지어はリネーム
		SIF CHECK_BRA("超Ｈ", ARG)
			CALL RENAME_BRA, ARG
	ENDIF
ENDIF

IF EQUIP:ARG:팬티 && EQUIP:ARG:보디슈츠
	IF CHECK_BODYS("Ｔ프론트", ARG) && !CHECK_PANTIES("Ｔ프론트", ARG)
		CALL SET_BODYS, ARG
	ELSEIF CHECK_BODYS("하이레그", ARG) && !CHECK_PANTIES("食い込み", ARG)
		CALL SET_BODYS, ARG
	ELSEIF CHECK_BODYS("Ｔ백", ARG) && !CHECK_PANTIES("Ｔ백", ARG)
		CALL SET_BODYS, ARG
	ELSEIF CHECK_BODYS("Ｖ백", ARG) && !CHECK_PANTIES("Ｔ백", ARG) && !CHECK_PANTIES("Ｖ백", ARG)
		CALL SET_BODYS, ARG
	ENDIF
ENDIF
IF EQUIP:ARG:팬티 && EQUIP:ARG:겉옷
	IF CHECK_OUTER("팬티스타킹", ARG)
	ELSEIF CHECK_OUTER("Ｔ프론트", ARG) && !CHECK_PANTIES("Ｔ프론트", ARG)
		CALL SET_OUTER, ARG
	ELSEIF CHECK_OUTER("하이레그", ARG) && !CHECK_PANTIES("食い込み", ARG)
		CALL SET_OUTER, ARG
	ELSEIF CHECK_OUTER("Ｔ백", ARG) && !CHECK_PANTIES("Ｔ백", ARG)
		CALL SET_OUTER, ARG
	ELSEIF CHECK_OUTER("Ｖ백", ARG) && !CHECK_PANTIES("Ｔ백", ARG) && !CHECK_PANTIES("Ｖ백", ARG)
		CALL SET_OUTER, ARG
	ENDIF
ENDIF

TCVAR:ARG:26 = 0
RETURN ARG:1
;-------------------------------------------------
;ARGの穿いている팬티の名前. ARG:1==0なら正式名称 by CSTR. ARG:1==1なら短縮名称 by EQUIP. 
;-------------------------------------------------
@PANTIES(ARG, ARG:1)
#FUNCTIONS
SELECTCASE ARG:1
CASE 0
	LOCALS = %CSTR:ARG:40%
	SIF CONFIG("衣装の細かい色표시の금지")
		RETURNF LOCALS
	LOCALS:1 = %TEXTS("衣装の色", EQUIP:ARG:팬티)%
	SIF LOCALS:1 != "" && (SUBSTRING(CSTR:ARG:40, 0, 2) != SUBSTRING(LOCALS:1, 0, 2) )
		LOCALS = %LOCALS%(%LOCALS:1%)
CASE 1
	IF CHECK_PANTIES("복서팬티", ARG)
		LOCALS = 복서팬티
	ELSEIF CHECK_PANTIES("브리프", ARG)
		LOCALS = 브리프
	ELSEIF CHECK_PANTIES("트렁크스", ARG)
		LOCALS = 트렁크스
	ELSEIF CHECK_PANTIES("훈도시", ARG)
		LOCALS = 훈도시
	;비키니って呼称は上下合わせないとちょっと微妙かも
	;ELSEIF CHECK_PANTIES("수영복", ARG)
	;	LOCALS = 비키니
	ELSEIF CHECK_PANTIES("스패츠", ARG)
		LOCALS = 스패츠
	ELSEIF CHECK_PANTIES("드로워즈", ARG)
		LOCALS = 드로워즈
	ELSEIF CHECK_PANTIES("Ｃ스트링", ARG)
		LOCALS = Ｃ스트링
	;ELSEIF CHECK_PANTIES("前貼り", ARG)
	;	LOCALS = 前貼り
	;ELSEIF CHECK_PANTIES("絆創膏", ARG)
	;	LOCALS = 絆創膏
	ELSE
		LOCALS = 팬티
	ENDIF
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;ARG番の팬티の正式名称
;ARG:1 > 0ならARG:1は캐릭터번호として働く
;-------------------------------------------------
@NAME_PANTIES(ARG, ARG:1)
#FUNCTIONS
LOCALS = %PALAM_PANTIES(ARG, ARG:1)%
RETURNF LOCALS

;-------------------------------------------------
;bit値に応じてキャラARGの穿いている팬티の名前をリネーム
;主にＨ한팬티用
;-------------------------------------------------
@RENAME_PANTIES, ARG
SIF ARG < 0
	RETURN 0
SIF ARG == 0 && TARGET
	ARG = TARGET
SIF EQUIP:ARG:팬티 == 0
	RETURN 0

VARSET LOCALS
IF CHECK_PANTIES("레이스", ARG)
	SELECTCASE TEXTS("衣装の色", EQUIP:ARG:팬티)
	CASE "검정", "보라"
		LOCALS = %TEXTS("衣装の色", EQUIP:ARG:팬티)%レース팬티
	CASEELSE
		LOCALS = 레이스팬티
	ENDSELECT
ELSEIF CHECK_PANTIES("실크", ARG)
	LOCALS = 실크の下着
ELSEIF CHECK_PANTIES("마이크로", ARG)
	LOCALS = マイクロショーツ
ELSEIF CHECK_PANTIES("Ｇ스트링", ARG)
	LOCALS = Ｇ스트링
ELSEIF CHECK_PANTIES("탕가", ARG)
	LOCALS = 탕가
ELSEIF CHECK_PANTIES("끈", ARG)
	IF CHECK_PANTIES("하이레그", ARG)
		LOCALS = 하이레그끈팬티
	ELSEIF CHECK_PANTIES("Ｔ백", ARG)
		LOCALS = Ｔ백끈팬티
	ELSEIF CHECK_PANTIES("로우레그", ARG)
		LOCALS = 로우레그끈팬티
	ELSE
		LOCALS = 끈팬티
	ENDIF
ELSEIF CHECK_PANTIES("하이레그", ARG)
	LOCALS = 하이레그
ELSEIF CHECK_PANTIES("Ｔ백", ARG)
	LOCALS = Ｔ백
ELSEIF CHECK_PANTIES("로우레그", ARG)
	LOCALS = 로우레그
ELSEIF CHECK_PANTIES("로우라이즈", ARG)
	LOCALS = 로우라이즈
ELSEIF CHECK_PANTIES("시스루", ARG)
	LOCALS = 시스루팬티
ENDIF

SIF CHECK_PANTIES("시스루", ARG) && LOCALS != "시스루팬티"
	LOCALS = %LOCALS%(透)

SIF STRLENS(LOCALS)
	CSTR:ARG:40 = %LOCALS%

;-------------------------------------------------
;ARG番の팬티の詳細パラ
;ARG:1 > 0ならARG:1は캐릭터번호として働き, ARG:1 > 0 && ARG:2 > 0ならEQUIPを定める
;-------------------------------------------------
@PALAM_PANTIES(ARG, ARG:1, ARG:2)
#FUNCTIONS
VARSET LOCAL
VARSET LOCALS
IF ARG:1 > 0 && COND("다리 사용", ARG:1) == 0
	SIF ARG:2 > 0
		EQUIP:(ARG:1):팬티 = LOCAL
	RETURNF LOCALS
ENDIF

SELECTCASE ARG
CASE 1
	IF CFLAG:(ARG:1):8 == 2
		LOCALS = 음마의비키니
		LOCAL = SETFLAG_PANTIES("수영복", "Ｇ스트링", "끈", "검정")
	ELSEIF CFLAG:(ARG:1):8 == 3
		LOCALS = 음마의비키니
		LOCAL = SETFLAG_PANTIES("수영복", "Ｇ스트링", "끈", "검정")
	ELSE
		LOCALS = 음마의비키니
		LOCAL = SETFLAG_PANTIES("수영복", "Ｇ스트링", "끈", "보라")
	ENDIF
CASE 4
	IF CFLAG:(ARG:1):8 == 3
		LOCALS = 드로워즈
		LOCAL = SETFLAG_PANTIES("드로워즈", "프릴", "보라")
	ELSE
		LOCALS = 드로워즈
		LOCAL = SETFLAG_PANTIES("드로워즈", "프릴", "순백")
	ENDIF
CASE 6
	IF CFLAG:(ARG:1):8 == 2
		LOCALS = 魔고양이の팬티
		LOCAL = SETFLAG_PANTIES("Ｖ백", "로우라이즈", "모피", "파랑")
	ELSEIF CFLAG:(ARG:1):8 == 3
		LOCALS = 魔고양이の팬티
		LOCAL = SETFLAG_PANTIES("Ｖ백", "로우라이즈", "모피", "검정")
	ELSEIF CFLAG:(ARG:1):8 == 4
		LOCALS = 魔고양이の팬티
		LOCAL = SETFLAG_PANTIES("Ｖ백", "로우라이즈", "모피", "노랑")
	ELSE
		LOCALS = 魔고양이の팬티
		LOCAL = SETFLAG_PANTIES("Ｖ백", "로우라이즈", "모피")
	ENDIF
CASE 7
	IF TALENT:(ARG:1):사족보행 == 0
		LOCALS = 人늑대の레더팬티
		LOCAL = SETFLAG_PANTIES("탕가", "가죽", "검정")
	ENDIF
CASE 8, 9, 32, 45, 52, 56, 59, 96
	LOCALS = 팬티
	LOCAL = SETFLAG_PANTIES("大人しめ")
CASE 11
	IF CFLAG:(ARG:1):8 == 2
		LOCALS = 끈팬티
		LOCAL = SETFLAG_PANTIES("끈", "실크", "순백")
	ENDIF
CASE 18
	LOCALS = 망사팬티
	LOCAL = SETFLAG_PANTIES("탕가", "레이스", "실크", "검정")
CASE 20, 21
	IF PENIS(ARG:1)
		LOCALS = 팬티
		LOCAL = SETFLAG_PANTIES("청초")
	ENDIF
CASE 22
	LOCALS = 훈도시
	LOCAL = SETFLAG_PANTIES("ふんどし")
CASE 23, 58
	LOCALS = 섹시팬티
	LOCAL = SETFLAG_PANTIES("하이레그", "Ｔ백", "프릴", "검정")
CASE 24
	LOCALS = 끈팬티
	LOCAL = SETFLAG_PANTIES("끈", "실크", "순백")
CASE 25
	LOCALS = Ｔ백
	LOCAL = SETFLAG_PANTIES("끈", "Ｔ백", "透け", "실크", "빨강")
CASE 26
	LOCALS = 탕가
	LOCAL = SETFLAG_PANTIES("탕가", "연록")
CASE 31
	LOCALS = 팬티
	LOCAL = SETFLAG_PANTIES("로우라이즈", "주황")
CASE 33
	IF CFLAG:(ARG:1):8 == 4
		LOCALS = 검은팬티
		LOCAL = SETFLAG_PANTIES("로우라이즈", "프릴", "검정")
	ELSE
		LOCALS = 연분홍색팬티
		LOCAL = SETFLAG_PANTIES("로우라이즈", "프릴", "연분홍")
	ENDIF
CASE 34
	LOCALS = 群파랑の비키니
	LOCAL = SETFLAG_PANTIES("수영복", "Ｇ스트링", "끈", "연파랑")
CASE 35
	LOCALS = 진홍のスキャンティ
	LOCAL = SETFLAG_PANTIES("로우레그", "끈", "진홍")
CASE 38
	LOCALS = 新초록の비키니
	LOCAL = SETFLAG_PANTIES("수영복", "Ｇ스트링", "끈", "연록")
CASE 39
	LOCALS = マイクロ비키니
	LOCAL = SETFLAG_PANTIES("수영복", "마이크로", "끈", "파랑")
CASE 40
	LOCALS = 주황색のボーイレッグ
	LOCAL = SETFLAG_PANTIES("실크", "주황")
CASE 47
	LOCALS = Ｈ한속옷
	LOCAL = SETFLAG_PANTIES("Ｈ", "超Ｈ")
CASE 49
	LOCALS = 청초한팬티
	LOCAL = SETFLAG_PANTIES("실크", "청초")
CASE 50
	LOCALS = 검은팬티
	LOCAL = SETFLAG_PANTIES("검정")
CASE 54
	LOCALS = 勝負下着
	LOCAL = SETFLAG_PANTIES("Ｈ", "超Ｈ")
CASE 55
	LOCALS = 드로워즈
	LOCAL = SETFLAG_PANTIES("드로워즈", "순백")
CASE 57
	LOCALS = 팬티
	LOCAL = SETFLAG_PANTIES("ちょっとＨ")
CASE 61
	LOCALS = 하이레그
	LOCAL = SETFLAG_PANTIES("하이레그")
CASE 94
	LOCALS = 브리프
	LOCAL = SETFLAG_PANTIES("브리프")
CASE 100
	LOCALS = 줄무늬팬티
	LOCAL = SETFLAG_PANTIES("줄무늬", "줄무늬색")
CASE 101
	LOCALS = 검은레이스팬티
	LOCAL = SETFLAG_PANTIES("레이스", "시스루", "검정")
CASE 102
	LOCALS = 프릴팬티
	LOCAL = SETFLAG_PANTIES("프릴", "大人しめの色")
CASE 103
	LOCALS = 끈팬티
	LOCAL = SETFLAG_PANTIES("끈", "청초")
CASE 104
	LOCALS = Ｔ백
	LOCAL = SETFLAG_PANTIES("Ｔ백", "ちょっとＨ")
CASE 105
	LOCALS = 로우라이즈
	LOCAL = SETFLAG_PANTIES("로우라이즈", "大人しめの色")
CASE 106
	LOCALS = 시스루팬티
	LOCAL = SETFLAG_PANTIES("시스루", "청초")
CASE 107
	LOCALS = 하이레그
	LOCAL = SETFLAG_PANTIES("하이레그", "ちょっとＨ")
CASE 108
	LOCALS = 레더팬티
	LOCAL = SETFLAG_PANTIES("탕가", "가죽", "파스너")
CASE 109
	LOCALS = 로우레그
	LOCAL = SETFLAG_PANTIES("로우레그", "大人しめの色")
CASE 110
	IF ITEM:비키니
		LOCALS = 비키니
		LOCAL = SETFLAG_PANTIES("수영복", "탕가", "끈")
	ENDIF
CASE 111
	IF ITEM:비키니
		LOCALS = 미니멀비키니
		LOCAL = SETFLAG_PANTIES("수영복", "마이크로", "끈")
	ENDIF
CASE 112
	LOCALS = 레이스팬티
	LOCAL = SETFLAG_PANTIES("레이스", "Ｖ백", "청초")
CASE 113
	LOCALS = スキャンティ
	LOCAL = SETFLAG_PANTIES("마이크로")
	SIF RAND:2
		LOCAL += SETFLAG_PANTIES("끈")
CASE 114
	LOCALS = Ｃ스트링
	LOCAL = SETFLAG_PANTIES("Ｃ스트링", "コキ불가")
CASE 115
	LOCALS = 훈도시
	LOCAL = SETFLAG_PANTIES("ふんどし")
;이하は保留
;CASE 116
;	LOCALS = 前貼り
;	LOCAL = SETFLAG_PANTIES("前貼り", "コキ불가")
;CASE 117
;	LOCALS = 絆創膏
;	LOCAL = SETFLAG_PANTIES("絆創膏", "コキ불가")
;CASE 118
;	LOCALS = 腰巻
;	LOCAL = SETFLAG_PANTIES("腰巻")

CASE 120
	LOCALS = 복서팬티
	LOCAL = SETFLAG_PANTIES("복서팬티")
CASE 121
	LOCALS = 브리프
	LOCAL = SETFLAG_PANTIES("브리프")
CASE 122
	LOCALS = 트렁크스
	LOCAL = SETFLAG_PANTIES("트렁크스")

CASE 130
	LOCALS = Ｈ한팬티
	LOCAL = SETFLAG_PANTIES("Ｈ", "超Ｈ")

;大人しめ
;CASE 131
;	LOCAL = SETFLAG_PANTIES("大人しめ")
;청초
;CASE 132
;	LOCAL = SETFLAG_PANTIES("청초")
;아이
;CASE 134
;소녀
;CASE 135
;언니
;CASE 136
;어른
;CASE 137

;남자
;CASE 139

CASE 140
	LOCALS = 정조대
	LOCAL = SETFLAG_PANTIES("가죽", "정조대", "通気性없음")
ENDSELECT

SIF ARG:1 > 0 && ARG:2 > 0
	EQUIP:(ARG:1):팬티 = LOCAL

RETURNF LOCALS

;-------------------------------------------------
;지시されたとおりの팬티の플래그を설정するために使う
;-------------------------------------------------
@SETFLAG_PANTIES(ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7, ARGS:8, ARGS:9, ARGS:10)
#FUNCTION
#DIM LCOUNT

LOCAL = 0
FOR LCOUNT, 1, 11
	ARGS = %ARGS:LCOUNT%
	SELECTCASE ARGS
	CASE "끈"
		SETBIT LOCAL, 0
	CASE "하이레그"
		SETBIT LOCAL, 1
	CASE "Ｔ프론트", "T프론트"
		SETBIT LOCAL, 2
	CASE "Ｖ백", "V백"
		SETBIT LOCAL, 3
	CASE "Ｔ백", "T백"
		SETBIT LOCAL, 4
	CASE "로우라이즈"
		SETBIT LOCAL, 5
	CASE "로우레그"
		SETBIT LOCAL, 6
	CASE "스패츠"
		SETBIT LOCAL, 7
	CASE "드로워즈"
		SETBIT LOCAL, 8
	CASE "훈도시", "フンドシ", "ふんどし"
		SETBIT LOCAL, 9
	CASE "超Ｈ"
		SETBIT LOCAL, 10
	CASE "정조대"
		SETBIT LOCAL, 11
		LOCAL |= SETFLAG_COMMON("ずれ불가", "コキ불가", "탈의불가", "Ａ노출")
	CASE "복서팬티"
		SETBIT LOCAL, 12
		LOCAL |= SETFLAG_COMMON("ずれ불가")
	CASE "브리프"
		SETBIT LOCAL, 13
		LOCAL |= SETFLAG_COMMON("ずれ불가")
	CASE "트렁크스"
		SETBIT LOCAL, 14
		LOCAL |= SETFLAG_COMMON("ずれ불가")
	CASE "Ｃ스트링"
		SETBIT LOCAL, 2
		SETBIT LOCAL, 4
		SETBIT LOCAL, 15
		LOCAL |= SETFLAG_COMMON("ずれ불가", "コキ불가")
	;하이레그+Ｖバック
	CASE "탕가"
		SETBIT LOCAL, 1
		SETBIT LOCAL, 3
	;하이레그+Ｔ백+로우라이즈
	CASE "Ｇ스트링", "G스트링", "Ｇスト", "Gスト"
		SETBIT LOCAL, 1
		SETBIT LOCAL, 4
		SETBIT LOCAL, 5
	;하이레그+Ｔ백+로우레그
	CASE "마이크로"
		SETBIT LOCAL, 1
		SETBIT LOCAL, 4
		SETBIT LOCAL, 6
	;オート팬티
	CASE "大人しめ"
		SIF RAND:4 == 0
			SETBIT LOCAL, 5
		LOCAL |= SETFLAG_COMMON("大人しめの色")
	CASE "청초"
		SIF RAND:3 == 0
			SETBIT LOCAL, 0
		SIF RAND:3 == 0
			LOCAL |= SETFLAG_COMMON("프릴")
		SIF RAND:3 == 0
			LOCAL |= SETFLAG_COMMON("실크")
		IF RAND:3
			LOCAL |= SETFLAG_COMMON("순백")
		ELSE
			LOCAL |= SETFLAG_COMMON("大人しめの色")
		ENDIF
	CASE "ちょっとＨ"
		SIF RAND:3
			SETBIT LOCAL, 0
		SIF RAND:3
			SETBIT LOCAL, 1
		SIF RAND:3
			SETBIT LOCAL, 3+RAND:2
		SIF RAND:3
			SETBIT LOCAL, 6
		LOCAL |= SETFLAG_COMMON("Ｈめの色")
	CASE "Ｈ"
		SIF RAND:3
			SETBIT LOCAL, 0
		SIF RAND:4
			SETBIT LOCAL, 1
		SIF RAND:4 || GETBIT(LOCAL, 1) == 0
			SETBIT LOCAL, 3+RAND:2
		SIF RAND:4
			SETBIT LOCAL, 5+RAND:2
		SIF RAND:5 == 0
			LOCAL |= SETFLAG_COMMON("실크")
		SIF RAND:5 == 0
			LOCAL |= SETFLAG_COMMON("레이스")
		IF RAND:4 == 0
			LOCAL |= SETFLAG_COMMON("시스루")
		ELSEIF RAND:2 || GETBITS(LOCAL, 1, 4) == 0
			IF RAND:5 == 0
				LOCAL |= SETFLAG_COMMON("파스너")
			ELSEIF RAND:4 == 0
				LOCAL |= SETFLAG_COMMON("슬릿")
			ELSEIF RAND:3 == 0
				LOCAL |= SETFLAG_COMMON("Ｖ노출")
			ELSEIF RAND:2
				LOCAL |= SETFLAG_COMMON("Ａ노출")
				CLEARBIT LOCAL, 4
			ELSE
				LOCAL |= SETFLAG_COMMON("ＶＡ노출")
				CLEARBIT LOCAL, 4
			ENDIF
		ENDIF
		LOCAL |= SETFLAG_COMMON("Ｈめの色")
	CASEELSE
		SIF STRLENS(ARGS)
			LOCAL |= SETFLAG_COMMON(ARGS)
	ENDSELECT
	SIF STRLENS(ARGS:(LCOUNT + 1) ) == 0
		RETURNF LOCAL
NEXT
RETURNF LOCAL

;-------------------------------------------------
;ARGの팬티が条件ARGSを満たすか確認する
;-------------------------------------------------
@CHECK_PANTIES(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
SIF EQUIP:ARG:팬티 == 0
	RETURNF 0
SELECTCASE ARGS
;ずれ不可でなく, ずらしていない
CASE "ずらし可", "ずらせる"
	;SIF SETFLAG_PANTIES("Ｖ노출")&EQUIP:ARG:팬티
	;	RETURNF 0
	IF SETFLAG_PANTIES("파스너")&EQUIP:ARG:팬티
		SIF SETFLAG_PANTIES("파스너開")&EQUIP:ARG:팬티
			RETURNF 0
		SIF CHECK_PANTIES("파스너閉")
			RETURNF 1
	ENDIF
	SIF SETFLAG_PANTIES("ずらした")&EQUIP:ARG:팬티
		RETURNF 0
	SIF SETFLAG_PANTIES("ずれ불가")&EQUIP:ARG:팬티
		RETURNF 0
	RETURNF 1
;파스너があり, あけていない
CASE "파스너閉"
	SIF SETFLAG_PANTIES("파스너開")&EQUIP:ARG:팬티
		RETURNF 0
	SIF SETFLAG_PANTIES("파스너")&EQUIP:ARG:팬티
		RETURNF 1
CASE "クロッチ"
	SIF SETFLAG_PANTIES("파스너開", "ずらした", "破いた", "Ｖ노출", "슬릿")&EQUIP:ARG:팬티
		RETURNF 0
	RETURNF 1
CASE "Ａクロッチ"
	SIF SETFLAG_PANTIES("파스너開", "ずらした", "破いた", "Ａ노출", "슬릿")&EQUIP:ARG:팬티
		RETURNF 0
	RETURNF 1
CASE "食い込み"
	SIF SETFLAG_PANTIES("ずらした", "파스너開", "Ｖ노출", "슬릿", "破いた")&EQUIP:ARG:팬티
		RETURNF 0
	SIF SETFLAG_PANTIES("하이레그", "Ｔ프론트")&EQUIP:ARG:팬티
		RETURNF 1
CASE "Ｈ"
	SIF SETFLAG_PANTIES("시스루", "하이레그", "Ｔ백", "파스너", "슬릿")&EQUIP:ARG:팬티
		RETURNF 1
CASE "마이크로"
	SIF !(SETFLAG_PANTIES("하이레그")&EQUIP:ARG:팬티)
		RETURNF 0
	SIF !(SETFLAG_PANTIES("Ｔ백")&EQUIP:ARG:팬티)
		RETURNF 0
	SIF !(SETFLAG_PANTIES("로우레그")&EQUIP:ARG:팬티)
		RETURNF 0
	RETURNF 1
CASE "蒸れ"
	SIF SETFLAG_PANTIES("ずらした", "파스너開", "Ｖ노출", "슬릿", "破いた")&EQUIP:ARG:팬티
		RETURNF 0
	SIF CHECK_STAIN(STAIN:ARG:팬티, "蒸れ")
		RETURNF 1
CASE "染み"
	SIF SETFLAG_PANTIES("ずらした", "파스너開", "Ｖ노출", "슬릿", "破いた", "通気性없음")&EQUIP:ARG:팬티
		RETURNF 0
	SIF CHECK_STAIN(STAIN:ARG:팬티, "濡れ")
		RETURNF 1
CASE "男物"
	SIF SETFLAG_PANTIES("복서팬티", "브리프", "트렁크스")&EQUIP:ARG:팬티
		RETURNF 1
CASEELSE
	SIF (SETFLAG_PANTIES(ARGS)&EQUIP:ARG:팬티) == SETFLAG_PANTIES(ARGS)
		RETURNF 1
ENDSELECT
RETURNF 0
