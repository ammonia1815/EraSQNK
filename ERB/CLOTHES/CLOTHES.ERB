;-------------------------------------------------
;ARGの身につけている衣装全てから条件を判定して文字列を出力する. 
;-------------------------------------------------
@CLO(ARGS, ARG)
#FUNCTIONS
LOCALS = 
SIF ARG == 0 && TARGET
	ARG = TARGET
SELECTCASE ARGS
CASE "마음에듬：水着"

CASE "가랑이"
	IF CHECK_CLO("보디슈츠노출", ARG)
		LOCALS = %BODYS(ARG, 1)%
	ELSEIF CHECK_CLO("겉옷노출", ARG)
		LOCALS = %OUTER(ARG, 1)%
	ELSEIF CHECK_CLO("팬티노출", ARG)
		LOCALS = %PANTIES(ARG, 1)%
	ENDIF
CASE "팬티훑기"
	IF EQUIP:ARG:겉옷 && !CHECK_OUTER("コキ불가", ARG)
		LOCALS = %OUTER(ARG, 1)%
	ELSEIF EQUIP:ARG:팬티 && !CHECK_PANTIES("コキ불가", ARG)
		LOCALS = %PANTIES(ARG, 1)%
	ENDIF
CASE "コスプレ"
	IF SHIRT(ARG, 1) == "Ｙ셔츠"
		IF CHECK_CLO("알몸", ARG)
			LOCALS = 알몸Ｙ셔츠
		ELSE
			LOCALS = Ｙ셔츠
		ENDIF
	ELSEIF SHIRT(ARG) == "여교사"
		LOCALS = 여교사
	ELSEIF SHIRT(ARG) == "일본전통옷"
		LOCALS = 일본전통옷
	ELSEIF SHIRT(ARG, 1) == "메이드복"
		LOCALS = 메이드복
	ELSEIF SHIRT(ARG, 1) == "체조복"
		LOCALS = 체조복
	ELSEIF SHIRT(ARG, 1) == "간호사복"
		LOCALS = 간호사복
	ELSEIF SHIRT(ARG, 1) == "차이나드레스"
		LOCALS = 차이나드레스
	ELSEIF SHIRT(ARG, 1) == "세라복"
		LOCALS = 세라복
	ELSEIF SHIRT(ARG, 1) == "무녀복"
		LOCALS = 무녀복
	ELSEIF SHIRT(ARG, 1) == "베이비돌"
		IF CHECK_CLO("알몸", ARG)
			LOCALS = 베이비돌온리
		ELSE
			LOCALS = 베이비돌
		ENDIF
	ELSEIF SHIRT(ARG) == "웨딩드레스"
		LOCALS = 웨딩드레스
	ELSEIF SHIRT(ARG) == "고스로리드레스"
		LOCALS = 고스로리드레스
	ELSEIF SHIRT(ARG) == "치어리더"
		LOCALS = 치어리더
	ELSEIF PANTIES(ARG, 1) == "비키니" && BRA(ARG, 1) == "비키니"
		LOCALS = 비키니
	ELSEIF BODYS(ARG, 1) == "학교수영복"
		LOCALS = 학교수영복
	ELSEIF BODYS(ARG, 1) == "위험한레오타드"
		LOCALS = 위험한레오타드
	ELSEIF BODYS(ARG, 1) == "본디지"
		LOCALS = 본디지
	ELSEIF BODYS(ARG, 1) == "바니슈트"
		LOCALS = 바니슈트
	ELSE
		LOCALS = 해당없음
	ENDIF
ENDSELECT
RETURNF LOCALS



;-------------------------------------------------
;ARGの身につけている衣装全てを総合して, ARGSが가능かを判定する
;また, どの커맨드を선택できるか, を判定する時の速度向上のためにTCVAR:26を用いる
;TCVAR:26はEVENTCOMの最初と, 플래그を弄る関数などで初期化されるため, 조교中には悪さをしない…はず. 
;-------------------------------------------------
@CHECK_CLO(ARGS, ARG)
#FUNCTION
VARSET LOCAL
SIF ARG == 0 && TARGET
	ARG = TARGET
;下には팬티など, セットたりえるもの必須
SELECTCASE ARGS
CASE "토플리스"
	SIF EQUIP:ARG:팬티 + EQUIP:ARG:스커트 + EQUIP:ARG:겉옷 && EQUIP:ARG:브래지어 + EQUIP:ARG:웃옷 + EQUIP:ARG:보디슈츠 == 0
		RETURNF 1
;上には브래지어など, セットたりえるもの必須
CASE "바텀리스"
	SIF EQUIP:ARG:브래지어 && EQUIP:ARG:팬티 + EQUIP:ARG:스커트 + EQUIP:ARG:보디슈츠 + EQUIP:ARG:겉옷 == 0
		RETURNF 1
;스커트など, 팬티が見えない状態必須
CASE "노팬티"
	SIF EQUIP:ARG:웃옷 + EQUIP:ARG:스커트 && EQUIP:ARG:팬티 + EQUIP:ARG:보디슈츠 + EQUIP:ARG:겉옷 == 0
		RETURNF 1
CASE "全裸"
	SIF EQUIP:ARG:팬티 + EQUIP:ARG:브래지어 + EQUIP:ARG:웃옷 + EQUIP:ARG:스커트 + EQUIP:ARG:보디슈츠 + EQUIP:ARG:겉옷
		RETURNF 0
	SIF EQUIP:ARG:양말 + EQUIP:ARG:신발 + EQUIP:ARG:장갑
		RETURNF 0
	RETURNF 1
CASE "알몸"
	SIF EQUIP:ARG:팬티 + EQUIP:ARG:브래지어 + EQUIP:ARG:웃옷 + EQUIP:ARG:스커트 + EQUIP:ARG:보디슈츠 + EQUIP:ARG:겉옷
		RETURNF 0
	RETURNF 1
CASE "下着姿"
	SIF EQUIP:ARG:팬티 + EQUIP:ARG:브래지어 && EQUIP:ARG:웃옷 + EQUIP:ARG:스커트 + EQUIP:ARG:보디슈츠 + EQUIP:ARG:겉옷 == 0
		RETURNF 1
CASE "겉옷直穿き"
	SIF EQUIP:ARG:겉옷 && EQUIP:ARG:팬티 == 0
		RETURNF 1
CASE "自分の옷"
	SIF EQUIP:ARG:웃옷 && NAME_SHIRT(BASE:ARG:종족, ARG) == SHIRT(ARG, 2)
		RETURNF 1
	SIF EQUIP:ARG:보디슈츠 && NAME_BODYS(BASE:ARG:종족, ARG) == BODYS(ARG, 2)
		RETURNF 1

;이하, 시간がかかるのでTCVAR使用
CASE "팬티훑기", "팬티コキ"
	SIF GETBIT(TCVAR:ARG:26, 1)
		RETURNF 1
	SIF EQUIP:ARG:보디슈츠
		RETURNF 0
	IF EQUIP:ARG:겉옷
		SIF !CHECK_OUTER("コキ불가", ARG)
			LOCAL = 1
	ELSEIF EQUIP:ARG:팬티 && !CHECK_PANTIES("コキ불가", ARG)
		LOCAL = 1
	ENDIF
CASE "食い込み"
	SIF GETBIT(TCVAR:ARG:26, 2)
		RETURNF 1
	;Ｖ노출
	SIF GETBIT(TCVAR:ARG:26, 14)
		RETURNF 0
	SIF EQUIP:ARG:팬티 + EQUIP:ARG:보디슈츠 + EQUIP:ARG:겉옷 == 0
		RETURNF 0
	SIF EQUIP:ARG:보디슈츠 && !CHECK_BODYS("食い込み", ARG)
		RETURNF 0
	SIF EQUIP:ARG:겉옷 && !CHECK_OUTER("食い込み", ARG)
		RETURNF 0
	SIF EQUIP:ARG:팬티 && !CHECK_PANTIES("食い込み", ARG)
		RETURNF 0
	LOCAL = 2
CASE "蒸れ"
	SIF GETBIT(TCVAR:ARG:26, 3)
		RETURNF 1
	IF EQUIP:ARG:보디슈츠 && CHECK_BODYS("蒸れ", ARG)
		LOCAL = 3
	ELSEIF EQUIP:ARG:겉옷 && CHECK_OUTER("蒸れ", ARG)
		LOCAL = 3
	ELSEIF EQUIP:ARG:팬티 && CHECK_PANTIES("蒸れ", ARG)
		LOCAL = 3
	ENDIF
CASE "染み", "濡れ"
	SIF GETBIT(TCVAR:ARG:26, 4)
		RETURNF 1
	IF EQUIP:ARG:보디슈츠
		SIF CHECK_BODYS("染み", ARG)
			LOCAL = 4
	ELSEIF EQUIP:ARG:겉옷
		SIF CHECK_OUTER("染み", ARG)
			LOCAL = 4
	ELSEIF EQUIP:ARG:팬티
		SIF CHECK_PANTIES("染み", ARG)
			LOCAL = 4
	ENDIF
;브래지어など, 가슴을覆うものは"はだけ"る
CASE "はだけ可"
	SIF GETBIT(TCVAR:ARG:26, 5)
		RETURNF 1
	IF EQUIP:ARG:웃옷 && CHECK_SHIRT("はだけ可", ARG)
		LOCAL = 5
	ELSEIF EQUIP:ARG:보디슈츠 && CHECK_BODYS("はだけ可", ARG)
		LOCAL = 5
	ELSEIF EQUIP:ARG:브래지어 && CHECK_BRA("はだけ可", ARG)
		SIF EQUIP:ARG:웃옷 && CHECK_SHIRT("はだけ불가", ARG)
			RETURNF 0
		SIF EQUIP:ARG:보디슈츠 && CHECK_BODYS("はだけ불가", ARG)
			RETURNF 0
		LOCAL = 5
	ENDIF
CASE "가슴골노출"
	SIF GETBIT(TCVAR:ARG:26, 6)
		RETURNF 1
	SIF EQUIP:ARG:웃옷 && !CHECK_SHIRT("はだけた", ARG) && !CHECK_SHIRT("가슴골노출", ARG)
		RETURNF 0
	SIF EQUIP:ARG:보디슈츠 && !CHECK_BODYS("はだけた", ARG) && !CHECK_BODYS("가슴골노출", ARG)
		RETURNF 0
	SIF EQUIP:ARG:브래지어 && !CHECK_BRA("はだけた", ARG) && !CHECK_BRA("가슴골노출", ARG)
		RETURNF 0
	LOCAL = 6
CASE "유두노출", "Ｂ노출"
	SIF GETBIT(TCVAR:ARG:26, 7)
		RETURNF 1
	SIF EQUIP:ARG:웃옷 && !CHECK_SHIRT("はだけた", ARG) && !CHECK_SHIRT("유두노출", ARG)
		RETURNF 0
	SIF EQUIP:ARG:보디슈츠 && !CHECK_BODYS("はだけた", ARG) && !CHECK_BODYS("유두노출", ARG)
		RETURNF 0
	SIF EQUIP:ARG:브래지어 && !CHECK_BRA("はだけた", ARG) && !CHECK_BRA("유두노출", ARG)
		RETURNF 0
	LOCAL = 7
CASE "ずらし可", "ずらせる"
	SIF GETBIT(TCVAR:ARG:26, 8)
		RETURNF 1
	IF EQUIP:ARG:보디슈츠 && CHECK_BODYS("ずらせる", ARG)
		LOCAL = 8
	ELSEIF EQUIP:ARG:겉옷 && CHECK_OUTER("ずらせる", ARG)
		LOCAL = 8
	ELSEIF EQUIP:ARG:팬티 && CHECK_PANTIES("ずらせる", ARG)
		LOCAL = 8
	ENDIF
CASE "블루머노출"
	SIF GETBIT(TCVAR:ARG:26, 9)
		RETURNF 1
	SIF EQUIP:ARG:보디슈츠 && CHECK_BODYS("クロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:겉옷 && CHECK_OUTER("블루머", ARG) && CHECK_OUTER("クロッチ", ARG)
		LOCAL = 9
CASE "겉옷노출"
	SIF GETBIT(TCVAR:ARG:26, 10)
		RETURNF 1
	SIF EQUIP:ARG:보디슈츠 && CHECK_BODYS("クロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:겉옷 && CHECK_OUTER("クロッチ", ARG)
		LOCAL = 10
CASE "보디슈츠노출"
	SIF GETBIT(TCVAR:ARG:26, 11)
		RETURNF 1
	SIF EQUIP:ARG:보디슈츠 && CHECK_BODYS("クロッチ", ARG)
		LOCAL = 11
CASE "팬티스타킹노출"
	SIF GETBIT(TCVAR:ARG:26, 12)
		RETURNF 1
	SIF EQUIP:ARG:보디슈츠 && CHECK_BODYS("クロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:겉옷 && CHECK_OUTER("팬티스타킹", ARG) && CHECK_OUTER("クロッチ", ARG)
		LOCAL = 12
CASE "팬티노출"
	SIF GETBIT(TCVAR:ARG:26, 13)
		RETURNF 1
	SIF EQUIP:ARG:보디슈츠 && CHECK_BODYS("クロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:겉옷 && CHECK_OUTER("クロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:팬티 && CHECK_PANTIES("クロッチ", ARG)
		LOCAL = 13
CASE "Ｖ노출"
	SIF GETBIT(TCVAR:ARG:26, 14)
		RETURNF 1
	SIF EQUIP:ARG:보디슈츠 && CHECK_BODYS("クロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:겉옷 && CHECK_OUTER("クロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:팬티 && CHECK_PANTIES("クロッチ", ARG)
		RETURNF 0
	LOCAL = 14
;これはＶ노출を含む
CASE "Ｖ노출可"
	SIF GETBIT(TCVAR:ARG:26, 14) || GETBIT(TCVAR:ARG:26, 15)
		RETURNF 1
	;조교外ならＯＫとする
	SIF COND("조교中", ARG) == 0 && CHECK_PANTIES("정조대", ARG) == 0
		RETURNF 1
	SIF EQUIP:ARG:보디슈츠 && CHECK_BODYS("クロッチ", ARG) && !CHECK_BODYS("ずらせる", ARG)
		RETURNF 0
	SIF EQUIP:ARG:겉옷 && !CHECK_OUTER("팬티스타킹", ARG) && CHECK_OUTER("クロッチ", ARG) && !CHECK_OUTER("ずらせる", ARG)
		RETURNF 0
	SIF EQUIP:ARG:팬티 && CHECK_PANTIES("クロッチ", ARG) && !CHECK_PANTIES("ずらせる", ARG)
		RETURNF 0
	LOCAL = 15
CASE "Ａ노출"
	SIF GETBIT(TCVAR:ARG:26, 16)
		RETURNF 1
	SIF EQUIP:ARG:보디슈츠 && CHECK_BODYS("Ａクロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:겉옷 && CHECK_OUTER("Ａクロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:팬티 && CHECK_PANTIES("Ａクロッチ", ARG)
		RETURNF 0
	LOCAL = 16
;これはＡ노출を含む
CASE "Ａ노출可"
	SIF GETBIT(TCVAR:ARG:26, 16) || GETBIT(TCVAR:ARG:26, 17)
		RETURNF 1
	;조교外ならＯＫとする
	SIF COND("조교中", ARG) == 0 && CHECK_PANTIES("정조대", ARG) == 0
		RETURNF 1
	SIF EQUIP:ARG:보디슈츠 && CHECK_BODYS("Ａクロッチ", ARG) && !CHECK_BODYS("ずらせる", ARG)
		RETURNF 0
	SIF EQUIP:ARG:겉옷 && !CHECK_OUTER("팬티스타킹", ARG) && CHECK_OUTER("Ａクロッチ", ARG) && !CHECK_OUTER("ずらせる", ARG)
		RETURNF 0
	SIF EQUIP:ARG:팬티 && CHECK_PANTIES("Ａクロッチ", ARG) && !CHECK_PANTIES("ずらせる", ARG)
		RETURNF 0
	LOCAL = 17
CASE "수영복차림"
	SIF GETBIT(TCVAR:ARG:26, 18)
		RETURNF 1
	SIF EQUIP:ARG:웃옷 + EQUIP:스커트 + EQUIP:겉옷
		RETURNF 0
	IF EQUIP:ARG:보디슈츠 && CHECK_BODYS("수영복", ARG)
		LOCAL = 18
	ELSEIF EQUIP:ARG:팬티 && CHECK_PANTIES("수영복", ARG)
		LOCAL = 18
	ENDIF
CASE "비키니姿"
	SIF GETBIT(TCVAR:ARG:26, 19)
		RETURNF 1
	SIF EQUIP:ARG:웃옷 + EQUIP:스커트 + EQUIP:ARG:보디슈츠 + EQUIP:ARG:겉옷
		RETURNF 0
	SIF EQUIP:ARG:팬티 && CHECK_PANTIES("수영복", ARG)
		LOCAL = 19
CASE "ずらした"
	SIF GETBIT(TCVAR:ARG:26, 20)
		RETURNF 1
	IF EQUIP:ARG:보디슈츠 && CHECK_BODYS("ずらした", ARG)
		LOCAL = 20
	ELSEIF EQUIP:ARG:겉옷 && CHECK_OUTER("ずらした", ARG)
		LOCAL = 20
	ELSEIF EQUIP:ARG:팬티 && CHECK_PANTIES("ずらした", ARG)
		LOCAL = 20
	ENDIF
CASE "겨드랑이노출"
	SIF GETBIT(TCVAR:ARG:26, 21)
		RETURNF 1
	SIF EQUIP:ARG:웃옷 && CHECK_SHIRT("겨드랑이노출", ARG) == 0
		RETURNF 0
	LOCAL = 21
CASE "발노출"
	SIF GETBIT(TCVAR:ARG:26, 22)
		RETURNF 1
	SIF EQUIP:ARG:신발
		RETURNF 0
	SIF EQUIP:ARG:양말 && CHECK_SOCKS("발노출", ARG) == 0
		RETURNF 0
	LOCAL = 22
ENDSELECT
;TCVARに가능な事柄を記録
IF LOCAL > 0
	SETBIT TCVAR:ARG:26, LOCAL
	RETURNF 1
ENDIF

RETURNF 0


;-------------------------------------------------
;色やゲーム的な制限など, それぞれにいちいち설정すると不具合起こしそうな플래그はまとめてここで설정
;ビットの19番まではそれぞれで個別설정. 
;つまり, ここでは20～63を설정する
;-------------------------------------------------
@SETFLAG_COMMON(ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7, ARGS:8, ARGS:9, ARGS:10)
#FUNCTION
#DIM LCOUNT

VARSET LOCAL
FOR LCOUNT, 1, 11
	ARGS = %ARGS:LCOUNT%
	;材質, 形状等
	SELECTCASE ARGS
	CASE "수영복"
		SETBIT LOCAL, 20
	CASE "透け", "시스루"
		SETBIT LOCAL, 21
	CASE "실크", "絹"
		SETBIT LOCAL, 22
	CASE "레이스"
		SETBIT LOCAL, 23
	CASE "革", "가죽"
		SETBIT LOCAL, 24
		SETBIT LOCAL, 49
		SETBIT LOCAL, 50
		SETBIT LOCAL, 52
	CASE "모피"
		SETBIT LOCAL, 25
	CASE "갑옷"
		SETBIT LOCAL, 26
	CASE "날개"
		SETBIT LOCAL, 27
	CASE "エナメル"
		SETBIT LOCAL, 28
		SETBIT LOCAL, 52
	CASE "프릴"
		SETBIT LOCAL, 29
	CASE "파스너", "지퍼"
		SETBIT LOCAL, 30
	CASE "슬릿"
		SETBIT LOCAL, 31
	CASE "Ｖ노출"
		SETBIT LOCAL, 32
	CASE "Ａ노출"
		SETBIT LOCAL, 33
	CASE "ＶＡ노출"
		SETBIT LOCAL, 32
		SETBIT LOCAL, 33
	CASE "가슴골노출"
		SETBIT LOCAL, 34
	CASE "유두노출"
		SETBIT LOCAL, 35
	CASE "가슴노출"
		SETBIT LOCAL, 36
		SETBIT LOCAL, 34
		SETBIT LOCAL, 35
	CASE "손가락노출", "발손가락노출"
		SETBIT LOCAL, 37
	CASE "손노출", "발노출"
		SETBIT LOCAL, 38
		SETBIT LOCAL, 37
	CASE "겨드랑이노출"
		SETBIT LOCAL, 39

	;ゲーム的な制限等
	CASE "つなぎ"
		SETBIT LOCAL, 47
	CASE "원피스"
		SETBIT LOCAL, 48
	CASE "ずれ불가", "はだけ불가"
		SETBIT LOCAL, 49
	CASE "コキ불가", "ズリ불가"
		SETBIT LOCAL, 50
	CASE "탈의불가"
		SETBIT LOCAL, 51
	CASE "通気性なし", "通気性없음"
		SETBIT LOCAL, 52
	;色や模様
	CASE "줄무늬", "縞"
		SETBIT LOCAL, 53
	CASE "빨강"
		SETBIT LOCAL, 54
	CASE "초록"
		SETBIT LOCAL, 55
	CASE "노랑"
		SETBIT LOCAL, 56
	CASE "파랑"
		SETBIT LOCAL, 57
	CASE "하양"
		SETBIT LOCAL, 58
	CASE "濃"
		SETBIT LOCAL, 59
	;合成色
	CASE "연분홍"
		SETBIT LOCAL, 54
		SETBIT LOCAL, 58
	CASE "진홍"
		SETBIT LOCAL, 54
		SETBIT LOCAL, 59
	CASE "연록"
		SETBIT LOCAL, 55
		SETBIT LOCAL, 58
	CASE "연파랑"
		SETBIT LOCAL, 57
		SETBIT LOCAL, 58
	CASE "감색"
		SETBIT LOCAL, 57
		SETBIT LOCAL, 59
	CASE "주황"
		SETBIT LOCAL, 54
		SETBIT LOCAL, 56
	CASE "보라"
		SETBIT LOCAL, 54
		SETBIT LOCAL, 57
	CASE "순백"
		SETBIT LOCAL, 58
		SETBIT LOCAL, 59
	CASE "검정"
		SETBIT LOCAL, 54
		SETBIT LOCAL, 55
		SETBIT LOCAL, 56
		SETBIT LOCAL, 57
	;특수な손가락定
	CASE "줄무늬색"
		IF RAND:5 == 0
			SETBIT LOCAL, 54
			SETBIT LOCAL, 55
			SETBIT LOCAL, 56
			SETBIT LOCAL, 57
		ELSEIF RAND:4 == 0
			SETBIT LOCAL, 57
			SETBIT LOCAL, 58
		ELSEIF RAND:3 == 0
			SETBIT LOCAL, 56
			SETBIT LOCAL, 58
		ELSEIF RAND:2
			SETBIT LOCAL, 55
			SETBIT LOCAL, 58
		ELSE
			SETBIT LOCAL, 54
			SETBIT LOCAL, 58
		ENDIF
	CASE "大人しめの色"
		SIF RAND:3 == 0
			SETBIT LOCAL, 53
		IF RAND:2 && GETBIT(LOCAL, 53) == 0
			IF RAND:3
				SETBIT LOCAL, 58
			ELSE
				SETBIT LOCAL, 58
				SETBIT LOCAL, 59
			ENDIF
		ELSEIF RAND:4 == 0
			SETBIT LOCAL, 57
			SETBIT LOCAL, 58
		ELSEIF RAND:3 == 0
			SETBIT LOCAL, 56
			SETBIT LOCAL, 58
		ELSEIF RAND:2
			SETBIT LOCAL, 55
			SETBIT LOCAL, 58
		ELSE
			SETBIT LOCAL, 54
			SETBIT LOCAL, 58
		ENDIF
	CASE "Ｈめの色"
		IF RAND:5 == 0
			SETBIT LOCAL, 58
			SETBIT LOCAL, 59
		ELSEIF RAND:4 == 0
			SETBIT LOCAL, 54
			SETBIT LOCAL, 59
		ELSEIF RAND:3 == 0
			SETBIT LOCAL, 54
		ELSEIF RAND:2
			SETBIT LOCAL, 54
			SETBIT LOCAL, 57
		ELSE
			SETBIT LOCAL, 54
			SETBIT LOCAL, 55
			SETBIT LOCAL, 56
			SETBIT LOCAL, 57
		ENDIF
	;状態変化60～63
	CASE "はだけた", "はだけ"
		SETBIT LOCAL, 60
	CASE "ずらした", "ずらし"
		SETBIT LOCAL, 61
	CASE "파스너開"
		SETBIT LOCAL, 62
	CASE "破いた"
		SETBIT LOCAL, 63
	CASEELSE
		THROW %GAMEBASE_YEAR%버젼 BUG : "%ARGS%" 를 찾을 수 없습니다.
	ENDSELECT
	SIF STRLENS(ARGS:(LCOUNT + 1) ) == 0
		RETURNF LOCAL
NEXT
RETURNF LOCAL


;-------------------------------------------------
;服装の汚れやずらしなどを直して, 初期状態に戻す
;-------------------------------------------------
@RESETFLAG_CLO, ARG
FOR LOCAL, 10, 20
	STAIN:ARG:LOCAL = 0
NEXT
FOR LOCAL, 40, 50
	FOR LOCAL:1, 60, 64
		CLEARBIT EQUIP:ARG:LOCAL, LOCAL:1
	NEXT
NEXT

;-------------------------------------------------
;ARGの종족衣装の有無を判定(どれか一つでも満たしていると1を返す)
;-------------------------------------------------
@CLO_ORI(ARG, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5)
#FUNCTION
#DIM LCOUNT
FOR LCOUNT, 1, 6
	ARGS = %ARGS:LCOUNT%

	SELECTCASE ARGS
	CASE "옷"
		ARGS = %NAME_SHIRT(BASE:ARG:종족, ARG)%
	CASE "슈츠"
		ARGS = %NAME_BODYS(BASE:ARG:종족, ARG)%
	CASE "겉옷"
		ARGS = %NAME_OUTER(BASE:ARG:종족, ARG)%
	CASE "下着"
		ARGS = %NAME_PANTIES(BASE:ARG:종족, ARG)%
		SIF STRLENS(ARGS)
			RETURNF 1
		ARGS = %NAME_BRA(BASE:ARG:종족, ARG)%
	ENDSELECT

	SIF STRLENS(ARGS)
		RETURNF 1
NEXT
RETURNF 0

;-------------------------------------------------
;ARG:2の色をARG:1と同じにする
;-------------------------------------------------
@SAME_COLOR, ARG, ARG:1, ARG:2
SIF EQUIP:ARG:(ARG:1) == 0 || EQUIP:ARG:(ARG:2) == 0
	RETURN 0
;色が固定されている衣装
SIF SUBSTRING(CSTR:ARG:(ARG:2), 0, 2) == SUBSTRING(TEXTS("衣装の色", EQUIP:ARG:(ARG:2) ), 0, 2)
	RETURN 0
;특수な材質
SIF ARG:2 == 41 && CHECK_BRA("홑옷", ARG)
	RETURN 0
FOR LOCAL, 54, 60
	CLEARBIT EQUIP:ARG:(ARG:2), LOCAL
	SIF GETBIT(EQUIP:ARG:(ARG:1), LOCAL)
		SETBIT EQUIP:ARG:(ARG:2), LOCAL
NEXT

